$PACKAGE APAP.REDOENQ
SUBROUTINE REDO.E.NOF.ACCOUNT.CLOSING(Y.FINAL.ARRAY)
*******************************************************************************
*Company Name :  ASOCIACION POPULAR DE AHORROS Y PRISTAMOS
*Program Name :  REDO.E.NOF.ACCOUNT.CLOSING
********************************************************************************
*
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_ENQUIRY.COMMON
    $INSERT I_F.ACCOUNT
    $INSERT I_F.CUSTOMER
    $INSERT I_F.CUSTOMER.POSITION
    $INSERT I_F.CATEGORY
    $INSERT I_F.RELATION
    $INSERT I_F.STMT.ENTRY
    $INSERT I_F.REDO.TEL.TYPE
    $INSERT I_F.EB.CONTRACT.BALANCES
*
**********************************************************************************************
*Description  : It measures possible causes of closing,
*              and treatment of a particular complaint that may arise to improve service quality
*************************************************************************************************
*
*Modification Details:
*=====================
*      Date          Who             Reference               Description
*     ------         -----           -------------           -------------
*   4 NOV 2010     MD Preethi      0DR-2010-03-0088 118     Initial Creation
* 11-APRIL-2023      Conversion Tool       R22 Auto Conversion  - F.READ to CACHE.READ , VM to @VM , SM to @SM , FM to @FM , = to EQ and ++ to +=
* 11-APRIL-2023      Harsha                R22 Manual Conversion - No changes
*********************************************************************************

    GOSUB INIT
    GOSUB OPEN
    GOSUB LOC.REF.VALUES
    GOSUB PROCESS

RETURN

*====
INIT:
*====
    FN.ACCOUNT.CLOSED       = 'F.ACCOUNT.CLOSED'
    F.ACCOUNT.CLOSED        = ''
    FN.ACCOUNT.HIS          = 'F.ACCOUNT$HIS'
    F.ACCOUNT.HIS           = ''
    Y.TRANS.DATE            = ''
    FN.CUSTOMER             = 'F.CUSTOMER'
    F.CUSTOMER              = ''
    FN.CUSTOMER.POSITION    = 'F.CUSTOMER.POSITION'
    F.CUSTOMER.POSITION     = ''
    FN.RELATION             = 'F.RELATION'
    F.RELATION              = ''
    FN.CATEGORY             = 'F.CATEGORY'
    F.CATEGORY              = ''
    Y.FLAG                  = ''
    FN.REDO.TEL.TYPE = 'F.REDO.TEL.TYPE'
    F.REDO.TEL.TYPE = ''
    R.TEL.TYPE = ''
    FN.STMT.ENTRY = 'F.STMT.ENTRY'
    F.STMT.ENTRY = ''
    R.STMT.REC = ''
    FN.ACCOUNT = 'F.ACCOUNT'
    F.ACCOUNT = ''
    FN.STMT.PRINT = 'F.STMT.PRINTED'
    F.STMT.PRINT = ''

RETURN

*====
OPEN:
*====
    CALL OPF(FN.ACCOUNT.CLOSED,F.ACCOUNT.CLOSED)
    CALL OPF(FN.CUSTOMER,F.CUSTOMER)
    CALL OPF(FN.ACCOUNT.HIS,F.ACCOUNT.HIS)
    CALL OPF(FN.CUSTOMER.POSITION,F.CUSTOMER.POSITION)
    CALL OPF(FN.RELATION,F.RELATION)
    CALL OPF(FN.CATEGORY,F.CATEGORY)
    CALL OPF(FN.REDO.TEL.TYPE,F.REDO.TEL.TYPE)
    CALL OPF(FN.STMT.ENTRY,F.STMT.ENTRY)
    CALL OPF(FN.ACCOUNT,F.ACCOUNT)
    CALL OPF(FN.STMT.PRINT,F.STMT.PRINT)

RETURN

*==============
LOC.REF.VALUES:
*==============

    APPL.ARRAY               = "ACCOUNT":@FM:"CUSTOMER"
    FLD.ARRAY                = 'L.AC.CAN.REASON':@FM:'L.CU.TEL.TYPE':@VM:'L.CU.TIPO.CL':@VM:'L.CU.TEL.AREA':@VM:'L.CU.TEL.NO'
    FLD.POS                  = ''

    CALL MULTI.GET.LOC.REF(APPL.ARRAY,FLD.ARRAY,FLD.POS)
    LOC.L.CAN.REASON         =  FLD.POS<1,1>
    LOC.L.CU.TEL.TYPE        =  FLD.POS<2,1>
    LOC.L.CU.TIPO.CL.POS     =  FLD.POS<2,2>
    TEL.AREA.POS             =  FLD.POS<2,3>
    L.CU.TEL.NO.POS          =  FLD.POS<2,4>
RETURN

*=======
PROCESS:
*=======
*SEL.ACC="SELECT ":FN.ACCOUNT.CLOSED:" WITH @ID" ;*PACS00532842-s/e
    SEL.ACC="SELECT ":FN.ACCOUNT.CLOSED:

    LOCATE "DATE" IN D.FIELDS<1> SETTING Y.OP.TIME.POS THEN
        Y.OP.TIME.VAL = D.RANGE.AND.VALUE<Y.OP.TIME.POS>
*SEL.ACC :=" AND ACCT.CLOSE.DATE EQ ":Y.OP.TIME.VAL     ;*PACS00532842-s/e
        SEL.ACC :=" WITH ACCT.CLOSE.DATE EQ ":Y.OP.TIME.VAL
    END

    LOCATE "ACCOUNT.EXECUTIVE" IN D.FIELDS<1> SETTING Y.ACCT.EXE.POS THEN
        Y.ACCT.EXE.VAL = D.RANGE.AND.VALUE<Y.ACCT.EXE.POS>
    END
    CALL EB.READLIST(SEL.ACC,SEL.LIST,'',NO.OF.REC,Y.ERR)
    LOOP
        REMOVE  Y.ACCT.NO  FROM SEL.LIST SETTING Y.AC.POS
    WHILE Y.ACCT.NO : Y.AC.POS
        CALL EB.READ.HISTORY.REC(F.ACCOUNT.HIS,Y.ACCT.NO,R.ACCOUNT,Y.ERR)
        IF Y.ERR EQ '' THEN
            GOSUB MAIN.PROCESS
        END
        GOSUB NULLIFY.VALUES
    REPEAT

RETURN

*-------------------
MAIN.PROCESS:
*--------------------
    Y.CO.CODE = R.ACCOUNT<AC.CO.CODE>
    Y.COMP = ID.COMPANY
    IF Y.COMP EQ Y.CO.CODE THEN
        Y.ACT.NO = NUM(Y.ACCT.NO[1,3])

        GOSUB FETCH.ACC.VALUES
        IF Y.ACT.NO THEN
            GOSUB ACCOUNT.NAME
        END ELSE
            Y.ACCOUNT.NAME = R.ACCOUNT<AC.ACCOUNT.TITLE.1>
        END
        GOSUB CUSTOMER.INFO
    END
RETURN
*================
FETCH.ACC.VALUES:
*================
    Y.TRANS.DATE         =   R.ACCOUNT<AC.CLOSURE.DATE>
    Y.OP.TIME            =   R.ACCOUNT<AC.DATE.TIME>
    Y.ACCT.EXE           =   R.ACCOUNT<AC.ACCOUNT.OFFICER>

    IF Y.ACCT.EXE.VAL THEN
        IF Y.ACCT.EXE.VAL NE Y.ACCT.EXE THEN
            Y.FLAG = '1'
        END
    END
*  R.ECB='' ; ECB.ERR='' ;*Tus Start
*  CALL EB.READ.HVT("EB.CONTRACT.BALANCES",Y.ACCT.NO,R.ECB,ECB.ERR)
    Y.CURR.ACCT.BAL      =   R.ACCOUNT<AC.ONLINE.ACTUAL.BAL>
*  Y.CURR.ACCT.BAL      =   R.ECB<ECB.ONLINE.ACTUAL.BAL> ;*Tus End
    Y.CATEGORY           =   R.ACCOUNT<AC.CATEGORY>
    Y.CUSTOMER.ID        =   R.ACCOUNT<AC.CUSTOMER>
    Y.AGENCY             =   R.ACCOUNT<AC.CO.CODE>
    Y.CAN.BRANCH         =   R.ACCOUNT<AC.CO.CODE>
    Y.RELATION.CODE      =   R.ACCOUNT<AC.RELATION.CODE>
    Y.RELATION.COUNT     =   DCOUNT(Y.RELATION.CODE,@VM)
    Y.CURRENCY           =   R.ACCOUNT<AC.CURRENCY>

RETURN

*============
ACCOUNT.NAME:
*============

    IF Y.RELATION.CODE EQ '' THEN

        CALL F.READ(FN.CUSTOMER,Y.CUSTOMER.ID,R.CUSTOMER,F.CUSTOMER,Y.CUS.ERR)
        IF Y.CUS.ERR EQ '' THEN
            GOSUB REL.ID.DETS.TEMP
        END
    END

    Y.COUNT = 1
    LOOP
    WHILE Y.COUNT LE Y.RELATION.COUNT

        Y.RELATION.ID = R.ACCOUNT<AC.RELATION.CODE,Y.COUNT>

        IF Y.RELATION.ID GE 500 AND Y.RELATION.ID LE 529 THEN
            CALL F.READ(FN.RELATION,Y.RELATION.ID,R.RELATION,F.RELATION,Y.REL.ERR)
            IF Y.REL.ERR EQ '' THEN
                GOSUB REL.ID.DETS
            END
        END
        Y.COUNT += 1

    REPEAT

RETURN

*================
REL.ID.DETS.TEMP:
*================

    IF R.CUSTOMER<EB.CUS.LOCAL.REF,LOC.L.CU.TIPO.CL.POS> EQ "PERSONA FISICA" OR R.CUSTOMER<EB.CUS.LOCAL.REF,LOC.L.CU.TIPO.CL.POS> EQ "CLIENTE MENOR" THEN
        Y.ACCOUNT.NAME = R.CUSTOMER<EB.CUS.GIVEN.NAMES>:" ":R.CUSTOMER<EB.CUS.FAMILY.NAME>
    END

    IF R.CUSTOMER<EB.CUS.LOCAL.REF,LOC.L.CU.TIPO.CL.POS> EQ "PERSONA JURIDICA" THEN
        Y.ACCOUNT.NAME = R.CUSTOMER<EB.CUS.NAME.1,1>:" ":R.CUSTOMER<EB.CUS.NAME.2,1>
    END

    IF NOT(R.CUSTOMER<EB.CUS.LOCAL.REF,LOC.L.CU.TIPO.CL.POS>) THEN
        Y.ACCOUNT.NAME = R.CUSTOMER<EB.CUS.SHORT.NAME>
    END

RETURN

*============
REL.ID.DETS:
*============

    Y.REL.DESC          = R.RELATION<EB.REL.DESCRIPTION,1>
    Y.JOINT.CUSTOMER.ID = R.ACCOUNT<AC.JOINT.HOLDER,Y.COUNT>

    CALL F.READ(FN.CUSTOMER,Y.JOINT.CUSTOMER.ID,R.CUSTOMER,F.CUSTOMER,Y.CUS.ERR)

    Y.JOINTHOLDER.NAME = R.CUSTOMER<EB.CUS.LOCAL.REF,LOC.L.CU.TIPO.CL.POS>

    IF R.CUSTOMER<EB.CUS.LOCAL.REF,LOC.L.CU.TIPO.CL.POS> EQ "PERSONA FISICA" OR R.CUSTOMER<EB.CUS.LOCAL.REF,LOC.L.CU.TIPO.CL.POS> EQ "CLIENTE MENOR" THEN
        Y.JOINT.NAME = R.CUSTOMER<EB.CUS.GIVEN.NAMES>:" ":R.CUSTOMER<EB.CUS.FAMILY.NAME>
    END

    IF R.CUSTOMER<EB.CUS.LOCAL.REF,LOC.L.CU.TIPO.CL.POS> EQ "PERSONA JURIDICA" THEN
        Y.JOINT.NAME = R.CUSTOMER<EB.CUS.NAME.1,1>:" ":R.CUSTOMER<EB.CUS.NAME.2,1>
    END

    IF NOT(R.CUSTOMER<EB.CUS.LOCAL.REF,LOC.L.CU.TIPO.CL.POS>) THEN
        Y.JOINT.NAME = R.CUSTOMER<EB.CUS.SHORT.NAME>
    END

    Y.ACCOUNT.NAME<-1> = Y.REL.DESC:' ':Y.JOINT.NAME

RETURN

*=============
CUSTOMER.INFO:
*=============
    CALL F.READ(FN.CUSTOMER,Y.CUSTOMER.ID,R.CUSTOMER,F.CUSTOMER,Y.CUS.ERR1)
    IF Y.CUS.ERR1 EQ '' THEN
        GOSUB MAIN.PROCESS1
    END
RETURN
*-------------
MAIN.PROCESS1:
*--------------
    Y.REGION             =   R.CUSTOMER<EB.CUS.ACCOUNT.OFFICER>
    Y.CUS.TEL.AREA = R.CUSTOMER<EB.CUS.LOCAL.REF><1,TEL.AREA.POS>
    Y.CUS.TEL.NO = R.CUSTOMER<EB.CUS.LOCAL.REF><1,L.CU.TEL.NO.POS>
    Y.CUS.TEL.TYPE         =   R.CUSTOMER<EB.CUS.LOCAL.REF><1,LOC.L.CU.TEL.TYPE>
    Y.COUNT =  DCOUNT(Y.CUS.TEL.TYPE,@SM)
    Y.CNTR = 1
    LOOP
    WHILE Y.CNTR LE Y.COUNT
        Y.IND.TEL.TYPE = Y.CUS.TEL.TYPE<1,1,Y.CNTR>
        CALL F.READ(FN.REDO.TEL.TYPE,Y.IND.TEL.TYPE,R.TEL.TYPE,F.REDO.TEL.TYPE,Y.TEL.TYP.ERR)
        Y.CU.TEL.DES = R.TEL.TYPE<REDO.DESCRIPTION>
        Y.CUS.TEL.DES = Y.CU.TEL.DES<1,2>
        Y.IND.TEL.AREA = Y.CUS.TEL.AREA<1,1,Y.CNTR>
        Y.IND.TEL.NO = Y.CUS.TEL.NO<1,1,Y.CNTR>
        Y.TEL.DETS<-1> = Y.CUS.TEL.DES:' ':Y.IND.TEL.AREA:Y.IND.TEL.NO
        Y.CNTR += 1
    REPEAT
    CHANGE @FM TO @VM IN Y.TEL.DETS


    Y.REG.CNT = LEN(Y.REGION)
    IF Y.REG.CNT GT '8' THEN
        Y.REG.CNT1 = Y.REG.CNT-8
        Y.REG.CNT2 = Y.REG.CNT1+1
        Y.REGION = Y.REGION[Y.REG.CNT2,2]
    END
    IF Y.REG.CNT EQ 8 THEN
        Y.REGION = Y.REGION[1,2]
    END
    IF Y.REG.CNT LT 8 THEN
        Y.REGION = ''
    END

    SEL.CMD              =  "SELECT ":FN.CUSTOMER.POSITION:' WITH @ID LIKE ':Y.CUSTOMER.ID:'*...'
    CALL EB.READLIST(SEL.CMD,SEL.CMD.LIST,'',NO.OF.REC.CMD,Y.ERR1)
    LOOP
        REMOVE Y.CUS.POS.ID FROM SEL.CMD.LIST SETTING Y.POS
    WHILE Y.CUS.POS.ID : Y.POS
        Y.MOD            =   FIELD(Y.CUS.POS.ID,'*',3)
        Y.TRANS.REF      =   FIELD(Y.CUS.POS.ID,'*',6)
        IF Y.OTHER.PROD EQ '' THEN
            Y.OTHER.PROD = Y.MOD :" ": Y.TRANS.REF
        END ELSE
            Y.OTHER.PROD := @VM :Y.MOD :" ": Y.TRANS.REF
        END
    REPEAT
    Y.CAN.REASON         =   R.ACCOUNT<AC.LOCAL.REF><1,LOC.L.CAN.REASON>

    Y.LAST.DBT.DATE      =   R.ACCOUNT<AC.DATE.LAST.DR.CUST>
* LOCATE 'CUST-DR' IN R.ECB<ECB.INITIATOR.TYPE,1> SETTING CUST.DR.POS THEN
*    Y.LAST.DBT.DATE = R.ECB<ECB.INITIATOR.TYPE,CUST.DR.POS>
*    Y.LAST.DBT.DATE = R.ECB<ECB.DATE.LAST,CUST.DR.POS>
*  END
    Y.AC.NO = FIELD(Y.ACCT.NO,';',1)
*PACS00532842-s
*IF Y.LAST.DBT.DATE THEN
*SEL.STMT.CMD = 'SELECT ':FN.STMT.PRINT:' WITH @ID LIKE ...':Y.AC.NO:'-':Y.LAST.DBT.DATE:'...'
*CALL EB.READLIST(SEL.STMT.CMD,SEL.STMT.LIST,'',NOF.STMT.RECS,STMT.SEL.ERR)
*IF SEL.STMT.LIST THEN
*Y.STMT.PRINT.ID = SEL.STMT.LIST
*CALL F.READ(FN.STMT.PRINT,Y.STMT.PRINT.ID,R.STMT.PRINT,F.STMT.PRINT,Y.STMT.PRI.ERR)
*IF R.STMT.PRINT THEN
*Y.CNT = DCOUNT(R.STMT.PRINT,FM)
*Y.STMT.ID = R.STMT.PRINT<Y.CNT>

*CALL F.READ(FN.STMT.ENTRY,Y.STMT.ID,R.STMT.REC,F.STMT.ENTRY,Y.STMT.ERR)
*IF Y.STMT.ERR EQ '' THEN
*Y.CAN.BRANCH = R.STMT.REC<AC.STE.COMPANY.CODE>
*END
*END
*END
*END
*PACS00532842-e
    CALL CACHE.READ(FN.CATEGORY, Y.CATEGORY, R.CATEGORY, Y.ERR)	  ;*R22 Auto Conversion  - F.READ to CACHE.READ
    Y.PROD.TYPE          =   R.CATEGORY<EB.CAT.DESCRIPTION>
    CHANGE '*' TO '$' IN Y.PROD.TYPE
    Y.USER.INP           =   R.ACCOUNT<AC.INPUTTER>
    GOSUB FORM.ARRAY
RETURN
*==========
FORM.ARRAY:
*==========

    CHANGE '*' TO '$' IN Y.ACCOUNT.NAME
    CHANGE @FM TO 'VM' IN Y.ACCOUNT.NAME
    IF Y.FLAG NE '1' THEN
        Y.FINAL.ARRAY<-1>        =   Y.TRANS.DATE:'*':Y.OP.TIME:'*':Y.ACCT.NO:'*': Y.ACCOUNT.NAME:'*':Y.CAN.REASON:'*':Y.ACCT.EXE:'*':Y.CURR.ACCT.BAL:'*':Y.PROD.TYPE:'*':Y.REGION:'*':Y.AGENCY:'*':Y.CAN.BRANCH:'*':Y.USER.INP:'*':Y.OTHER.PROD:'*':Y.TEL.DETS:'*':Y.CURRENCY
*                                           1              2            3                 4                 5              6                  7                 8             9            10             11              12              13             14             15
    END
RETURN
*---------------
NULLIFY.VALUES:
*---------------
    Y.TRANS.DATE = ''          ; Y.OP.TIME = ''       ; Y.ACCT.NO = ''            ; Y.ACCOUNT.NAME = ''      ;
    Y.CAN.REASON = ''          ; Y.ACCT.EXE = ''      ; Y.CURR.ACCT.BAL = ''      ; Y.PROD.TYPE = ''         ;
    Y.REGION = ''              ; Y.AGENCY = ''        ; Y.CAN.BRANCH = ''         ; Y.USER.INP = ''          ;
    Y.OTHER.PROD = ''          ; Y.TEL.DETS = ''      ; Y.CUS.TEL.AREA = ''       ; Y.CUS.TEL.NO = ''        ;
    Y.CUS.TEL.TYPE = ''        ; Y.IND.TEL.TYPE = ''  ; Y.IND.TEL.NO = ''         ; Y.CU.TEL.DES = ''        ;
    Y.CUS.TEL.DES = ''         ; Y.IND.TEL.AREA = ''  ; Y.FLAG = ''
RETURN
END
