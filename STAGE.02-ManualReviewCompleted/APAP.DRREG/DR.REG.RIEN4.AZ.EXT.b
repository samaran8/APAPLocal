* @ValidationCode : MjotNDQyNDEwMTMxOkNwMTI1MjoxNjgwNzY3NjgwMzc0OmFqaXRoOi0xOi0xOjA6MDpmYWxzZTpOL0E6UjIxX0FNUi4wOi0xOi0x
* @ValidationInfo : Timestamp         : 06 Apr 2023 13:24:40
* @ValidationInfo : Encoding          : Cp1252
* @ValidationInfo : User Name         : ajith
* @ValidationInfo : Nb tests success  : N/A
* @ValidationInfo : Nb tests failure  : N/A
* @ValidationInfo : Rating            : N/A
* @ValidationInfo : Coverage          : N/A
* @ValidationInfo : Strict flag       : N/A
* @ValidationInfo : Bypass GateKeeper : false
* @ValidationInfo : Compiler Version  : R21_AMR.0
* @ValidationInfo : Copyright Temenos Headquarters SA 1993-2021. All rights reserved.
$PACKAGE APAP.DRREG
SUBROUTINE DR.REG.RIEN4.AZ.EXT(REC.ID)
*----------------------------------------------------------------------------
* Company Name   : APAP
* Developed By   : gangadhar@temenos.com
* Program Name   : DR.REG.RIEN4.AZ.EXT
* Date           : 27-May-2013
*----------------------------------------------------------------------------
* Description:
*------------
* This multi-thread job is meant for to extact the AZ.ACCOUNT/ACCOUNT Details Product wise.
*----------------------------------------------------------------------------
*
* Modification History :
* ----------------------
*   Date       Author              Modification Description
* 16-09-2014   Ashokkumar         PACS00367490- Added interest amount to the PRINCIPAL balance.
*                                               corrected the interest rate issue.
* 20-11-2014   Ashokkumar         PACS00367490- Added the interest amount to the saving account.
*                                               Reinvested amount will be taken from OPEN.ACTUAL.BAL.
*---------------------------------------------------------------------------------------
*DATE               WHO                       REFERENCE                 DESCRIPTION
*06-04-2023       CONVERSION TOOLS            AUTO R22 CODE CONVERSION   VM to @VM , <= to LE ,= to EQ
*06-04-2023       AJITHKUMAR                  MANUAL R22 CODE CONVERSION NO CHANGE
*----------------------------------------------------------------------------------------



*----------------------------------------------------------------------------

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_BATCH.FILES
    $INSERT I_TSA.COMMON
    $INSERT I_F.CUSTOMER
    $INSERT I_F.AZ.ACCOUNT
    $INSERT I_F.ACCOUNT
    $INSERT I_F.BASIC.INTEREST
    $INSERT I_F.GROUP.CREDIT.INT
    $INSERT I_F.GROUP.DATE
    $INSERT I_F.EB.CONTRACT.BALANCES
    $INSERT I_F.DATES
    $INSERT I_DR.REG.RIEN4.AZ.EXT.COMMON
    $INSERT I_F.DR.REG.RIEN4.AZ.REP
    $INSERT I_F.DR.REG.RIEN4.AZ.REP2
    $INSERT I_F.DR.REG.RIEN4.AZ.REP3
    $INSERT I_F.DR.REG.RIEN4.AZ.REP4
    $INSERT I_F.DR.REG.RIEN4.AC.REP1
    $INSERT I_F.DR.REG.RIEN4.AC.REP2
*
    GOSUB PROCESS
RETURN

INIT.PROCESS:
*-----------*
*
    Y.INT.RATE = ''; Y.DAYS = ''; Y.MAT.DATE = ''; YREC.ID = ''
    Y.PRINCIPAL = ''; Y.DAYS.FLAG = ''; YWORK.AMT = 0; ERR.AZ.ACCOUNT.HST = ''
    R.AZ.ACCOUNT = ''; AZ.ACCOUNT.ERR = ''; CAT.VAL = ''
*
    CALL F.READ(FN.AZ.ACCOUNT,REC.ID,R.AZ.ACCOUNT,F.AZ.ACCOUNT,AZ.ACCOUNT.ERR)
    YREC.ID = REC.ID
    IF NOT(R.AZ.ACCOUNT) THEN
        CALL EB.READ.HISTORY.REC(F.AZ.ACCOUNT.HST,YREC.ID,R.AZ.ACCOUNT,ERR.AZ.ACCOUNT.HST)
    END
    Y.INT.RATE = R.AZ.ACCOUNT<AZ.INTEREST.RATE>
    Y.INT.RATE = FMT(Y.INT.RATE,'L2%6')
    Y.MAT.DATE = R.AZ.ACCOUNT<AZ.MATURITY.DATE>
    CAT.VAL = R.AZ.ACCOUNT<AZ.CATEGORY>
    Y.REGION = ''
    IF LEN(Y.MAT.DATE) EQ '8' THEN
        Y.DAYS   = 'C'
        CALL CDD(Y.REGION, Y.MAT.DATE, Y.LWORK.DAY, Y.DAYS)
        Y.DAYS = ABS(Y.DAYS)
    END
    Y.PRINCIPAL = R.AZ.ACCOUNT<AZ.PRINCIPAL>
    GOSUB READ.ACCOUNT
    Y.REINVEST.VAL = R.ACCOUNT<AC.LOCAL.REF,L.AC.REINVESTED.POS>
    IF Y.REINVEST.VAL EQ 'YES' THEN
        R.AC1 = ''; ACC1.ERR = ''; ERR.ACCOUNT.HST = ''; YR.ACCOUNT = ''; YREC.ID = ''; Y.WORK.AMT = 0
        AC1.ID = R.AZ.ACCOUNT<AZ.INTEREST.LIQU.ACCT>
        CALL F.READ(FN.ACCOUNT,AC1.ID,R.AC1,F.ACCOUNT,ACC1.ERR)
        GOSUB READ.AC.HST
*        Y.WORK.AMT = R.AC1<AC.LOCAL.REF,L.AC.AV.BAL.POS>
        Y.WORK.AMT = R.AC1<AC.OPEN.ACTUAL.BAL>
        YREC.ID = AC1.ID ;  YR.ACCOUNT = R.AC1
        GOSUB GET.INT.PAY.AMT
        YWORK.AMT = Y.WORK.AMT + INTEREST.PAYABLE
    END
    GOSUB CHECK.DAYS.RANGE
    YR.ACCOUNT = ''; YREC.ID = ''
    YREC.ID = REC.ID ; YR.ACCOUNT = R.ACCOUNT
    GOSUB GET.INT.PAY.AMT
RETURN

READ.AC.HST:
*************
    IF NOT(R.ACCOUNT) THEN
        YAC1.ID = AC1.ID
        CALL EB.READ.HISTORY.REC(F.ACCOUNT.HST,YAC1.ID,R.AC1,ERR.ACCOUNT.HST)
    END
RETURN

GET.INT.PAY.AMT:
****************
*
    R.EB.CONTRACT.BALANCES = ''; EB.CONTRACT.BALANCES.ERR = ''; VAL.CODE = ''
    OPEN.BAL = ''; CRED.MVMT = ''; DEBT.MVMT = ''; INTEREST.PAYABLE = 0
    CALL F.READ(FN.EB.CONTRACT.BALANCES,YREC.ID,R.EB.CONTRACT.BALANCES,F.EB.CONTRACT.BALANCES,EB.CONTRACT.BALANCES.ERR)
    IF R.EB.CONTRACT.BALANCES THEN
        YTYPE.SYSDATE = R.EB.CONTRACT.BALANCES<ECB.TYPE.SYSDATE>
        YTYPE = DCOUNT(YTYPE.SYSDATE,@VM)
        YTOT.OPEN.BAL = ''
        FOR COUT = 1 TO YTYPE
            SYS.DATE = ''; YSYSTYPE = ''
            Y.TYPE.SYSDATE = R.EB.CONTRACT.BALANCES<ECB.TYPE.SYSDATE,COUT>
            YSYSTYPE = FIELD(Y.TYPE.SYSDATE,'-',1)
            SYS.DATE = FIELD(Y.TYPE.SYSDATE,'-',2)

            YOPEN.BALANCE = '' ; YCREDIT.MVMT = '' ; YAC.BAL  = ''; YDEBIT.MVMT = ''
            IF (YSYSTYPE EQ '50000' AND SYS.DATE LE YTODAY) THEN ;*R22 AUTO CODE CONVERSION
                YOPEN.BALANCE = R.EB.CONTRACT.BALANCES<ECB.OPEN.BALANCE,COUT,1>
                YCREDIT.MVMT = R.EB.CONTRACT.BALANCES<ECB.CREDIT.MVMT,COUT,1>
                YDEBIT.MVMT = R.EB.CONTRACT.BALANCES<ECB.DEBIT.MVMT,COUT,1>
                YAC.BAL  = YOPEN.BALANCE + YCREDIT.MVMT + YDEBIT.MVMT
            END
            INTEREST.PAYABLE += YAC.BAL
        NEXT COUT
    END
    IF NOT(INTEREST.PAYABLE) THEN
        INTEREST.PAYABLE = YR.ACCOUNT<AC.ACCR.CR.AMOUNT>
    END
RETURN

INIT.AC.PROCESS:
*--------------*
    CR.BINT.RATE = ''; INTEREST.PAYABLE = 0
    GOSUB READ.ACCOUNT
    CAT.VAL = R.ACCOUNT<AC.CATEGORY>
    COND.GRP = R.ACCOUNT<AC.CONDITION.GROUP>
    CCY.VAL = R.ACCOUNT<AC.CURRENCY>
    GRP.DT.ID = COND.GRP:CCY.VAL
    CALL F.READ(FN.GROUP.DATE,GRP.DT.ID,R.GROUP.DATE,F.GROUP.DATE,GROUP.DATE.ERR)
    GRP.CR.DATE = R.GROUP.DATE<AC.GRD.CREDIT.GROUP.DATE>
    GCI.ID = COND.GRP:CCY.VAL:GRP.CR.DATE
    CALL F.READ(FN.GROUP.CREDIT.INT,GCI.ID,R.GROUP.CREDIT.INT,F.GROUP.CREDIT.INT,GROUP.CREDIT.INT.ERR)
    IF R.GROUP.CREDIT.INT<IC.GCI.CR.INT.RATE,1> THEN
        CR.INT.RATE = R.GROUP.CREDIT.INT<IC.GCI.CR.INT.RATE,1>
    END ELSE
        CR.BINT.RATE = R.GROUP.CREDIT.INT<IC.GCI.CR.BASIC.RATE,1>
    END
    IF CR.INT.RATE EQ '' THEN
        BI.ID = CR.BINT.RATE:CCY.VAL:GRP.CR.DATE
        CALL F.READ(FN.BASIC.INTEREST,BI.ID,R.BASIC.INTEREST,F.BASIC.INTEREST,BASIC.INTEREST.ERR)
        CR.INT.RATE = R.BASIC.INTEREST<EB.BIN.INTEREST.RATE>
    END
*    ONLINE.ACT.AMT = R.ACCOUNT<AC.ONLINE.ACTUAL.BAL>
    IF FLAG EQ 1 THEN ;*Tus start ;*R22 AUTO CODE CONVERSION
        ONLINE.ACT.AMT = R.ACCOUNT<AC.OPEN.ACTUAL.BAL>
    END ELSE
        ONLINE.ACT.AMT=R.ECB<ECB.OPEN.ACTUAL.BAL>
    END ;*Tus End
    GOSUB GET.INT.PAY.AMT
RETURN

READ.ACCOUNT:
*************
    R.ACCOUNT = ''; ACCOUNT.ERR = ''; ERR.ACCOUNT.HST = ''
    CALL F.READ(FN.ACCOUNT,REC.ID,R.ACCOUNT,F.ACCOUNT,ACCOUNT.ERR)
    FLAG='' R.ECB='' ;ECB.ERR='' ;*Tus start
    CALL EB.READ.HVT('EB.CONTRACT.BALANCES',REC.ID,R.ECB,ECB.ERR) ;*Tus End
    YREC.ID = REC.ID
    IF NOT(R.ACCOUNT) THEN
        FLAG=1 ;*Tus S/E
        CALL EB.READ.HISTORY.REC(F.ACCOUNT.HST,YREC.ID,R.ACCOUNT,ERR.ACCOUNT.HST)
    END
RETURN

PROCESS:
*------*
    C1.POSN = ''; C2.POSN = ''; C3.POSN = ''; C4.POSN = ''; C5.POSN = ''; C6.POSN = ''; YTODAY = ''
    YTODAY = TODAY
    CALL CDT('',YTODAY,'-1C')
    BEGIN CASE
        CASE CONTROL.LIST<1,1> EQ 'REP1'
            GOSUB INIT.PROCESS
            LOCATE CAT.VAL IN YCAT.LIST1<1> SETTING C1.POSN THEN
                GOSUB PROCESS.DEPOSITS
            END
            LOCATE CAT.VAL IN YCAT.LIST2<1> SETTING C2.POSN THEN
                GOSUB PROCESS.DEPOSITS2
            END
            LOCATE CAT.VAL IN YCAT.LIST3<1> SETTING C3.POSN THEN
                GOSUB PROCESS.DEPOSITS3
            END
            LOCATE CAT.VAL IN YCAT.LIST4<1> SETTING C4.POSN THEN
                GOSUB PROCESS.DEPOSITS4
            END
        CASE CONTROL.LIST<1,1> EQ 'REP2'
            GOSUB INIT.AC.PROCESS
            LOCATE CAT.VAL IN YCAT.LIST5<1> SETTING C5.POSN THEN
                GOSUB PROCESS.ACCOUNTS1
            END
            LOCATE CAT.VAL IN YCAT.LIST6<1> SETTING C6.POSN THEN
                GOSUB PROCESS.ACCOUNTS2
            END
    END CASE
RETURN

PROCESS.DEPOSITS2:
*---------------*
*
    R.DR.REG.RIEN4.AZ.REP2 = ''
    R.DR.REG.RIEN4.AZ.REP2<DR.RIEN4.REP2.INT.RATE> = Y.INT.RATE
    R.DR.REG.RIEN4.AZ.REP2<DR.RIEN4.REP2.MAT.DATE> = Y.MAT.DATE
    R.DR.REG.RIEN4.AZ.REP2<DR.RIEN4.REP2.DAYS> = Y.DAYS
    R.DR.REG.RIEN4.AZ.REP2<DR.RIEN4.REP2.PRINCIPAL> = Y.PRINCIPAL + YWORK.AMT + INTEREST.PAYABLE
    R.DR.REG.RIEN4.AZ.REP2<DR.RIEN4.REP2.DAY.RANGE> = Y.DAYS.FLAG
    AZ.REP.ID = REC.ID
    CALL F.WRITE(FN.DR.REG.RIEN4.AZ.REP2,AZ.REP.ID,R.DR.REG.RIEN4.AZ.REP2)
RETURN

PROCESS.DEPOSITS3:
*---------------*
*
    R.DR.REG.RIEN4.AZ.REP3 = ''
    R.DR.REG.RIEN4.AZ.REP3<DR.RIEN4.REP3.INT.RATE> = Y.INT.RATE
    R.DR.REG.RIEN4.AZ.REP3<DR.RIEN4.REP3.MAT.DATE> = Y.MAT.DATE
    R.DR.REG.RIEN4.AZ.REP3<DR.RIEN4.REP3.DAYS> = Y.DAYS
    R.DR.REG.RIEN4.AZ.REP3<DR.RIEN4.REP3.PRINCIPAL> = Y.PRINCIPAL + YWORK.AMT + INTEREST.PAYABLE
    R.DR.REG.RIEN4.AZ.REP3<DR.RIEN4.REP3.DAY.RANGE> = Y.DAYS.FLAG
    AZ.REP.ID = REC.ID
    CALL F.WRITE(FN.DR.REG.RIEN4.AZ.REP3,AZ.REP.ID,R.DR.REG.RIEN4.AZ.REP3)
RETURN

PROCESS.DEPOSITS4:
*---------------*
*
    R.DR.REG.RIEN4.AZ.REP4 = ''
    R.DR.REG.RIEN4.AZ.REP4<DR.RIEN4.REP4.INT.RATE> = Y.INT.RATE
    R.DR.REG.RIEN4.AZ.REP4<DR.RIEN4.REP4.MAT.DATE> = Y.MAT.DATE
    R.DR.REG.RIEN4.AZ.REP4<DR.RIEN4.REP4.DAYS> = Y.DAYS
    R.DR.REG.RIEN4.AZ.REP4<DR.RIEN4.REP4.PRINCIPAL> = Y.PRINCIPAL + YWORK.AMT + INTEREST.PAYABLE
    R.DR.REG.RIEN4.AZ.REP4<DR.RIEN4.REP4.DAY.RANGE> = Y.DAYS.FLAG
    AZ.REP.ID = REC.ID
    CALL F.WRITE(FN.DR.REG.RIEN4.AZ.REP4,AZ.REP.ID,R.DR.REG.RIEN4.AZ.REP4)
RETURN

PROCESS.ACCOUNTS1:
*---------------*
*
    R.DR.REG.RIEN4.AC.REP1 = ''
    R.DR.REG.RIEN4.AC.REP1<DR.RIEN4.AC1.INT.RATE> = CR.INT.RATE
    R.DR.REG.RIEN4.AC.REP1<DR.RIEN4.AC1.ONLINE.ACT.BAL> = ONLINE.ACT.AMT + INTEREST.PAYABLE
    R.DR.REG.RIEN4.AC.REP1<DR.RIEN4.AC1.CATEGORY> = CAT.VAL
    CALL F.WRITE(FN.DR.REG.RIEN4.AC.REP1,REC.ID,R.DR.REG.RIEN4.AC.REP1)
RETURN

PROCESS.ACCOUNTS2:
*---------------*
*
    R.DR.REG.RIEN4.AC.REP2 = ''
    R.DR.REG.RIEN4.AC.REP2<DR.RIEN4.AC2.INT.RATE> = CR.INT.RATE
    R.DR.REG.RIEN4.AC.REP2<DR.RIEN4.AC2.ONLINE.ACT.BAL> = ONLINE.ACT.AMT + INTEREST.PAYABLE
    R.DR.REG.RIEN4.AC.REP2<DR.RIEN4.AC2.CATEGORY> = CAT.VAL
    CALL F.WRITE(FN.DR.REG.RIEN4.AC.REP2,REC.ID,R.DR.REG.RIEN4.AC.REP2)
RETURN

PROCESS.DEPOSITS:
*---------------*
*
    R.DR.REG.RIEN4.AZ.REP = ''
    R.DR.REG.RIEN4.AZ.REP<DR.RIEN4.INT.RATE> = Y.INT.RATE
    R.DR.REG.RIEN4.AZ.REP<DR.RIEN4.MAT.DATE> = Y.MAT.DATE
    R.DR.REG.RIEN4.AZ.REP<DR.RIEN4.DAYS> = Y.DAYS
    R.DR.REG.RIEN4.AZ.REP<DR.RIEN4.PRINCIPAL> = Y.PRINCIPAL + YWORK.AMT + INTEREST.PAYABLE
    R.DR.REG.RIEN4.AZ.REP<DR.RIEN4.DAY.RANGE> = Y.DAYS.FLAG
    AZ.REP.ID = REC.ID
    CALL F.WRITE(FN.DR.REG.RIEN4.AZ.REP,AZ.REP.ID,R.DR.REG.RIEN4.AZ.REP)
RETURN

CHECK.DAYS.RANGE:
*---------------*
*
    BEGIN CASE
        CASE Y.DAYS GE 0 AND Y.DAYS LE 15
            Y.DAYS.FLAG = 1
        CASE Y.DAYS GE 16 AND Y.DAYS LE 30
            Y.DAYS.FLAG = 2
        CASE Y.DAYS GE 31 AND Y.DAYS LE 60
            Y.DAYS.FLAG = 3
        CASE Y.DAYS GE 61 AND Y.DAYS LE 90
            Y.DAYS.FLAG = 4
        CASE Y.DAYS GE 91 AND Y.DAYS LE 180
            Y.DAYS.FLAG = 5
        CASE Y.DAYS GE 181 AND Y.DAYS LE 360
            Y.DAYS.FLAG = 6
        CASE Y.DAYS GE 361 AND Y.DAYS LE 720
            Y.DAYS.FLAG = 7
        CASE Y.DAYS GE 721 AND Y.DAYS LE 1080
            Y.DAYS.FLAG = 8
        CASE Y.DAYS GE 1081 AND Y.DAYS LE 1440
            Y.DAYS.FLAG = 9
        CASE Y.DAYS GE 1441 AND Y.DAYS LE 1800
            Y.DAYS.FLAG = 10
        CASE Y.DAYS GE 1801
            Y.DAYS.FLAG = 11
    END CASE
*
RETURN
*----------------------------------------------------------------------------
END
