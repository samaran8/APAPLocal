$PACKAGE APAP.LAPAP
*-----------------------------------------------------------------------------
*MODIFICATION HISTORY:
*
*DATE           WHO                 REFERENCE               DESCRIPTION
*21-APR-2023    VICTORIA S          R22 MANUAL CONVERSION   NO CHANGE
*21-APR-2023    CONVERSION TOOL     R22 AUTO CONVERSION     FM TO @FM,VM TO @VM
*-----------------------------------------------------------------------------
SUBROUTINE REDO.V.DD.ACCT.OVERDRFT.BAL
*
* Client: APAP
* Description: The unauthorised overdraft balance error attached to the Version's 'FUNDS.TRANSFER,AI.REDO.ARC.DD.PROCESS'
*               and 'FUNDS.TRANSFER,REDO.DD.PROCESS'.
* Name : V.P.Ashokkumar
*

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.ACCOUNT
    $INSERT I_F.FUNDS.TRANSFER

    GOSUB READ.ACCOUNT
    GOSUB FT.PROCESS
RETURN

FT.PROCESS:
***********
    YDEBIT.AMT = R.NEW(FT.DEBIT.AMOUNT)
    IF NOT(YDEBIT.AMT) THEN
        YDEBIT.AMT = R.NEW(FT.CREDIT.AMOUNT)
    END
    YTRAN.AMT = R.NEW(FT.LOCAL.REF)<1,POS.L.TT.TRANS.AMT>
    YACTUAL.AMT = YDEBIT.AMT + Y.ACCOUNT.BALANCE

    IF YTRAN.AMT GT YACTUAL.AMT THEN
        AF = FT.DEBIT.ACCT.NO
        ETEXT  = 'EB-REDO.ACCT.UNAUTH.OD'
        CALL STORE.END.ERROR
    END
RETURN

READ.ACCOUNT:
**************
    FN.ACCOUNT = 'F.ACCOUNT'; F.ACCOUNT = ''
    CALL OPF(FN.ACCOUNT,F.ACCOUNT)

    ERR.ACCOUNT = ''; R.ACCOUNT = ''; Y.ACCOUNT.BALANCE = 0
    Y.TRANSIT.AMOUNT = ''; Y.L.AC.AVL.BAL = ''; YVAL.POSN = ''
    YACTUAL.AMT = 0; YTRAN.AMT = ''

    YFILE.NAME = 'ACCOUNT':@FM:'FUNDS.TRANSFER' ;*R22 AUTO CONVERSION
    YFIELD.NME = 'L.AC.AV.BAL':@VM:'L.AC.TRAN.AVAIL':@FM:'L.TT.TRANS.AMT' ;*R22 AUTO CONVERSION
    CALL MULTI.GET.LOC.REF(YFILE.NAME,YFIELD.NME,YVAL.POSN)
    POS.AVL.BAL = YVAL.POSN<1,1>
    POS.TRANS.AMT = YVAL.POSN<1,2>
    POS.L.TT.TRANS.AMT = YVAL.POSN<2,1>

    YDEBIT.ACCT = ''
    YDEBIT.ACCT = R.NEW(FT.DEBIT.ACCT.NO)
    CALL F.READ(FN.ACCOUNT,YDEBIT.ACCT,R.ACCOUNT,F.ACCOUNT,ERR.ACCOUNT)
    Y.TRANSIT.AMOUNT  = R.ACCOUNT<AC.LOCAL.REF,POS.TRANS.AMT>
    Y.L.AC.AVL.BAL    = R.ACCOUNT<AC.LOCAL.REF,POS.AVL.BAL>
    Y.ACCOUNT.BALANCE = Y.TRANSIT.AMOUNT + Y.L.AC.AVL.BAL
RETURN

END
