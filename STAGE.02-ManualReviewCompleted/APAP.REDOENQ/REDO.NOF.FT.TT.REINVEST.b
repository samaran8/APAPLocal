$PACKAGE APAP.REDOENQ
SUBROUTINE REDO.NOF.FT.TT.REINVEST(ENQ.DATA)
*-----------------------------------------------------------------------------
* Company Name : ASOCIACION POPULAR DE AHORROS Y PRESTAMOS
* Developed By : Pradeep M
* Program Name : REDO.NOF.FT.TT.REINVEST
*-----------------------------------------------------------------------------
* Description :Bulit routine to assign value to set variable.
* Linked with :
* In Parameter :
* Out Parameter :
*
**DATE           ODR                   DEVELOPER               VERSION
* 10-11-2011    N11 ISSUES              RIYAS
* 13-APRIL-2023      Harsha                R22 Auto Conversion  - FM to @FM , VM to @VM
* 13-APRIL-2023      Harsha                R22 Manual Conversion - No changes
*-----------------------------------------------------------------------------
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_ENQUIRY.COMMON
    $INSERT I_F.FUNDS.TRANSFER
    $INSERT I_F.TELLER

    GOSUB OPEN.PROCESS
    GOSUB PROCESS

RETURN

OPEN.PROCESS:
*-----------

    FN.FUNDS.TRANSFER.NAU ='F.FUNDS.TRANSFER$NAU'
    F.FUNDS.TRANSFER.NAU =''
    CALL OPF(FN.FUNDS.TRANSFER.NAU,F.FUNDS.TRANSFER.NAU)

    FN.TELLER.NAU ='F.TELLER$NAU'
    F.TELLER.NAU =''
    CALL OPF(FN.TELLER.NAU,F.TELLER.NAU)

    LF.APP = 'TELLER':@FM:'FUNDS.TRANSFER'
    LF.FLD = 'L.COMMENTS':@VM:'L.TT.AZ.ACC.REF':@FM:'L.COMMENTS':@VM:'L.FT.AZ.ACC.REF'
    LF.POS = ''
    CALL MULTI.GET.LOC.REF(LF.APP,LF.FLD,LF.POS)
    LOC.TT.COMMENTS   = LF.POS<1,1>
    LOC.TT.AZ.ACC.REF = LF.POS<1,2>
    LOC.FT.COMMENTS   = LF.POS<2,1>
    LOC.FT.AZ.ACC.REF = LF.POS<2,2>
RETURN

PROCESS:
*-------
    SEL.LIST=''
    NO.OF.REC=''
    ERR.FT=''
    SEL.LIST1=''
    NO.OF.REC1=''
    ERR.TT=''

    SEL.CMD="SELECT ":FN.FUNDS.TRANSFER.NAU:" WITH RECORD.STATUS EQ ":'INAU':" AND L.COMMENTS LIKE ":'TT':"..."
    SEL.CMD1="SELECT ":FN.TELLER.NAU:" WITH RECORD.STATUS EQ ":'INAU':" AND L.COMMENTS LIKE ":'TT':"..."
    LOCATE "TRANS.ID" IN D.FIELDS<1> SETTING VAL.POS THEN
        Y.TRANS.ID = D.RANGE.AND.VALUE<VAL.POS>
    END
    LOCATE "AZ.REFERENCE" IN D.FIELDS<1> SETTING REF.POS THEN
        Y.AZ.REFERENCE  = D.RANGE.AND.VALUE<REF.POS>
    END
    IF Y.TRANS.ID THEN
        SEL.CMD="SELECT ":FN.FUNDS.TRANSFER.NAU:" WITH @ID EQ ":Y.TRANS.ID:" AND RECORD.STATUS EQ ":'INAU':" AND L.COMMENTS LIKE ":'TT':"..."
        SEL.CMD1="SELECT ":FN.TELLER.NAU:" WITH @ID EQ ":Y.TRANS.ID:" AND RECORD.STATUS EQ ":'INAU':" AND L.COMMENTS LIKE ":'TT':"..."
    END

    IF Y.AZ.REFERENCE THEN
        SEL.CMD="SELECT ":FN.FUNDS.TRANSFER.NAU:" WITH L.FT.AZ.ACC.REF EQ ":Y.AZ.REFERENCE:" AND RECORD.STATUS EQ ":'INAU':" AND L.COMMENTS LIKE ":'TT':"..."
        SEL.CMD1="SELECT ":FN.TELLER.NAU:" WITH L.TT.AZ.ACC.REF EQ ":Y.AZ.REFERENCE:" AND RECORD.STATUS EQ ":'INAU':" AND L.COMMENTS LIKE ":'TT':"..."
    END

    IF Y.TRANS.ID AND Y.AZ.REFERENCE THEN
        SEL.CMD="SELECT ":FN.FUNDS.TRANSFER.NAU:" WITH @ID EQ ":Y.TRANS.ID:" AND  L.FT.AZ.ACC.REF EQ ":Y.AZ.REFERENCE:" AND RECORD.STATUS EQ ":'INAU':" AND L.COMMENTS LIKE ":'TT':"..."
        SEL.CMD1="SELECT ":FN.TELLER.NAU:" WITH @ID EQ ":Y.TRANS.ID:" AND L.TT.AZ.ACC.REF EQ ":Y.AZ.REFERENCE:" AND RECORD.STATUS EQ ":'INAU':" AND L.COMMENTS LIKE ":'TT':"..."
    END

    CALL EB.READLIST(SEL.CMD,SEL.LIST,'',NO.OF.REC,ERR.FT)
    CALL EB.READLIST(SEL.CMD1,SEL.LIST1,'',NO.OF.REC1,ERR.TT)
    Y.DATA<-1>=SEL.LIST
    Y.DATA<-1>=SEL.LIST1

    LOOP
        REMOVE Y.TRANSACTION.ID FROM Y.DATA SETTING Y.TRAN.POS
    WHILE Y.TRANSACTION.ID : Y.TRAN.POS

        CALL F.READ(FN.TELLER.NAU,Y.TRANSACTION.ID,R.TELLER,F.TELLER.NAU,TELLER.ERR)
        IF R.TELLER THEN
            Y.AZ.REF = R.TELLER<TT.TE.LOCAL.REF,LOC.TT.AZ.ACC.REF>
            Y.AMOUNT.LOCAL = R.TELLER<TT.TE.AMOUNT.LOCAL.1>
            Y.COMMENTS = R.TELLER<TT.TE.LOCAL.REF,LOC.TT.COMMENTS>
        END ELSE
            CALL F.READ(FN.FUNDS.TRANSFER.NAU,Y.TRANSACTION.ID,R.FUNDS.TRANSFER,F.FUNDS.TRANSFER.NAU,FT.ERR)
            Y.AZ.REF = R.FUNDS.TRANSFER<FT.LOCAL.REF,LOC.FT.AZ.ACC.REF>
            Y.AMOUNT.LOCAL = R.FUNDS.TRANSFER<FT.DEBIT.AMOUNT>
            Y.COMMENTS = R.FUNDS.TRANSFER<FT.LOCAL.REF,LOC.FT.COMMENTS>
        END
        IF NOT(Y.AZ.REF) THEN
            Y.AZ.REF = ' '
        END
        Y.TXN.ID = Y.COMMENTS[14,2]
        BEGIN CASE
            CASE Y.TXN.ID EQ 'TT'
                Y.PGM.VERSION = "TELLER,REINV.WDL"
            CASE Y.TXN.ID EQ 'FT'
                Y.PGM.VERSION = "FUNDS.TRANSFER,REINV.WDL"
            CASE Y.TXN.ID EQ 'NG'
                Y.PGM.VERSION = "FUNDS.TRANSFER,CHQ.OTHERS.DEPOSIT"
            CASE Y.TXN.ID EQ 'GO'
                Y.PGM.VERSION = "FUNDS.TRANSFER,CHQ.NO.TAX.DEPOSIT"
            CASE Y.TXN.ID EQ 'GT'
                Y.PGM.VERSION = "FUNDS.TRANSFER,CHQ.GOVT.WITH.TAX"
        END CASE
        ENQ.DATA<-1> = Y.TRANSACTION.ID:"*":Y.AZ.REF:"*":Y.AMOUNT.LOCAL:"*":Y.PGM.VERSION
        Y.TRANSACTION.ID = ''; Y.AZ.REF = ''; Y.AMOUNT.LOCAL = ''; Y.COMMENTS = '';Y.PGM.VERSION = ''
    REPEAT
RETURN

END
