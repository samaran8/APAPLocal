$PACKAGE APAP.LAPAP
SUBROUTINE E.NOFILE.REDO.RTE.TXNS.INAU.INAO(Y.FINAL)
*-----------------------------------------------------------

*-----------------------------------------------------------
* Input  Arg: N/A
* Output Arg: N/A
* Deals With: Nofile routine to show all the INAU and INAO transactions of FT and TT
*-----------------------------------------------------------------------------------
* Who           Date           Dev Ref                   Modification
* APAP          23 Mar 2017    RTE INAU INAO Report      Initial Draft
* Date                  who                   Reference              
* 21-04-2023         CONVERSTION TOOL      R22 AUTO CONVERSTION -$INSERT T24.BP TO $INSERT AND F.READ TO CACHE.READ AND REMOVED FV.FTTC AND  FV.COM AND FV.TT
* 21-04-2023          ANIL KUMAR B         R22 MANUAL CONVERSTION -NO CHANGES
*-----------------------------------------------------------------------------------

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.FUNDS.TRANSFER
    $INSERT I_F.FT.TXN.TYPE.CONDITION
    $INSERT I_F.COMPANY
    $INSERT I_F.TELLER.TRANSACTION
    $INSERT I_F.TELLER
    $INSERT I_ENQUIRY.COMMON
*PARA ABRIR EL ACHIVO DE FUNDS.TRANSFER$NAU
    FN.FT = "F.FUNDS.TRANSFER$NAU"
    FV.FT = ""
    CALL OPF(FN.FT, FV.FT)

*PARA ABRIR EL ACHIVO DE FT.TXN.TYPE.CONDITION
    FN.FTTC = "F.FT.TXN.TYPE.CONDITION"
    FV.FTTC = ""
    CALL OPF(FN.FTTC, FV.FTTC)

    FN.TT.TRANS = "F.TELLER.TRANSACTION"
    FV.TT.TRANS = ""
    CALL OPF(FN.TT.TRANS,FV.TT.TRANS)

*PARA ABRIR EL ACHIVO DE COMPANY
    FN.COM = "F.COMPANY"
    FV.COM = ""
    CALL OPF(FN.COM, FV.COM)

    FN.TT = "F.TELLER$NAU"
    FV.TT = ""
    CALL OPF(FN.TT,FV.TT)

    R.FT = ""
    FT.ERR = ""

    R.FTTC = ""
    FTTC.ERR = ""

    R.COM = ""
    COM.ERR = ""

    SEL.LIST = ""
    NO.OF.REC = ""
    SEL.ERR = ""

    LOCATE "DEBIT.ACCOUNT" IN D.FIELDS<1> SETTING DB.POS THEN
        Y.DEBIT.ACCOUNT = D.RANGE.AND.VALUE <DB.POS>
    END
    LOCATE "CREDIT.ACCOUNT" IN D.FIELDS<1> SETTING CR.POS THEN
        Y.CREDIT.ACCOUNT = D.RANGE.AND.VALUE <CR.POS>
    END

    BEGIN CASE

        CASE Y.DEBIT.ACCOUNT NE ''
            SEL.CMD.FT = "SELECT " : FN.FT : " WITH PROCESSING.DATE EQ " : TODAY :" AND DEBIT.ACCT.NO EQ " :Y.DEBIT.ACCOUNT
        CASE Y.CREDIT.ACCOUNT NE ''
            SEL.CMD.FT = "SELECT " : FN.FT : " WITH PROCESSING.DATE EQ " : TODAY :" AND CREDIT.ACCT.NO EQ " :Y.CREDIT.ACCOUNT
        CASE Y.DEBIT.ACCOUNT EQ '' AND Y.CREDIT.ACCOUNT EQ ''
            SEL.CMD.FT = "SELECT " : FN.FT : " WITH PROCESSING.DATE EQ " : TODAY
    END CASE

* REALIZAMOS LA LLAMADA
    CALL EB.READLIST(SEL.CMD.FT, SEL.LIST.FT,"", NO.OF.REC.FT, SEL.ERR.FT)
    LOOP
        REMOVE Y.FT.ID FROM SEL.LIST.FT SETTING FT.POS
    WHILE Y.FT.ID DO
        CALL F.READ(FN.FT,Y.FT.ID,R.FT, FV.FT, FT.ERR)
        Y.DB.ACCT.NO = R.FT<FT.DEBIT.ACCT.NO>
        Y.CR.ACCT.NO = R.FT<FT.CREDIT.ACCT.NO>
        IF Y.DB.ACCT.NO[1,3] MATCHES '3A' AND Y.CR.ACCT.NO[1,3] MATCHES '3A' THEN
            CONTINUE
        END
*        IF NOT(NUM(Y.DB.ACCT.NO)) AND NOT(NUM(Y.CR.ACCT.NO)) THEN
        Y.TRX.TYPE = R.FT<FT.TRANSACTION.TYPE>

        Y.VAL.DATE = R.FT<FT.DEBIT.VALUE.DATE>
        Y.CR.AMOUNT = R.FT<FT.CREDIT.AMOUNT>
        IF Y.CR.AMOUNT EQ '' THEN
            CONTINUE
        END
        Y.DATE.TIME = R.FT<FT.DATE.TIME>
        Y.TIME = Y.DATE.TIME[7,4]
        Y.PR.DATE = R.FT<FT.PROCESSING.DATE>
        Y.CO.CODE = R.FT<FT.CO.CODE>
        Y.INPUTTER = FIELD(R.FT<FT.INPUTTER>,"_",2)
        Y.AUTHO = FIELD(R.FT<FT.AUTHORISER>,"_",2)

        CALL CACHE.READ(FN.FTTC, Y.TRX.TYPE, R.FTTC, FTTC.ERR) ;*R22 AUTO CONVERSTION F.READ TO CACHE.READ AND REMOVED FV.FTTC 
        Y.TYPE.DESC = R.FTTC<FT6.DESCRIPTION>

        CALL CACHE.READ(FN.COM, Y.CO.CODE, R.COM, COM.ERR) ;*R22 AUTO CONVERSTION F.READ TO CACHE.READ AND REMOVED FV.COM
        Y.COM.NAME = R.COM<EB.COM.COMPANY.NAME>
        Y.RECORD.STATUS = R.FT<FT.RECORD.STATUS>
        Y.FINAL<-1> = Y.FT.ID:"|":Y.CO.CODE:"|":Y.DB.ACCT.NO:"|":Y.TYPE.DESC:"|":Y.CR.AMOUNT:"|":Y.CR.ACCT.NO:"|":Y.TIME:"|":Y.PR.DATE:"|":Y.COM.NAME:"|":Y.INPUTTER:"|":Y.RECORD.STATUS

    REPEAT

    SEL.CMD.TT = "SELECT " : FN.TT : " WITH VALUE.DATE.1 EQ " : TODAY

    CALL EB.READLIST(SEL.CMD.TT, SEL.LIST.TT,"", NO.OF.REC.TT, SEL.ERR.TT)
    LOOP
        REMOVE Y.TT.ID FROM SEL.LIST.TT SETTING TT.POS
    WHILE Y.TT.ID DO
        CALL F.READ(FN.TT,Y.TT.ID,R.TT, FV.TT, TT.ERR)
        Y.TRX.TYPE = R.TT<TT.TE.TRANSACTION.CODE>
        Y.CR.DR.MARKER = R.TT<TT.TE.DR.CR.MARKER>
        BEGIN CASE
            CASE Y.CR.DR.MARKER EQ 'DEBIT'
                Y.DB.ACCT.NO = R.TT<TT.TE.ACCOUNT.1>
                Y.VAL.DATE = R.TT<TT.TE.VALUE.DATE.1>
                Y.CR.AMOUNT = R.TT<TT.TE.AMOUNT.LOCAL.2>
                Y.CR.ACCT.NO = R.TT<TT.TE.ACCOUNT.2>
            CASE Y.CR.DR.MARKER EQ 'CREDIT'
                Y.DB.ACCT.NO = R.TT<TT.TE.ACCOUNT.2>
                Y.VAL.DATE = R.TT<TT.TE.VALUE.DATE.2>
                Y.CR.AMOUNT = R.TT<TT.TE.AMOUNT.LOCAL.1>
                Y.CR.ACCT.NO = R.TT<TT.TE.ACCOUNT.1>
        END CASE

        IF Y.DB.ACCT.NO[1,3] MATCHES '3A' AND Y.CR.ACCT.NO[1,3] MATCHES '3A' THEN
            CONTINUE
        END
        IF Y.DB.ACCT.NO EQ Y.DEBIT.ACCOUNT OR Y.CR.ACCT.NO EQ Y.CREDIT.ACCOUNT OR (Y.DEBIT.ACCOUNT EQ '' AND Y.CREDIT.ACCOUNT EQ '') THEN
            Y.PR.DATE = R.TT<TT.TE.VALUE.DATE.1>
            Y.DATE.TIME = R.TT<TT.TE.DATE.TIME>
            Y.TIME = Y.DATE.TIME[7,4]
            Y.CO.CODE = R.TT<TT.TE.CO.CODE>
            Y.INPUTTER = FIELD(R.TT<TT.TE.INPUTTER>,"_",2)
            Y.AUTHO = FIELD(R.TT<TT.TE.AUTHORISER>,"_",2)
            CALL CACHE.READ(FN.TT.TRANS, Y.TRX.TYPE, R.TT.TRANS, TT.TRANS.ERR) ;*R22 AUTO CONVERSTION F.READ TO CACHE.READ AND REMOVED FV.TT
            Y.TYPE.DESC = R.TT.TRANS<TT.TR.SHORT.DESC>
            CALL CACHE.READ(FN.COM, Y.CO.CODE, R.COM, COM.ERR)  ;*R22 AUTO CONVERSTION F.READ TO CACHE.READ AND REMOVED FV.COM
            Y.COM.NAME = R.COM<EB.COM.COMPANY.NAME>
            Y.RECORD.STATUS = R.TT<TT.TE.RECORD.STATUS>
            Y.FINAL<-1> = Y.TT.ID:"|":Y.CO.CODE:"|":Y.DB.ACCT.NO:"|":Y.TYPE.DESC:"|":Y.CR.AMOUNT:"|":Y.CR.ACCT.NO:"|":Y.TIME:"|":Y.PR.DATE:"|":Y.COM.NAME:"|":Y.INPUTTER:"|":Y.RECORD.STATUS
        END

    REPEAT

END
