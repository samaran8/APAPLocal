* @ValidationCode : MjoxMTg1NDk3ODcxOkNwMTI1MjoxNjgxMjc3MzE5NDgxOklUU1M6LTE6LTE6MDowOmZhbHNlOk4vQTpSMjFfQU1SLjA6LTE6LTE=
* @ValidationInfo : Timestamp         : 12 Apr 2023 10:58:39
* @ValidationInfo : Encoding          : Cp1252
* @ValidationInfo : User Name         : ITSS
* @ValidationInfo : Nb tests success  : N/A
* @ValidationInfo : Nb tests failure  : N/A
* @ValidationInfo : Rating            : N/A
* @ValidationInfo : Coverage          : N/A
* @ValidationInfo : Strict flag       : N/A
* @ValidationInfo : Bypass GateKeeper : false
* @ValidationInfo : Compiler Version  : R21_AMR.0
* @ValidationInfo : Copyright Temenos Headquarters SA 1993-2021. All rights reserved.
$PACKAGE APAP.REDOBATCH
SUBROUTINE REDO.B.LY.POINT.GEN.PRE
*-------------------------------------------------------------------------------------------------
*DESCRIPTION:
*  This routine is attached to the batch record BNK/REDO.B.LY.POINT.GEN
*  This routine selects the necesary records from various files and updates it in temporary file
* ------------------------------------------------------------------------------------------------
* Input/Output:
*--------------
* IN  : -NA-
* OUT : -NA-
*
* Dependencies:
*---------------
* CALLS     : -NA-
* CALLED BY : -NA-
*
* Revision History:
*------------------
*   Date               who           Reference            Description
* 17-JUN-2013       RMONDRAGON   ODR-2011-06-0243      Initial Creation
* Date                   who                   Reference              
* 12-04-2023         CONVERSTION TOOL     R22 AUTO CONVERSTION -  VM TO @VM AND ++ TO += 1 
* 12-04-2023          ANIL KUMAR B        R22 MANUAL CONVERSTION -NO CHANGES
*-------------------------------------------------------------------------------------------------

    $INSERT I_COMMON
    $INSERT I_EQUATE

    $INSERT I_F.REDO.LY.MODALITY
    $INSERT I_F.REDO.LY.PDT.TYPE
    $INSERT I_F.REDO.LY.PROGRAM
    $INSERT I_REDO.B.LY.POINT.GEN.COMMON

    GOSUB INIT
    GOSUB OPEN.FILE
    GOSUB PROCESS.REDO.LY.MOD
    GOSUB WRITE.REDO.LY.PRG

RETURN

*----
INIT:
*----

    PRG.MOD.LST = ''         ; PRG.PER.MOD = '';
    PRG.LST = ''             ; PRG.ST.DATE.LST = '';
    PRG.END.DATE.LST = ''    ; PRG.DAYS.EXP.LST = '';
    PRG.EXP.DATE.LST = ''    ; PRG.CUS.GRP.LST = '';
    PRG.POINT.VALUE.LST = '' ; PRG.AVAIL.IF.DELAY.LST = '';
    PRG.POINT.USE.LST = ''   ; PRG.MOD.TYP = '';
    PRG.MOD.EVE = ''         ; PRG.PTS.IN.MOD = '';
    PRG.PROD.LST = ''        ; PRG.COND.TYPE.EXINC = '';
    PRG.APP.EXC.COND = ''    ; PRG.EXC.EST.ACCT = '';
    PRG.APP.INC.COND = ''    ; PRG.INC.EST.ACCT = '';
    PRG.AIR.LST = ''         ; PRG.MINGEN.IN.MOD = '';
    PRG.MAXGEN.IN.MOD = ''   ; PRG.FORM.GEN.IN.MOD = '';
    PRG.GEN.AMT.IN.MOD = ''  ; PRG.CCY.IN.MOD = '';
    PRG.RECSEL = ''

RETURN

*---------
OPEN.FILE:
*---------

    FN.REDO.LY.MODALITY = 'F.REDO.LY.MODALITY'
    F.REDO.LY.MODALITY = ''
    CALL OPF(FN.REDO.LY.MODALITY,F.REDO.LY.MODALITY)

    FN.REDO.LY.PDT.TYPE = 'F.REDO.LY.PDT.TYPE'
    F.REDO.LY.PDT.TYPE = ''
    CALL OPF(FN.REDO.LY.PDT.TYPE,F.REDO.LY.PDT.TYPE)

    FN.REDO.LY.PROGRAM = 'F.REDO.LY.PROGRAM'
    F.REDO.LY.PROGRAM = ''
    CALL OPF(FN.REDO.LY.PROGRAM,F.REDO.LY.PROGRAM)

    FN.TEMP.LY.POINT.GEN = 'F.TEMP.LY.POINT.GEN'
    F.TEMP.LY.POINT.GEN = ''
    OPEN FN.TEMP.LY.POINT.GEN TO F.TEMP.LY.POINT.GEN ELSE

        TEXT = 'Error in opening : ':FN.TEMP.LY.POINT.GEN
        CALL FATAL.ERROR('REDO.B.LY.POINT.GEN.PRE')
    END
    CLEARFILE F.TEMP.LY.POINT.GEN

RETURN

*-------------------
PROCESS.REDO.LY.MOD:
*-------------------

    SEL.MOD = ''
    SEL.MOD =  'SSELECT ':FN.REDO.LY.MODALITY:' WITH (TYPE EQ 2) OR (TYPE EQ 3) OR (TYPE EQ 4) OR (TYPE EQ 5) OR (TYPE EQ 8) '
    SEL.MOD := 'OR (TYPE EQ 6 AND (EVENT EQ 1 OR EVENT EQ 2 OR EVENT EQ 3))'
    MOD.LST = ''; NO.OF.MOD = 0; MOD.ERR = ''
    CALL EB.READLIST(SEL.MOD,MOD.LST,'',NO.OF.MOD,MOD.ERR)

    IF NO.OF.MOD NE 0 THEN
        PRG.RECSEL = 'Y'
    END ELSE
        PRG.RECSEL = 'N'
    END

    MOD.CNT = 1
    Y.SEARCH.PARAM = ''
    LOOP
    WHILE MOD.CNT LE NO.OF.MOD
        Y.MOD.SPEC = MOD.LST<MOD.CNT>
        PRG.MOD.LST<MOD.CNT> = Y.MOD.SPEC
        R.MOD = ''; MOD.ERR = ''
        CALL F.READ(FN.REDO.LY.MODALITY,Y.MOD.SPEC,R.MOD,F.REDO.LY.MODALITY,MOD.ERR)
        IF R.MOD THEN
            MOD.TYP = R.MOD<REDO.MOD.TYPE>
            MOD.EVE = R.MOD<REDO.MOD.EVENT>
            PRD.GRP = R.MOD<REDO.MOD.PRODUCT.GROUP>
            FORM.GEN = R.MOD<REDO.MOD.FORM.GENERATION>
            MOD.CCY = R.MOD<REDO.MOD.CURRENCY>
            IF MOD.CCY EQ '' THEN
                MOD.CCY = 'DOP'
            END
            GEN.AMT = R.MOD<REDO.MOD.GEN.AMT>
            GEN.PTS = R.MOD<REDO.MOD.GEN.POINTS>
            GOSUB GET.LY.PRODUCT.BY.GROUP
            IF FORM.GEN EQ '1' THEN
                GOSUB DEF.GEN.LIM.WHOLE
            END ELSE
                GOSUB DEF.GEN.LIM.FACTOR
            END
        END
        GOSUB PROCESS.REDO.LY.PRG
        MOD.CNT += 1
    REPEAT

RETURN

*-------------------
PROCESS.REDO.LY.PRG:
*-------------------

    SEL.PRG = 'SELECT ':FN.REDO.LY.PROGRAM:' WITH MODALITY EQ ':Y.MOD.SPEC:' AND STATUS EQ "Activo"'
    PROGRAM.LST = ''
    CALL EB.READLIST(SEL.PRG,PROGRAM.LST,'',NO.OF.PRG,PRG.ERR)

    PRG.PER.MOD<MOD.CNT> = NO.OF.PRG

    PRG.ID.CNT = 0
    LOOP
        REMOVE PRG.ID FROM PROGRAM.LST SETTING PRG.ID.POS
    WHILE PRG.ID:PRG.ID.POS
        PRG.ID.CNT += 1
        CALL F.READ(FN.REDO.LY.PROGRAM,PRG.ID,R.REDO.LY.PROGRAM,F.REDO.LY.PROGRAM,PRG.ERR)
        IF R.REDO.LY.PROGRAM THEN
            PRG.LST<MOD.CNT,PRG.ID.CNT> = PRG.ID
            PRG.ST.DATE.LST<MOD.CNT,PRG.ID.CNT> = R.REDO.LY.PROGRAM<REDO.PROG.START.DATE>
            PRG.END.DATE.LST<MOD.CNT,PRG.ID.CNT> = R.REDO.LY.PROGRAM<REDO.PROG.END.DATE>
            PRG.DAYS.EXP.LST<MOD.CNT,PRG.ID.CNT> = R.REDO.LY.PROGRAM<REDO.PROG.DAYS.EXP>
            PRG.EXP.DATE.LST<MOD.CNT,PRG.ID.CNT> = R.REDO.LY.PROGRAM<REDO.PROG.EXP.DATE>
            PRG.CUS.GRP.LST<MOD.CNT,PRG.ID.CNT> = R.REDO.LY.PROGRAM<REDO.PROG.GROUP.CUS>
            PRG.POINT.VALUE.LST<MOD.CNT,PRG.ID.CNT> = R.REDO.LY.PROGRAM<REDO.PROG.POINT.VALUE>
            PRG.AVAIL.IF.DELAY.LST<MOD.CNT,PRG.ID.CNT> = R.REDO.LY.PROGRAM<REDO.PROG.AVAIL.IF.DELAY>
            PRG.POINT.USE.LST<MOD.CNT,PRG.ID.CNT> = R.REDO.LY.PROGRAM<REDO.PROG.POINT.USE>
            PRG.COND.TYPE.EXINC<MOD.CNT,PRG.ID.CNT> = R.REDO.LY.PROGRAM<REDO.PROG.COND.TYPE.EXINC>
            PRG.APP.EXC.COND<MOD.CNT,PRG.ID.CNT> = R.REDO.LY.PROGRAM<REDO.PROG.APP.EXC.COND>
            Y.TEMP.EXC.EST.ACCT = R.REDO.LY.PROGRAM<REDO.PROG.EXC.EST.ACCT>
            CHANGE @VM TO '*' IN Y.TEMP.EXC.EST.ACCT
            PRG.EXC.EST.ACCT<MOD.CNT,PRG.ID.CNT> = Y.TEMP.EXC.EST.ACCT
            PRG.APP.INC.COND<MOD.CNT,PRG.ID.CNT> = R.REDO.LY.PROGRAM<REDO.PROG.APP.INC.COND>
            Y.TEMP.INC.EST.ACCT = R.REDO.LY.PROGRAM<REDO.PROG.INC.EST.ACCT>
            CHANGE @VM TO '*' IN Y.TEMP.INC.EST.ACCT
            PRG.INC.EST.ACCT<MOD.CNT,PRG.ID.CNT> = Y.TEMP.INC.EST.ACCT
            PRG.AIR.LST<MOD.CNT,PRG.ID.CNT> = R.REDO.LY.PROGRAM<REDO.PROG.AIRL.PROG>

            PRG.MOD.TYP<MOD.CNT,PRG.ID.CNT> = MOD.TYP
            PRG.MOD.EVE<MOD.CNT,PRG.ID.CNT> = MOD.EVE
            PRG.PTS.IN.MOD<MOD.CNT,PRG.ID.CNT> = GEN.PTS
            PRG.PROD.LST<MOD.CNT,PRG.ID.CNT> = PROD.LST
            PRG.MINGEN.IN.MOD<MOD.CNT,PRG.ID.CNT> = MIN.GEN
            PRG.MAXGEN.IN.MOD<MOD.CNT,PRG.ID.CNT> = MAX.GEN
            PRG.FORM.GEN.IN.MOD<MOD.CNT,PRG.ID.CNT> = FORM.GEN
            PRG.GEN.AMT.IN.MOD<MOD.CNT,PRG.ID.CNT> = GEN.AMT
            PRG.CCY.IN.MOD<MOD.CNT,PRG.ID.CNT> = MOD.CCY
        END
    REPEAT

RETURN

*-----------------------
GET.LY.PRODUCT.BY.GROUP:
*-----------------------

    CALL F.READ(FN.REDO.LY.PDT.TYPE,PRD.GRP,R.PDT.TYPE,F.REDO.LY.PDT.TYPE,PDT.TYPE.ERR)
    IF R.PDT.TYPE THEN
        PROD.LST = R.PDT.TYPE<REDO.PDT.PRODUCT>
        CHANGE @VM TO '*' IN PROD.LST
    END

RETURN

*-----------------
DEF.GEN.LIM.WHOLE:
*-----------------

    IF GEN.AMT EQ 'Interes' THEN
        GEN.PTS = R.MOD<REDO.MOD.INT.GEN.POINTS>
        MIN.GEN = R.MOD<REDO.MOD.INT.LOW.LIM.AMT>
        MAX.GEN = R.MOD<REDO.MOD.INT.UP.LIM.AMT>
    END ELSE
        GEN.PTS = R.MOD<REDO.MOD.GEN.POINTS>
        MIN.GEN = R.MOD<REDO.MOD.LOW.LIM.AMT>
        MAX.GEN = R.MOD<REDO.MOD.UP.LIM.AMT>
    END
    CHANGE @VM TO '*' IN GEN.PTS
    CHANGE @VM TO '*' IN MIN.GEN
    CHANGE @VM TO '*' IN MAX.GEN

RETURN

*------------------
DEF.GEN.LIM.FACTOR:
*------------------

    IF GEN.AMT EQ 'Interes' THEN
        GEN.PTS = R.MOD<REDO.MOD.INT.GEN.FACTOR>
        MIN.GEN = R.MOD<REDO.MOD.INT.MIN.GEN>
        MAX.GEN = R.MOD<REDO.MOD.INT.MAX.GEN>
    END ELSE
        GEN.PTS = R.MOD<REDO.MOD.GEN.FACTOR>
        MIN.GEN = R.MOD<REDO.MOD.MIN.GEN>
        MAX.GEN = R.MOD<REDO.MOD.MAX.GEN>
    END

RETURN

*-----------------
WRITE.REDO.LY.PRG:
*-----------------


*  WRITE PRG.MOD.LST TO F.TEMP.LY.POINT.GEN,'MOD' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'MOD',PRG.MOD.LST);*Tus End

*  WRITE PRG.PER.MOD TO F.TEMP.LY.POINT.GEN,'NOPRG' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'NOPRG',PRG.PER.MOD);*Tus End

*  WRITE PRG.LST TO F.TEMP.LY.POINT.GEN,'PRG' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'PRG',PRG.LST);*Tus End

*  WRITE PRG.ST.DATE.LST TO F.TEMP.LY.POINT.GEN,'ST.DATE' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'ST.DATE',PRG.ST.DATE.LST);* Tus End

*  WRITE PRG.END.DATE.LST TO F.TEMP.LY.POINT.GEN,'END.DATE' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'END.DATE',PRG.END.DATE.LST);* Tus End

*  WRITE PRG.DAYS.EXP.LST TO F.TEMP.LY.POINT.GEN,'DAYS.EXP' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'DAYS.EXP',PRG.DAYS.EXP.LST);* Tus End

*  WRITE PRG.EXP.DATE.LST TO F.TEMP.LY.POINT.GEN,'EXP.DATE' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'EXP.DATE',PRG.EXP.DATE.LST);* Tus End

*  WRITE PRG.CUS.GRP.LST TO F.TEMP.LY.POINT.GEN,'CUS.GROUP' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'CUS.GROUP',PRG.CUS.GRP.LST);* Tus End

*  WRITE PRG.POINT.VALUE.LST TO F.TEMP.LY.POINT.GEN,'POINT.VALUE' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'POINT.VALUE',PRG.POINT.VALUE.LST) ; * Tus End

*  WRITE PRG.AVAIL.IF.DELAY.LST TO F.TEMP.LY.POINT.GEN,'AVAIL.IF.DELAY' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'AVAIL.IF.DELAY',PRG.AVAIL.IF.DELAY.LST); * Tus End

*  WRITE PRG.POINT.USE.LST TO F.TEMP.LY.POINT.GEN,'POINT.USE' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'POINT.USE',PRG.POINT.USE.LST); * Tus End

*  WRITE PRG.COND.TYPE.EXINC TO F.TEMP.LY.POINT.GEN,'COND.TYPE.EXINC' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'COND.TYPE.EXINC',PRG.COND.TYPE.EXINC); *Tus End

*  WRITE PRG.APP.EXC.COND TO F.TEMP.LY.POINT.GEN,'APP.EXC.COND' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'APP.EXC.COND',PRG.APP.EXC.COND); * Tus End

*  WRITE PRG.EXC.EST.ACCT TO F.TEMP.LY.POINT.GEN,'EXC.EST.ACCT' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'EXC.EST.ACCT',PRG.EXC.EST.ACCT);* Tus End

*  WRITE PRG.APP.INC.COND TO F.TEMP.LY.POINT.GEN,'APP.INC.COND' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'APP.INC.COND',PRG.APP.INC.COND); * Tus End

*  WRITE PRG.INC.EST.ACCT TO F.TEMP.LY.POINT.GEN,'INC.EST.ACCT' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'INC.EST.ACCT',PRG.INC.EST.ACCT) ;* Tus End

*  WRITE PRG.AIR.LST TO F.TEMP.LY.POINT.GEN,'AIR' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'AIR',PRG.AIR.LST) ;*Tus  End


*  WRITE PRG.MOD.TYP TO F.TEMP.LY.POINT.GEN,'MOD.TYPE' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'MOD.TYPE',PRG.MOD.TYP) ;* Tus End

*  WRITE PRG.MOD.EVE TO F.TEMP.LY.POINT.GEN,'MOD.EVENT' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'MOD.EVENT',PRG.MOD.EVE);* Tus End

*  WRITE PRG.PTS.IN.MOD TO F.TEMP.LY.POINT.GEN,'PTS.IN.MOD' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'PTS.IN.MOD',PRG.PTS.IN.MOD) ;* Tus End

*  WRITE PRG.PROD.LST TO F.TEMP.LY.POINT.GEN,'PROD' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'PROD',PRG.PROD.LST);* Tus End

*  WRITE PRG.MINGEN.IN.MOD TO F.TEMP.LY.POINT.GEN,'MIN.GEN.IN.MOD' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'MIN.GEN.IN.MOD',PRG.MINGEN.IN.MOD);* Tus End

*  WRITE PRG.MAXGEN.IN.MOD TO F.TEMP.LY.POINT.GEN,'MAX.GEN.IN.MOD' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'MAX.GEN.IN.MOD',PRG.MAXGEN.IN.MOD) ;* Tus End

*  WRITE PRG.FORM.GEN.IN.MOD TO F.TEMP.LY.POINT.GEN,'FORM.GEN.IN.MOD' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'FORM.GEN.IN.MOD',PRG.FORM.GEN.IN.MOD) ;* Tus End

*  WRITE PRG.GEN.AMT.IN.MOD TO F.TEMP.LY.POINT.GEN,'GEN.AMT.IN.MOD' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'GEN.AMT.IN.MOD',PRG.GEN.AMT.IN.MOD) ;*Tus End

*  WRITE PRG.CCY.IN.MOD TO F.TEMP.LY.POINT.GEN,'CCY.IN.MOD' ;*Tus Start
    CALL F.WRITE(FN.TEMP.LY.POINT.GEN,'CCY.IN.MOD',PRG.CCY.IN.MOD) ;*Tus End

RETURN

END
