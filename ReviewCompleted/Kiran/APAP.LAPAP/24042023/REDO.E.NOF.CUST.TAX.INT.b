* @ValidationCode : MjotMzgyMjM2NDE5OkNwMTI1MjoxNjgyMDY4NjUzOTM2OjMzM3N1Oi0xOi0xOjA6MDpmYWxzZTpOL0E6UjIxX0FNUi4wOi0xOi0x
* @ValidationInfo : Timestamp         : 21 Apr 2023 14:47:33
* @ValidationInfo : Encoding          : Cp1252
* @ValidationInfo : User Name         : 333su
* @ValidationInfo : Nb tests success  : N/A
* @ValidationInfo : Nb tests failure  : N/A
* @ValidationInfo : Rating            : N/A
* @ValidationInfo : Coverage          : N/A
* @ValidationInfo : Strict flag       : N/A
* @ValidationInfo : Bypass GateKeeper : false
* @ValidationInfo : Compiler Version  : R21_AMR.0
* @ValidationInfo : Copyright Temenos Headquarters SA 1993-2021. All rights reserved.
$PACKAGE APAP.LAPAP
*-----------------------------------------------------------------------------------
*Modification History:
*DATE                 WHO                  REFERENCE                     DESCRIPTION
*21/04/2023      CONVERSION TOOL     AUTO R22 CODE CONVERSION      FM TO @FM, VM TO @VM, SM TO @SM,++ TO +=, F.READ TO CACHE.READ,INCLUDE TO INSERT
*21/04/2023         SURESH           MANUAL R22 CODE CONVERSION           NOCHANGE
*-----------------------------------------------------------------------------------
SUBROUTINE REDO.E.NOF.CUST.TAX.INT(YARRY.VAL)
*---------------------------------------------------------------------------------------------------------------
*  Company   Name    : Asociacion Popular de Ahorros y Prestamos
*  Developed By      : V.P.Ashokkumar
*  Program   Name    : REDO.E.NOF.CUST.TAX.INT
*---------------------------------------------------------------------------------------------------------------
* Incoming/Outgoing Parameters
*---------------------------------------------------------------------------------------------------------------
* In  : --N/A--
* Out : YARRAY.VAL
*---------------------------------------------------------------------------------------------------------------
* DESCRIPTION       : This is a NOFILE enquiry routine to get a report with
*                     the investment details.
*---------------------------------------------------------------------------------------------------------------

    $INSERT I_COMMON ;*AUTO R22 CODE CONVERSION - START
    $INSERT I_EQUATE
    $INSERT I_ENQUIRY.COMMON
    $INSERT I_F.ACCOUNT
    $INSERT I_F.STMT.ACCT.CR
    $INSERT I_F.CATEGORY
    $INSERT I_F.CUSTOMER
    $INSERT I_F.AZ.PRODUCT.PARAMETER
    $INSERT I_F.ACCOUNT.CLASS
    $INSERT I_F.AZ.ACCOUNT
    $INSERT I_F.REDO.CUST.PRD.LIST ;*AUTO R22 CODE CONVERSION - END

    GOSUB INIT
    GOSUB GET.LOCATE
    GOSUB READ.PROCESS
    GOSUB PROCESS
    GOSUB FIN.PROCESS
RETURN

INIT:
*****
    FN.ACCOUNT = 'F.ACCOUNT'; F.ACCOUNT = ''
    CALL OPF(FN.ACCOUNT,F.ACCOUNT)
    FN.ACCOUNT.HST = 'F.ACCOUNT$HIS'; F.ACCOUNT.HST = ''
    CALL OPF(FN.ACCOUNT.HST,F.ACCOUNT.HST)
    FN.STMT.ACCT.CR = 'F.STMT.ACCT.CR'; F.STMT.ACCT.CR = ''
    CALL OPF(FN.STMT.ACCT.CR,F.STMT.ACCT.CR)
    FN.CUSTOMER.ACCOUNT = 'F.CUSTOMER.ACCOUNT'; F.CUSTOMER.ACCOUNT = ''
    CALL OPF(FN.CUSTOMER.ACCOUNT,F.CUSTOMER.ACCOUNT)
    FN.JOINT.CONTRACTS.XREF='F.JOINT.CONTRACTS.XREF'; F.JOINT.CONTRACTS.XREF=''
    CALL OPF(FN.JOINT.CONTRACTS.XREF,F.JOINT.CONTRACTS.XREF)
    FN.AZ.ACCOUNT='F.AZ.ACCOUNT'; F.AZ.ACCOUNT=''
    CALL OPF(FN.AZ.ACCOUNT,F.AZ.ACCOUNT)
    FN.CATEGORY = 'F.CATEGORY'; F.CATEGORY = ''
    CALL OPF(FN.CATEGORY,F.CATEGORY)
    FN.CUSTOMER = 'F.CUSTOMER'; F.CUSTOMER = ''
    CALL OPF(FN.CUSTOMER,F.CUSTOMER)
    FN.CUSTOMER.HST = 'F.CUSTOMER$HIS'; F.CUSTOMER.HST = ''
    CALL OPF(FN.CUSTOMER.HST,F.CUSTOMER.HST)
    FN.ACCT.CLASS = 'F.ACCOUNT.CLASS';  F.ACCT.CLASS = ''
    CALL OPF(FN.ACCT.CLASS,F.ACCT.CLASS)
    FN.AZ.PRD.PARM = 'F.AZ.PRODUCT.PARAMETER';  F.AZ.PRD.PARM = ''
    CALL OPF(FN.AZ.PRD.PARM,F.AZ.PRD.PARM)
    FN.REDO.CUST.PRD.LIST = 'F.REDO.CUST.PRD.LIST'; F.REDO.CUST.PRD.LIST = ''
    CALL OPF(FN.REDO.CUST.PRD.LIST,F.REDO.CUST.PRD.LIST)
    FN.AZ.ACCOUNT = 'F.AZ.ACCOUNT'; F.AZ.ACCOUNT = ''
    CALL OPF(FN.AZ.ACCOUNT,F.AZ.ACCOUNT)
    FN.AZ.ACCOUNT.HST = 'F.AZ.ACCOUNT$HIS'; F.AZ.ACCOUNT.HST = ''
    CALL OPF(FN.AZ.ACCOUNT.HST,F.AZ.ACCOUNT.HST)
    YARRY.VAL.AC = ''; YARRY.VAL.AZ = ''
    TOT.TAX.AC = 0; TOT.TAX.AZ = 0; TOT.INT.AC = 0; TOT.INT.AZ = 0
RETURN

GET.LOCATE:
***********
    Y.CUS.VAL = ''; Y.ACCT.VAL = ''; YACCT.DET = ''
    LOCATE "CUSTOMER.ID" IN D.FIELDS<1> SETTING Y.CUS.POS THEN
        Y.CUS.OPERAND       = D.LOGICAL.OPERANDS<Y.CUS.POS>
        Y.CUS.VAL               = D.RANGE.AND.VALUE<Y.CUS.POS>
    END
    LOCATE "DATE.RANGE" IN D.FIELDS<1> SETTING Y.TXD.POS THEN
        Y.TAX.DTE.OPERAND       = D.LOGICAL.OPERANDS<Y.TXD.POS>
        Y.TAX.DTE               = D.RANGE.AND.VALUE<Y.TXD.POS>
        YDATE1 = FIELD(Y.TAX.DTE,@SM,1)
        YDATE2 = FIELD(Y.TAX.DTE,@SM,2)
    END
    LOCATE "ACCOUNT.ID" IN D.FIELDS<1> SETTING Y.ACT.POS THEN
        Y.ACCT.OPERAND       = D.LOGICAL.OPERANDS<Y.ACT.POS>
        Y.ACCT.VAL               = D.RANGE.AND.VALUE<Y.ACT.POS,1>
    END

    IF NOT(Y.ACCT.VAL) AND NOT(Y.CUS.VAL) THEN
        ENQ.ERROR = 'EB-CUST.OR.ACCT.INPUT'
        GOSUB PGM.END
    END
    IF Y.ACCT.VAL AND Y.CUS.VAL THEN
        ENQ.ERROR = 'EB-CUST.OR.ACCT.INPUT'
        GOSUB PGM.END
    END

    LOC.APP = "CUSTOMER":@FM:"ACCOUNT"
    CUS.FLDS = "L.CU.CIDENT":@VM:"L.CU.RNC":@VM:"L.CU.NOUNICO":@VM:"L.CU.ACTANAC":@VM:"L.CU.PASS.NAT":@FM:"L.AC.REINVESTED"
    LOC.FLD = CUS.FLDS
    LOC.POS = ""
    CALL MULTI.GET.LOC.REF(LOC.APP, LOC.FLD, LOC.POS)
    L.CU.CIDENT.POS = LOC.POS<1,1>
    L.CU.RNC.POS = LOC.POS<1,2>
    L.CU.NOUNICO.POS = LOC.POS<1,3>
    L.CU.ACTANAC.POS = LOC.POS<1,4>
    L.CU.PASS.NAT.POS = LOC.POS<1,5>
    L.AC.REINVESTED.POS = LOC.POS<2,1>
RETURN

READ.PROCESS:
*************
    Y.SAVINGS.CATEGORY = ''; Y.AZ.CATEGORY = ''
    Y.ACCT.CLASS = 'SAVINGS'
    CALL CACHE.READ(FN.ACCT.CLASS, Y.ACCT.CLASS, R.ACCT.CLASS, Y.AC.CL.ERR) ;*AUTO R22 CODE CONVERSION
    Y.SAVINGS.CATEGORY = R.ACCT.CLASS<AC.CLS.CATEGORY>
    CHANGE @VM TO @FM IN Y.SAVINGS.CATEGORY

    SEL.CMD.APP = 'SELECT ':FN.AZ.PRD.PARM
    CALL EB.READLIST(SEL.CMD.APP,SEL.APP.LIST,'',NO.OF.APP.RECS,SEL.APP.ERR)
    LOOP
        REMOVE Y.APP.ID FROM SEL.APP.LIST SETTING Y.APP.POS
    WHILE Y.APP.ID:Y.APP.POS
        R.APP.REC = ''; Y.APP.ERR = ''
        CALL F.READ(FN.AZ.PRD.PARM,Y.APP.ID,R.APP.REC,F.AZ.PRD.PARM,Y.APP.ERR)
        Y.AZ.CATEGORY<-1> = R.APP.REC<AZ.APP.ALLOWED.CATEG>
    REPEAT
RETURN

FIN.PROCESS:
************
    IF YARRY.VAL.AC THEN
        YARRY.VAL.AC<-1> = AZ.FLAG:'*SUBTOTAL AHORRO *******':TOT.INT.AC:'*':TOT.TAX.AC
        YARRY.VAL = YARRY.VAL.AC
    END
    IF YARRY.VAL.AZ THEN
        YARRY.VAL.AZ<-1> = AZ.FLAG:'*SUBTOTAL CERTIFICADO *******':TOT.INT.AZ:'*':TOT.TAX.AZ
        YARRY.VAL<-1> = YARRY.VAL.AZ
    END
    IF YARRY.VAL THEN
        YFIN.TOT.INT = 0; YFIN.TOT.TAX = 0
        YFIN.TOT.INT = TOT.INT.AC + TOT.INT.AZ
        YFIN.TOT.TAX = TOT.TAX.AC + TOT.TAX.AZ
        YARRY.VAL<-1> = AZ.FLAG:'*TOTAL GENERAL *******':YFIN.TOT.INT:'*':YFIN.TOT.TAX
    END
RETURN

PROCESS:
********
    YACCT.DET = ''
    IF Y.ACCT.VAL THEN
        YACCT.DET = Y.ACCT.VAL
        GOSUB READ.ACCOUNT
        Y.CUS.VAL = R.ACCOUNT<AC.CUSTOMER>
    END

    IF NOT(Y.ACCT.VAL) AND Y.CUS.VAL THEN
        ERR.CUST.ACCT = '';ERR.JOINT.CONTRACTS.XREF = ''; YACCT.DET.1 = ''; YACCT.DET.2 = ''
        CALL F.READ(FN.CUSTOMER.ACCOUNT,Y.CUS.VAL,YACCT.DET.1,F.CUSTOMER.ACCOUNT,ERR.CUST.ACCT)
        IF YACCT.DET.1 THEN
            YACCT.DET = YACCT.DET.1
        END
        CALL F.READ(FN.JOINT.CONTRACTS.XREF,Y.CUS.VAL,YACCT.DET.2,F.JOINT.CONTRACTS.XREF,ERR.JOINT.CONTRACTS.XREF)
        IF YACCT.DET.2 THEN
            YACCT.DET<-1> = YACCT.DET.2
        END
        R.REDO.CUST.PRD.LIST = ''; ERR.REDO.CUST.PRD.LIST = ''; YCLOSED.ACC.ID = ''
        CALL F.READ(FN.REDO.CUST.PRD.LIST,Y.CUS.VAL,R.REDO.CUST.PRD.LIST,F.REDO.CUST.PRD.LIST,ERR.REDO.CUST.PRD.LIST)
        IF R.REDO.CUST.PRD.LIST THEN
            YACCT.REC = ''; YACCT.CNT = 0
            YACCT.REC = R.REDO.CUST.PRD.LIST<1>
            YACCT.CNT = DCOUNT(YACCT.REC,@VM)
            GOSUB GET.CLOSED.ACCT
        END
        IF YCLOSED.ACC.ID THEN
            YACCT.DET<-1> = YCLOSED.ACC.ID
        END
    END
    GOSUB SUB.PROCESS
RETURN

GET.CLOSED.ACCT:
****************
    REC.FND = 0; YCUST.CNT = 0
    LOOP
    UNTIL YACCT.CNT EQ YCUST.CNT
        YCUST.CNT += 1 ;*AUTO R22 CODE CONVERSION
        YACC.ID = R.REDO.CUST.PRD.LIST<1,YCUST.CNT>
        YAC.TYPE = R.REDO.CUST.PRD.LIST<3,YCUST.CNT>
        YAC.STATUS = R.REDO.CUST.PRD.LIST<2,YCUST.CNT>

        IF YAC.STATUS EQ 'CLOSED' AND YAC.TYPE EQ 'CUSTOMER' THEN
            YCLOSED.ACC.ID<-1> = YACC.ID
        END
    REPEAT
RETURN

SUB.PROCESS:
************
    LOOP
        REMOVE YACCT.ID FROM YACCT.DET SETTING ACT.POSN
    WHILE YACCT.ID:ACT.POSN
        YCATEG = ''; YACCT.CREDIT.DATE = ''; AZ.FLAG = 0
        GOSUB READ.ACCOUNT
        YCATEG = R.ACCOUNT<AC.CATEGORY>
        YACCT.CREDIT.DATE=R.ACCOUNT<AC.CAP.DATE.CR.INT>
        LOCATE YCATEG IN Y.SAVINGS.CATEGORY SETTING SAV.POS THEN
            AZ.FLAG = 1
        END ELSE
            LOCATE YCATEG IN Y.AZ.CATEGORY SETTING AZ.POS THEN
                AZ.FLAG = 2
            END ELSE
                CONTINUE
            END
        END
        IF NOT(YACCT.CREDIT.DATE) THEN
            CONTINUE
        END
        YACCT.TITLE = ''; YREINVST = ''; YLIQ.ACCT = ''
        YCUST = ''; YRELAT.CDE = ''; YJT.HOLDER = ''; YGRP.FLG = 0
        GOSUB GET.CATEG.VAL
        YCUST = R.ACCOUNT<AC.CUSTOMER>
        GOSUB GET.CUST.DET
        YCURR = R.ACCOUNT<AC.CURRENCY>
        YACCT.TITLE = R.ACCOUNT<AC.ACCOUNT.TITLE.1,1>
        YRELAT.CDE = R.ACCOUNT<AC.RELATION.CODE>
        IF YRELAT.CDE AND Y.CUS.VAL NE YCUST THEN
            GOSUB GET.JOINT.HOLD
            IF YGRP.FLG NE 1 THEN
                CONTINUE
            END
        END
        GOSUB CLOSE.MONTH.RANGE.VALUES.PROCESS
        GOSUB SUB.PROC.MIN

        YREINVST = R.ACCOUNT<AC.LOCAL.REF,L.AC.REINVESTED.POS>
        IF AZ.FLAG EQ 2 AND YREINVST EQ 'YES' THEN
            GOSUB GET.AZ.ACCT
            YACCT.ID = YLIQ.ACCT
            IF YLIQ.ACCT THEN
                GOSUB READ.ACCOUNT
                YACCT.CREDIT.DATE=R.ACCOUNT<AC.CAP.DATE.CR.INT>
                YCATEG = R.ACCOUNT<AC.CATEGORY>
                GOSUB GET.CATEG.VAL
                YCUST = R.ACCOUNT<AC.CUSTOMER>
                GOSUB GET.CUST.DET
                YCURR = R.ACCOUNT<AC.CURRENCY>
                YACCT.TITLE = R.ACCOUNT<AC.ACCOUNT.TITLE.1,1>
                GOSUB CLOSE.MONTH.RANGE.VALUES.PROCESS
                GOSUB SUB.PROC.MIN
            END
        END
    REPEAT
RETURN

GET.AZ.ACCT:
************
    ERR.AZ.ACCT = ''; R.AZ.ACCOUNT = ''
    CALL F.READ(FN.AZ.ACCOUNT,YACCT.ID,R.AZ.ACCOUNT,F.AZ.ACCOUNT,ERR.AZ.ACCT)
    IF NOT(R.AZ.ACCOUNT) THEN
        YACCT.ID.HST = YACCT.ID
        ERR.AZ.ACCOUNT = ''; R.AZ.ACCOUNT = ''
        CALL EB.READ.HISTORY.REC(F.AZ.ACCOUNT.HST,YACCT.ID.HST,R.AZ.ACCOUNT,ERR.AZ.ACCOUNT)
    END
    IF R.AZ.ACCOUNT THEN
        YLIQ.ACCT = R.AZ.ACCOUNT<AZ.INTEREST.LIQU.ACCT>
    END
RETURN

SUB.PROC.MIN:
*************
    IF YTOT.INT.AC NE 0 AND AZ.FLAG EQ 1 THEN
        YARRY.VAL.AC<-1> = AZ.FLAG:'*SUBTOTAL *******':YTOT.INT.AC:'*':YTOT.TAX.AC
        TOT.TAX.AC += YTOT.TAX.AC
        TOT.INT.AC += YTOT.INT.AC
    END
    IF YTOT.INT.AZ NE 0 AND AZ.FLAG EQ 2 THEN
        YARRY.VAL.AZ<-1> = AZ.FLAG:'*SUBTOTAL *******':YTOT.INT.AZ:'*':YTOT.TAX.AZ
        TOT.TAX.AZ += YTOT.TAX.AZ
        TOT.INT.AZ += YTOT.INT.AZ
    END
RETURN

GET.CATEG.VAL:
**************
    ERR.CATEG = ''; R.CATEGORY = ''; YCATEG.DES = ''
    CALL CACHE.READ(FN.CATEGORY, YCATEG, R.CATEGORY, ERR.CATEG) ;*AUTO R22 CODE CONVERSION
    YCATEG.DES = R.CATEGORY<EB.CAT.DESCRIPTION,LNGG>
    IF NOT(YCATEG.DES) THEN
        YCATEG.DES = R.CATEGORY<EB.CAT.DESCRIPTION,1>
    END
RETURN

GET.JOINT.HOLD:
***************
    JOINT.HOLD.VAL = "500":@FM:"501":@FM:"600":@FM:"601"
    YCNT = 1
    LOOP
        REMOVE JT.HOLD.ID FROM YRELAT.CDE SETTING JT.POSN
    WHILE JT.HOLD.ID:JT.POSN

        LOCATE JT.HOLD.ID IN JOINT.HOLD.VAL SETTING PONN THEN
            Y.RELAT.CDE = ''; YJOIN.HOLD = ''
            YJOIN.HOLD = R.ACCOUNT<AC.JOINT.HOLDER,YCNT>
            IF YJOIN.HOLD EQ Y.CUS.VAL THEN
                YGRP.FLG = 1
            END
        END
        YCNT += 1 ;*AUTO R22 CODE CONVERSION
    REPEAT
RETURN

GET.CUST.DET:
*************
    ERR.CUST = ''; R.CUSTOMER = ''
    CALL F.READ(FN.CUSTOMER,YCUST,R.CUSTOMER,F.CUSTOMER,ERR.CUST)
    IF NOT(R.CUSTOMER) THEN
        YCUST.HST = YCUST; ERR.CUST = ''
        CALL EB.READ.HISTORY.REC(F.CUSTOMER.HST,YCUST.HST,R.CUSTOMER,ERR.CUST)
    END
    L.CU.CIDENT.VAL = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.CIDENT.POS>
    L.CU.RNC.VAL = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.RNC.POS>
    L.CU.PASS.NAT.VAL = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.PASS.NAT.POS>
    L.CU.ACTANAC.VAL = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.ACTANAC.POS>
    L.CU.NOUNICO.VAL = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.NOUNICO.POS>
    IF L.CU.CIDENT.VAL THEN
        CUSTOMER.ID = L.CU.CIDENT.VAL
    END
    IF L.CU.RNC.VAL THEN
        CUSTOMER.ID = L.CU.RNC.VAL
    END
    IF L.CU.NOUNICO.VAL THEN
        CUSTOMER.ID = L.CU.NOUNICO.VAL
    END
    IF L.CU.ACTANAC.VAL THEN
        CUSTOMER.ID = L.CU.ACTANAC.VAL
    END
    IF L.CU.PASS.NAT.VAL THEN
        CUSTOMER.ID = L.CU.PASS.NAT.VAL
    END
RETURN

CLOSE.MONTH.RANGE.VALUES.PROCESS:
*********************************
    YTOT.TAX.AC = 0; YTOT.INT.AC = 0; YTOT.TAX.AZ = 0; YTOT.INT.AZ = 0
    LOOP
        REMOVE YCR.DATE FROM YACCT.CREDIT.DATE SETTING CR.POSN
    WHILE YCR.DATE:CR.POSN
        YTP.CRDATE = ''; YTP.CRDATE = YCR.DATE
        CALL CDT('',YTP.CRDATE,'+1C')
        IF YTP.CRDATE LT YDATE1 OR YTP.CRDATE GT YDATE2 THEN
            CONTINUE
        END
        Y.STMT.CR.ID.NEW = ''
        Y.STMT.CR.ID.NEW = YACCT.ID:"-":YCR.DATE
        R.STMT.ACCT.CR = ''; STMT.ACCT.CR.ERR = ''; YINT = 0; YTAX = 0
        CALL F.READ(FN.STMT.ACCT.CR,Y.STMT.CR.ID.NEW,R.STMT.ACCT.CR,F.STMT.ACCT.CR,STMT.ACCT.CR.ERR)
        YINT = R.STMT.ACCT.CR<IC.STMCR.TOTAL.INTEREST>
        YTAX = R.STMT.ACCT.CR<IC.STMCR.TAX.FOR.CUSTOMER>
        IF (YINT NE 0 AND YINT NE '' AND AZ.FLAG EQ 1) THEN
            YARRY.VAL.AC<-1> = AZ.FLAG:'*':YCATEG.DES:'*':YACCT.ID:'*':YCUST:'*':CUSTOMER.ID:'*':YACCT.TITLE:'*':YCURR:'*':Y.STMT.CR.ID.NEW:'*':YINT:'*':YTAX
            YTOT.TAX.AC += YTAX
            YTOT.INT.AC += YINT
        END
        IF (YINT NE 0 AND YINT NE '' AND AZ.FLAG EQ 2) THEN
            YARRY.VAL.AZ<-1> = AZ.FLAG:'*':YCATEG.DES:'*':YACCT.ID:'*':YCUST:'*':CUSTOMER.ID:'*':YACCT.TITLE:'*':YCURR:'*':Y.STMT.CR.ID.NEW:'*':YINT:'*':YTAX
            YTOT.TAX.AZ += YTAX
            YTOT.INT.AZ += YINT
        END
    REPEAT
RETURN

READ.ACCOUNT:
*************
    ERR.ACCT = ''; R.ACCOUNT = ''
    CALL F.READ(FN.ACCOUNT,YACCT.ID,R.ACCOUNT,F.ACCOUNT,ERR.ACCT)
    IF NOT(R.ACCOUNT) THEN
        YACCT.ID.HST = YACCT.ID
        ERR.ACCOUNT = ''; R.ACCOUNT = ''
        CALL EB.READ.HISTORY.REC(F.ACCOUNT.HST,YACCT.ID.HST,R.ACCOUNT,ERR.ACCOUNT)
    END
RETURN

PGM.END:
*********

END
