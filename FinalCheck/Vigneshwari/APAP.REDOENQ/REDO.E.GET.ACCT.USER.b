$PACKAGE APAP.REDOENQ
SUBROUTINE REDO.E.GET.ACCT.USER(ENQ.DATA)
*-----------------------------------------------------------------------------
* Company Name : ASOCIACION POPULAR DE AHORROS Y PRESTAMOS
* Developed By : Sakthi Sellappillai
* Program Name : REDO.E.GET.ACCT.USER
*-----------------------------------------------------------------------------
* Description : This subroutine is attached as a BUILD routine in the Enquiry REDO.SAV.ACCOUNT.LIST
* Linked with : Enquiry REDO.SAV.ACCOUNT.LIST  as BUILD routine
* In Parameter : ENQ.DATA
* Out Parameter : None
*
**DATE           ODR                   DEVELOPER               VERSION
*
*05/05/11       PACS00055393            GANESH H                MODIFICAION
* 11-APRIL-2023      Conversion Tool       R22 Auto Conversion  - Added IF E EQ "EB-UNKNOWN.VARIABLE" THEN , VM to @VM , FM to @FM and ! to *
* 11-APRIL-2023      Harsha                R22 Manual Conversion - No changes  
*-----------------------------------------------------------------------------
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_ENQUIRY.COMMON
    $INSERT I_System
    $INSERT I_EB.EXTERNAL.COMMON
    $INSERT I_F.CUSTOMER.ACCOUNT
    $INSERT I_F.ACCOUNT.CLASS
    $INSERT I_F.ACCOUNT
    $INSERT I_F.ACCOUNT.PARAMETER
    $INSERT I_F.AZ.ACCOUNT

    GOSUB INITIALISE
    GOSUB READ.PARAM.FILES
    GOSUB PROCESS
    GOSUB FORM.ENQ.DATA

RETURN
*-----------------------------------------------------------------------------
INITIALISE:
*-----------------------------------------------------------------------------

    F.ACCOUNT.PARAM=''
    FN.ACCOUNT.PARAM='F.ACCOUNT.PARAMETER'
*  CALL OPF(FN.ACCOUNT.PARAM,F.ACCOUNT.PARAM)

    F.CUSTOMER.ACCOUNT = ''
    FN.CUSTOMER.ACCOUNT = 'F.CUSTOMER.ACCOUNT'
    R.CUSTOMER.ACCOUNT.REC = ''
    Y.CUST.ACCT.ERR = ''
    CALL OPF(FN.CUSTOMER.ACCOUNT,F.CUSTOMER.ACCOUNT)
    F.ACCOUNT=''
    FN.ACCOUNT='F.ACCOUNT'
    CALL OPF(FN.ACCOUNT,F.ACCOUNT)

    F.AZ.ACCOUNT=''
    FN.AZ.ACCOUNT='F.AZ.ACCOUNT'
    CALL OPF(FN.AZ.ACCOUNT,F.AZ.ACCOUNT)

    Y.VAR.EXT.CUSTOMER = ''
    Y.VAR.EXT.ACCOUNTS=''

    Y.FIELD.COUNT = ''

RETURN
******************
READ.PARAM.FILES:
*****************
*PACS00055393-S
    SYSTEM.ID ='SYSTEM'
    CALL CACHE.READ(FN.ACCOUNT.PARAM,SYSTEM.ID,R.PARAM.REC,AC.PARAM.ERR)
    IF NOT(AC.PARAM.ERR) THEN
        CATEG.DESC       = R.PARAM.REC<AC.PAR.ACCT.CATEG.DESC>
        CHANGE @VM TO @FM IN CATEG.DESC
        FINDSTR "Savings" IN CATEG.DESC SETTING SAV.DESC.POS THEN
            SAV.CATG.START.RG=R.PARAM.REC<AC.PAR.ACCT.CATEG.STR,SAV.DESC.POS>
            SAV.CATG.END.RG = R.PARAM.REC<AC.PAR.ACCT.CATEG.END,SAV.DESC.POS>
        END
        FINDSTR "Current" IN CATEG.DESC SETTING CUR.DESC.POS THEN
            CUR.CATG.START.RG=R.PARAM.REC<AC.PAR.ACCT.CATEG.STR,CUR.DESC.POS>
            CUR.CATG.END.RG = R.PARAM.REC<AC.PAR.ACCT.CATEG.END,CUR.DESC.POS>
        END
    END
    Y.VAR.EXT.CUSTOMER = System.getVariable('EXT.CUSTOMER')
    IF E EQ "EB-UNKNOWN.VARIABLE" THEN      ;*R22 Auto Conversion  - Added IF E EQ "EB-UNKNOWN.VARIABLE" THEN
        Y.VAR.EXT.CUSTOMER = ""
    END
    R.CUSTOMER.ACCOUNT.REC= System.getVariable("EXT.CUSTOMER.ACCOUNTS")
    IF E EQ "EB-UNKNOWN.VARIABLE" THEN      ;*R22 Auto Conversion  - Added IF E EQ "EB-UNKNOWN.VARIABLE" THEN
        R.CUSTOMER.ACCOUNT.REC = ""
    END
    CHANGE @SM TO @FM IN R.CUSTOMER.ACCOUNT.REC
    TOT.ACCOUNTS = DCOUNT(R.CUSTOMER.ACCOUNT.REC,@FM)
*PACS00055393-E
RETURN
*-----------------------------------------------------------------------------
PROCESS:
*-----------------------------------------------------------------------------
*PACS00055393-S
    BEGIN CASE
        CASE ENQ.DATA<1,1> MATCHES 'AI.REDO.SAV.ACCOUNT.LIST':@VM:'AI.REDO.CHEQUE.LIST.ACCT'
            GOSUB GET.SAVINGS.ACCOUNT

        CASE ENQ.DATA<1,1> MATCHES 'AI.REDO.CUR.ACCOUNT.LIST':@VM:'AI.REDO.CUR.CHEQUE.LIST.ACCT'
            GOSUB GET.CURRENT.ACCOUNT

        CASE ENQ.DATA<1,1> MATCHES 'AI.REDO.ACCT.LIST':@VM:'AI.REDO.ACCT.LIST.OT.BNK':@VM:'AI.REDO.ACCT.LIST.TO':@VM:'AI.REDO.LOAN.ACCT.TO':@VM:'AI.REDO.LOAN.ADV.PYMT.TO'
            GOSUB GET.CUR.SAV.ACCOUNTS
        CASE ENQ.DATA<1,1> MATCHES 'AI.REDO.BANK.STOP.PAY.ACCT.LIST'
            GOSUB GET.CUR.SAV.ACCOUNTS
    END CASE

RETURN
*PACS00055393-E
*******************
GET.SAVINGS.ACCOUNT:
*******************
*PACS00055393-S
*TOT.ACCOUNTS = DCOUNT(R.CUSTOMER.ACCOUNT.REC,FM)
    FOR CNT.ACCT=1 TO TOT.ACCOUNTS
        ACCT.ID =R.CUSTOMER.ACCOUNT.REC<CNT.ACCT>
*CALL F.READ(FN.ACCOUNT,ACCT.ID,ACCT.REC,F.ACCOUNT,ACCOUNT.ERR)
        GOSUB READ.ACCOUNT
        IF NOT(ACCOUNT.ERR) THEN
*CALL F.READ(FN.AZ.ACCOUNT,ACCT.ID,AZ.REC,F.AZ.ACCOUNT,AZ.ERR)
            GOSUB READ.AZ.ACCOUNT
            IF AZ.ERR THEN
                ACCT.CATEGORY = ACCT.REC<AC.CATEGORY>
                IF ACCT.CATEGORY GE SAV.CATG.START.RG AND ACCT.CATEGORY LE SAV.CATG.END.RG THEN
                    Y.VAR.EXT.ACCOUNTS<-1>=ACCT.ID
                END
            END
        END

    NEXT CNT.ACCT

RETURN
*PACS00055393-E
*******************
GET.CURRENT.ACCOUNT:
*******************
*PACS00055393-S
*TOT.ACCOUNTS = DCOUNT(R.CUSTOMER.ACCOUNT.REC,FM)
    FOR CNT.ACCT=1 TO TOT.ACCOUNTS
        ACCT.ID =R.CUSTOMER.ACCOUNT.REC<CNT.ACCT>
*CALL F.READ(FN.ACCOUNT,ACCT.ID,ACCT.REC,F.ACCOUNT,ACCOUNT.ERR)
        GOSUB READ.ACCOUNT
        IF NOT(ACCOUNT.ERR) THEN
            ACCT.CATEGORY = ACCT.REC<AC.CATEGORY>
            IF ACCT.CATEGORY GE CUR.CATG.START.RG AND ACCT.CATEGORY LE CUR.CATG.END.RG THEN
                Y.VAR.EXT.ACCOUNTS<-1>=ACCT.ID
            END
        END
    NEXT CNT.ACCT

RETURN
*PACS00055393-E
**********************
GET.CUR.SAV.ACCOUNTS:
*********************
*PACS00055393-S
*TOT.ACCOUNTS = DCOUNT(R.CUSTOMER.ACCOUNT.REC,FM)
    FOR CNT.ACCT=1 TO TOT.ACCOUNTS
        ACCT.ID =R.CUSTOMER.ACCOUNT.REC<CNT.ACCT>
*CALL F.READ(FN.ACCOUNT,ACCT.ID,ACCT.REC,F.ACCOUNT,ACCOUNT.ERR)
        GOSUB READ.ACCOUNT
        ARR.ID=ACCT.REC<AC.ARRANGEMENT.ID>
        IF NOT(ACCOUNT.ERR) AND NOT(ARR.ID) THEN
* CALL F.READ(FN.AZ.ACCOUNT,ACCT.ID,AZ.REC,F.AZ.ACCOUNT,AZ.ERR)
            GOSUB READ.AZ.ACCOUNT

            IF AZ.ERR THEN
                ACCT.CATEGORY = ACCT.REC<AC.CATEGORY>
                IF ACCT.CATEGORY GE CUR.CATG.START.RG AND ACCT.CATEGORY LE SAV.CATG.END.RG THEN
                    Y.VAR.EXT.ACCOUNTS<-1>=ACCT.ID
                END
            END
        END
    NEXT CNT.ACCT

RETURN
*PACS00055393-E
**************
READ.ACCOUNT:
*************
    CALL F.READ(FN.ACCOUNT,ACCT.ID,ACCT.REC,F.ACCOUNT,ACCOUNT.ERR)

RETURN

****************
READ.AZ.ACCOUNT:
*****************
    CALL F.READ(FN.AZ.ACCOUNT,ACCT.ID,AZ.REC,F.AZ.ACCOUNT,AZ.ERR)


RETURN
FORM.ENQ.DATA:
**************
    Y.FIELD.COUNT = DCOUNT(ENQ.DATA<2>,@VM)
    CHANGE @FM TO ' ' IN Y.VAR.EXT.ACCOUNTS
    ENQ.DATA<2, Y.FIELD.COUNT +1>= '@ID'
    ENQ.DATA<3, Y.FIELD.COUNT +1> = 'EQ'
    ENQ.DATA<4, Y.FIELD.COUNT +1>= Y.VAR.EXT.ACCOUNTS

RETURN
*-----------------------------------------------------------------------------
END
*---------------------------*END OF SUBROUTINE*-------------------------------
