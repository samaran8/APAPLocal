$PACKAGE APAP.REDOENQ
SUBROUTINE REDO.E.NCF.ISSUED.RTN(TXN.ARRAY)
*----------------------------------------------------------------------------------------------------
*DESCRIPTION : This is a no file enquiry routine for the enquiry REDO.E.NCF.ISSUED
*-----------------------------------------------------------------------------------------------------
*-----------------------------------------------------------------------------------------------------
* * Input / Output
* --------------
* IN     : TXN.ARRAY
* OUT    : -NA-
*-----------------------------------------------------------------------------------------------------
* COMPANY NAME : APAP
* DEVELOPED BY : PRABHU N
* PROGRAM NAME : REDO.E.NCF.ISSUED.RTN
*-----------------------------------------------------------------------------------------------------
* Modification History :
*-----------------------
*DATE             WHO                REFERENCE         DESCRIPTION
*10.03.2010      PRABHU           ODR-2009-10-0321    INITIAL CREATION
* 11-APRIL-2023      Conversion Tool       R22 Auto Conversion  - SM to @SM , VM to @VM 
* 11-APRIL-2023      Harsha                R22 Manual Conversion - No changes
* -----------------------------------------------------------------------------------------------------

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_ENQUIRY.COMMON
    $INSERT I_F.CUSTOMER
    $INSERT I_F.REDO.NCF.ISSUED
    $INSERT I_F.REDO.L.NCF.STATUS
    $INSERT I_F.REDO.L.NCF.ISSUE

    GOSUB INIT
    GOSUB PROCESS
RETURN
INIT:
******
    SEL.NCF.ISSUED.CMD=''
    FN.REDO.NCF.ISSUED='F.REDO.NCF.ISSUED'
    F.REDO.NCF.ISSUED =''
    CALL OPF(FN.REDO.NCF.ISSUED,F.REDO.NCF.ISSUED)

    FN.REDO.L.NCF.STATUS='F.REDO.L.NCF.STATUS'
    F.REDO.L.NCF.STATUS =''
    CALL OPF(FN.REDO.L.NCF.STATUS,F.REDO.L.NCF.STATUS)

    FN.CUSTOMER='F.CUSTOMER'
    F.CUSTOMER =''
    CALL OPF(FN.CUSTOMER,F.CUSTOMER)

    FN.REDO.L.NCF.ISSUE='F.REDO.L.NCF.ISSUE'
    F.REDO.L.NCF.ISSUE =''
    CALL OPF(FN.REDO.L.NCF.ISSUE,F.REDO.L.NCF.ISSUE)

RETURN

PROCESS:
*********
    LOCATE "TRANSACTION.REF" IN D.FIELDS<1> SETTING POS THEN
        SEL.NCF.ISSUED.CMD='SELECT ':FN.REDO.NCF.ISSUED:'  WITH TXN.ID EQ ':D.RANGE.AND.VALUE<POS>
    END
    LOCATE "NCF" IN D.FIELDS<1> SETTING POS THEN
        SEL.NCF.ISSUED.CMD='SELECT ':FN.REDO.NCF.ISSUED:' WITH NCF EQ ':D.RANGE.AND.VALUE<POS>
    END
    LOCATE "MODIFIED.NCF" IN D.FIELDS<1> SETTING POS THEN
        SEL.NCF.ISSUED.CMD='SELECT ':FN.REDO.NCF.ISSUED:' WITH MODIFIED.NCF  EQ ':D.RANGE.AND.VALUE<POS>
    END
    IF SEL.NCF.ISSUED.CMD NE '' THEN
        CALL  EB.READLIST(SEL.NCF.ISSUED.CMD,SEL.NCF.ISSUED.LIST,'',NCF.ISSUED.COUNT,NCF.ERR)
        CALL F.READ(FN.REDO.NCF.ISSUED,SEL.NCF.ISSUED.LIST,R.REDO.NCF.ISSUED.CUS,F.REDO.NCF.ISSUED,ERR)
        Y.CUSTOMER.ID=R.REDO.NCF.ISSUED.CUS<ST.IS.CUSTOMER>
        GOSUB CUST.DATA
        GOSUB LIST.DATA
    END
    ELSE
        GOSUB LIST.PROCESS
    END
RETURN
*-----------
LIST.PROCESS:
*------------
    LOCATE "CUSTOMER.ID" IN D.FIELDS<1> SETTING POS THEN
        Y.CUSTOMER.ID=D.RANGE.AND.VALUE<POS>
        GOSUB CUST.DATA
    END
    IF Y.CUSTOMER.ID EQ '' THEN
        LOCATE "RNC" IN D.FIELDS<1> SETTING POS THEN
            SEL.CUST.CMD    ='SELECT ':FN.CUSTOMER:' WITH L.CU.RNC EQ ':D.RANGE.AND.VALUE<POS>
            SEL.EXT.CUST.CMD='SELECT ':FN.REDO.L.NCF.ISSUE:' WITH RNC.NUMBER EQ ':D.RANGE.AND.VALUE<POS>
        END
        LOCATE "CEDULA.ID" IN D.FIELDS<1> SETTING POS THEN
            SEL.CUST.CMD    ='SELECT ':FN.CUSTOMER:' WITH L.CU.CIDENT EQ ':D.RANGE.AND.VALUE<POS>
            SEL.EXT.CUST.CMD='SELECT ':FN.REDO.L.NCF.ISSUE:' WITH CEDULA.ID EQ ':D.RANGE.AND.VALUE<POS>
        END
        LOCATE "UNIQUE.ID" IN D.FIELDS<1> SETTING POS THEN
            SEL.CUST.CMD    ='SELECT ':FN.CUSTOMER:' WITH L.CU.NOUNICO EQ ':D.RANGE.AND.VALUE<POS>
            SEL.EXT.CUST.CMD='SELECT ':FN.REDO.L.NCF.ISSUE:' WITH UNIQUE.ID.NUM EQ ':D.RANGE.AND.VALUE<POS>
        END
        LOCATE "PASSPORT" IN D.FIELDS<1> SETTING POS THEN
            SEL.CUST.CMD    ='SELECT ':FN.CUSTOMER:' WITH LEGAL.ID EQ ':D.RANGE.AND.VALUE<POS>
            SEL.EXT.CUST.CMD='SELECT ':FN.REDO.L.NCF.ISSUE:' WITH PASSPORT EQ ':D.RANGE.AND.VALUE<POS>
        END

        CALL EB.READLIST(SEL.CUST.CMD,SEL.CUST.LIST,'',NCF.COUNT,NCF.ERR)
        IF SEL.CUST.LIST NE '' THEN
            Y.CUSTOMER.ID=SEL.CUST.LIST
            GOSUB CUST.DATA
        END
        ELSE
            CALL EB.READLIST(SEL.EXT.CUST.CMD,SEL.EXT.CUST.LIST,'',NCF.COUNT,NCF.ERR)
            CALL F.READ(FN.REDO.L.NCF.ISSUE,SEL.EXT.CUST.LIST,R.NCF.ISSUE,F.REDO.L.NCF.ISSUE,NCF.ERR)
            Y.CUSTOMER.ID   =R.NCF.ISSUE<NCF.IS.CUSTOMER>
            VAR.CUSTOMER.RNC=R.NCF.ISSUE<NCF.IS.RNC.NUMBER>
            VAR.CUST.ID.TYPE=R.NCF.ISSUE<NCF.IS.IDENDITY.TYPE>
        END
    END
    GOSUB GET.NCF.DATA
RETURN
*------------
GET.NCF.DATA:
*------------
    VAR.DATE.STMT=''

    LOCATE "DATE" IN D.FIELDS<1> SETTING POS THEN
        VAR.DATE          =D.RANGE.AND.VALUE<POS>
        VAR.START.DATE    =FIELD(VAR.DATE,@SM,1)
        VAR.END.DATE      =FIELD(VAR.DATE,@SM,2)
        IF VAR.START.DATE NE '' AND VAR.END.DATE NE '' THEN
            VAR.DATE.STMT=' AND DATE GE ':VAR.START.DATE : ' AND DATE LE ':VAR.END.DATE
        END
    END
    SEL.NCF.ISSUED.CMD='SELECT ':FN.REDO.NCF.ISSUED:' WITH CUSTOMER EQ ':Y.CUSTOMER.ID:VAR.DATE.STMT
    CALL EB.READLIST(SEL.NCF.ISSUED.CMD,SEL.NCF.ISSUED.LIST,'',NCF.ISSUED.COUNT,NCF.ERROR)
    GOSUB LIST.DATA
RETURN
*---------
LIST.DATA:
*---------
    VAR.TOTAL.CHARGE=0
    VAR.LINE.NO=0
    LOOP
        REMOVE Y.NCF.ISSUED.ID FROM SEL.NCF.ISSUED.LIST  SETTING NCF.POS
    WHILE Y.NCF.ISSUED.ID:NCF.POS
        CALL F.READ(FN.REDO.NCF.ISSUED,Y.NCF.ISSUED.ID,R.REDO.NCF.ISSUED,F.REDO.NCF.ISSUED,ERR.NCF)
        VAR.NCF          =R.REDO.NCF.ISSUED<ST.IS.NCF>
        VAR.TAX          =R.REDO.NCF.ISSUED<ST.IS.TAX.AMOUNT>
        VAR.CHARGE.AMOUNT=R.REDO.NCF.ISSUED<ST.IS.CHARGE.AMOUNT>
        VAR.DATE         =R.REDO.NCF.ISSUED<ST.IS.DATE>
        VAR.TOTAL.CHARGE =VAR.CHARGE.AMOUNT+VAR.TOTAL.CHARGE
        VAR.LINE.NO += 1
        CALL F.READ(FN.REDO.L.NCF.STATUS,Y.NCF.ISSUED.ID,R.REDO.NCF.STATUS,F.REDO.NCF.STATUS,ERR.NCF)
        VAR.STATUS       =R.REDO.NCF.STATUS<NCF.ST.STATUS>
        VAR.MODIFIED.NCF =R.REDO.NCF.STATUS<ST.IS.MODIFIED.NCF>
        TXN.ARRAY<-1>=VAR.CUSTOMER.RNC:'*':VAR.DATE:'*':NCF.ISSUED.COUNT:'*':VAR.TOTAL.CHARGE:'*':VAR.LINE.NO:'*':VAR.CUSTOMER.RNC:'*':VAR.CUST.ID.TYPE:'*':VAR.NCF:'*':VAR.MODIFIED.NCF:'*':VAR.DATE:'*':VAR.TAX:'*':VAR.CHARGE.AMOUNT:'*':VAR.STATUS
    REPEAT
RETURN
*--------
CUST.DATA:
*--------
    CALL F.READ(FN.CUSTOMER,Y.CUSTOMER.ID,R.CUSTOMER,F.CUSTOMER,CUST.ERR)
    IF R.CUSTOMER NE '' THEN
        LREF.APP         = 'CUSTOMER'
        LREF.FIELDS      = 'L.CU.TIPO.CL':@VM:'L.CU.RNC'
        CALL MULTI.GET.LOC.REF(LREF.APP,LREF.FIELDS,LREF.POS)
        L.CU.TIPO.CL.POS = LREF.POS<1,1>
        L.CU.RNC.POS     =LREF.POS<1,2>
        VAR.CUSTOMER.RNC =R.CUSTOMER<EB.CUS.LOCAL.REF><1,L.CU.RNC.POS>
        VAR.CUST.ID.TYPE =R.CUSTOMER<EB.CUS.LOCAL.REF><1,L.CU.TIPO.CL.POS>
    END
    ELSE
        SEL.NCF.ISSUE.CMD='SELECT  ': FN.REDO.L.NCF.ISSUE:' WITH  CUSTOMER EQ ': Y.CUSTOMER.ID
        CALL EB.READLIST(SEL.NCF.ISSUE.CMD,SEL.NCF.ISSUE.LIST,'',NCF.COUNT,NCF.ERR)
        CALL F.READ(FN.REDO.L.NCF.ISSUE,SEL.NCF.ISSUE.LIST,R.NCF.ISSUE,F.REDO.L.NCF.ISSUE,NCF.ERR)
        VAR.CUSTOMER.RNC=R.NCF.ISSUE<NCF.IS.RNC.NUMBER>
        VAR.CUST.ID.TYPE=R.NCF.ISSUE<NCF.IS.IDENDITY.TYPE>
    END
RETURN
END
