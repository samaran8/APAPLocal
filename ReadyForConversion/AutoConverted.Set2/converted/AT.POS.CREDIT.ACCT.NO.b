*-----------------------------------------------------------------------------
* <Rating>-49</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE AT.POS.CREDIT.ACCT.NO(INCOMING,CREDIT.ACCT.NO)
*-------------------------------------------------------------------------------
    $INCLUDE T24.BP I_COMMON
    $INCLUDE T24.BP I_EQUATE
    $INCLUDE ATM.BP I_F.AT.POS.MERCHANT.ACCT
    $INCLUDE ATM.BP I_F.ATM.BRANCH

    $INCLUDE ATM.BP I_F.AT.POS.BIN.ACCT
    $INCLUDE ATM.BP I_F.ATM.PARAMETER
    $INCLUDE TAM.BP I_F.REDO.APAP.H.PARAMETER

*developed by anitha for anbb on 05/06/08

* INCOMING ----> 32*%*41
* 32 - Acquirer BIN (Money Issuer BIN) - 6 digits
* 41 - Merchant ID - 15 digits

    Y.ISSU.BIN.NO=FIELD(INCOMING,'%',1)
    Y.MERCH.ID=FIELD(INCOMING,'%',2)

    GOSUB OPEN.FILES
    GOSUB BIN.CHK
    RETURN

BIN.CHK:

* Fix for 2795720 [BRD001 - FAST FUNDS SERVICES]

    IF Y.ISSU.BIN.NO EQ '26' THEN

        FN.REDO.APAP.H.PARAMETER = 'F.REDO.APAP.H.PARAMETER'
        CALL CACHE.READ(FN.REDO.APAP.H.PARAMETER,'SYSTEM',R.REDO.APAP.H.PARAMETER,ERR.PARAM)

        LOCATE 'COBRAR' IN R.REDO.APAP.H.PARAMETER<PARAM.OCT.FF.ACCT,1> SETTING OCT.POS THEN
            CREDIT.ACCT.NO = R.REDO.APAP.H.PARAMETER<PARAM.OCT.DOP.ACCT,OCT.POS>
        END
        RETURN
    END

* End of Fix

    IF Y.ISSU.BIN.NO EQ '1' THEN
        GOSUB OUR.PROCESS
    END ELSE
        GOSUB OTH.PROCESS
    END

    RETURN

*-------------------------------------------------------------------------------
OPEN.FILES:
*
    FN.ATM.PARAMETER = 'F.ATM.PARAMETER'
    CALL OPF(FN.ATM.PARAMETER,F.ATM.PARAMETER)

    CALL F.READ(FN.ATM.PARAMETER,'SYSTEM',R.ATM.PARAMETER,F.ATM.PARAMETER,ER.ATM.PARAMETER)
    OUR.BIN = R.ATM.PARAMETER<ATM.PARA.BANK.IMD>

    FN.ATM.ISO.BRANCH = 'F.ATM.BRANCH'
    F.ATM.ISO.BRANCH = ''
    CALL OPF(FN.ATM.ISO.BRANCH,F.ATM.ISO.BRANCH)

    FN.AT.POS.BIN.ACCT = 'F.AT.POS.BIN.ACCT'
    F.AT.POS.BIN.ACCT = ''
    CALL OPF(FN.AT.POS.BIN.ACCT,F.AT.POS.BIN.ACCT)

    FN.AT.POS.MERCHANT.ACCT = 'F.AT.POS.MERCHANT.ACCT'
    F.AT.POS.MERCHANT.ACCT = ''
    CALL OPF(FN.AT.POS.MERCHANT.ACCT,F.AT.POS.MERCHANT.ACCT)

    RETURN          ;* open files

*-------------------------------------------------------------------------------
OUR.PROCESS:
*
    R.AT.POS.MERCHANT.ACCT = ''
    ER.AT.POS.MERCHANT.ACCT = ''
    CALL F.READ(FN.AT.POS.MERCHANT.ACCT,Y.MERCH.ID,R.AT.POS.MERCHANT.ACCT,F.AT.POS.MERCHANT.ACCT,ER.AT.POS.MERCHANT.ACCT)
    CREDIT.ACCT.NO = R.AT.POS.MERCHANT.ACCT<AT.PMA.MERCHANT.ACCT.NO>

    IF NOT(CREDIT.ACCT.NO) THEN
        DEFAULT.MERCHANT.ID = '123456'
        CALL F.READ(FN.AT.POS.MERCHANT.ACCT,DEFAULT.MERCHANT.ID,R.AT.POS.MERCHANT.ACCT,F.AT.POS.MERCHANT.ACCT,ER.AT.POS.MERCHANT.ACCT)
        CREDIT.ACCT.NO = R.AT.POS.MERCHANT.ACCT<AT.PMA.MERCHANT.ACCT.NO>
    END

    RETURN          ;* from OUR.PROCESS

*-------------------------------------------------------------------------------
OTH.PROCESS:
*
    R.AT.POS.BIN.ACCT = ''
    ER.AT.POS.BIN.ACCT = ''
    CALL F.READ(FN.AT.POS.BIN.ACCT,Y.ISSU.BIN.NO,R.AT.POS.BIN.ACCT,F.AT.POS.BIN.ACCT,ER.AT.POS.BIN.ACCT)

    IF NOT(R.AT.POS.BIN.ACCT) THEN
        DEFAULT.BIN = '123123'
        CALL F.READ(FN.AT.POS.BIN.ACCT,DEFAULT.BIN,R.AT.POS.BIN.ACCT,F.AT.POS.BIN.ACCT,ER.AT.POS.BIN.ACCT)
    END
    CREDIT.ACCT.NO = R.AT.POS.BIN.ACCT<POS.POS.PAY.ACCT>

    RETURN          ;* from OTH.PROCESS

*-------------------------------------------------------------------------------
