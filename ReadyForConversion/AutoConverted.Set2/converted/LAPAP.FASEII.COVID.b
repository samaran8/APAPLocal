*-----------------------------------------------------------------------------
* <Rating>1166</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE LAPAP.FASEII.COVID(AA.ARR.ID)

*----------------------------------------------
* FASE II - ET-5290
* Fecha: 17/06/2020
*PARA LOS BACK_TO_BACK
*1. EXCLUIR DE LA ETAPA 1, REVISAR CAMPO TIPO_PERIODICIDAD
*Es necesario ajustar la DMT para que coloque en dicha propiedad la fecha de vencimiento final del contrato.
*Lo anterior es v√°lido la propiedad completa, es decir para los campos F.INICIO y F.FINAL.
*----------------------------------------------
    $INSERT T24.BP I_COMMON

    $INSERT T24.BP I_EQUATE
    $INSERT T24.BP I_BATCH.FILES
    $INSERT T24.BP I_TSA.COMMON
    $INSERT T24.BP I_F.AA.BILL.DETAILS
    $INSERT T24.BP I_F.AA.INTEREST
    $INSERT T24.BP I_F.AA.INTEREST.ACCRUALS
    $INSERT T24.BP I_F.AA.PAYMENT.SCHEDULE
    $INSERT T24.BP I_F.AA.ARRANGEMENT
    $INSERT T24.BP I_F.AA.ACCOUNT.DETAILS
    $INSERT T24.BP I_F.AA.TERM.AMOUNT
    $INSERT TAM.BP I_F.APAP.H.INSURANCE.DETAILS
    $INSERT T24.BP I_F.DATES
    $INSERT LAPAP.BP I_LAPAP.FASEII.COVID.COMO

    GOSUB MAIN.PROCESS

    RETURN


MAIN.PROCESS:
************

    Y.ARRANGEMENT.ID = '';
*Total capitalizar
    Y.PROPIEDAD.AJUSTE.IN  = '';
    Y.TIPO.POLIZA.IN = '';
    Y.NUEVA.TASA.IN = '';
    Y.FECHA.VENCIMIENTO.IN = '';
    Y.CONTADOR.PRESTAMO = 0;

    LOOP
        REMOVE Y.REGISTRO FROM AA.ARR.ID SETTING PROCESO.POS
    WHILE  Y.REGISTRO DO

        Y.REGISTRO.POS = CHANGE(Y.REGISTRO,'|',FM);

        Y.ARRANGEMENT.ID             = Y.REGISTRO.POS<1>
        Y.MONTO.CURACCOUNT.IN        = Y.REGISTRO.POS<2>
        Y.NUEVA.TASA.IN              = Y.REGISTRO.POS<3>
        Y.FECHA.VENCIMIENTO.IN       = Y.REGISTRO.POS<4>

        Y.TIPO.POLIZA.IN             = Y.REGISTRO.POS<5>
        Y.TIPO.POLIZA.MONTO          = CHANGE(Y.TIPO.POLIZA.IN,',',FM);

        IF Y.ARRANGEMENT.ID EQ '' THEN
            CONTINUE
        END

        Y.CONTADOR.PRESTAMO = Y.CONTADOR.PRESTAMO + 1;
        CRT Y.CONTADOR.PRESTAMO:". ":Y.ARRANGEMENT.ID;

        CALL REDO.B.CON.LNS.BY.DEBTOR.AA.RECS(Y.ARRANGEMENT.ID,OUT.RECORD)
        R.AA.TERM.AMOUNT          = FIELD(OUT.RECORD,"*",1)
        R.AA.PAYMENT.SCHEDULE.APP = FIELD(OUT.RECORD,"*",3)
        R.AA.INTEREST             = FIELD(OUT.RECORD,"*",7)

        GOSUB GET.ARRAGEMENT
        GOSUB VERIFICAR.BACK.TO.BACK
        GOSUB GET.AA.PAYMENT.SHEDULE
        GOSUB READ.BILL.DETAILS

*Verificar si el prestamo pertenece a la FASEI. Sino, excluir
        Y.POSEE.FASEI = 'NO';
        Y.CNT1 = DCOUNT(Y.PAYMENT.TYPE,FM);
        Y.FECHA.EFECTIVA.GRUP4 = '';
        Y.COLOCAR.FECHA.EFECTIVA = "NO";

        X = 0;
        FOR X = 1 TO Y.CNT1
            IF Y.PAYMENT.TYPE<X> EQ 'SOLO.INTERES' AND Y.START.DATE.PAY<X> EQ Y.END.DATE.PAY<X> THEN
                Y.POSEE.FASEI           = 'SI';
                Y.FECHA.EFECTIVA.GRUP4  = Y.END.DATE.PAY<X>;
            END

*Si la fecha de cualquiera de los payments del plan de pago es igual al TODAY, enviarle como fecha efectiva el LAST.WORK.DATE
            IF Y.START.DATE.PAY<X> EQ Y.TODAY THEN
                Y.COLOCAR.FECHA.EFECTIVA = 'SI';
            END
        NEXT X

*Prestamos que no poseen la SUPERCUOTA, seran excluidos del proceso completo
        IF Y.POSEE.FASEI EQ 'NO' THEN
            Y.ID.GRUPO = Y.ARRANGEMENT.ID:"*":"EXCLUIDO"
            R.LAPAP.FASEII.COVID19 = Y.ARRANGEMENT.ID:"|PRESTAMO NO POSEE SOLO.INTERES FASE I|";
            GOSUB CHECK.ARCHIVO.FILES
            CONTINUE
        END

*Back.to.back sin cuota emitida, seran excluidos del proceso completo
        IF Y.PRESTAMO.BACK.TO.BACK EQ 'SI' AND Y.CUOTA.EMITIDA EQ 'NO' THEN
            Y.ID.GRUPO = Y.ARRANGEMENT.ID:"*":"EXCLUIDO"
            R.LAPAP.FASEII.COVID19 = Y.ARRANGEMENT.ID:"|PRESTAMO BACK.TO.BACK SIN LA SUPERCUOTA EMITIDA|EXCLUIDO DE LA FASE II|";
            GOSUB CHECK.ARCHIVO.FILES
            CONTINUE
        END

        GOSUB GRUPO1.TASA.FIJA.CERO
        GOSUB GRUPO2.INCREM.CAPITAL
        GOSUB GRUPO3.EXT.PLAZO.PRESTAMO
        GOSUB GRUPO4.PROG.FUTURO.TASA

*Comentando a espera de confirmacion por el funcional
*GOSUB GRUPO5.ACTUALIZAR.

    REPEAT

    RETURN

GET.ARRAGEMENT:
***************

    R.ARRANGEMENT = ''; ERR.ARRAGEMENT = ''; CLIENTE.ID = ''
    CALL AA.GET.ARRANGEMENT(Y.ARRANGEMENT.ID,R.ARRANGEMENT,ERR.ARRAGEMENT)

    Y.COMPANY          = R.ARRANGEMENT<AA.ARR.CO.CODE>
    Y.ARR.STATUS       = R.ARRANGEMENT<AA.ARR.ARR.STATUS>
    Y.PRODUCT.GROUP    = R.ARRANGEMENT<AA.ARR.PRODUCT.GROUP>
    Y.NUMERO.PRESTAMO  = R.ARRANGEMENT<AA.ARR.LINKED.APPL.ID>

    IF Y.ARR.STATUS EQ 'CLOSE' THEN
        RETURN
    END

    RETURN

VERIFICAR.BACK.TO.BACK:
***********************

    Y.PRESTAMO.BACK.TO.BACK = 'NO';
    CALL GET.LOC.REF("AA.PRD.DES.INTEREST","L.AA.REV.RT.TY",REVRATE.POS)

    Y.L.AA.REV.RT.TY = R.AA.INTEREST<AA.INT.LOCAL.REF,REVRATE.POS>

    IF Y.L.AA.REV.RT.TY EQ "Back.To.Back" OR Y.L.AA.REV.RT.TY EQ "BACK.TO.BACK" THEN
        Y.PRESTAMO.BACK.TO.BACK = 'SI';
    END

    RETURN

GRUPO1.TASA.FIJA.CERO:
*********************

    IF Y.FECHA.EFECTIVA.GRUP4 GT Y.TODAY THEN

        IF Y.PRESTAMO.BACK.TO.BACK EQ 'SI' THEN

            Y.ID.GRUPO = Y.ARRANGEMENT.ID:"*":"EXCLUIDO"
            R.LAPAP.FASEII.COVID19 = Y.ARRANGEMENT.ID:"|PRESTAMO BACK.TO.BACK|EXCLUIDO DE LA ETAPA 1|";
            GOSUB CHECK.ARCHIVO.FILES

        END ELSE

            GRUPO1 = "";
            GRUPO1<-1> = "||":Y.ARRANGEMENT.ID:"||LENDING-CHANGE-PRINCIPALINT||":Y.COMPANY:"||PRINCIPALINT||FIXED.RATE||0||"
            GRUPO1<-1> = "||":Y.ARRANGEMENT.ID:"||LENDING-CHANGE-PENALTINT||":Y.COMPANY:"||PENALTINT||FIXED.RATE||0||"

            Y.ID.GRUPO = Y.ARRANGEMENT.ID:"*":"GRUPO1"
            R.LAPAP.FASEII.COVID19 = GRUPO1;
            GOSUB CHECK.ARCHIVO.FILES

        END

    END ELSE
        Y.ID.GRUPO = Y.ARRANGEMENT.ID:"*":"EXCLUIDO"
        R.LAPAP.FASEII.COVID19 = Y.ARRANGEMENT.ID:"|LA FECHA DE LA FACTURA SUPERCUOTA ES MENOR O IGUAL AL TODAY|EXCLUIDO DE LA ETAPA 1|";
        GOSUB CHECK.ARCHIVO.FILES
    END

    RETURN


GRUPO2.INCREM.CAPITAL:
************************

    GRUPO2 = "";
    GOSUB GET.UNCACCOUNT
*Si tiene la cuota de SOLO.INTERES  pendiente de pago, entonces buscarel DUEACCPRINCIPALINT

    IF Y.MONTO.CURACCOUNT.IN EQ '0' OR Y.MONTO.CURACCOUNT.IN EQ '0.00' THEN

        Y.ID.GRUPO = Y.ARRANGEMENT.ID:"*":"EXCLUIDO"
        R.LAPAP.FASEII.COVID19 = Y.ARRANGEMENT.ID:"|PRESTAMO EXCLUIDO DE LA ETAPA #2 - CURRACCOUNT IGUAL A 0|";
        GOSUB CHECK.ARCHIVO.FILES

    END ELSE

*Supercuota emitida
        IF Y.CUOTA.EMITIDA EQ 'SI' THEN

*Supercuota emitida  y pendiente de pago
            IF Y.CUOTA.PENDIENTE.PAGO EQ "SI" THEN

                IF Y.UNCACCOUNT NE 0 THEN

                    Y.UNCACCOUNT = "-":Y.UNCACCOUNT;
                    GRUPO2<-1> = "||":Y.ARRANGEMENT.ID:"||LENDING-ADJUST.ALL-MANT.SALD.CUOTA||":Y.COMPANY:"||MANT.SALD.CUOTA||ADJ.BAL.AMT:1:1::ADJ.BAL.AMT:1:2::ADJ.PROP.AMT:1:1||":Y.MONTO.CURACCOUNT.IN:"::":Y.UNCACCOUNT:"::":Y.DUE.ACCPRINCIPALINT:"||"

                END ELSE
                    GRUPO2<-1> = "||":Y.ARRANGEMENT.ID:"||LENDING-ADJUST.ALL-MANT.SALD.CUOTA||":Y.COMPANY:"||MANT.SALD.CUOTA||ADJ.BAL.AMT:1:1::ADJ.PROP.AMT:1:1||":Y.MONTO.CURACCOUNT.IN:"::":Y.DUE.ACCPRINCIPALINT:"||"
                END

                Y.ID.GRUPO = Y.ARRANGEMENT.ID:"*":"GRUPO2"
                R.LAPAP.FASEII.COVID19 = GRUPO2;
                GOSUB CHECK.ARCHIVO.FILES

*Supercuota emitida  y pagada
            END ELSE

                IF Y.UNCACCOUNT NE 0 THEN

                    Y.UNCACCOUNT = "-":Y.UNCACCOUNT;
                    GRUPO2<-1> = "||":Y.ARRANGEMENT.ID:"||LENDING-ADJUST.BALANCE-MANT.SALD.CUOTA||":Y.COMPANY:"||MANT.SALD.CUOTA||ADJ.BAL.AMT:1:1::ADJ.BAL.AMT:1:2||":Y.MONTO.CURACCOUNT.IN:"::":Y.UNCACCOUNT:"||"

                END ELSE
                    GRUPO2<-1> = "||":Y.ARRANGEMENT.ID:"||LENDING-ADJUST.BALANCE-MANT.SALD.CUOTA||":Y.COMPANY:"||MANT.SALD.CUOTA||ADJ.BAL.AMT:1:1||":Y.MONTO.CURACCOUNT.IN:"||"
                END

                Y.ID.GRUPO = Y.ARRANGEMENT.ID:"*":"GRUPO2"
                R.LAPAP.FASEII.COVID19 = GRUPO2;
                GOSUB CHECK.ARCHIVO.FILES

            END

*Sin la supercuota emitida
        END ELSE

*Sin cuota pendiente
            GOSUB GET.ACCPRINCIPALINT

            IF Y.UNCACCOUNT NE 0 THEN

                Y.UNCACCOUNT = "-":Y.UNCACCOUNT;
                GRUPO2<-1> = "||":Y.ARRANGEMENT.ID:"||LENDING-ADJUST.BALANCE-MANT.SALD.CUOTA||":Y.COMPANY:"||MANT.SALD.CUOTA||ADJ.BAL.AMT:1:1::ADJ.BAL.AMT:1:2::ADJ.BAL.AMT:2:1||":Y.MONTO.CURACCOUNT.IN:"::":Y.UNCACCOUNT:"::":Y.ACCPRINCIPALINT:"||"

            END ELSE

*Adjustar el CURACCOUNT con el valor enviado en el archivo y el ACCPRINCIPALINT a 0.
                GRUPO2<-1> = "||":Y.ARRANGEMENT.ID:"||LENDING-ADJUST.BALANCE-MANT.SALD.CUOTA||":Y.COMPANY:"||MANT.SALD.CUOTA||ADJ.BAL.AMT:1:1::ADJ.BAL.AMT:2:1||":Y.MONTO.CURACCOUNT.IN:"::":Y.ACCPRINCIPALINT:"||"

            END

            Y.ID.GRUPO = Y.ARRANGEMENT.ID:"*":"GRUPO2"
            R.LAPAP.FASEII.COVID19 = GRUPO2;
            GOSUB CHECK.ARCHIVO.FILES

        END

    END

    RETURN

GET.AA.PAYMENT.SHEDULE:
***********************

    Y.DIA.FRECUENCIA = ''
    Y.PAYMENT.TYPE =  R.AA.PAYMENT.SCHEDULE.APP<AA.PS.PAYMENT.TYPE>
    Y.PAYMENT.TYPE = CHANGE(Y.PAYMENT.TYPE,SM,FM)
    Y.PAYMENT.TYPE = CHANGE(Y.PAYMENT.TYPE,VM,FM)

    Y.PAYMENT.METHOD = R.AA.PAYMENT.SCHEDULE.APP<AA.PS.PAYMENT.METHOD>
    Y.PAYMENT.METHOD = CHANGE(Y.PAYMENT.METHOD,SM,FM)
    Y.PAYMENT.METHOD = CHANGE(Y.PAYMENT.METHOD,VM,FM)

    Y.START.DATE.PAY = R.AA.PAYMENT.SCHEDULE.APP<AA.PS.START.DATE>
    Y.START.DATE.PAY = CHANGE(Y.START.DATE.PAY,SM,FM)
    Y.START.DATE.PAY = CHANGE(Y.START.DATE.PAY,VM,FM)

    Y.END.DATE.PAY = R.AA.PAYMENT.SCHEDULE.APP<AA.PS.END.DATE>
    Y.END.DATE.PAY = CHANGE(Y.END.DATE.PAY,SM,FM)
    Y.END.DATE.PAY = CHANGE(Y.END.DATE.PAY,VM,FM)

    Y.PROPERTY = R.AA.PAYMENT.SCHEDULE.APP<AA.PS.PROPERTY>

    Y.AA.DUE.FREQ  = R.AA.PAYMENT.SCHEDULE.APP<AA.PS.DUE.FREQ>
    Y.AA.DUE.FREQ = CHANGE(Y.AA.DUE.FREQ,SM,FM)
    Y.AA.DUE.FREQ = CHANGE(Y.AA.DUE.FREQ,VM,FM)

    IF DCOUNT(Y.AA.DUE.FREQ,FM) GT 1 THEN
        Y.AA.DUE.FREQ = Y.AA.DUE.FREQ<1>
    END

    Y.NEW.DUE.FREQ = Y.AA.DUE.FREQ[14,2]
    IF NUM(Y.NEW.DUE.FREQ) EQ 0 THEN
        Y.NEW.DUE.FREQ = Y.AA.DUE.FREQ[14,1];
        Y.NEW.DUE.FREQ = '0':Y.NEW.DUE.FREQ;
    END

*Fecha vencimiento  y END.DATE = Fecha del ARCHIVO
    Y.NUEVO.END.DATE = '';
    Y.NUEVO.END.DATE = Y.FECHA.VENCIMIENTO.IN;

    RETURN


GRUPO3.EXT.PLAZO.PRESTAMO:
***************************
    GRUPO3 = "";
    Y.FIELD.NAME = ""

*Si la fecha de la primera propiedad es igual al TODAY, entonces se le enviara como fecha efectiva el LAST.WORKING.DATE
    IF Y.COLOCAR.FECHA.EFECTIVA EQ 'SI' THEN
        Y.FIELD.NAME ="||":Y.ARRANGEMENT.ID:"||LENDING-CHANGE.TERM-COMMITMENT||":Y.COMPANY:"||":Y.LAST.WORKING.DATE:"||COMMITMENT;;REPAYMENT.SCHEDULE||MATURITY.DATE;;"
    END ELSE
        Y.FIELD.NAME ="||":Y.ARRANGEMENT.ID:"||LENDING-CHANGE.TERM-COMMITMENT||":Y.COMPANY:"||||COMMITMENT;;REPAYMENT.SCHEDULE||MATURITY.DATE;;"
    END

    Y.FIELD.VALUE = Y.FECHA.VENCIMIENTO.IN:";;"

    Y.CNT.PAY              = DCOUNT(Y.PAYMENT.TYPE,FM);
    Y.TIPO.POLIZA.MONTO    = CHANGE(Y.TIPO.POLIZA.MONTO,',',FM);
    Y.CNT.POLIZA           = DCOUNT(Y.TIPO.POLIZA.MONTO,FM);

*Si no vienen polizas o cargos en el archivo solo buscar el paymet CONSTANTE y  SOLO.INTERES

    IF Y.CNT.POLIZA EQ 0 THEN

        FIND 'CONSTANTE' IN Y.PAYMENT.TYPE SETTING V.FLD, V.VAL THEN
            Y.PROPERTY.POS = "";
            Y.PROPERTY.POS = Y.PROPERTY<1,V.FLD,1>;
            Y.FIELD.NAME := "PAYMENT.TYPE:":V.FLD:":1::PAYMENT.METHOD:":V.FLD:":1::PROPERTY:":V.FLD:":1::END.DATE:":V.FLD:":1::"
            Y.FIELD.VALUE := Y.PAYMENT.TYPE<V.FLD>:"::":Y.PAYMENT.METHOD<V.FLD>:"::":Y.PROPERTY.POS:"::":Y.NUEVO.END.DATE:"::"
        END

        FIND 'CAPVENC' IN Y.PAYMENT.TYPE SETTING V.FLD, V.VAL THEN
            Y.PROPERTY.POS = "";
            Y.PROPERTY.POS = Y.PROPERTY<1,V.FLD,1>;
            Y.FIELD.NAME := "PAYMENT.TYPE:":V.FLD:":1::PAYMENT.METHOD:":V.FLD:":1::PROPERTY:":V.FLD:":1::START.DATE:":V.FLD:":1::END.DATE:":V.FLD:":1::"
            Y.FIELD.VALUE := Y.PAYMENT.TYPE<V.FLD>:"::":Y.PAYMENT.METHOD<V.FLD>:"::":Y.PROPERTY.POS:"::":Y.NUEVO.END.DATE:"::":Y.NUEVO.END.DATE:"::"
        END

        FIND 'SOLO.INTERES' IN Y.PAYMENT.TYPE SETTING V.FLD, V.VAL THEN
            IF Y.START.DATE.PAY<V.FLD> NE Y.END.DATE.PAY<V.FLD> THEN
                Y.PROPERTY.POS = "";
                Y.PROPERTY.POS = Y.PROPERTY<1,V.FLD,1>;
                Y.FIELD.NAME := "PAYMENT.TYPE:":V.FLD:":1::PAYMENT.METHOD:":V.FLD:":1::PROPERTY:":V.FLD:":1::END.DATE:":V.FLD:":1::"
                Y.FIELD.VALUE := Y.PAYMENT.TYPE<V.FLD>:"::":Y.PAYMENT.METHOD<V.FLD>:"::":Y.PROPERTY.POS:"::":Y.NUEVO.END.DATE:"::"
            END
        END

        IF RIGHT(Y.FIELD.NAME,2) EQ "::" AND RIGHT(Y.FIELD.VALUE,2) EQ "::" THEN
            Y.FIELD.NAME = SUBSTRINGS (Y.FIELD.NAME, 0, (LEN(Y.FIELD.NAME)-2));
            Y.FIELD.VALUE = SUBSTRINGS (Y.FIELD.VALUE, 0, (LEN(Y.FIELD.VALUE)-2));
            Y.FIELD.NAME := "||"
            Y.FIELD.VALUE := "||"
        END

    END ELSE

        FIND 'CONSTANTE' IN Y.PAYMENT.TYPE SETTING V.FLD, V.VAL THEN
            Y.PROPERTY.POS = "";
            Y.PROPERTY.POS = Y.PROPERTY<1,V.FLD,1>;
            Y.FIELD.NAME := "PAYMENT.TYPE:":V.FLD:":1::PAYMENT.METHOD:":V.FLD:":1::PROPERTY:":V.FLD:":1::END.DATE:":V.FLD:":1::"
            Y.FIELD.VALUE := Y.PAYMENT.TYPE<V.FLD>:"::":Y.PAYMENT.METHOD<V.FLD>:"::":Y.PROPERTY.POS:"::":Y.NUEVO.END.DATE:"::"
        END

        FIND 'CAPVENC' IN Y.PAYMENT.TYPE SETTING V.FLD, V.VAL THEN
            Y.PROPERTY.POS = "";
            Y.PROPERTY.POS = Y.PROPERTY<1,V.FLD,1>;
            Y.FIELD.NAME := "PAYMENT.TYPE:":V.FLD:":1::PAYMENT.METHOD:":V.FLD:":1::PROPERTY:":V.FLD:":1::START.DATE:":V.FLD:":1::END.DATE:":V.FLD:":1::"
            Y.FIELD.VALUE := Y.PAYMENT.TYPE<V.FLD>:"::":Y.PAYMENT.METHOD<V.FLD>:"::":Y.PROPERTY.POS:"::":Y.NUEVO.END.DATE:"::":Y.NUEVO.END.DATE:"::"
        END

        FIND 'SOLO.INTERES' IN Y.PAYMENT.TYPE SETTING V.FLD, V.VAL THEN

            IF Y.START.DATE.PAY<V.FLD> NE Y.END.DATE.PAY<V.FLD> THEN
                Y.PROPERTY.POS = "";
                Y.PROPERTY.POS = Y.PROPERTY<1,V.FLD,1>;
                Y.FIELD.NAME := "PAYMENT.TYPE:":V.FLD:":1::PAYMENT.METHOD:":V.FLD:":1::PROPERTY:":V.FLD:":1::END.DATE:":V.FLD:":1::"
                Y.FIELD.VALUE := Y.PAYMENT.TYPE<V.FLD>:"::":Y.PAYMENT.METHOD<V.FLD>:"::":Y.PROPERTY.POS:"::":Y.NUEVO.END.DATE:"::"
            END

        END

        J = 0;
        FOR J = 1 TO Y.CNT.POLIZA

            Y.TIPO.POLIZA.SELECT = "";
            Y.DATA  = CHANGE(Y.TIPO.POLIZA.MONTO<J>,'*',FM);
            Y.TIPO.POLIZA.SELECT  = Y.DATA<1>

            IF J EQ Y.CNT.POLIZA THEN

                FIND Y.TIPO.POLIZA.SELECT IN Y.PROPERTY SETTING V.FLD, V.VAL THEN
                    Y.PROPERTY.POS = "";
                    Y.PROPERTY.POS       = Y.PROPERTY<1,V.VAL,1>;
                    Y.DATA               = CHANGE(Y.TIPO.POLIZA.MONTO<J>,'*',FM);
                    Y.MONTO.PRIMA.CARGO  = Y.DATA<2>

                    Y.FIELD.NAME := "PAYMENT.TYPE:":V.VAL:":1::PAYMENT.METHOD:":V.VAL:":1::PROPERTY:":V.VAL:":1::END.DATE:":V.VAL:":1::ACTUAL.AMT:":V.VAL:":1||"
                    Y.FIELD.VALUE := Y.PAYMENT.TYPE<V.VAL>:"::":Y.PAYMENT.METHOD<V.VAL>:"::":Y.PROPERTY.POS:"::":Y.NUEVO.END.DATE:"::":Y.MONTO.PRIMA.CARGO:"||"

                END ELSE
*Si el registro tiene un seguro o cargo, y es el ultimo de la lista, pero el mismo esta mal escrito entonce cerrar el delimitador
                    IF RIGHT(Y.FIELD.NAME,2) EQ "::" AND RIGHT(Y.FIELD.VALUE,2) EQ "::" THEN
                        Y.FIELD.NAME = SUBSTRINGS (Y.FIELD.NAME, 0, (LEN(Y.FIELD.NAME)-2));
                        Y.FIELD.VALUE = SUBSTRINGS (Y.FIELD.VALUE, 0, (LEN(Y.FIELD.VALUE)-2));
                        Y.FIELD.NAME := "||"
                        Y.FIELD.VALUE := "||"
                    END
                END

            END ELSE

                FIND Y.TIPO.POLIZA.SELECT IN Y.PROPERTY SETTING V.FLD, V.VAL THEN
                    Y.PROPERTY.POS = "";
                    Y.PROPERTY.POS        = Y.PROPERTY<1,V.VAL,1>;
                    Y.DATA               = CHANGE(Y.TIPO.POLIZA.MONTO<J>,'*',FM);
                    Y.MONTO.PRIMA.CARGO  = Y.DATA<2>

                    Y.FIELD.NAME := "PAYMENT.TYPE:":V.VAL:":1::PAYMENT.METHOD:":V.VAL:":1::PROPERTY:":V.VAL:":1::END.DATE:":V.VAL:":1::ACTUAL.AMT:":V.VAL:":1::"
                    Y.FIELD.VALUE := Y.PAYMENT.TYPE<V.VAL>:"::":Y.PAYMENT.METHOD<V.VAL>:"::":Y.PROPERTY.POS:"::":Y.NUEVO.END.DATE:"::":Y.MONTO.PRIMA.CARGO:"::"
                END

            END


        NEXT J

    END


    GRUPO3<-1> = Y.FIELD.NAME:Y.FIELD.VALUE
    Y.ID.GRUPO = Y.ARRANGEMENT.ID:"*":"GRUPO3";
    R.LAPAP.FASEII.COVID19 = GRUPO3;
    GOSUB CHECK.ARCHIVO.FILES

    RETURN

GRUPO4.PROG.FUTURO.TASA:
**********************

*Fecha efectiva = END.DATE del payment.date SOLO.INTERES
    GRUPO4 = "";

    IF Y.PRESTAMO.BACK.TO.BACK EQ 'SI' AND Y.CUOTA.EMITIDA EQ 'SI' THEN

        GRUPO4<-1> = "||":Y.ARRANGEMENT.ID:"||LENDING-CHANGE-PRINCIPALINT||":Y.COMPANY:"||":Y.FECHA.EFECTIVA.GRUP4:"||PRINCIPALINT||MARGIN.RATE:1:1||":Y.NUEVA.TASA.IN:"||"
        GRUPO4<-1> = "||":Y.ARRANGEMENT.ID:"||LENDING-CHANGE-PENALTINT||":Y.COMPANY:"||":Y.FECHA.EFECTIVA.GRUP4:"||PENALTINT||MARGIN.RATE:1:1||":Y.NUEVA.TASA.IN:"||"

    END ELSE
        GRUPO4<-1> = "||":Y.ARRANGEMENT.ID:"||LENDING-CHANGE-PRINCIPALINT||":Y.COMPANY:"||":Y.FECHA.EFECTIVA.GRUP4:"||PRINCIPALINT||FIXED.RATE||":Y.NUEVA.TASA.IN:"||"
        GRUPO4<-1> = "||":Y.ARRANGEMENT.ID:"||LENDING-CHANGE-PENALTINT||":Y.COMPANY:"||":Y.FECHA.EFECTIVA.GRUP4:"||PENALTINT||FIXED.RATE||":Y.NUEVA.TASA.IN:"||"
    END

    Y.ID.GRUPO = Y.ARRANGEMENT.ID:"*":"GRUPO4"
    R.LAPAP.FASEII.COVID19 = GRUPO4;
    GOSUB CHECK.ARCHIVO.FILES

    RETURN

GRUPO5.ACTUALIZAR.POLIZA:
************************

    GOSUB GET.LOC.REF
    GOSUB GET.MAX.COLLATERAL.ID
    GOSUB GET.POLIZA.INFO

    RETURN

READ.BILL.DETAILS:
******************

    Y.DUE.ACCPRINCIPALINT = 0;
    Y.CUOTA.EMITIDA = "NO"
    Y.CUOTA.PENDIENTE.PAGO = "NO"
    SEL.CMD = ''; SEL.LIST = ''; NO.OF.REC = ''; SEL.ERR = '' ; Y.OS.TOTAL.AMOUNT = ''
    SEL.CMD = " SELECT " : FN.AA.BILL: " WITH ARRANGEMENT.ID EQ " :"'":Y.ARRANGEMENT.ID:"'":" AND PAYMENT.DATE GE ":"'":Y.FECHA.FACTURA:"'":" AND PAYMENT.DATE LE ":"'":Y.FECHA.FIN.FACTURA:"'"
    SEL.CMD := " AND PROPERTY NE 'ACCOUNT'"
    CALL EB.READLIST(SEL.CMD, SEL.LIST, "", NO.OF.REC, SEL.ERR)

    LOOP
        REMOVE Y.BILL.ID FROM SEL.LIST SETTING BILL.POS
    WHILE Y.BILL.ID DO

        R.AA.BILL = ''; BL.ERR = ''
        Y.BL.ID = ''; Y.BL.ID = Y.BILL.ID
        CALL F.READ(FN.AA.BILL,Y.BL.ID,R.AA.BILL,F.AA.BILL,BL.ERR)

***Si la factura es generada con balance de cancelacion no considerar
        Y.AA.BD.PROPERTY = R.AA.BILL<AA.BD.PROPERTY>
        Y.AA.BD.PROPERTY = CHANGE(Y.AA.BD.PROPERTY,SM,FM)
        Y.AA.BD.PROPERTY = CHANGE(Y.AA.BD.PROPERTY,VM,FM)
        Y.BILL.TYPE = R.AA.BILL<AA.BD.BILL.TYPE>
        LOCATE 'PAYOFF' IN Y.BILL.TYPE<1,1> SETTING BIII.POSN THEN
            CONTINUE
        END

        Y.PAYMENT.TYPE.BILL = "";
        Y.OS.TOTAL.AMOUNT = SUM(R.AA.BILL<AA.BD.OS.TOTAL.AMOUNT>)
        Y.PAYMENT.DATE = R.AA.BILL<AA.BD.PAYMENT.DATE>

        Y.PAYMENT.TYPE.BILL = R.AA.BILL<AA.BD.PAYMENT.TYPE>
        Y.PAYMENT.TYPE.BILL = CHANGE(Y.PAYMENT.TYPE.BILL,SM,FM)
        Y.PAYMENT.TYPE.BILL = CHANGE(Y.PAYMENT.TYPE.BILL,VM,FM)
        Y.PAYMENT.TYPE.BILL.SELECT = Y.PAYMENT.TYPE.BILL<1>;

        Y.BILL.DATE = R.AA.BILL<AA.BD.BILL.DATE>
        Y.BILL.FINAL.DATE = R.AA.BILL<AA.BD.BILL.FINAL.DATE>

        IF Y.PAYMENT.TYPE.BILL.SELECT EQ 'SOLO.INTERES' AND Y.BILL.DATE EQ Y.BILL.FINAL.DATE THEN

            IF Y.AA.BD.PROPERTY<1> EQ 'PRINCIPALINT' THEN

                Y.OS.PROP.AMOUNT       = R.AA.BILL<AA.BD.OS.PROP.AMOUNT>
                Y.OS.PROP.AMOUNT       = CHANGE(Y.OS.PROP.AMOUNT,SM,FM)
                Y.OS.PROP.AMOUNT       = CHANGE(Y.OS.PROP.AMOUNT,VM,FM)
                Y.DUE.ACCPRINCIPALINT  = Y.OS.PROP.AMOUNT<1>

                IF Y.DUE.ACCPRINCIPALINT GT 0 THEN
                    Y.CUOTA.PENDIENTE.PAGO = "SI"
                    Y.CUOTA.EMITIDA = "SI"
                END ELSE
                    Y.CUOTA.PENDIENTE.PAGO = "NO"
                    Y.CUOTA.EMITIDA = "SI"
                END

            END

*En caso de que no tenga la factura emitida
        END ELSE

            IF Y.CUOTA.EMITIDA NE "SI" THEN
                Y.CUOTA.EMITIDA = "NO"
                Y.CUOTA.PENDIENTE.PAGO = "NO"
            END

        END

    REPEAT

    RETURN

GET.ACCPRINCIPALINT:
*****************

    Y.ACCPRINCIPALINT = 0;

    R.AA.INTEREST.ACCRUALS = ''; BL.ERR = ''
    Y.AA.INTERES = Y.ARRANGEMENT.ID:"-PRINCIPALINT"
    CALL F.READ(FN.AA.INTEREST.ACCRUALS,Y.AA.INTERES,R.AA.INTEREST.ACCRUALS,F.AA.INTEREST.ACCRUALS,BL.ERR)

    Y.CNT.MONTOS = DCOUNT(R.AA.INTEREST.ACCRUALS<AA.INT.ACC.TOT.ACCR.AMT>,  @VM);
    Y.MAX.PRINCIPALINT  = R.AA.INTEREST.ACCRUALS<AA.INT.ACC.TOT.ACCR.AMT,Y.CNT.MONTOS>

    IF Y.MAX.PRINCIPALINT GT 0 THEN
        Y.ACCPRINCIPALINT = Y.MAX.PRINCIPALINT
    END

    RETURN

GET.UNCACCOUNT:
****************

    Y.UNCACCOUNT = 0;
    BAL.TYPES = 'UNCACCOUNT';

    REQUEST.TYPE<4>  = 'ECB'
    START.DATE = Y.TODAY ; END.DATE = '' ; SYSTEM.DATE = '';   ERROR.MESSAGE = '';
    CALL AA.GET.PERIOD.BALANCES(Y.NUMERO.PRESTAMO, BAL.TYPES, REQUEST.TYPE, START.DATE, END.DATE, SYSTEM.DATE, BAL.DETAILS, ERROR.MESSAGE)

    Y.UNCACCOUNT.BALANCE = ABS(BAL.DETAILS<4>);

    IF Y.UNCACCOUNT.BALANCE GT 0 THEN
        Y.UNCACCOUNT = Y.UNCACCOUNT.BALANCE;
    END

    RETURN

GET.LOC.REF:
************

    Y.APPLN = "AA.ARR.TERM.AMOUNT"
    Y.FLD = "L.AA.COL":VM:"L.AA.COL.VAL"
    Y.POS = ""

    CALL MULTI.GET.LOC.REF(Y.APPLN,Y.FLD,Y.POS)
    L.AA.COL.POS        = Y.POS<1,1>
    L.AA.COL.VAL.POS    = Y.POS<1,2>

    RETURN

GET.MAX.COLLATERAL.ID:
**********************

    COLLATERAL.ID = '';
    COLLATERAL.VALUE = '';
    COLLATERAL.VALUE.MAX= "";
    COLLATERAL.ID.MAX= "";

    CALL REDO.B.CON.LNS.BY.DEBTOR.AA.RECS(Y.ARRANGEMENT.ID,OUT.RECORD)
    R.AA.TERM.AMOUNT.APP      = FIELD(OUT.RECORD,"*",1)

    COLLATERAL.ID = R.AA.TERM.AMOUNT.APP<AA.AMT.LOCAL.REF,L.AA.COL.POS>
    COLLATERAL.ID  = CHANGE(COLLATERAL.ID ,SM,FM)
    COLLATERAL.ID  = CHANGE(COLLATERAL.ID ,VM,FM)

    COLLATERAL.VALUE = R.AA.TERM.AMOUNT.APP<AA.AMT.LOCAL.REF,L.AA.COL.VAL.POS>
    COLLATERAL.VALUE = CHANGE(COLLATERAL.VALUE,SM,FM)
    COLLATERAL.VALUE = CHANGE(COLLATERAL.VALUE,VM,FM)

*Obtener el ID.GARANTIA que contenga el mayor valor
    COLL.ID.COUNT = DCOUNT(COLLATERAL.ID,FM);

    IF COLL.ID.COUNT GT 1 THEN

        COLLATERAL.VALUE.POS = SORT(COLLATERAL.VALUE)
        COLLATERAL.VALUE.MAX = COLLATERAL.VALUE.POS<COLL.ID.COUNT>

        LOCATE COLLATERAL.VALUE.MAX IN COLLATERAL.VALUE SETTING POS.L.LOCAL THEN
            COLLATERAL.ID.MAX =  COLLATERAL.ID<POS.L.LOCAL>
        END

    END ELSE
        COLLATERAL.ID.MAX    = COLLATERAL.ID;
        COLLATERAL.VALUE.MAX = COLLATERAL.VALUE
    END

    RETURN


GET.POLIZA.INFO:
****************

    TIPO.POLIZA = '';
    R.APAP.H.INSURANCE.ID.CONCAT =''; ERR.AHIC = ''; INSUR.REC = ''; INSUR.REC.CNT = 0; YGRP.CNT = 0; POLIZA.ID = '';
    NO.OF.REC = ''; SEL.ERR = '' ;

    SEL.CMD = "SELECT ":FN.APAP.H.INSURANCE.DETAILS:" WITH ASSOCIATED.LOAN EQ ":Y.ARRANGEMENT.ID: " BY POL.EXP.DATE";
    CALL EB.READLIST(SEL.CMD, SEL.LIST, "", NO.OF.REC, SEL.ERR);
    Y.DATA = SEL.LIST;
    INSUR.REC.CNT = DCOUNT(SEL.LIST,FM)

    LOOP
        REMOVE POLIZA.ID FROM Y.DATA SETTING INSUR.POS
    WHILE POLIZA.ID DO

        Y.FECHA.VENC.POLIZA = ''; POLIZA.STATUS = ''; Y.INS.DET.INS.AMOUNT = '';
        ERR.APAP.H.INSURANCE.DETAILS = ''; R.APAP.H.INSURANCE.DETAILS = '';  TIPO.POLIZA = '';
        CALL F.READ(FN.APAP.H.INSURANCE.DETAILS,POLIZA.ID,R.APAP.H.INSURANCE.DETAILS,F.APAP.H.INSURANCE.DETAILS,ERR.APAP.H.INSURANCE.DETAILS)

        TIPO.POLIZA = R.APAP.H.INSURANCE.DETAILS<INS.DET.INS.POLICY.TYPE>
        POLIZA.STATUS = R.APAP.H.INSURANCE.DETAILS<INS.DET.POLICY.STATUS>
        Y.FECHA.VENC.POLIZA = R.APAP.H.INSURANCE.DETAILS<INS.DET.POL.EXP.DATE>

        IF Y.FECHA.VENC.POLIZA AND Y.FECHA.VENC.POLIZA GT Y.TODAY AND POLIZA.STATUS EQ 'VIGENTE' THEN

            Y.TIPO.SEGURO = R.APAP.H.INSURANCE.DETAILS<INS.DET.CHARGE>
            Y.CLASE.POLIZA = R.APAP.H.INSURANCE.DETAILS<INS.DET.CLASS.POLICY>

            IF POLIZA.ID NE '' THEN

                IF TIPO.POLIZA EQ 'VI' THEN
                    COLLATERAL.ID.MAX.TEMP = '';
                END ELSE
                    COLLATERAL.ID.MAX.TEMP = COLLATERAL.ID.MAX;
                END
*En la reunion del 14-05-2020, Jose Brenes indico que se filtrara las polizas clase GROUP
*IF TIPO.POLIZA EQ 'PVP' OR TIPO.POLIZA EQ 'PVC' OR TIPO.POLIZA EQ 'VI' THEN

                IF Y.CLASE.POLIZA EQ 'GROUP' OR Y.CLASE.POLIZA EQ 'GRUPO' THEN

                    FIND Y.TIPO.SEGURO IN Y.TIPO.POLIZA.MONTO SETTING V.FLD, V.VAL THEN

                        Y.DATA               = CHANGE(Y.TIPO.POLIZA.MONTO<V.FLD>,'*',FM);
                        Y.MONTO.ASEGURADO    = Y.DATA<2>
                        Y.MONTO.PRIMA.CARGO  = Y.DATA<3>

                        GRUPO5<-1> = POLIZA.ID:"||":COLLATERAL.ID.MAX.TEMP:"||":Y.MONTO.ASEGURADO:"||":Y.MONTO.PRIMA.CARGO:"||":Y.FECHA.VENCIMIENTO.IN:"||":Y.FECHA.VENCIMIENTO.IN:"||"

                    END

                END

*Y.FILE.NAME = 'CARGA5.FASEII.ACTUALIZAR.POLIZA.txt';
                Y.ID.GRUPO = Y.ARRANGEMENT.ID:"*":"GRUPO5";
                R.LAPAP.FASEII.COVID19 = GRUPO5;
                GOSUB CHECK.ARCHIVO.FILES

            END

        END

    REPEAT
    RETURN


CHECK.ARCHIVO.FILES:
********************
    CALL F.WRITE(FN.LAPAP.FASEII.COVID19,Y.ID.GRUPO,R.LAPAP.FASEII.COVID19);
    RETURN

END
