*-----------------------------------------------------------------------------
* <Rating>180</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE REDO.B.AA.ACC.NEG.VAL(ARR.LN.DATA)
*--------------------------------------------------------------------------------------------------
*  M O D I F I C A T I O N S
* ***************************
*--------------------------------------------------------------------------------------------------
* Defect Reference       Modified By                    Date of Change        Change Details
*
*                       Ashokkumar.V.P                  07/09/2015
*--------------------------------------------------------------------------------------------------
    $INCLUDE T24.BP I_COMMON
    $INCLUDE T24.BP I_EQUATE
    $INCLUDE T24.BP I_ENQUIRY.COMMON
    $INCLUDE T24.BP I_F.AA.ARRANGEMENT
    $INCLUDE T24.BP I_F.EB.CONTRACT.BALANCES
    $INCLUDE T24.BP I_GTS.COMMON
    $INCLUDE T24.BP I_F.AA.ACCOUNT.DETAILS
    $INCLUDE T24.BP I_F.AA.BILL.DETAILS
    $INCLUDE T24.BP I_F.AA.PAYMENT.SCHEDULE
    $INCLUDE T24.BP I_F.ACCOUNT
    $INCLUDE LAPAP.BP I_REDO.B.AA.ACC.NEG.VAL.COMMON
    $INCLUDE T24.BP I_F.AA.OVERDUE
    $INCLUDE T24.BP I_F.AA.ACTIVITY.HISTORY
    $INSERT TAM.BP I_F.REDO.APAP.PROPERTY.PARAM

    GOSUB MAIN.PROCESS
    RETURN

MAIN.PROCESS:
*************
    YFINAL.ARRY = ''; LINE.TYP.ACC = "ACC_Cantidad_4"; LINE.TYP.WOF = "WOF_Cantidad_5"
    AA.ID = ARR.LN.DATA
    ERR.AA.ARR = ''; R.AA.ARR = ''; Y.ACCOUNT = ''; Y.LINK.POS = ''
    CALL F.READ(FN.AA.ARR,AA.ID,R.AA.ARR,F.AA.ARR,ERR.AA.ARR)
    Y.MAIN.PROD.GROUP = R.AA.ARR<AA.ARR.PRODUCT.GROUP>
    Y.MAIN.ARR.STATUS = R.AA.ARR<AA.ARR.ARR.STATUS>
    Y.MAIN.ARR.PRCT = R.AA.ARR<AA.ARR.PRODUCT>
    Y.MAIN.STRT.DTE = R.AA.ARR<AA.ARR.START.DATE>
    IF Y.MAIN.STRT.DTE GT YLST.TODAY THEN
        RETURN
    END
    Y.CUSTOMER.ID = R.AA.ARR<AA.ARR.CUSTOMER>
    Y.CURRENCY    = R.AA.ARR<AA.ARR.CURRENCY>
    Y.LINKED.APPL = R.AA.ARR<AA.ARR.LINKED.APPL>
    Y.LINKED.APPL.ID = R.AA.ARR<AA.ARR.LINKED.APPL.ID>
    LOCATE "ACCOUNT" IN Y.LINKED.APPL<1,1> SETTING Y.LINK.POS THEN
        Y.ACCOUNT =Y.LINKED.APPL.ID<Y.LINK.POS>
    END

    R.ACCOUNT = ''; Y.ACC.ERR = ''; YCUSTOMER = ''; YOD.STAT = ''; YORG.ACTUAL.AMOUNT = 0
    CALL F.READ(FN.ACCOUNT,Y.ACCOUNT,R.ACCOUNT,F.ACCOUNT,Y.ACC.ERR)
    IF Y.ACC.ERR THEN
        RETURN
    END
    YOD.STAT = R.ACCOUNT<AC.LOCAL.REF,L.OD.STATUS.POS>
    YCUSTOMER = R.ACCOUNT<AC.CUSTOMER>
    YORG.ACTUAL.AMOUNT = R.ACCOUNT<AC.LOCAL.REF,POS.AVL.BAL>
    ARRAY.VAL = ''; Y.LOAN.STATUS = ''; Y.CLOSE.LN.FLG = 0
    GOSUB GET.LOAN.STATUS
    GOSUB GET.CLOSED.LOAN.CHK
    IF Y.CLOSE.LN.FLG EQ 1 OR Y.LOAN.STATUS EQ 'Write-off' THEN
        GOSUB PROCESS.ECB
    END
    RETURN

PROCESS.ECB:
************
    R.EB.CON.BAL = ''; ERR.EB.CON.BAL = ''; Y.BAL.PRIC.GP = ''; DATE.LSTUPD = 0; YARR.ASST = ''; YARR.ASSTORG = ''; MRACCT.VAL = 0; TEMP.FLG = 0
    Y.BAL.PRIC.ORG = ''; TPACCT.VAL = 0; ACCTPP.VAL = 0; MPACCT.VAL = 0; MRPACCT.VAL = 0; CPACCT.VAL = 0; ACC.PENVAL = 0; ACC.PRINVAL = 0
    ACC.PENVALSP = 0; ACC.PRINVALSP = 0; ACC.PENVALBL = 0; ACC.PRINVALBL = 0
    CALL F.READ(FN.EB.CON.BAL,Y.ACCOUNT,R.EB.CON.BAL,F.EB.CON.BAL,EB.CON.BAL.ERR)
    IF NOT(R.EB.CON.BAL) THEN
        RETURN
    END
    DATE.LSTUPD = R.EB.CON.BAL<ECB.DATE.LAST.UPDATE>
    Y.CNT.EB.CON = '1'
    Y.CUR.ASSET.TYPE = R.EB.CON.BAL<ECB.CURR.ASSET.TYPE>
    Y.DCNT.CUR.ASS = DCOUNT(Y.CUR.ASSET.TYPE,VM)
    Y.CNT.CUR.TYPE = '1'
    LOOP
    WHILE Y.CNT.CUR.TYPE LE Y.DCNT.CUR.ASS
        Y.TYPE.SYSDATE = ''; Y.PRIC.POS = ''; SYS.DATE = ''
        Y.ASSET.TYPE = Y.CUR.ASSET.TYPE<1,Y.CNT.CUR.TYPE>
        Y.TYPE.SYSDATE = R.EB.CON.BAL<ECB.TYPE.SYSDATE,Y.CNT.CUR.TYPE>
        YSYSTYPE = FIELD(Y.TYPE.SYSDATE,'-',1)
        SYS.DATE = FIELD(Y.TYPE.SYSDATE,'-',2)
        IF SYS.DATE <= YLST.TODAY THEN
            GOSUB CREDIT.MVMT.APPL
        END
        Y.CNT.CUR.TYPE += 1
    REPEAT
    WRK.FILE.ID = ''; LINE.TYP = ''
    GOSUB PRICNI.MVMT.APPL
    IF (ACC.PRINVAL NE 0 OR ACC.PENVAL NE 0 OR ACC.PRINVALSP NE 0 OR ACC.PENVALSP NE 0 OR ACC.PRINVALBL NE 0 OR ACC.PENVALBL NE 0) AND TEMP.FLG NE 1 AND Y.CLOSE.LN.FLG EQ 1 THEN
        YFINAL.ARRY = AA.ID:"*":Y.ACCOUNT:"*":Y.MAIN.ARR.STATUS:"*":Y.LOAN.STATUS:"*":YOD.STAT:"*":ACC.PRINVAL:"*":ACC.PENVAL:"*":ACC.PRINVALSP:"*":ACC.PENVALSP:"*":ACC.PRINVALBL:"*":ACC.PENVALBL:"*":COMACCT.VAL:"*":YORG.ACTUAL.AMOUNT
        LINE.TYP = LINE.TYP.ACC
        GOSUB WRITE.ARRY
    END
    IF (Y.LOAN.STATUS EQ 'Write-off') THEN
        IF ((ACC.PRINVAL NE 0 AND ACC.PRINVALBL EQ 0 AND ACC.PRINVALSP EQ 0) OR (ACC.PENVAL NE 0 AND ACC.PENVALBL EQ 0 AND ACC.PENVALSP EQ 0)) THEN
            YFINAL.ARRY = AA.ID:"*":Y.ACCOUNT:"*":Y.MAIN.ARR.STATUS:"*":Y.LOAN.STATUS:"*":YOD.STAT:"*":ACC.PRINVAL:"*":ACC.PENVAL:"*":ACC.PRINVALSP:"*":ACC.PENVALSP:"*":ACC.PRINVALBL:"*":ACC.PENVALBL:"*":COMACCT.VAL:"*":YORG.ACTUAL.AMOUNT
            LINE.TYP = LINE.TYP.WOF
            GOSUB WRITE.ARRY
            RETURN
        END
    END
    RETURN

WRITE.ARRY:
***********
    WRK.FILE.ID = AA.ID:'.':LINE.TYP
    CALL F.WRITE(FN.DR.REG.AA.PROB.WORKFILE,WRK.FILE.ID,YFINAL.ARRY)
    YFINAL.ARRY = ''
    RETURN

PRICNI.MVMT.APPL:
*---------------
    YARR.CNT = 0; X = 1
    YARR.CNT = DCOUNT(YARR.ASST,FM)
    LOOP
    UNTIL X GT YARR.CNT
        ECB.AMT = 0; ORG.ECB.AMT = 0; ASSET.VAL = ''
        ASSET.VAL = YARR.ASST<X>
        ECB.AMT = Y.BAL.PRIC.GP<X>
        ORG.ECB.AMT = Y.BAL.PRIC.ORG<X>
        TEMP.ASST = ASSET.VAL[4,99]
        IF ORG.ECB.AMT EQ 0 THEN
            X++
            CONTINUE
        END

        IF ASSET.VAL EQ "ACCPRINCIPALINT" THEN
            ACC.PRINVAL += ORG.ECB.AMT
            X++
            CONTINUE
        END
        IF ASSET.VAL EQ "ACCPENALTINT" THEN
            ACC.PENVAL += ORG.ECB.AMT
            X++
            CONTINUE
        END

        IF ASSET.VAL EQ "ACCPRINCIPALINTSP" THEN
            ACC.PRINVALSP += ORG.ECB.AMT
            X++
            CONTINUE
        END
        IF ASSET.VAL EQ "ACCPENALTINTSP" THEN
            ACC.PENVALSP += ORG.ECB.AMT
            X++
            CONTINUE
        END
        IF ASSET.VAL EQ "ACCPRINCIPALINTBL" THEN
            ACC.PRINVALBL += ORG.ECB.AMT
            X++
            CONTINUE
        END
        IF ASSET.VAL EQ "ACCPENALTINTBL" THEN
            ACC.PENVALBL += ORG.ECB.AMT
            X++
            CONTINUE
        END

        IF (TEMP.ASST[1,10] EQ 'COMMITMENT' OR ASSET.VAL EQ 'COMMITMENT') THEN
            COMACCT.VAL += ORG.ECB.AMT
            X++
            CONTINUE
        END
        TEMP.FLG = 1

        X++
    REPEAT
    RETURN

CREDIT.MVMT.APPL:
*---------------
    YFST.VAL = ''; YFST.VAL.CNT = ''; YBL.VAL = ''; BAL.DETAILS = ''
    YFLG.K = 0; Y.BAL.PRIC = 0; YFST.VAL1 = ''
    REQUEST.TYPE<4>='ECB'
    START.DATE = YLST.TODAY
    END.DATE=YLST.TODAY
    YFST.VAL = FIELD(Y.ASSET.TYPE,'-',1)
    YBL.VAL = RIGHT(YFST.VAL,2)
    YFST.VAL1 = YFST.VAL
    LOCATE YFST.VAL IN YARR.ASSTORG<1> SETTING V.POSN THEN
        RETURN
    END ELSE
        YARR.ASSTORG<-1> = YFST.VAL
    END
    ERROR.MESSAGE = ''; BALANCE.CHECK = ''
    BALANCE.TO.CHECK = YFST.VAL
    CALL AA.GET.PERIOD.BALANCES(Y.ACCOUNT, BALANCE.TO.CHECK,REQUEST.TYPE, START.DATE, END.DATE, '',BAL.DETAILS, ERROR.MESSAGE)
    IF BAL.DETAILS<4> EQ 0 THEN
        RETURN
    END
    YTEMP.BAL = 0
    IF YFLG.K NE 1 THEN
        YTEMP.BAL = BAL.DETAILS<4>
    END
    LOCATE YFST.VAL1 IN YARR.ASST<1> SETTING VSPOSN THEN
        Y.TEMP.VAL = 0; YFST.VAL = 0; YTEMP.VAL = 0
        YTEMP.VAL = Y.BAL.PRIC.GP<VSPOSN>
        YFST.VAL = Y.BAL.PRIC.ORG<VSPOSN>
        Y.TEMP.VAL = BAL.DETAILS<4> + YTEMP.VAL
        Y.BAL.PRIC.GP<VSPOSN> = Y.TEMP.VAL
        Y.BAL.PRIC.ORG<VSPOSN> = YFST.VAL + YTEMP.BAL
    END ELSE
        YARR.ASST<-1> = YFST.VAL1
        Y.BAL.PRIC.GP<-1> = BAL.DETAILS<4>
        Y.BAL.PRIC.ORG<-1> = YTEMP.BAL
    END
    RETURN

GET.LOAN.STATUS:
*--------------*
    ArrangementID = AA.ID
    idPropertyClass = 'OVERDUE'; Y.LOAN.STATUS = ''
    idProperty = ''; returnIds = ''; returnConditions = ''; returnError = ''; effectiveDate = ''
    CALL AA.GET.ARRANGEMENT.CONDITIONS(ArrangementID, idPropertyClass, idProperty, effectiveDate, returnIds, returnConditions, returnError)
    R.AA.OVERDUE = RAISE(returnConditions)
    Y.LOAN.STATUS = R.AA.OVERDUE<AA.OD.LOCAL.REF,Y.L.LOAN.STATUS.1.POS>
    RETURN

GET.CLOSED.LOAN.CHK:
********************
    ERR.AA.ACTIVITY.HISTORY = ''; R.AA.ACTIVITY.HISTORY = ''; Y.CLOSE.LN.FLG = 0; YACT.IS.STAT = ''; YACT.ID.ARR = ''
    CALL F.READ(FN.AA.ACTIVITY.HISTORY,AA.ID,R.AA.ACTIVITY.HISTORY,F.AA.ACTIVITY.HISTORY,ERR.AA.ACTIVITY.HISTORY)
    IF R.AA.ACTIVITY.HISTORY THEN
        YACT.ID.ARR = R.AA.ACTIVITY.HISTORY<AA.AH.ACTIVITY>
        YACT.IS.STAT = R.AA.ACTIVITY.HISTORY<AA.AH.ACT.STATUS>
        CHANGE VM TO FM IN YACT.ID.ARR
        CHANGE SM TO FM IN YACT.ID.ARR
        CHANGE VM TO FM IN YACT.IS.STAT
        CHANGE SM TO FM IN YACT.IS.STAT
    END
    Y.PROD.GUP = R.AA.ARR<AA.ARR.PRODUCT.GROUP>
    ERR.REDO.APAP.PROPERTY.PARAM = ''; R.REDO.APAP.PROPERTY.PARAM = ''; YPAYOFF.ACT = ''; YPAY.CNT = 0
    CALL F.READ(FN.REDO.APAP.PROPERTY.PARAM,Y.PROD.GUP,R.REDO.APAP.PROPERTY.PARAM,F.REDO.APAP.PROPERTY.PARAM,ERR.REDO.APAP.PROPERTY.PARAM)
    IF R.REDO.APAP.PROPERTY.PARAM THEN
        YPAYOFF.ACT = R.REDO.APAP.PROPERTY.PARAM<PROP.PARAM.PAYOFF.ACTIVITY>
        YPAY.CNT = DCOUNT(YPAYOFF.ACT,VM)
    END

    YCNT = 1
    LOOP
    WHILE YCNT LE YPAY.CNT
        YPAYOFF.ACT.1 = ''
        YPAYOFF.ACT.1 = R.REDO.APAP.PROPERTY.PARAM<PROP.PARAM.PAYOFF.ACTIVITY,YCNT>
        LOCATE YPAYOFF.ACT.1 IN YACT.ID.ARR<1> SETTING CHG.POSN.1 THEN
            YARR.STAT = YACT.IS.STAT<CHG.POSN.1>
            IF YARR.STAT EQ 'AUTH' THEN
                Y.CLOSE.LN.FLG = 1
                YCNT = YPAY.CNT + 1
                CONTINUE
            END
        END
        YCNT++
    REPEAT
    RETURN
END
