*-------------------------------------------------------------------------------
* <Rating>-111</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE REDO.B.ACCT.DET(ACCOUNT.ID)
*-------------------------------------------------------------------------------
* Company Name      : PAGE SOLUTIONS, INDIA
* Developed By      : Nirmal.P
* Reference         :
*-------------------------------------------------------------------------------
* Subroutine Type   : B
* Attached to       :
* Attached as       : Multi threaded Batch Routine.
*-------------------------------------------------------------------------------
* Input / Output :
*----------------
* IN     :
* OUT    :
*-------------------------------------------------------------------------------
*  M O D I F I C A T I O N S
* ***************************
*-----------------------------------------------------------------------------------------------------------------
* Defect Reference       Modified By                    Date of Change        Change Details
*(RTC/TUT/PACS)                                        (YYYY-MM-DD)
*-----------------------------------------------------------------------------------------------------------------
* PACS00361294          Ashokkumar.V.P                  14/11/2014           Changes based on mapping.
* PACS00361294          Ashokkumar.V.P                  14/11/2014           Changes tO OPEN.ACTUAL.BAL and Removed zero balance
* PACS00361294          Ashokkumar.V.P                  20/05/2015           Added new fields to display in the report
*-----------------------------------------------------------------------------------------------------------------
    $INSERT T24.BP I_COMMON
    $INSERT T24.BP I_EQUATE
    $INSERT T24.BP I_F.ACCOUNT
    $INSERT T24.BP I_F.CUSTOMER
    $INSERT T24.BP I_F.EB.CONTRACT.BALANCES
    $INSERT T24.BP I_F.EB.LOOKUP
    $INSERT T24.BP I_F.DATES
    $INSERT T24.BP I_F.BASIC.INTEREST
    $INSERT T24.BP I_F.GROUP.CREDIT.INT
    $INSERT T24.BP I_F.ACCOUNT.CREDIT.INT
    $INSERT T24.BP I_F.GROUP.DATE
    $INSERT T24.BP I_F.RE.STAT.REP.LINE
    $INSERT T24.BP I_F.COMPANY
    $INSERT T24.BP I_DAS.COMMON
    $INSERT T24.BP I_DAS.BASIC.INTEREST
    $INSERT TAM.BP I_F.REDO.AZACC.DESC
    $INSERT TAM.BP I_F.REDO.H.REPORTS.PARAM
    $INSERT LAPAP.BP I_REDO.B.ACCT.DET.COMMON
    $INCLUDE TAM.BP I_REDO.GENERIC.FIELD.POS.COMMON


    GOSUB PROCESS.PARA
    RETURN

PROCESS.PARA:
*------------
    R.ACCOUNT = ''; ACC.ERR = ''; R.CUSTOMER = ''; CUS.ERR = ''; Y.L.CU.TIPO.CL = ''; CUS.INDUST = ''; C$SPARE(470) = ''
    C$SPARE(451) = ''; C$SPARE(452) = ''; C$SPARE(453) = ''; C$SPARE(454) = ''; C$SPARE(455) = ''; C$SPARE(468) = ''
    C$SPARE(456) = ''; C$SPARE(457) = ''; C$SPARE(458) = ''; C$SPARE(459) = ''; C$SPARE(460) = ''; C$SPARE(469) = ''
    C$SPARE(461) = ''; C$SPARE(462) = ''; C$SPARE(463) = ''; C$SPARE(464) = ''; C$SPARE(467) = ''; C$SPARE(465) = ''
    Y.ALT.ACCT.TYPE = ''; Y.ALT.ACCT.ID = ''; Y.PREV.ACCOUNT = ''; Y.CATEG = ''
    CALL F.READ(FN.ACCOUNT,ACCOUNT.ID,R.ACCOUNT,F.ACCOUNT,ACC.ERR)
    Y.CUSTOMER = R.ACCOUNT<AC.CUSTOMER>
    IF NOT(Y.CUSTOMER) THEN
        RETURN
    END
    Y.CATEGORY = R.ACCOUNT<AC.CATEGORY>
    Y.CURRENCY = R.ACCOUNT<AC.CURRENCY>
    Y.ALT.ACCT.TYPE = R.ACCOUNT<AC.ALT.ACCT.TYPE>
    Y.ALT.ACCT.ID = R.ACCOUNT<AC.ALT.ACCT.ID>
    LOCATE Y.CATEGORY IN CATEG.LIST SETTING CAT.POSN THEN
        Y.CATEG = YL.CATEG<CAT.POSN>
    END ELSE
        RETURN
    END

    CALL F.READ(FN.CUSTOMER,Y.CUSTOMER,R.CUSTOMER,F.CUSTOMER,CUS.ERR)
    Y.L.CU.TIPO.CL = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.TIPO.CL.POS>
    YLCUS.IP.CLI = R.CUSTOMER<EB.CUS.LOCAL.REF,L.TIP.CLI.POSN>
    CUS.INDUST = R.CUSTOMER<EB.CUS.LOCAL.REF,L.APAP.INDUSTRY.POS>
    Y.NAME.1          = R.CUSTOMER<EB.CUS.NAME.1,LNGG>
    Y.NAME.2          = R.CUSTOMER<EB.CUS.NAME.2,LNGG>
    IF NOT(Y.NAME.1) THEN
        Y.NAME.1 = R.CUSTOMER<EB.CUS.NAME.1,1>
    END

    IF NOT(Y.NAME.2) THEN
        Y.NAME.2 = R.CUSTOMER<EB.CUS.NAME.2,1>
    END
    Y.GIVEN.NAMES     = R.CUSTOMER<EB.CUS.GIVEN.NAMES>
    Y.FAMILY.NAME     = R.CUSTOMER<EB.CUS.FAMILY.NAME>
    Y.SHORT.NAME      = R.CUSTOMER<EB.CUS.SHORT.NAME>
    IF NOT(CUS.INDUST) AND YLCUS.IP.CLI EQ '511' THEN
        CUS.INDUST = '950004'
    END
    C$SPARE(466) = CUS.INDUST
    LOC.CODE = ''; COMP.APP.ERR = ''; R.COMP.APP = ''
    LOC.CODE =  R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.LOCAL.POS>
    IF NOT(LOC.CODE) THEN
        Y.CO.CODE = R.CUSTOMER<EB.CUS.COMPANY.BOOK>
        CALL F.READ(FN.COMP.APP,Y.CO.CODE,R.COMP.APP,F.COMP.APP,COMP.APP.ERR)
        LOC.CODE = R.COMP.APP<EB.COM.LOCAL.REF,LCOMP.LOCALIDAD.POS>
    END
    C$SPARE(470) = LOC.CODE
    GOSUB FETCH.1.TO.3.FLDS
    GOSUB FETCH.4.TO.6.FLDS
    GOSUB FETCH.7.TO.10.FLDS
    GOSUB FETCH.11.TO.12.FLDS
    GOSUB FETCH.22.TO.23.FLDS
    IF Y.TOTAL.INT OR YOPEN.BAL THEN
        GOSUB MAP.RCL.RECORD
    END
    RETURN

FETCH.1.TO.3.FLDS:
*-----------------
    X.TODAY = R.DATES(EB.DAT.LAST.WORKING.DAY)
    Y.COURT.DT = X.TODAY[7,2]:'/':X.TODAY[5,2]:'/':X.TODAY[1,4]
    C$SPARE(451) = Y.COURT.DT
    C$SPARE(453) = ACCOUNT.ID

    LOCATE 'AT.SEL.CODES' IN PARAM.FIELD.NAME SETTING AT.POS THEN
        AT.LIST = R.REDO.H.REPORTS.PARAM<REDO.REP.PARAM.FIELD.VALUE><1,AT.POS>
        AT.INT = AT.LIST<1,1,1>
        AT.CR = AT.LIST<1,1,2>
        AT.DB = AT.LIST<1,1,3>
    END
    R.EB.CONTRACT.BALANCES = ''; EB.CONTRACT.BALANCES.ERR = ''
    CALL F.READ(FN.EB.CONTRACT.BALANCES,ACCOUNT.ID,R.EB.CONTRACT.BALANCES,F.EB.CONTRACT.BALANCES,EB.CONTRACT.BALANCES.ERR)
    IF R.EB.CONTRACT.BALANCES THEN
        GOSUB DEF.DESC.CRF.DC
        GOSUB DEF.DESC.CRF.5000
    END

    C$SPARE(453) = Y.AZACC.DESC
    C$SPARE(462) = Y.AZACC.INT.DESC
    C$SPARE(455) = Y.CATEG
    RETURN

DEF.DESC.CRF.DC:
****************
    Y.REGULATORY.AC.ACC = ''; Y.IN.CONSOL.KEY = ''; YCRF.TYPE = ''
    Y.CONSOL.KEY = R.EB.CONTRACT.BALANCES<ECB.CONSOL.KEY>
    IF Y.CONSOL.KEY THEN
        YCRF.TYPE = R.EB.CONTRACT.BALANCES<ECB.OPEN.ASSET.TYPE>
        Y.CONSOL.PART = FIELD(Y.CONSOL.KEY,'.',1,16)
        Y.IN.CONSOL.KEY = Y.CONSOL.PART:'.':YCRF.TYPE
        Y.VARIABLE = ''; Y.RPRTS = ''; Y.LINES = ''
        CALL RE.CALCUL.REP.AL.LINE(Y.IN.CONSOL.KEY,Y.RPRTS,Y.LINES,Y.VARIABLE)
        Y.LINE = Y.RPRTS:'.':Y.LINES
        CALL F.READ(FN.RE.STAT.REP.LINE,Y.LINE,R.LINE,F.RE.STAT.REP.LINE,REP.ERR)
        Y.REGULATORY.ACC.NO = R.LINE<RE.SRL.DESC,1>
        Y.AZACC.DESC = Y.REGULATORY.ACC.NO
    END
    RETURN

DEF.DESC.CRF.5000:
*****************
    Y.REGULATORY.AC.ACC = ''; Y.IN.CONSOL.KEY = ''
    Y.CONSOL.KEY = R.EB.CONTRACT.BALANCES<ECB.CONSOL.KEY>
    IF Y.CONSOL.KEY THEN
        Y.CONSOL.PART = FIELD(Y.CONSOL.KEY,'.',1,16)
        Y.IN.CONSOL.KEY = Y.CONSOL.PART:'.':AT.INT
        Y.VARIABLE = ''; Y.RPRTS = ''; Y.LINES = ''
        CALL RE.CALCUL.REP.AL.LINE(Y.IN.CONSOL.KEY,Y.RPRTS,Y.LINES,Y.VARIABLE)
        Y.LINE = Y.RPRTS:'.':Y.LINES
        CALL F.READ(FN.RE.STAT.REP.LINE,Y.LINE,R.LINE,F.RE.STAT.REP.LINE,REP.ERR)
        Y.REGULATORY.ACC.NO = R.LINE<RE.SRL.DESC,1>
        Y.AZACC.INT.DESC = Y.REGULATORY.ACC.NO
    END
    RETURN

FETCH.4.TO.6.FLDS:
*-----------------
    LOCATE Y.CURRENCY IN CUR.LIST SETTING CURRENCY.POS THEN
        Y.CURR = YL.CURR<CURRENCY.POS>
    END
    C$SPARE(456) = Y.CURR

    BALANCE.TYPE = AT.INT
    SUB.TYPE= ''; BAL.DATE = Y.TODAY; Y.TOTAL.INT = ''; ECB.BAL.LCY= ''
    CALL AC.GET.ECB.BALANCE(ACCOUNT.ID, BALANCE.TYPE, SUB.TYPE, BAL.DATE,Y.TOTAL.INT,ECB.BAL.LCY)

    C$SPARE(457) = Y.TOTAL.INT

    YOPEN.BAL = R.ACCOUNT<AC.OPEN.ACTUAL.BAL>
    C$SPARE(467) = YOPEN.BAL

    Y.OVR.SEGM = R.CUSTOMER<EB.CUS.LOCAL.REF,Y.OVR.SEGM.POS>
    Y.SEGMENTO = R.CUSTOMER<EB.CUS.LOCAL.REF,Y.SEGMENTO.POS>
    IF Y.OVR.SEGM NE '' THEN
        Y.SUB.POP = Y.OVR.SEGM
    END ELSE
        Y.SUB.POP = Y.SEGMENTO
    END
    C$SPARE(458) = Y.SUB.POP
    RETURN

FETCH.7.TO.10.FLDS:
*------------------
    OUT.ARR = ''
    CALL DR.REG.GET.CUST.TYPE(R.CUSTOMER,OUT.ARR)
    C$SPARE(459) = OUT.ARR<1>
    Y.CONST.VAL = '001'
    Y.VAL.CONSTANT = FMT(Y.CONST.VAL,'R%3')
    C$SPARE(464) = Y.VAL.CONSTANT

    GOSUB GET.VINC.RELAT
    C$SPARE(460) = Y.VINC.TYPE

    Y.OPEN.DATE = R.ACCOUNT<AC.OPENING.DATE>
    Y.OPENING.DT = Y.OPEN.DATE[7,2]:'/':Y.OPEN.DATE[5,2]:'/':Y.OPEN.DATE[1,4]
    IF Y.OPENING.DT THEN
        C$SPARE(463) = Y.OPENING.DT
    END

    STAT1 = ''; STAT2 = ''; Y.POS.RESTRICT = '';STAT.VAL = ''
    Y.STATUS1.POS = Y.POS<2,1>
    STAT1 = R.ACCOUNT<AC.LOCAL.REF,Y.STATUS1.POS>
    Y.STATUS2.POS = Y.POS<2,2>
    STAT2 = R.ACCOUNT<AC.LOCAL.REF,Y.STATUS2.POS>
    Y.POS.RESTRICT = R.ACCOUNT<AC.POSTING.RESTRICT>
    STAT2.CNT = DCOUNT(STAT2,SM)

    IF STAT2.CNT GE 2 THEN
        GOSUB GET.STATUS.MULTIVAL
        C$SPARE(461) = STAT.VAL
        RETURN
    END

    BEGIN CASE
    CASE STAT2 EQ 'DECEASED'
        STAT.VAL = 'R'
    CASE STAT2 EQ 'GARNISHMENT'
        STAT.VAL = 'E'
    CASE STAT2 EQ 'GUARANTEE.STATUS'
        STAT.VAL = 'G'
    CASE STAT1 EQ 'ACTIVE' OR STAT1 EQ '6MINACTIVE' OR STAT1 EQ ''
        STAT.VAL = 'A'
    CASE STAT1 EQ 'ABANDONED'
        STAT.VAL = 'B'
    CASE STAT1 EQ '3YINACTIVE'
        STAT.VAL = 'I'
    END CASE

    C$SPARE(461) = STAT.VAL
    RETURN

GET.STATUS.MULTIVAL:
********************
    D.POSN = ''; G.POSN = ''
    LOCATE 'DECEASED' IN STAT2<1,1,1> SETTING D.POSN THEN
        STAT.VAL = 'R'
    END ELSE
        LOCATE 'GARNISHMENT' IN STAT2<1,1,1> SETTING G.POSN THEN
            STAT.VAL = 'E'
        END ELSE
            STAT.VAL = 'G'
        END
    END
    RETURN

GET.VINC.RELAT:
***************
    Y.RELATION.CODE = R.CUSTOMER<EB.CUS.RELATION.CODE>
    IF Y.RELATION.CODE EQ '' THEN
        Y.VINC.TYPE = R.EB.LOOKUP< EB.LU.DATA.VALUE,1>
        RETURN
    END
    Y.REL.CNT = DCOUNT(Y.RELATION.CODE,VM)
    LOOP
        REMOVE Y.RLN.CODE FROM Y.RELATION.CODE SETTING RLN.POS
    WHILE Y.RLN.CODE:RLN.POS
        LOCATE Y.RLN.CODE IN Y.LNK.TYP.NAME<1,1> SETTING Y.LNK.TYP.POS THEN
            Y.VINC.TYPE = R.EB.LOOKUP< EB.LU.DATA.VALUE,Y.LNK.TYP.POS>
            RETURN
        END
    REPEAT
    IF NOT(Y.VINC.TYPE) THEN
        Y.VINC.TYPE = R.EB.LOOKUP< EB.LU.DATA.VALUE,1>
    END
    RETURN

FETCH.11.TO.12.FLDS:
*-------------------
    C$SPARE(452) = OUT.ARR<2>
    Y.CUST.NAME = ''; Y.CUST.GN.NAME = ''
    IF Y.L.CU.TIPO.CL EQ "PERSONA FISICA" OR Y.L.CU.TIPO.CL EQ "CLIENTE MENOR" THEN
        Y.CUST.NAME    = Y.GIVEN.NAMES
        Y.CUST.GN.NAME = Y.FAMILY.NAME
        Y.COMP.DEBTOR = Y.CUST.NAME:' ':Y.CUST.GN.NAME
    END
    IF Y.L.CU.TIPO.CL EQ "PERSONA JURIDICA" THEN
        Y.CUST.NAME    = Y.NAME.1:' ':Y.NAME.2
        Y.CUST.GN.NAME = Y.SHORT.NAME
        Y.COMP.DEBTOR = Y.CUST.NAME
    END
    C$SPARE(454) = Y.COMP.DEBTOR
    INT.RATE = ''
    CALL DR.REP.AC.INT.CALC(ACCOUNT.ID,R.ACCOUNT,INT.RATE)
    C$SPARE(465) = INT.RATE
    RETURN

FETCH.22.TO.23.FLDS:
********************
    YDEP.LP = ''
    LOCATE Y.CATEGORY IN CATEGINT.LIST SETTING CATIN.POS THEN
        C$SPARE(468) = 'V'
    END ELSE
        C$SPARE(468) = 'F'
    END

    IF CUS.INDUST GE '651000' AND CUS.INDUST LE '672204' THEN
        YDEP.LP = 'Y'
    END ELSE
        YDEP.LP = 'M'
    END
    IF CUS.INDUST EQ "651104" THEN
        YDEP.LP = 'M'
    END
    C$SPARE(469) = YDEP.LP
    RETURN

MAP.RCL.RECORD:
*--------------
    IF Y.TOTAL.INT EQ 0 AND YOPEN.BAL EQ 0 THEN
        RETURN
    END

    R.RETURN.MSG = ''
    MAP.FMT = 'MAP'
    ID.RCON.L = "REDO.RCL.RN07"
    APP = FN.ACCOUNT
    ID.APP = ACCOUNT.ID
    R.APP = R.ACCOUNT
    CALL RAD.CONDUIT.LINEAR.TRANSLATION (MAP.FMT,ID.RCON.L,APP,ID.APP,R.APP,R.RETURN.MSG,ERR.MSG)
    IF R.RETURN.MSG THEN
        WRK.FILE.ID = Y.CURRENCY:'.':ACCOUNT.ID
        CALL F.WRITE(FN.DR.REG.RIEN7.WORKFILE,WRK.FILE.ID,R.RETURN.MSG)
    END
    RETURN

END
