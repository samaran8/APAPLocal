*-----------------------------------------------------------------------------
* <Rating>-136</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE DR.REG.DIVISAS.EXTRACT(REC.ID)
*----------------------------------------------------------------------------
* Company Name   : APAP
* Company Name   : APAP
* Program Name   : DR.REG.DIVISAS.EXTRACT
*----------------------------------------------------------------------------
* Description:
*------------
* This multi-thread job is meant for to extact the TELLER Details for each Customer.
*----------------------------------------------------------------------------
*
* Modification History :
* ----------------------
*   Date       Author              Modification Description
* 05-Dec-2017  Ashokkumar.V.P      CN007023 - Initial Version.
* 17-Jan-2018  Ashokkumar          CN008165 - new fields Grupo Contraparte y Contraparte added
* 09-Jul-2018  Anthony Mart√≠nez    CN008837 - Exclude internal transactions made by APAP
*----------------------------------------------------------------------------

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_BATCH.FILES
    $INSERT I_TSA.COMMON
    $INSERT I_F.CUSTOMER
    $INSERT I_F.TELLER
    $INSERT I_F.FOREX
    $INSERT I_F.COMPANY
    $INSERT I_F.FUNDS.TRANSFER
    $INSERT I_F.EB.LOOKUP
    $INSERT I_F.POS.MVMT.TODAY
    $INSERT I_F.FT.TXN.TYPE.CONDITION
    $INCLUDE LAPAP.BP I_DR.REG.DIVISAS.EXTRACT.COMMON
    $INCLUDE LAPAP.BP I_F.REDO.APAP.INSTIT.FINANC.PARAM
*
    GOSUB PROCESS
    RETURN

PROCESS:
*********
    RETURN.MSG = ''; YGRP.CONT = ''
    IF REC.ID[1,2] EQ 'TT' THEN
        GOSUB TELLER.PROCESS
        RETURN
    END
    IF REC.ID[1,2] EQ 'FT' THEN
        GOSUB FUNDS.TRANSFER.PROCESS
    END
    RETURN

TELLER.PROCESS:
***************
    R.TELLER = ''; TELLER.ERR = ''; RETURN.MSG = ''
    CALL F.READ(FN.TELLER,REC.ID,R.TELLER,F.TELLER,TELLER.ERR)
    IF NOT(R.TELLER) THEN
        CALL EB.READ.HISTORY.REC(F.TELLER.HST,REC.ID,R.TELLER,TELLER.ERR)
    END
    YREC.STATUS = R.TELLER<TT.TE.RECORD.STATUS>
    IF NOT(R.TELLER) OR YREC.STATUS EQ 'REVE' THEN
        RETURN
    END

    YTRANS.CODE = R.TELLER<TT.TE.TRANSACTION.CODE>
    IF R.TELLER<TT.TE.CURRENCY.1> NE LCCY OR R.TELLER<TT.TE.CURRENCY.2> NE LCCY THEN
        GOSUB FETCH.TT.FIELDS
    END
    RETURN

GET.CUSTOMER:
*************
    R.CUSTOMER = ''; CUSTOMER.ERR = ''; YRNC.VAL = '';  YCUS.SECTOR = ''; FLD12 = ''
    CALL F.READ(FN.CUSTOMER,CUS.ID,R.CUSTOMER,F.CUSTOMER,CUSTOMER.ERR)
    CUS.NATION = R.CUSTOMER<EB.CUS.NATIONALITY>
    YRNC.VAL = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.RNC.POS>
    YCUS.SECTOR = R.CUSTOMER<EB.CUS.SECTOR>

    BEGIN CASE
    CASE R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.TIPO.CL.POS> EQ 'PERSONA FISICA' OR R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.TIPO.CL.POS> EQ 'CLIENTE MENOR'
        FLD12 = R.CUSTOMER<EB.CUS.GIVEN.NAMES>:' ':R.CUSTOMER<EB.CUS.FAMILY.NAME>
    CASE R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.TIPO.CL.POS> EQ 'PERSONA JURIDICA'
        FLD12 = R.CUSTOMER<EB.CUS.NAME.1>:' ':R.CUSTOMER<EB.CUS.NAME.2>
    END CASE
    RETURN

INIT.FLD:
*************
    FLD1 = '' ;FLD2 = '' ;FLD3 = '' ;FLD4 = '' ;FLD5 = '' ;FLD6 = '' ;FLD7 = '' ;FLD8 = '' ;FLD9 = ''
    FLD10 = '' ;FLD11 = '' ;FLD12 = '' ;FLD13 = '' ;FLD14 = ''; YFLD1 = ''; YPROCES.DTE = ''; YTXN.TYPE = ''
    NO.REPORT = 'NO'
    RETURN

READ.INST.PARAM:
****************
    R.REDO.APAP.INSTIT.FINANC.PARAM = ''; ERR.REDO.APAP.INSTIT.FINANC.PARAM = ''
    CALL F.READ(FN.REDO.APAP.INSTIT.FINANC.PARAM,YRNC.VAL,R.REDO.APAP.INSTIT.FINANC.PARAM,F.REDO.APAP.INSTIT.FINANC.PARAM,ERR.REDO.APAP.INSTIT.FINANC.PARAM)
    IF R.REDO.APAP.INSTIT.FINANC.PARAM THEN
        YFLD2 = R.REDO.APAP.INSTIT.FINANC.PARAM<INST.FIN.TIPO.INSTITUTION>
        YFLD3 = R.REDO.APAP.INSTIT.FINANC.PARAM<INST.FIN.NUMERO.INSTITUTION>
        IF NOT(CUS.ID) THEN
            CUS.ID = R.REDO.APAP.INSTIT.FINANC.PARAM<INST.FIN.CUSTOMER.CODE>
            GOSUB GET.CUSTOMER
        END
    END
    RETURN

FETCH.TT.FIELDS:
****************
*
    GOSUB INIT.FLD
    DTE2 = R.TELLER<TT.TE.VALUE.DATE.1>
    DTE3 = R.TELLER<TT.TE.AUTH.DATE>
    Y.GDAYS = 1
    FLD5 = DTE2[7,2]:'/':DTE2[5,2]:'/':DTE2[1,4]
    FLD6 = DTE3[7,2]:'/':DTE3[5,2]:'/':DTE3[1,4]
    CUS.ID = ''
    CUS.ID = R.TELLER<TT.TE.CUSTOMER.1>
    IF CUS.ID THEN
        CUS.ID = CUS.ID
    END ELSE
        CUS.ID = R.TELLER<TT.TE.CUSTOMER.2>
    END

    NC.CLNT.TYPE = R.TELLER<TT.TE.LOCAL.REF,L.TT.CLNT.TYPE.POS>
    TT.LEGAL.VAL = R.TELLER<TT.TE.LOCAL.REF,L.TT.LEGAL.ID.POS>
    LEGAL.VAL.1 = FIELD(TT.LEGAL.VAL,'.',1)
    FLD12 = FIELD(TT.LEGAL.VAL,'.',3)
    LEGAL.VAL.4 = FIELD(TT.LEGAL.VAL,".",4)

    GOSUB GET.CUSTOMER

    IF NOT(R.CUSTOMER) AND LEGAL.VAL.1 EQ 'RNC' THEN
        YRNC.VAL = FIELD(TT.LEGAL.VAL,'.',2)
    END

    IF NOT(YRNC.VAL) THEN
        CUS.ID = R.TELLER<TT.TE.LOCAL.REF,L.TT.DOC.NUM.POS>
    END

    IF YRNC.VAL THEN
        GOSUB READ.INST.PARAM
        IF YRNC.VAL EQ '401007551' THEN
            R.REDO.APAP.INSTIT.FINANC.PARAM = '401007551'
            YGRP.CONT = 'BCRD'
            YFLD2 = 'NA'
            YFLD3 = '001'
        END

*--TRANSACCION INTERNA, PERTENECE A APAP
        IF YRNC.VAL EQ '4-01-00013-1' OR YRNC.VAL EQ '401000131' THEN
            RETURN
        END

        IF R.REDO.APAP.INSTIT.FINANC.PARAM THEN
            GOSUB GET.TT.FLD10
            GOSUB GET.TT.FLD2.3.4
            GOSUB UPDATE.WORKFILE
        END
    END
    RETURN

GET.TT.FLD10:
************
    YTT.RATE = ''
    YTT.RATE = R.TELLER<TT.TE.DEAL.RATE>
    IF NOT(YTT.RATE) THEN
        YTT.RATE = R.TELLER<TT.TE.RATE.1>
    END
    IF NOT(YTT.RATE) THEN
        YTT.RATE = R.TELLER<TT.TE.RATE.2>
    END
    FLD10 = YTT.RATE
    RETURN

GET.TT.FLD2.3.4:
****************
    YMARKER = R.TELLER<TT.TE.DR.CR.MARKER>
    BEGIN CASE
    CASE R.TELLER<TT.TE.CURRENCY.1> NE LCCY AND YMARKER EQ 'CREDIT'
        FLD4 = "V"
        FLD8 = R.TELLER<TT.TE.CURRENCY.1>
        FLD9 = R.TELLER<TT.TE.AMOUNT.FCY.1>
    CASE R.TELLER<TT.TE.CURRENCY.1> NE LCCY AND YMARKER EQ 'DEBIT'
        FLD4 = "C"
        FLD8 = R.TELLER<TT.TE.CURRENCY.1>
        FLD9 = R.TELLER<TT.TE.AMOUNT.FCY.1>
    CASE R.TELLER<TT.TE.CURRENCY.2> NE LCCY AND YMARKER EQ 'DEBIT'
        FLD4 = "V"
        FLD8 = R.TELLER<TT.TE.CURRENCY.2>
        FLD9 = R.TELLER<TT.TE.AMOUNT.FCY.2>
    CASE R.TELLER<TT.TE.CURRENCY.2> NE LCCY AND YMARKER EQ 'CREDIT'
        FLD4 = "C"
        FLD8 = R.TELLER<TT.TE.CURRENCY.2>
        FLD9 = R.TELLER<TT.TE.AMOUNT.FCY.2>
    END CASE
    RETURN

FUNDS.TRANSFER.PROCESS:
***********************
    R.FUNDS.TRANSFER = ''; RETURN.MSG = ''; FUNDS.TRANSFER.ERR = ''
    CALL F.READ(FN.FUNDS.TRANSFER,REC.ID,R.FUNDS.TRANSFER,F.FUNDS.TRANSFER,FUNDS.TRANSFER.ERR)
    IF NOT(R.FUNDS.TRANSFER) THEN
        CALL EB.READ.HISTORY.REC(F.FUNDS.TRANSFER.HST,REC.ID,R.FUNDS.TRANSFER,FUNDS.TRANSFER.ERR)
    END
    YREC.STATUS = R.FUNDS.TRANSFER<FT.RECORD.STATUS>
    IF NOT(R.FUNDS.TRANSFER) OR YREC.STATUS EQ 'REVE' THEN
        RETURN
    END

    YTRANS.CODE = R.FUNDS.TRANSFER<FT.TRANSACTION.TYPE>
    IF R.FUNDS.TRANSFER<FT.DEBIT.CURRENCY> EQ R.FUNDS.TRANSFER<FT.CREDIT.CURRENCY> THEN
        RETURN
    END
    GOSUB FETCH.FT.FIELDS
    RETURN

FETCH.FT.FIELDS:
****************
*
    GOSUB INIT.FLD
    DTE1 = R.FUNDS.TRANSFER<FT.DEBIT.VALUE.DATE>
    YPROCES.DTE = R.FUNDS.TRANSFER<FT.PROCESSING.DATE>
    IF DTE1 AND YPROCES.DTE THEN
        Y.GDAYS = 'C'
        CALL CDD('',DTE1,YPROCES.DTE,Y.GDAYS)
    END
    FLD5 = DTE1[7,2]:'/':DTE1[5,2]:'/':DTE1[1,4]
    FLD6 = YPROCES.DTE[7,2]:'/':YPROCES.DTE[5,2]:'/':YPROCES.DTE[1,4]

    CUS.ID = ''; NC.CLNT.TYPE = ''; FT.LEGAL.VAL = ''
    BEGIN CASE
    CASE R.FUNDS.TRANSFER<FT.DEBIT.CURRENCY> NE LCCY AND R.FUNDS.TRANSFER<FT.DEBIT.CURRENCY> NE ''
        FLD4 =  "C"
        CUS.ID = R.FUNDS.TRANSFER<FT.DEBIT.CUSTOMER>
        FLD9 = R.FUNDS.TRANSFER<FT.AMOUNT.DEBITED>[4,99]
        FLD8 = R.FUNDS.TRANSFER<FT.DEBIT.CURRENCY>
    CASE R.FUNDS.TRANSFER<FT.CREDIT.CURRENCY> NE LCCY AND R.FUNDS.TRANSFER<FT.CREDIT.CURRENCY> NE ''
        FLD4 =  "V"
        CUS.ID = R.FUNDS.TRANSFER<FT.CREDIT.CUSTOMER>
        FLD9 = R.FUNDS.TRANSFER<FT.AMOUNT.CREDITED>[4,99]
        FLD8 = R.FUNDS.TRANSFER<FT.CREDIT.CURRENCY>
    END CASE

    NC.CLNT.TYPE = R.FUNDS.TRANSFER<FT.LOCAL.REF,L.FT.CLNT.TYPE.POS>
    FT.LEGAL.VAL = R.FUNDS.TRANSFER<FT.LOCAL.REF,L.FT.LEGAL.ID.POS>

    GOSUB GET.CUSTOMER

    IF NOT(R.CUSTOMER) AND FT.LEGAL.VAL THEN
        LEGAL.VAL.1 = FIELD(FT.LEGAL.VAL,'.',1)
        FLD12 = FIELD(FT.LEGAL.VAL,'.',3)
        IF LEGAL.VAL.1 THEN
            YRNC.VAL = FIELD(FT.LEGAL.VAL,'.',2)
        END
    END

    IF NOT(YRNC.VAL) THEN
        CUS.ID = R.FUNDS.TRANSFER<FT.CHARGED.CUSTOMER>
        GOSUB GET.CUSTOMER
    END

    IF YRNC.VAL THEN
        GOSUB READ.INST.PARAM
        IF YRNC.VAL EQ '401007551' THEN
            R.REDO.APAP.INSTIT.FINANC.PARAM = '401007551'
            YGRP.CONT = 'BCRD'
            YFLD2 = 'NA'
            YFLD3 = '001'
        END

*--TRANSACCION INTERNA, PERTENECE A APAP
        IF YRNC.VAL EQ '4-01-00013-1' OR YRNC.VAL EQ '401000131' THEN
            RETURN
        END

        IF R.REDO.APAP.INSTIT.FINANC.PARAM THEN
            GOSUB GET.FT.FLD10
            GOSUB UPDATE.WORKFILE
        END
    END
    RETURN

GET.FT.FLD10:
************
    FLD10 = R.FUNDS.TRANSFER<FT.CUSTOMER.RATE>
    IF NOT(FLD10) THEN
        FLD10 = R.FUNDS.TRANSFER<FT.TREASURY.RATE>
    END
    RETURN

GET.GRUPO.CONTRA:
*****************
*   LOCATE YCUS.SECTOR IN Y.SECT.VAL.ARR<1,1> SETTING SEC.POS THEN
*       YGRP.CONT = Y.SECT.DIS.ARR<1,SEC.POS>
*   END
    IF CUS.NATION NE 'DO' THEN
        YGRP.CONT = 'EFE'
    END
    IF YGRP.CONT EQ '' AND CUS.NATION EQ 'DO' THEN
        YGRP.CONT = 'EIF'
    END
    IF YRNC.VAL EQ '401007551' THEN
        YGRP.CONT = 'BCRD'
    END
    RETURN

UPDATE.WORKFILE:
***************
    YDELIM = "|"
    IF NOT(YRNC.VAL) THEN
        RETURN
    END
    GOSUB GET.GRUPO.CONTRA

    FLD2 = FMT(YFLD2,'L#2')
    FLD3 = FMT(YFLD3,'R%3')
    FLD4 = FMT(FLD4,'L#1')
    FLD5 = FMT(FLD5,'L#10')
    FLD6 = FMT(FLD6,'L#10')
    FLD7 = FMT('SP','L#2')
    FLD8 = FMT(FLD8,'L#3')
    FLD9 = FMT(FLD9,'R2%17')
*FLD10 = FMT(FLD10,'R2%5')
    FLD10 = DROUND(FLD10,4)   ;* Tasa promedio ponderada
    FLD12 = FMT(FLD12,'L#100')
*
    RETURN.MSG = ''
    RETURN.MSG = FLD2:YDELIM:FLD3:YDELIM:FLD4:YDELIM:FLD5:YDELIM:FLD6:YDELIM:FLD7:YDELIM:FLD8:YDELIM:FLD9:YDELIM:FLD10:YDELIM:YGRP.CONT:YDELIM:FLD12
    IF RETURN.MSG THEN
        CALL F.WRITE(FN.DR.REG.DIVIAS.WORKFILE, REC.ID, RETURN.MSG)
    END
    RETURN
*----------------------------------------------------------------------------
END
