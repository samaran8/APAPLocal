*-----------------------------------------------------------------------------
* <Rating>472</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE LAPAP.MIGRATE.WOFF
*----------------------------------------------------------------------------------------------------
* Description           : Rutina MAIN para la excritura de los contratos WOFF, que seran migrados
*                         esta rutina lee de un archivo savelist y inserta los contratos que estan dentro del savelist
*                         en la tabla F.ST.LAPAP.INFILEPRESTAMO
* Developed On          : 19-11-2019
* Developed By          : APAP
* Development Reference : GDC-704
*----------------------------------------------------------------------------------------------------
    $INSERT T24.BP I_COMMON
    $INSERT T24.BP I_EQUATE
    $INSERT T24.BP I_BATCH.FILES
    $INSERT T24.BP I_TSA.COMMON
    $INSERT BP I_F.ST.LAPAP.INFILEPRESTAMO
    $INSERT T24.BP I_F.EB.CONTRACT.BALANCES
    $INSERT T24.BP I_F.DATES
    $INSERT T24.BP I_F.AA.BILL.DETAILS
    $INSERT T24.BP I_F.ACCOUNT
    $INSERT T24.BP I_F.AA.ARRANGEMENT.ACTIVITY
    $INSERT T24.BP I_F.AA.OVERDUE
    $INSERT T24.BP I_F.AA.INTEREST
    $INSERT T24.BP I_F.AA.TERM.AMOUNT
    $INSERT T24.BP I_F.AA.ACCOUNT.DETAILS
    $INSERT T24.BP I_F.AA.PAYMENT.SCHEDULE
    $INSERT T24.BP I_F.AA.INTEREST.ACCRUALS
    $INSERT T24.BP I_F.AA.ACCOUNT
    $INSERT T24.BP I_F.AA.CUSTOMER
    $INSERT T24.BP I_F.AA.ARRANGEMENT
    $INSERT TAM.BP I_F.REDO.H.REPORTS.PARAM

    GOSUB MAIN.PROCESS


    RETURN

MAIN.PROCESS:
    GOSUB OPEN.FILES
    CALL EB.CLEAR.FILE(FN.ST.LAPAP.INFILEPRESTAMO, FV.ST.LAPAP.INFILEPRESTAMO)
    R.CHK.DIR = '' ; CHK.DIR.ERROR = ''
    CALL F.READ(FN.CHK.DIR,Y.ARCHIVO.CASTIGO,R.CHK.DIR,F.CHK.DIR,CHK.DIR.ERROR)

    IF NOT(R.CHK.DIR) THEN
        RETURN
    END

    LOOP
        REMOVE Y.ARRANGEMENT.ID FROM R.CHK.DIR SETTING PROCESO.POS
    WHILE Y.ARRANGEMENT.ID DO
        AA.ARR.ID = Y.ARRANGEMENT.ID
        AA.ARR.ID = CHANGE(AA.ARR.ID,',',FM)
        AA.ARR.ID = AA.ARR.ID<1>
        CALL REDO.B.CON.LNS.BY.DEBTOR.AA.RECS(AA.ARR.ID,OUT.RECORD)
        R.AA.TERM.AMOUNT.APP      = FIELD(OUT.RECORD,"*",1)
        R.AA.ACCOUNT.DETAILS.APP  = FIELD(OUT.RECORD,"*",2)
        R.AA.PAYMENT.SCHEDULE.APP = FIELD(OUT.RECORD,"*",3)
        R.INTEREST.ACCRUALS.APP   = FIELD(OUT.RECORD,"*",4)
        R.AA.OVERDUE.APP          = FIELD(OUT.RECORD,"*",5)
        R.AA.LIMIT.APP            = FIELD(OUT.RECORD,"*",6)
        R.AA.INTEREST.APP         = FIELD(OUT.RECORD,"*",7)
        R.AA.ACCOUNT.APP          = FIELD(OUT.RECORD,"*",8)
        R.AA.CUSTOMER             = FIELD(OUT.RECORD,"*",9)
        GOSUB GET.ARRAGEMENT

        IF Y.ARR.STATUS EQ 'CURRENT' OR Y.ARR.STATUS EQ 'EXPIRED' THEN
            IF (Y.PRODUCT.GROUP EQ 'LINEAS.DE.CREDITO.TC' OR Y.PRODUCT.GROUP EQ 'RODUCTOS.WOF') THEN
                CONTINUE
            END
            GOSUB GET.OVERDUE
            GOSUB GET.ACCOUNT
            GOSUB GET.TERM.AMOUNT
            GOSUB GET.AA.ACCOUNT
            GOSUB GET.AA.CUSTOMER
            GOSUB GET.AA.PAYMENT.SHEDULE
            GOSUB GET.AA.ACCOUNT.DETAILS.BILLS
            GOSUB GET.BALANCES.EB.DETALLES
            GOSUB SET.WRITE.INFILE
        END

    REPEAT

    RETURN

GET.ARRAGEMENT:
*******************
    R.ARRANGEMENT = ''; ERR.ARRAGEMENT = ''; CLIENTE.ID = ''
    CALL AA.GET.ARRANGEMENT(AA.ARR.ID,R.ARRANGEMENT,ERR.ARRAGEMENT)
    Y.ID.CLIENTE = R.ARRANGEMENT<AA.ARR.CUSTOMER>
    Y.PRODUCT.GROUP = R.ARRANGEMENT<AA.ARR.PRODUCT.GROUP>
    Y.PRODUCT = R.ARRANGEMENT<AA.ARR.PRODUCT>
    Y.LINKED.APPL.ID = R.ARRANGEMENT<AA.ARR.LINKED.APPL.ID,1,1>
    Y.ARR.STATUS =  R.ARRANGEMENT<AA.ARR.ARR.STATUS>
    Y.NROPRESTAMO = Y.LINKED.APPL.ID
    Y.FECHA.ORIGINAL.APE = R.ARRANGEMENT<AA.ARR.ORIG.CONTRACT.DATE>
    Y.START.DATE =  R.ARRANGEMENT<AA.ARR.START.DATE>
    Y.COMPANY =  R.ARRANGEMENT<AA.ARR.CO.CODE>
    Y.FECHA.APERTURA = TODAY

    BEGIN CASE
    CASE Y.PRODUCT.GROUP EQ 'CONSUMO'
        Y.TIPO.PRODUCTO = 'CONSUMO.WOF'
    CASE Y.PRODUCT.GROUP EQ 'COMERCIAL'
        Y.TIPO.PRODUCTO = 'COMERCIAL.WOF'
    CASE Y.PRODUCT.GROUP EQ 'HIPOTECARIO'
        Y.TIPO.PRODUCTO = 'HIPOTECARIO.WOF'
    CASE Y.PRODUCT.GROUP EQ 'LINEAS.DE.CREDITO'
        Y.TIPO.PRODUCTO = 'LINEAS.DE.CREDITO.WOF'
    CASE 1
        Y.TIPO.PRODUCTO = Y.PRODUCT.GROUP
    END CASE

    RETURN

GET.OVERDUE:
    Y.FECHA.CASTIGO = R.AA.OVERDUE.APP<AA.OD.LOCAL.REF,L.STATUS.CHG.DT.POS>
    Y.ESTADO.ACTUAL = R.AA.OVERDUE.APP<AA.OD.LOCAL.REF,L.LOAN.STATUS.1.POS>
    Y.ESTADO.ACTUAL = CHANGE(Y.ESTADO.ACTUAL,SM,FM)
    Y.ESTADO.ACTUAL = CHANGE (Y.ESTADO.ACTUAL,VM,FM)
    Y.ESTADO.ACTUAL = Y.ESTADO.ACTUAL<1>
    Y.FECHA.CASTIGO = R.AA.OVERDUE.APP<AA.OD.LOCAL.REF,L.STATUS.CHG.DT.POS>
    Y.FECHA.CASTIGO = CHANGE(Y.FECHA.CASTIGO,SM,FM)
    Y.FECHA.CASTIGO = CHANGE(Y.FECHA.CASTIGO,SM,FM)
    Y.FECHA.CASTIGO = Y.FECHA.CASTIGO<1>
    *IF  Y.FECHA.CASTIGO EQ '' THEN
        Y.FECHA.CASTIGO = TODAY
    *END
    Y.OBSERVACIONES = R.AA.OVERDUE.APP<AA.OD.LOCAL.REF,L.LOAN.COMMENT1.POS>
    Y.OBSERVACIONES = CHANGE(Y.OBSERVACIONES,SM,FM)
    Y.OBSERVACIONES = CHANGE(Y.OBSERVACIONES,SM,FM)
    Y.OBSERVACIONES = Y.OBSERVACIONES<1>
    Y.FECHA.INICIO.ADJUD = R.AA.OVERDUE.APP<AA.OD.LOCAL.REF,L.ADJ.DATE.POS>
    Y.FECHA.INICIO.ADJUD = CHANGE(Y.FECHA.INICIO.ADJUD,SM,FM)
    Y.FECHA.INICIO.ADJUD = CHANGE(Y.FECHA.INICIO.ADJUD,SM,FM)
    Y.FECHA.INICIO.ADJUD = Y.FECHA.INICIO.ADJUD<1>
    Y.ESTATUS = 'Normal'
    Y.CONDICION1 = 'CASTIGADO'
    Y.CONDICION2 = 'LEGAL'


    RETURN

GET.ACCOUNT:
    R.ACCOUNT = ''; ERROR.ACCOUNT = ''; Y.ALT.ACCT.TYPE = ''
    CALL F.READ(FN.ACCOUNT,Y.NROPRESTAMO,R.ACCOUNT,FV.ACCOUNT,ERROR.ACCOUNT)
    Y.ALT.ACCT.TYPE = R.ACCOUNT<AC.ALT.ACCT.TYPE>

    LOCATE "ALTERNO1" IN Y.ALT.ACCT.TYPE<1,1> SETTING POS.ACC1  THEN
        Y.ID.ALTERNO.LEGACY = R.ACCOUNT<AC.ALT.ACCT.ID,POS.ACC1>
    END
    LOCATE "ALTERNO2" IN Y.ALT.ACCT.TYPE<1,1> SETTING POS.ACC  THEN
        Y.ID.ALTERNO.PLAN.Z = R.ACCOUNT<AC.ALT.ACCT.ID,POS.ACC>
    END
    LOCATE "ALTERNO3" IN Y.ALT.ACCT.TYPE<1,1> SETTING POS.ACC  THEN
        Y.ID.ALTERNO.WOF = R.ACCOUNT<AC.ALT.ACCT.ID,POS.ACC>
    END

    Y.OFICIAL.PRINCIPAL = R.ACCOUNT<AC.ACCOUNT.OFFICER>

    RETURN

GET.TERM.AMOUNT:
    Y.FECHA.VENCIMIENTO = R.AA.TERM.AMOUNT.APP<AA.AMT.MATURITY.DATE>
    Y.MONTO.ORIGINAL = R.AA.TERM.AMOUNT.APP<AA.AMT.AMOUNT>
    RETURN

GET.AA.ACCOUNT:
    Y.CODIGO.SUCURSAL = R.AA.ACCOUNT.APP<AA.AC.LOCAL.REF,L.AA.AGNCY.CODE.POS>
    Y.TIPO.FACILIDAD.CRED = R.AA.ACCOUNT.APP<AA.AC.LOCAL.REF,L.CR.FACILITY.POS>
    Y.ORIGEN.RECURSOS = R.AA.ACCOUNT.APP<AA.AC.LOCAL.REF,L.ORIGEN.RECURSOS.POS>
    RETURN

GET.AA.CUSTOMER:
    Y.CODEUDOR = '' ; Y.TIPO.CAMPANA = ''; Y.COMPANIA.AFILIADA  = ''
    Y.OTHER.PARTY = ''; Y.ROLE = ''; Y.CODEUDOR = ''
    Y.TIPO.CAMPANA = R.AA.CUSTOMER<AA.CUS.LOCAL.REF,L.AA.CAMP.TY.POS>
    Y.COMPANIA.AFILIADA = R.AA.CUSTOMER<AA.CUS.LOCAL.REF,L.AA.AFF.COM.POS>
    Y.OTHER.PARTY = R.AA.CUSTOMER<AA.CUS.OTHER.PARTY>
    Y.ROLE = R.AA.CUSTOMER<AA.CUS.ROLE>
    LOCATE "CO-SIGNER" IN Y.ROLE<1,1> SETTING ROLE.POS THEN
        Y.CODEUDOR = Y.OTHER.PARTY<1,ROLE.POS>
    END

    RETURN

GET.AA.PAYMENT.SHEDULE:
    Y.AA.DUE.FREQ  = R.AA.PAYMENT.SCHEDULE.APP<AA.PS.DUE.FREQ>
    Y.AA.DUE.FREQ = CHANGE(Y.AA.DUE.FREQ,SM,FM)
    Y.AA.DUE.FREQ = CHANGE(Y.AA.DUE.FREQ,VM,FM)
    IF DCOUNT(Y.AA.DUE.FREQ,FM) GT 1 THEN
        Y.AA.DUE.FREQ = Y.AA.DUE.FREQ<1>
    END
    Y.NEW.DUE.FREQ = Y.AA.DUE.FREQ[14,2]
    IF NUM(Y.NEW.DUE.FREQ) EQ 0 THEN
        Y.NEW.DUE.FREQ = Y.AA.DUE.FREQ[14,1]
    END
    Y.DIA.DE.PAGO = Y.NEW.DUE.FREQ

    RETURN

GET.AA.ACCOUNT.DETAILS.BILLS:
    Y.BIL.IDS  = ''; Y.BIL.TYPE = '';  Y.BILL.STATUS = '' ; AA.AC.ER = ''
    R.AA.AC = ''; FLG.R = 0 ; YBILL.CNT = 0 ; Y.BILL.DATE  = ''; Y.CANTIDAD.CUOTA = ''
    Y.BL.ID = ''; Y.OVR.AMT.CNT = ''; Y.ARRAY.PAYMENT.TYPE = '' ; YBILL.CNT = ''
    R.AA.AC = ''; AA.AC.ER = ''
    CALL F.READ(FN.AA.AC,AA.ARR.ID,R.AA.AC,F.AA.AC,AA.AC.ER)
    Y.BIL.IDS = R.AA.AC<AA.AD.BILL.ID>
    Y.BIL.IDS = CHANGE(Y.BIL.IDS,SM,FM)
    Y.BIL.IDS = CHANGE(Y.BIL.IDS,VM,FM)
    YBILL.CNT = DCOUNT(Y.BIL.IDS,FM)
    LOOP
    WHILE YBILL.CNT GT 0 DO
        FLG.R += 1
        Y.BL.ID = Y.BIL.IDS<FLG.R>
        BL.ERR = ''; R.AA.BILL = ''
        CALL F.READ(FN.AA.BILL,Y.BL.ID,R.AA.BILL,F.AA.BILL,BL.ERR)
        IF NOT(R.AA.BILL) THEN
            BL.ERR = ''; R.AA.BILL = ''
            CALL F.READ(FN.AA.BILL.HST,Y.BL.ID,R.AA.BILL,F.AA.BILL.HST,BL.ERR)
        END
        IF R.AA.BILL<AA.BD.SETTLE.STATUS,1> EQ 'UNPAID'  THEN
            Y.OVR.AMT.CNT += 1
            Y.ARRAY.PAYMENT.TYPE<-1> = R.AA.BILL<AA.BD.PAYMENT.DATE>
        END
        YBILL.CNT -= 1
    REPEAT

    Y.BILL.DATE = MINIMUM(Y.ARRAY.PAYMENT.TYPE)
    Y.CANTIDAD.CUOTA = Y.OVR.AMT.CNT

    IF NOT(Y.CANTIDAD.CUOTA) THEN
        GOSUB GET.AA.NO.FACTURA
    END


    RETURN

GET.AA.NO.FACTURA:
    SEL.CMD = ''; SEL.LIST = ''; NO.OF.REC = ''; SEL.ERR = '' ; Y.ARRAY.PAYMENT.TYPE = ''
    Y.BILL.DATE = ''
    SEL.CMD = " SELECT " : FN.AA.BILL: " WITH ARRANGEMENT.ID EQ " : AA.ARR.ID
    CALL EB.READLIST(SEL.CMD, SEL.LIST, "", NO.OF.REC, SEL.ERR)

    LOOP
        REMOVE Y.BILL.ID FROM SEL.LIST SETTING BILL.POS
    WHILE Y.BILL.ID DO
        R.AA.BILL = ''; BL.ERR = ''
        CALL F.READ(FN.AA.BILL,Y.BL.ID,R.AA.BILL,F.AA.BILL,BL.ERR)
        Y.ARRAY.PAYMENT.TYPE<-1> = R.AA.BILL<AA.BD.PAYMENT.DATE>
    REPEAT
    Y.BILL.DATE = MAXIMUM(Y.ARRAY.PAYMENT.TYPE)

    RETURN

GET.BALANCES.EB.DETALLES:
    Y.ACCOUNTWOF = 0; Y.PRINCIPALINTWOF = 0; Y.PRINCIPALINWOF = 0
    Y.PENALTYINTWOF = 0; Y.PRADMSEGWOF = 0; Y.PRADMVARWOF = 0
    Y.PRMORAWOF = 0 ; Y.MENOS.UNO = -1
*--ACCOUNT
    Y.NOMBRE.PROPIEDAD = "ACCOUNT" ; Y.UNCUENTA = "UNCACCOUNT"
    GOSUB GET.BALANCE.EB.PROPIEDAD
    Y.ACCOUNTWOF =  Y.MONTO.EB
*--PRINCIPALINT
    Y.NOMBRE.PROPIEDAD = "PRINCIPALINT"
    GOSUB GET.BALANCE.EB.PROPIEDAD
    Y.PRINCIPALINTWOF =  Y.MONTO.EB
*--PRINCIPALINTSP
    Y.NOMBRE.PROPIEDAD = "PRINCIPALINTSP"
    GOSUB GET.BALANCE.EB.PROPIEDAD
    Y.PRINCIPALINWOF =  Y.MONTO.EB
*--PENALTINT
    Y.NOMBRE.PROPIEDAD = "PENALTINT"
    GOSUB GET.BALANCE.EB.PROPIEDAD
    Y.PENALTYINTWOF =  Y.MONTO.EB
*--PRADMSEG
    Y.NOMBRE.PROPIEDAD = "PRADMSEG"
    GOSUB GET.BALANCE.EB.PROPIEDAD
    Y.PRADMSEGWOF =  Y.MONTO.EB
*--PRADMSEGCOMVAR
    Y.NOMBRE.PROPIEDAD = "PRADMSEGCOMVAR"
    GOSUB GET.BALANCE.EB.PROPIEDAD
    Y.PRADMVARWOF =  Y.MONTO.EB
*--PRMORA
    Y.NOMBRE.PROPIEDAD = "PRMORA"
    GOSUB GET.BALANCE.EB.PROPIEDAD
    Y.PRMORAWOF =  Y.MONTO.EB

    Y.PRINCIPALINTWOF = Y.PRINCIPALINTWOF + Y.PRINCIPALINWOF
    Y.PRINCIPALINWOF = Y.PRINCIPALINWOF * Y.MENOS.UNO


    RETURN


***----------------------***
GET.BALANCE.EB.PROPIEDAD:
***----------------------***
    R.EB.CONT.BAL = ''
    CB.ERROR = ''; YACCT.GRP.IN = 0 ; Y.MONTO.EB = 0; Y.FECHA.EB  = ''; Y.TOTAL.AMT = 0;
    Y.TO.OPEN = 0; Y.CREDIT.AMT = 0 ; Y.DEBIT.AMT = 0; Y.SUMA.AMT = 0;
    CALL F.READ(FN.EB.CONT.BAL,Y.NROPRESTAMO,R.EB.CONT.BAL,F.EB.CONT.BAL,EB.CONTRACT.BALANCES.ERR)
    Y.TYPE.CB =  R.EB.CONT.BAL<ECB.TYPE.SYSDATE>
    Y.CNT = DCOUNT(Y.TYPE.CB,@VM)
    Y.CB.ACCOUN.TYPE = ""
    FOR I = 1 TO Y.CNT
        Y.CB.ACCOUN.TYPE = R.EB.CONT.BAL<ECB.TYPE.SYSDATE,I>
        IF Y.PRODUCT.GROUP EQ 'PRODUCTOS.WOF' AND INDEX(Y.CB.ACCOUN.TYPE,'WOFBL',1) GT 0 THEN
            CONTINUE
        END
        FINDSTR Y.NOMBRE.PROPIEDAD IN Y.CB.ACCOUN.TYPE SETTING Ap, Vp THEN

            BEGIN CASE
            CASE Y.NOMBRE.PROPIEDAD EQ 'ACCOUNT'
                FINDSTR Y.UNCUENTA IN Y.CB.ACCOUN.TYPE SETTING Ap, Vp THEN
                    CONTINUE
                END
            CASE  Y.NOMBRE.PROPIEDAD EQ 'PRINCIPALINT'
                FINDSTR 'PRINCIPALINTBL' IN Y.CB.ACCOUN.TYPE SETTING Ap, Vp THEN
                    CONTINUE
                END
                FINDSTR 'PRINCIPALINTSP' IN Y.CB.ACCOUN.TYPE SETTING Ap, Vp THEN
                    CONTINUE
                END
            CASE Y.NOMBRE.PROPIEDAD EQ 'PENALTINT'
                FINDSTR 'PENALTINTBL' IN Y.CB.ACCOUN.TYPE SETTING Ap, Vp THEN
                    CONTINUE
                END
                FINDSTR 'PENALTINTSP' IN Y.CB.ACCOUN.TYPE SETTING Ap, Vp THEN
                    CONTINUE
                END

            CASE Y.NOMBRE.PROPIEDAD EQ 'PRADMSEG'
                FINDSTR 'PRADMSEGBL' IN Y.CB.ACCOUN.TYPE SETTING Ap, Vp THEN
                    CONTINUE
                END
            CASE Y.NOMBRE.PROPIEDAD EQ 'PRADMSEGCOMVAR'
                FINDSTR 'PRADMSEGCOMVARBL' IN Y.CB.ACCOUN.TYPE SETTING Ap, Vp THEN
                    CONTINUE
                END
            CASE Y.NOMBRE.PROPIEDAD EQ 'PRMORA'
                FINDSTR 'PRMORABL' IN Y.CB.ACCOUN.TYPE SETTING Ap, Vp THEN
                    CONTINUE
                END
            END CASE
            Y.TO.OPEN = R.EB.CONT.BAL<ECB.OPEN.BALANCE,I,1>
            Y.CREDIT.AMT = R.EB.CONT.BAL<ECB.CREDIT.MVMT,I,1>
            Y.DEBIT.AMT = R.EB.CONT.BAL<ECB.DEBIT.MVMT,I,1>
            Y.SUMA.AMT = Y.TO.OPEN + Y.CREDIT.AMT + Y.DEBIT.AMT
            Y.TOTAL.AMT = Y.TOTAL.AMT + Y.SUMA.AMT
            Y.MONTO.EB = Y.TOTAL.AMT
        END
    NEXT I
    RETURN
SET.WRITE.INFILE:
    R.ST.LAPAP.INFILEPRESTAMO = ""
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.ARRANGEMENT.ID> = AA.ARR.ID
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.START.DATE> = Y.START.DATE
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.CONTRATO> = Y.LINKED.APPL.ID
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.PRODUCT.GROUP> = Y.PRODUCT.GROUP
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.PRODUCT> = Y.PRODUCT
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.ARR.STATUS> = Y.ARR.STATUS
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.ESTADO.ACTUAL> = Y.ESTADO.ACTUAL
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.TIPO.PRODUCTO> = Y.TIPO.PRODUCTO
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.ID.CLIENTE> = Y.ID.CLIENTE
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.COMPANY> = Y.COMPANY
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.ID.ALTERNO.LEGACY> = Y.ID.ALTERNO.LEGACY
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.ID.ALTERNO.PLAN.Z> = Y.ID.ALTERNO.PLAN.Z
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.FECHA.ORIGINAL.APE> = Y.FECHA.ORIGINAL.APE
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.FECHA.APERTURA> = Y.FECHA.APERTURA
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.FECHA.VENCIMIENTO> = Y.FECHA.VENCIMIENTO
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.FECHA.CASTIGO> = Y.FECHA.CASTIGO
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.MONTO.ORIGINAL> = Y.MONTO.ORIGINAL
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.ESTATUS> = Y.ESTATUS
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.CONDICION1> = Y.CONDICION1
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.CONDICION2> = Y.CONDICION2
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.OBSERVACIONES> = Y.OBSERVACIONES
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.FECHA.INICIO.ADJUD> = Y.FECHA.INICIO.ADJUD
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.COMPANIA.AFILIADA> = Y.COMPANIA.AFILIADA
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.TIPO.CAMPANA> = Y.TIPO.CAMPANA
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.CODEUDOR> = Y.CODEUDOR
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.OFICIAL.PRINCIPAL> = Y.OFICIAL.PRINCIPAL
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.CODIGO.SUCURSAL> = Y.CODIGO.SUCURSAL
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.ORIGEN.DE.LOS.RECU> = Y.ORIGEN.RECURSOS
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.TIPO.FACILIDAD.CRE> = Y.TIPO.FACILIDAD.CRED
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.DIA.DE.PAGO> = Y.DIA.DE.PAGO
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.BILL.DATE> = Y.BILL.DATE
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.ACCOUNTWOF> = Y.ACCOUNTWOF
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.PRINCIPALINTWOF> = Y.PRINCIPALINTWOF
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.PRINCIPALINWOF> = Y.PRINCIPALINWOF
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.PENALTYINTWOF> = Y.PENALTYINTWOF
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.PRADMSEGWOF> = Y.PRADMSEGWOF
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.PRADMVARWOF> = Y.PRADMVARWOF
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.PRMORAWOF> = Y.PRMORAWOF
    R.ST.LAPAP.INFILEPRESTAMO<ST.LAP64.CANTIDAD.CUOTA> = Y.CANTIDAD.CUOTA
    CALL F.WRITE(FN.ST.LAPAP.INFILEPRESTAMO,AA.ARR.ID,R.ST.LAPAP.INFILEPRESTAMO)
    CALL JOURNAL.UPDATE(AA.ARR.ID)
    RETURN



OPEN.FILES:
    FN.REDO.H.REPORTS.PARAM = "F.REDO.H.REPORTS.PARAM"
    F.REDO.H.REPORTS.PARAM  = ""
    CALL OPF(FN.REDO.H.REPORTS.PARAM,F.REDO.H.REPORTS.PARAM)

    FN.ST.LAPAP.INFILEPRESTAMO  = 'F.ST.LAPAP.INFILEPRESTAMO'
    FV.ST.LAPAP.INFILEPRESTAMO = ''
    CALL OPF(FN.ST.LAPAP.INFILEPRESTAMO,FV.ST.LAPAP.INFILEPRESTAMO)

    FN.AA.ARRANGEMENT.ACTIVITY = 'F.AA.ARRANGEMENT.ACTIVITY'
    FV.AA.ARRANGEMENT.ACTIVITY = ''
    CALL OPF(FN.AA.ARRANGEMENT.ACTIVITY,FV.AA.ARRANGEMENT.ACTIVITY)

    FN.ACCOUNT = 'F.ACCOUNT'
    FV.ACCOUNT = ''
    CALL OPF(FN.ACCOUNT,FV.ACCOUNT)

    FN.DATES = 'F.DATES'
    FV.DATES = ''
    CALL OPF (FN.DATES, FV.DATES)

    FN.AA.AC = 'F.AA.ACCOUNT.DETAILS'
    F.AA.AC = ''
    CALL OPF(FN.AA.AC,F.AA.AC)

    FN.AA.BILL = 'F.AA.BILL.DETAILS'
    F.AA.BILL = ''
    CALL OPF(FN.AA.BILL,F.AA.BILL)

    FN.AA.BILL.HST = 'F.AA.BILL.DETAILS.HIST'
    F.AA.BILL.HST = ''
    CALL OPF(FN.AA.BILL.HST,F.AA.BILL.HST)

    FN.EB.CONT.BAL = 'F.EB.CONTRACT.BALANCES'
    F.EB.CONT.BAL = ''
    CALL OPF(FN.EB.CONT.BAL,F.EB.CONT.BAL)
**--------------------------------------------------------------------------------------------------------------
    ID.REPORT = 'LAPAP.ECB.BALANCES.WOF'
    CALL F.READ(FN.REDO.H.REPORTS.PARAM,ID.REPORT,R.REDO.H.REPORTS.PARAM,F.REDO.H.REPORTS.PARAM,ERROR.REPORT.PARAM)
    Y.FILE.NAME = R.REDO.H.REPORTS.PARAM<REDO.REP.PARAM.OUT.FILE.NAME>
    Y.FIELD.NME.ARR = R.REDO.H.REPORTS.PARAM<REDO.REP.PARAM.FIELD.NAME>
    Y.FIELD.VAL.ARR = R.REDO.H.REPORTS.PARAM<REDO.REP.PARAM.FIELD.VALUE>
    Y.DISP.TEXT.ARR = R.REDO.H.REPORTS.PARAM<REDO.REP.PARAM.DISPLAY.TEXT>

    LOCATE "ARCHIVO.CASTIGO" IN Y.FIELD.NME.ARR<1,1> SETTING COD.POS THEN
        Y.ARCHIVO.CASTIGO = Y.FIELD.VAL.ARR<1,COD.POS>
        Y.ARCHIVO.CASTIGO = CHANGE(Y.ARCHIVO.CASTIGO,SM,FM)
        Y.ARCHIVO.CASTIGO = CHANGE(Y.ARCHIVO.CASTIGO,VM,FM)
    END

    LOCATE "RUTA.INFILES" IN Y.FIELD.NME.ARR<1,1> SETTING COD.POS3 THEN
        Y.RUTA.INFILES = Y.FIELD.VAL.ARR<1,COD.POS3>
        Y.RUTA.INFILES = CHANGE(Y.RUTA.INFILES,SM,FM)
        Y.RUTA.INFILES = CHANGE(Y.RUTA.INFILES,VM,FM)
    END
    FN.CHK.DIR = Y.RUTA.INFILES ; F.CHK.DIR = "" ; Y.LISTA.CONSTANTE = ''
    CALL OPF(FN.CHK.DIR,F.CHK.DIR)

    GOSUB GET.CAMPO.LOCALES

    RETURN

GET.CAMPO.LOCALES:
    L.STATUS.CHG.DT.POS = ''; L.LOAN.STATUS.1.POS = ''; L.LOAN.COND.POS = ''; L.LOAN.COMMENT1.POS = ''
    L.LOAN.COND.POS = '';  L.ADJ.DATE.POS = ''; L.CR.FACILITY.POS = ''; L.AA.AGNCY.CODE.POS = '' ; L.AA.AFF.COM.POS = ''
    L.AA.AFF.COM.POS = ''; L.ORIGEN.RECURSOS.POS = ''

*OVERDUE AA
*********************
    CALL GET.LOC.REF ("AA.PRD.DES.OVERDUE", "L.STATUS.CHG.DT",L.STATUS.CHG.DT.POS)
    CALL GET.LOC.REF ("AA.PRD.DES.OVERDUE", "L.LOAN.STATUS.1",L.LOAN.STATUS.1.POS)
    CALL GET.LOC.REF ("AA.PRD.DES.OVERDUE", "L.LOAN.COND",L.LOAN.COND.POS)
    CALL GET.LOC.REF ("AA.PRD.DES.OVERDUE", "L.LOAN.COMMENT1",L.LOAN.COMMENT1.POS)
    CALL GET.LOC.REF ("AA.PRD.DES.OVERDUE", "L.ADJ.DATE",L.ADJ.DATE.POS)
*ACCOUNT AA
*********************
    CALL GET.LOC.REF ("AA.PRD.DES.ACCOUNT", "L.CR.FACILITY",L.CR.FACILITY.POS)
    CALL GET.LOC.REF ("AA.PRD.DES.ACCOUNT", "L.AA.AGNCY.CODE",L.AA.AGNCY.CODE.POS)
    CALL GET.LOC.REF ("AA.PRD.DES.ACCOUNT", "ORIGEN.RECURSOS",L.ORIGEN.RECURSOS.POS)
**CUSTOMER AA
*********************
    CALL GET.LOC.REF ("AA.PRD.DES.CUSTOMER", "L.AA.AFF.COM",L.AA.AFF.COM.POS)
    CALL GET.LOC.REF ("AA.PRD.DES.CUSTOMER", "L.AA.CAMP.TY",L.AA.CAMP.TY.POS)


    RETURN

END
