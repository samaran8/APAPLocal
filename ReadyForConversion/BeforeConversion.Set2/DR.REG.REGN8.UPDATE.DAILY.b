*-----------------------------------------------------------------------------
* <Rating>580</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE DR.REG.REGN8.UPDATE.DAILY
*----------------------------------------------------------------------------
* Company Name   : APAP
* Developed By   : gangadhar@temenos.com
* Program Name   : DR.REG.REGN8.UPDATE.DAILY
* Date           : 6-June-2013
*----------------------------------------------------------------------------
* Description:
*------------
* This multi-thread job is meant for to update TAX details into as needed by the report on daily basis.
*----------------------------------------------------------------------------
* Modification History :
* ----------------------
*   Date       Author              Modification Description
*07-09-2014   Ashokkumar.V.P       PACS00309080:- modified all the amount retrieving fields
*01-12-2014   Ashokkumar.V.P       PACS00309080:- Added AC.CHARGE.REQUEST file and retrived the Base amount of the transaction.
*03-04-2015   Ashokkumar.V.P       PACS00309080:- Applied the mmaping to changes to make it as generic for FT & TT
*----------------------------------------------------------------------------
    $INSERT T24.BP I_COMMON
    $INSERT T24.BP I_EQUATE
    $INSERT T24.BP I_BATCH.FILES
    $INSERT T24.BP I_TSA.COMMON
    $INSERT T24.BP I_F.STMT.ENTRY
    $INSERT T24.BP I_F.FOREX
    $INSERT T24.BP I_F.TELLER
    $INSERT T24.BP I_F.FUNDS.TRANSFER
    $INSERT T24.BP I_F.ACCOUNT
    $INSERT T24.BP I_ENQUIRY.COMMON
    $INSERT T24.BP I_F.DATES
    $INSERT T24.BP I_F.AC.CHARGE.REQUEST

    $INCLUDE TAM.BP I_F.REDO.CLEARING.OUTWARD
    $INCLUDE TAM.BP I_F.REDO.APAP.CLEARING.INWARD
    $INCLUDE REGREP.BP I_F.DR.REG.REGN8.CONCAT
    $INCLUDE REGREP.BP I_F.DR.REG.213IF01.CONCAT
    $INCLUDE TAM.BP I_F.REDO.H.REPORTS.PARAM
*
    GOSUB INIT.PROCESS
    GOSUB INIT.OPF
    GOSUB GET.LOC.POS
    GOSUB GET.PARAM.VAL
    GOSUB GET.VARIAB.INFO
    GOSUB PROCESS
    GOSUB PROCESS.CW
    GOSUB CONCAT.WRITE
    RETURN
*----------------------------------------------------------------------------
INIT.PROCESS:
*-----------*
    BUY.SELL.CNT = ''; FX.BUY.SELL.CNT = ''; CASH.WTDW.CNT = ''; DB.LOAN.REC.CNT = ''; AMT.VAL = ''; TOTAL.VAL = ''; CHQ.CNT = ''; TT.TXN.CNT = ''; FT.TXN.CNT = ''
    DEBTRF.CNT = ''; RESTRF.CNT = ''; DEB.PAY.CNT = ''; FT.PAY.CNT = ''; TT.PAY.CNT = ''; CC.CHG.CNT = ''; FTBCO.CNT = ''; FXBCO.CNT = ''; FT.BCOEXT.CNT = ''; FX.BCOEXT.CNT = ''
    INT.PAY.CNT = ''; ATM.CNT = ''; FT.TXN.AMT = ''; DEBTRF.AMT = ''; RESTRF.AMT = ''; DEB.PAY.AMT = ''; FT.PAY.AMT = ''; DB.LOAN.REC.AMT = ''; CC.CHG.AMT = ''; FTBCO.AMT = ''
    TAX.CDE = ''; FT.BCOEXT.AMT = ''; INT.PAY.AMT = ''; ATM.AMT = ''; SAV.AMT = ''; TT.TXN.AMT = ''; TT.PAY.AMT = ''; CHK.CURR.CAT = ''; FT.PAY.ACOR = ''; FT.CHQ.AC3 = ''
    FT.VERSION = ''; FT.LCY.TYPE = ''; FT.FCY.TYPE = ''; LRUN.WDATE = '';  CHK.SAV.CAT = ''; TT.CASH.WTDW.CODES = ''; STR.DATE = ''; END.DATE = ''; R.DR.REG.REGN8.CONCAT = ''
    TA.POSN = ''; YGP.STMT.ID.LIST = ''; ERR.AC.SUB.ACC = ''; R.AC.SUB.ACC = ''; L.APP = ''; TT.FLDS = ''; FLD.POS = ''; CHQ.AMT = ''; TAX.AMT = ''; YOUT.ID.LST = ''
    NOT.USED.CNT = 0; NOT.USED.AMT = ''; PAID.VAL = 0; TT.TRANSF.CNT = 0; TT.TRANSF.AMT = ''; SAV.CNT = 0; CASH.WTDW.AMT = ''; YFLG = 0; YGRP.ARRY = ''; YTOT.TAXAMT = 0

    FN.REDO.APAP.CLEARING.INWARD = 'F.REDO.APAP.CLEARING.INWARD'; F.REDO.APAP.CLEARING.INWARD = ''
    FN.STMT.ENTRY = 'F.STMT.ENTRY' ; F.STMT.ENTRY = ''
    FN.REDO.CLEARING.OUTWARD = 'F.REDO.CLEARING.OUTWARD'; F.REDO.CLEARING.OUTWARD = ''
    FN.TELLER = 'F.TELLER';  F.TELLER = ''
    FN.FUNDS.TRANSFER = 'F.FUNDS.TRANSFER'; F.FUNDS.TRANSFER = ''
    FN.FUNDS.TRANSFER.HIS = 'F.FUNDS.TRANSFER$HIS'; F.FUNDS.TRANSFER.HIS = ''
    FN.TELLER.HIS = 'F.TELLER$HIS'; F.TELLER.HIS = ''
    FN.FOREX.HIS = 'F.FOREX$HIS'; F.FOREX.HIS = ''
    FN.FOREX = 'F.FOREX'; F.FOREX = ''
    FN.ACCOUNT = 'F.ACCOUNT'; F.ACCOUNT = ''
    FN.DR.REG.REGN8.CONCAT = 'F.DR.REG.REGN8.CONCAT'; F.DR.REG.REGN8.CONCAT = ''
    FN.DR.REG.213IF01.CONCAT = 'F.DR.REG.213IF01.CONCAT'; F.DR.REG.213IF01.CONCAT = ''
    FN.AC.SUB.ACC = 'F.AC.SUB.ACCOUNT' ; F.AC.SUB.ACC = ''
    FN.ACCOUNT.HST = 'F.ACCOUNT$HIS'; F.ACCOUNT.HST = ''
    FN.AC.CHARGE.REQUEST = 'F.AC.CHARGE.REQUEST'; F.AC.CHARGE.REQUEST = ''
    FN.AC.CHARGE.REQUEST.HST = 'F.AC.CHARGE.REQUEST$HIS'; F.AC.CHARGE.REQUEST.HST = ''
    FN.REDO.H.REPORTS.PARAM = 'F.REDO.H.REPORTS.PARAM'; F.REDO.H.REPORTS.PARAM  = ''
    RETURN

INIT.OPF:
**********
    CALL OPF(FN.REDO.APAP.CLEARING.INWARD,F.REDO.APAP.CLEARING.INWARD)
    CALL OPF(FN.STMT.ENTRY,F.STMT.ENTRY)
    CALL OPF(FN.REDO.CLEARING.OUTWARD,F.REDO.CLEARING.OUTWARD)
    CALL OPF(FN.TELLER,F.TELLER)
    CALL OPF(FN.FUNDS.TRANSFER,F.FUNDS.TRANSFER)
    CALL OPF(FN.FUNDS.TRANSFER.HIS,F.FUNDS.TRANSFER.HIS)
    CALL OPF(FN.TELLER.HIS,F.TELLER.HIS)
    CALL OPF(FN.FOREX.HIS,F.FOREX.HIS)
    CALL OPF(FN.FOREX,F.FOREX)
    CALL OPF(FN.ACCOUNT,F.ACCOUNT)
    CALL OPF(FN.DR.REG.REGN8.CONCAT,F.DR.REG.REGN8.CONCAT)
    CALL OPF(FN.DR.REG.213IF01.CONCAT,F.DR.REG.213IF01.CONCAT)
    CALL OPF(FN.AC.SUB.ACC,F.AC.SUB.ACC)
    CALL OPF(FN.ACCOUNT.HST,F.ACCOUNT.HST)
    CALL OPF(FN.AC.CHARGE.REQUEST,F.AC.CHARGE.REQUEST)
    CALL OPF(FN.AC.CHARGE.REQUEST.HST,F.AC.CHARGE.REQUEST.HST)
    CALL OPF(FN.REDO.H.REPORTS.PARAM,F.REDO.H.REPORTS.PARAM)
    RETURN

GET.LOC.POS:
************
    L.APP = 'TELLER':FM:'FUNDS.TRANSFER'
    TT.FLDS = 'L.TRAN.AMOUNT':VM:'L.TT.TRANS.AMT':VM:'L.TT.TAX.CODE':VM:'L.TT.TAX.AMT':FM:'L.TT.TRANS.AMT':VM:'L.ACTUAL.VERSIO':VM:'L.TT.TAX.AMT'
    CALL MULTI.GET.LOC.REF(L.APP,TT.FLDS,FLD.POS)
    L.TRAN.AMOUNT.POS = FLD.POS<1,1>
    L.TT.TRANS.AMT.TT.POS = FLD.POS<1,2>
    L.TT.TAX.CODE.POS = FLD.POS<1,3>
    L.TT.TAX.AMT.POS = FLD.POS<1,4>
    L.TT.TRANS.AMT.POS = FLD.POS<2,1>
    L.ACTUAL.VERSIO.POS = FLD.POS<2,2>
    L.FT.TAX.AMT.POS = FLD.POS<2,3>
    RETURN

GET.PARAM.VAL:
***************
    CALL EB.CLEAR.FILE(FN.DR.REG.REGN8.CONCAT,F.DR.REG.REGN8.CONCAT)
    Y.PARAM.ID = 'REDO.REGN8'; R.REDO.H.REPORTS.PARAM = ''; Y.PARAM.ERR = ''
    CALL CACHE.READ(FN.REDO.H.REPORTS.PARAM,Y.PARAM.ID,R.REDO.H.REPORTS.PARAM,Y.PARAM.ERR)
    IF R.REDO.H.REPORTS.PARAM THEN
        Y.FILE.NAME = R.REDO.H.REPORTS.PARAM<REDO.REP.PARAM.OUT.FILE.NAME>
        Y.FIELD.NAME = R.REDO.H.REPORTS.PARAM<REDO.REP.PARAM.FIELD.NAME>
        Y.FIELD.VAL  = R.REDO.H.REPORTS.PARAM<REDO.REP.PARAM.FIELD.VALUE>
        Y.OUT.FILE.ID  = R.REDO.H.REPORTS.PARAM<REDO.REP.PARAM.OUT.FILE.NAME>
        FN.CHK.DIR = R.REDO.H.REPORTS.PARAM<REDO.REP.PARAM.OUT.DIR>
    END
    F.CHK.DIR = ''
    CALL OPF(FN.CHK.DIR,F.CHK.DIR)
    LOCATE 'FROM.DATE' IN Y.FIELD.NAME<1,1> SETTING Y.FRM.POS THEN
        STR.DATE = Y.FIELD.VAL<1,Y.FRM.POS>
    END
    LOCATE 'TO.DATE' IN Y.FIELD.NAME<1,1> SETTING Y.TO.POS THEN
        END.DATE = Y.FIELD.VAL<1,Y.TO.POS>
    END
    LOCATE 'LAST.RUN.DATE' IN Y.FIELD.NAME<1,1> SETTING Y.LRD.POS THEN
        LRUN.WDATE = Y.FIELD.VAL<1,Y.LRD.POS>
    END
    IF LRUN.WDATE THEN
        CALL CDT('',LRUN.WDATE,'+1C')
    END
    LAST.WORK.VAL = R.DATES(EB.DAT.LAST.WORKING.DAY)
    LAST.WDAY = TODAY
    CALL CDT('',LAST.WDAY,'-1C')
    IF NOT(STR.DATE) AND NOT(END.DATE) THEN
        IF LAST.WDAY LT LRUN.WDATE THEN
            LRUN.WDATE = LAST.WORK.VAL
        END
        STR.DATE = LRUN.WDATE
        END.DATE = LAST.WDAY
    END
    EXTRACT.FILE.ID = Y.OUT.FILE.ID:'_DETAIL_':LAST.WORK.VAL:'.txt'
    LOCATE 'TAX.ACCT.NO' IN Y.FIELD.NAME<1,1> SETTING Y.TAC.POS THEN
        TAX.ACC = Y.FIELD.VAL<1,Y.TAC.POS>
    END
    LOCATE 'TAX.ROUTE.NO' IN Y.FIELD.NAME<1,1> SETTING Y.TRN.POS THEN
        TAX.ROUTE.CODE = Y.FIELD.VAL<1,Y.TRN.POS>
    END
    RETURN

GET.VARIAB.INFO:
****************
    LOCATE 'TAX.CODE' IN Y.FIELD.NAME<1,1> SETTING Y.TC.POS THEN
        TAX.CDE = Y.FIELD.VAL<1,Y.TC.POS>
    END
    LOCATE 'SAVING.CATEG' IN Y.FIELD.NAME<1,1> SETTING Y.SCT.POS THEN
        CHK.CURR.CAT = Y.FIELD.VAL<1,Y.SCT.POS>
        CHANGE SM TO FM IN CHK.CURR.CAT
    END
    LOCATE 'CURRENT.CATEG' IN Y.FIELD.NAME<1,1> SETTING Y.CCT.POS THEN
        CHK.SAV.CAT = Y.FIELD.VAL<1,Y.CCT.POS>
        CHANGE SM TO FM IN CHK.SAV.CAT
    END
    LOCATE 'CHEQUES.PAGADOS.3' IN Y.FIELD.NAME<1,1> SETTING Y.CP3.POS THEN
        TT.TXN.CODE = Y.FIELD.VAL<1,Y.CP3.POS>
        CHANGE SM TO FM IN TT.TXN.CODE
    END
    LOCATE 'ELECTRONICAS.TRANSFERENCIAS.3P.4' IN Y.FIELD.NAME<1,1> SETTING Y.ETP4.POS THEN
        FT.TXN.CODE = Y.FIELD.VAL<1,Y.ETP4.POS>
        CHANGE SM TO FM IN FT.TXN.CODE
    END
    LOCATE 'ELECTRONICAS.TRANSFERENCIAS.DB.5.1' IN Y.FIELD.NAME<1,1> SETTING Y.ETD51.POS THEN
        DEBTRF.TXN.CODES = Y.FIELD.VAL<1,Y.ETD51.POS>
        CHANGE SM TO FM IN DEBTRF.TXN.CODES
    END
    LOCATE 'ELECTRONICAS.TRANSFERENCIAS.DB.5.2' IN Y.FIELD.NAME<1,1> SETTING Y.ETD52.POS THEN
        RESTRF.TXN.CODES = Y.FIELD.VAL<1,Y.ETD52.POS>
        CHANGE SM TO FM IN RESTRF.TXN.CODES
    END
    LOCATE 'ELECTRONICAS.NOMINAS.DB.6' IN Y.FIELD.NAME<1,1> SETTING Y.EN6.POS THEN
        DEB.PAY.TXN.CODES = Y.FIELD.VAL<1,Y.EN6.POS>
        CHANGE SM TO FM IN DEB.PAY.TXN.CODES
    END
    LOCATE 'TERCEROS.PAGO.DB.7' IN Y.FIELD.NAME<1,1> SETTING Y.TP7.POS THEN
        FT.PAY.ACOR = Y.FIELD.VAL<1,Y.TP7.POS>
        CHANGE SM TO FM IN FT.PAY.ACOR
    END
    GOSUB GET.VARIAB.INFO1
    RETURN

GET.VARIAB.INFO1:
*****************
    LOCATE 'COBRO.DE.PRESTAMOS.DB.9' IN Y.FIELD.NAME<1,1> SETTING Y.CDP9.POS THEN
        LOAN.REC.CODES = Y.FIELD.VAL<1,Y.CDP9.POS>
        CHANGE SM TO FM IN LOAN.REC.CODES
    END
    LOCATE 'COBRO.DE.TARJETAS.DE.CREDITO.DB.10' IN Y.FIELD.NAME<1,1> SETTING Y.CD10.POS THEN
        CC.CHG.CODES = Y.FIELD.VAL<1,Y.CD10.POS>
        CHANGE SM TO FM IN CC.CHG.CODES
    END
    LOCATE 'TRANSFERENCIAS.DE.BANCO.11' IN Y.FIELD.NAME<1,1> SETTING Y.TDB11.POS THEN
        FT.LCY.TYPE = Y.FIELD.VAL<1,Y.TDB11.POS>
        CHANGE SM TO FM IN FT.LCY.TYPE
    END
    LOCATE 'NACIONAL.BANCO.TRANSFERENCIA.12' IN Y.FIELD.NAME<1,1> SETTING Y.NBT12.POS THEN
        FT.FCY.TYPE = Y.FIELD.VAL<1,Y.NBT12.POS>
        CHANGE SM TO FM IN FT.FCY.TYPE
    END
    LOCATE 'CHEQ.INTERESES.DEPOSITOS.13' IN Y.FIELD.NAME<1,1> SETTING Y.CID13.POS THEN
        FT.CHQ.AC3 = Y.FIELD.VAL<1,Y.CID13.POS>
        CHANGE SM TO FM IN FT.CHQ.AC3
    END
    LOCATE 'FT.CHQ.VERSION.13' IN Y.FIELD.NAME<1,1> SETTING Y.FCV13.POS THEN
        FT.VERSION = Y.FIELD.VAL<1,Y.FCV13.POS>
        CHANGE SM TO FM IN FT.VERSION
    END
    LOCATE 'AUTOMATICOS.DE.CUENTAS' IN Y.FIELD.NAME<1,1> SETTING Y.ADC.POS THEN
        ATM.CODES = Y.FIELD.VAL<1,Y.ADC.POS>
        CHANGE SM TO FM IN ATM.CODES
    END
    LOCATE 'DE.AHORRO.EN.VENTANILLA' IN Y.FIELD.NAME<1,1> SETTING Y.DAEV.POS THEN
        TT.CASH.WTDW.CODES = Y.FIELD.VAL<1,Y.DAEV.POS>
        CHANGE SM TO FM IN TT.CASH.WTDW.CODES
    END
    RETURN

PROCESS:
********
    YHVT.FLG = ''; DB.AC = TAX.ACC
    GOSUB READ.ACCOUNT
    IF R.ACCOUNT THEN
        YHVT.FLG= R.ACCOUNT<AC.HVT.FLAG>
    END
    IF YHVT.FLG EQ 'YES' THEN
        TAX.AID = TAX.ACC
        GOSUB GET.STMT.DET
    END ELSE
        CALL F.READ(FN.AC.SUB.ACC,TAX.ACC,R.AC.SUB.ACC,F.AC.SUB.ACC,ERR.AC.SUB.ACC)
        GOSUB SUB.ACC.PROCESS
    END
    CNT.ENT = DCOUNT(YGP.STMT.ID.LIST,FM)
    CTR.ENT = 1

    YGRP.ARRY = "ENTRY ID,REFERENCE NO,CUSTOMER NO,TRANSACTION AMOUNT,TAX AMOUNT,TRANS TYPE,TRANS DATE"
    LOOP
    WHILE CTR.ENT LE CNT.ENT
        ENT.ID = FIELD(YGP.STMT.ID.LIST<CTR.ENT>,'*',2)
        FXBCO.AMT = ''; YTTT.AMT = ''; YTEMP.VAL = ''; CUST.ID = ''; TRANS.REF = ''
        FT.COMM.AMT = ''; YTEMP.TYP = ''; AMT.LCY = 0; BOOK.DTE = ''
        GOSUB READ.STMT.ENTRY
        THER.REF = R.STMT.ENTRY<AC.STE.THEIR.REFERENCE>
        AMT.LCY = R.STMT.ENTRY<AC.STE.AMOUNT.LCY>
        TRANS.REF = R.STMT.ENTRY<AC.STE.TRANS.REFERENCE>
        CUST.ID = R.STMT.ENTRY<AC.STE.CUSTOMER.ID>
        BOOK.DTE = R.STMT.ENTRY<AC.STE.BOOKING.DATE>
        GOSUB SUB.PROCESS
        YTOT.TAXAMT += AMT.LCY
        IF AMT.LCY THEN
            YGRP.ARRY<-1> = ENT.ID:",":TRANS.REF:",":CUST.ID:",":YTEMP.VAL:",":AMT.LCY:",":YTEMP.TYP:",":BOOK.DTE
        END
    REPEAT
    CALL F.WRITE(FN.CHK.DIR,EXTRACT.FILE.ID,YGRP.ARRY)
    YGRP.ARRY = ''
    GOSUB POPULATE.VALUES
    RETURN

READ.STMT.ENTRY:
****************
    R.STMT.ENTRY = ''; STMT.ENTRY.ERR = ''
    CALL F.READ(FN.STMT.ENTRY,ENT.ID,R.STMT.ENTRY,F.STMT.ENTRY,STMT.ENTRY.ERR)
    RETURN

SUB.PROCESS:
************
    IF TRANS.REF[1,3] EQ 'CHG' THEN
        GOSUB AC.CHRG.PROCS
        RETURN
    END
    IF AMT.LCY THEN
        YFLG = 0
        GOSUB GET.FIELD8
        GOSUB GET.FIELD9
        GOSUB GET.FIELD10
        GOSUB GET.FT.FIELD
        GOSUB FLAG.VALE
    END
    CTR.ENT += 1

    IF YFLG EQ 1 THEN
        NOT.USED.CNT +=1
        IF YTTT.AMT THEN
            NOT.USED.AMT += YTTT.AMT
        END
        IF FXBCO.AMT THEN
            NOT.USED.AMT += FXBCO.AMT
        END
        IF FT.COMM.AMT THEN
            NOT.USED.AMT += FT.COMM.AMT
        END
        YFLG = 0
    END
    RETURN

AC.CHRG.PROCS:
**************
    ERR.AC.CHARGE.REQUEST = ''; R.AC.CHARGE.REQUEST = ''; AC.CHARGE.REQUEST.HIS.ERR = ''; YCHG.AMT = ''
    CALL F.READ(FN.AC.CHARGE.REQUEST,TRANS.REF,R.AC.CHARGE.REQUEST,F.AC.CHARGE.REQUEST,ERR.AC.CHARGE.REQUEST)
    IF NOT(R.AC.CHARGE.REQUEST) THEN
        Y.TRANS.REF = TRANS.REF
        CALL EB.READ.HISTORY.REC(F.AC.CHARGE.REQUEST.HST,Y.TRANS.REF,R.AC.CHARGE.REQUEST,AC.CHARGE.REQUEST.HIS.ERR)
    END
    YCHG.AMT = R.AC.CHARGE.REQUEST<CHG.CHARGE.AMOUNT>
    YTEMP.VAL = YCHG.AMT
    IF AMT.LCY[1,1] EQ '-' THEN
        YCHG.AMT = YCHG.AMT * (-1)
    END
    NOT.USED.AMT += YCHG.AMT
    NOT.USED.CNT +=1
    CTR.ENT += 1
    RETURN

SUB.ACC.PROCESS:
****************
    LOOP
        REMOVE TAX.AID FROM R.AC.SUB.ACC SETTING TA.POSN
    WHILE TAX.AID:TA.POSN
        GOSUB GET.STMT.DET
    REPEAT
    RETURN

GET.STMT.DET:
*************
    STMT.ID.LIST = ''
    D.FIELDS = "ACCOUNT":FM:"BOOKING.DATE"
    D.LOGICAL.OPERANDS = "1":FM:"2"
    D.RANGE.AND.VALUE = TAX.AID:FM:STR.DATE:SM:END.DATE
    CALL E.STMT.ENQ.BY.CONCAT(STMT.ID.LIST)
    YGP.STMT.ID.LIST<-1> = STMT.ID.LIST
    RETURN

PROCESS.CW:
***********
    SEL.CMD = ''; CONCAT.LIST = ''; Y.SEL.CNT = ''; Y.ERR = ''; CONC.POSN = ''; ATM.CNT = ''; ATM.AMT = ''; SAV.CNT = ''; SAV.AMT = ''; CASH.WTDW.CNT = ''; CASH.WTDW.AMT = ''
    SEL.CMD = "SELECT ":FN.DR.REG.213IF01.CONCAT:" WITH CR.DATE GE ":STR.DATE:" AND CR.DATE LE ":END.DATE
    CALL EB.READLIST(SEL.CMD,CONCAT.LIST,'',Y.SEL.CNT,Y.ERR)
    LOOP
        REMOVE CONCAT.ID FROM CONCAT.LIST SETTING CONC.POSN
    WHILE CONCAT.ID:CONC.POSN
        R.DR.REG.213IF01.CONCAT = ''; DR.REG.213IF01.CONCAT.ERR = ''; CNT.TXN = ''
        CALL F.READ(FN.DR.REG.213IF01.CONCAT,CONCAT.ID,R.DR.REG.213IF01.CONCAT,F.DR.REG.213IF01.CONCAT,DR.REG.213IF01.CONCAT.ERR)
        CNT.TXN = DCOUNT(R.DR.REG.213IF01.CONCAT<DR.213IF01.CONCAT.REGN8.STMT.ID>,VM)
        GOSUB CONCAT.STMT.PROCES
    REPEAT
    GOSUB POPULATE.VALUES
    RETURN

CONCAT.STMT.PROCES:
*******************
    CTR.REC = 1
    LOOP
    WHILE CTR.REC LE CNT.TXN
        STMT.TXN.VAL = ''; THER.REF = ''; AMT.LCY = ''; TRANS.REF = ''; TXN.FT.TT = ''; STMT.TXN.TYPE1 = ''; TT.REC.STATUS = ''; DB.AC = ''
        TT.AMT.LCL = ''; YAMT.LCY = ''; YAMT.FCY = ''; YCCY = ''; FT.AMT.LCL = ''; DB.CAT = ''; TT.TXN.VAL = ''; FT.TXN.VAL = ''; ENT.ID = ''
        STMT.TXN.TYPE1 = R.DR.REG.213IF01.CONCAT<DR.213IF01.CONCAT.REGN8.STMT.ID,CTR.REC>
        ENT.ID = FIELD(STMT.TXN.TYPE1,'*',1)
        GOSUB READ.STMT.ENTRY
        CTR.REC += 1
        IF NOT(R.STMT.ENTRY) THEN
            CONTINUE
        END

        TXN.FT.TT = R.STMT.ENTRY<AC.STE.SYSTEM.ID>
        THER.REF = R.STMT.ENTRY<AC.STE.THEIR.REFERENCE>
        AMT.LCY = R.STMT.ENTRY<AC.STE.AMOUNT.LCY>
        TRANS.REF = R.STMT.ENTRY<AC.STE.TRANS.REFERENCE>
        DB.AC = R.STMT.ENTRY<AC.STE.ACCOUNT.NUMBER>
        GOSUB FT.TT.TXN.VAL
        IF TT.REC.STATUS EQ 'REVE' THEN
            YFLG = 2
            CONTINUE
        END
        GOSUB READ.ACCOUNT
        DB.CAT = R.ACCOUNT<AC.CATEGORY>
        GOSUB TT.ATM.CASH.VAL
    REPEAT
    RETURN

READ.ACCOUNT:
*************
    R.ACCOUNT = ''; ACCOUNT.ERR = ''; ERR.ACCOUNT = ''; Y.DB.AC = ''
    CALL F.READ(FN.ACCOUNT,DB.AC,R.ACCOUNT,F.ACCOUNT,ACCOUNT.ERR)
    IF NOT(R.ACCOUNT) THEN
        Y.DB.AC = DB.AC
        CALL EB.READ.HISTORY.REC(F.ACCOUNT.HST,Y.DB.AC,R.ACCOUNT,ERR.ACCOUNT)
    END
    RETURN

FT.TT.TXN.VAL:
**************
    TRANS.REF = FIELD(TRANS.REF,'\',1)
    IF TXN.FT.TT EQ 'TT' THEN
        GOSUB READ.TELLER
        TT.REC.STATUS = R.TELLER<TT.TE.RECORD.STATUS>
        TT.AMT.LCL = R.TELLER<TT.TE.AMOUNT.LOCAL.2>
        TT.TXN.VAL = R.TELLER<TT.TE.TRANSACTION.CODE>
    END ELSE
        GOSUB READ.FUNDS.TRANS
        TT.REC.STATUS = R.FUNDS.TRANSFER<FT.RECORD.STATUS>
        FT.TXN.VAL = R.FUNDS.TRANSFER<FT.TRANSACTION.TYPE>
        FT.AMT.LCL = R.FUNDS.TRANSFER<FT.LOC.AMT.DEBITED>
    END
    RETURN

TT.ATM.CASH.VAL:
****************
    ATM.POS = 0; CURR.CAT.POS = 0
    LOCATE FT.TXN.VAL IN ATM.CODES<1> SETTING ATM.POS THEN
        LOCATE DB.CAT IN CHK.CURR.CAT<1> SETTING CURR.CAT.POS THEN
            ATM.CNT += 1
            ATM.AMT += FT.AMT.LCL
        END
    END

    SAV.CAT.POS = 0; SAV.POS = ''
    LOCATE FT.TXN.VAL IN ATM.CODES<1> SETTING SAV.POS THEN
        LOCATE DB.CAT IN CHK.SAV.CAT<1> SETTING SAV.CAT.POS THEN
            SAV.CNT += 1
            SAV.AMT += FT.AMT.LCL
        END
    END

    WTDW.POS = 0
    LOCATE TT.TXN.VAL IN TT.CASH.WTDW.CODES<1> SETTING WTDW.POS THEN
        LOCATE DB.CAT IN CHK.SAV.CAT<1> SETTING SAV.CAT.POS THEN
            CASH.WTDW.CNT += 1
            CASH.WTDW.AMT += TT.AMT.LCL
        END
    END
    RETURN

GET.FIELD8:
*---------*
    R.REDO.APAP.CLEARING.INWARD = ''
    IF TRANS.REF THEN
        CALL F.READ(FN.REDO.APAP.CLEARING.INWARD,TRANS.REF,R.REDO.APAP.CLEARING.INWARD,F.REDO.APAP.CLEARING.INWARD,REDO.APAP.CLEARING.INWARD.ERR)
        IF R.REDO.APAP.CLEARING.INWARD THEN
            IF R.REDO.APAP.CLEARING.INWARD<CLEAR.CHQ.STATUS> EQ 'PAID' AND R.REDO.APAP.CLEARING.INWARD<CLEAR.CHQ.TAX.AMOUNT> NE 0 THEN
                PAID.VAL += 1
                TAX.AMT += R.REDO.APAP.CLEARING.INWARD<CLEAR.CHQ.AMOUNT>
                YFLG = 2
            END ELSE
                YFLG = 1
            END
        END
    END
    RETURN

GET.FIELD9:
*---------*
    R.REDO.CLEARING.OUTWARD = ''; OUT.ID = ''
    DRAW.ACCT = FIELD(TRANS.REF,'.',1); CHQ.NO = FIELD(TRANS.REF,'.',2)
    CHQ.NO = TRIM(CHQ.NO,"0","L")
    OUT.ID = DRAW.ACCT:'-':CHQ.NO
    CALL F.READ(FN.REDO.CLEARING.OUTWARD,OUT.ID,R.REDO.CLEARING.OUTWARD,F.REDO.CLEARING.OUTWARD,REDO.CLEARING.OUTWARD.ERR)
    IF R.REDO.CLEARING.OUTWARD THEN
        IF R.REDO.CLEARING.OUTWARD<CLEAR.OUT.ROUTE.NO> EQ TAX.ROUTE.CODE AND R.REDO.APAP.CLEARING.INWARD<CLEAR.CHQ.TAX.AMOUNT> NE 0 THEN
            CHQ.CNT += 1
            CHQ.AMT += R.REDO.CLEARING.OUTWARD<CLEAR.OUT.AMOUNT>
            YFLG = 2
        END ELSE
            YFLG = 1
        END
    END
    RETURN

GET.FIELD10:
*----------*
    YTTT.CODE = ''; YTTT.AMT.C = ''; YTTT.AMT = ''
    TRANS.REF = FIELD(TRANS.REF,'\',1)
    GOSUB READ.TELLER
    IF R.TELLER THEN
        TT.TXN.VAL = R.TELLER<TT.TE.TRANSACTION.CODE>
        YTEMP.TYP = TT.TXN.VAL
        TT.REC.STATUS = R.TELLER<TT.TE.RECORD.STATUS>
        YTTT.AMT = R.TELLER<TT.TE.AMOUNT.LOCAL.2>
        YTEMP.VAL = YTTT.AMT
        IF TT.REC.STATUS EQ 'REVE' OR AMT.LCY[1,1] EQ '-' THEN
            YTTT.AMT = YTTT.AMT * (-1)
        END
        GOSUB GET.FIELD10.SUB
    END
    RETURN

GET.FIELD10.SUB:
***************
    GOSUB GET.TT.TXN.PAY.AMT
    LOCATE TT.TXN.VAL IN FT.FCY.TYPE<1> SETTING TRANSF.POS THEN
        TT.TRANSF.CNT += 1
        TT.TRANSF.AMT += YTTT.AMT
        YFLG = 2
    END
    LOCATE TT.TXN.VAL IN FT.LCY.TYPE<1> SETTING FT.LCY1.POS THEN
        FTBCO.CNT += 1
        FTBCO.AMT += YTTT.AMT
        YFLG = 2
    END
    RETURN

GET.TT.TXN.PAY.AMT:
*******************
    TT.TXN.POS = ''; TT.PAY.POS = ''
    LOCATE TT.TXN.VAL IN TT.TXN.CODE<1> SETTING TT.TXN.POS THEN
        TT.TXN.CNT += 1
        TT.TXN.AMT += YTTT.AMT
        YFLG = 2
    END
*
    LOCATE TT.TXN.VAL IN FT.PAY.ACOR<1> SETTING TT.PAY.POS THEN
        TT.PAY.CNT += 1
        TT.PAY.AMT += YTTT.AMT
        YFLG = 2
    END
    RETURN

READ.TELLER:
************
    R.TELLER = ''; TELLER.ERR = '';Y.TRANS.REF = ''; TELLER.HIS.ERR = ''
    CALL F.READ(FN.TELLER,TRANS.REF,R.TELLER,F.TELLER,TELLER.ERR)
    IF NOT(R.TELLER) THEN
        Y.TRANS.REF = TRANS.REF
        CALL EB.READ.HISTORY.REC(F.TELLER.HIS,Y.TRANS.REF,R.TELLER,TELLER.HIS.ERR)
    END
    RETURN

READ.FUNDS.TRANS:
*****************
    R.FUNDS.TRANSFER = ''; FUNDS.TRANSFER.ERR = ''; Y.TRANS.REF = ''; FUNDS.TRANSFER.HIS.ERR = ''
    CALL F.READ(FN.FUNDS.TRANSFER,TRANS.REF,R.FUNDS.TRANSFER,F.FUNDS.TRANSFER,FUNDS.TRANSFER.ERR)
    IF NOT(R.FUNDS.TRANSFER) THEN
        Y.TRANS.REF = TRANS.REF
        CALL EB.READ.HISTORY.REC(F.FUNDS.TRANSFER.HIS,Y.TRANS.REF,R.FUNDS.TRANSFER,FUNDS.TRANSFER.HIS.ERR)
    END
    RETURN

GET.FT.FIELD:
*-----------*
    FT.TXN.VAL = ''; FT.REC.STATUS = ''; DB.AC = ''; FT.COMM.TYPE = ''
    FT.COMM.AMT = ''; FT.PAYMENT.DET = ''
    TRANS.REF = FIELD(TRANS.REF,'\',1)
    GOSUB READ.FUNDS.TRANS
    IF R.FUNDS.TRANSFER THEN
        FT.REC.STATUS = R.FUNDS.TRANSFER<FT.RECORD.STATUS>
        FT.PAYMENT.DET = R.FUNDS.TRANSFER<FT.PAYMENT.DETAILS>
        FT.TXN.VAL = R.FUNDS.TRANSFER<FT.TRANSACTION.TYPE>
        FT.COMM.AMT = R.FUNDS.TRANSFER<FT.LOC.AMT.DEBITED>
        YTEMP.VAL = FT.COMM.AMT
        YTEMP.TYP = FT.TXN.VAL
        IF FT.REC.STATUS EQ 'REVE' OR FT.PAYMENT.DET[1,7] EQ 'REVERSO' OR AMT.LCY[1,1] EQ '-' THEN
            FT.COMM.AMT = FT.COMM.AMT * (-1)
        END
        GOSUB GET.FT.TXN.DB.RES.TRF
        LOCATE FT.TXN.VAL IN DEB.PAY.TXN.CODES<1> SETTING PAY.POS THEN
            DEB.PAY.CNT += 1
            DEB.PAY.AMT += FT.COMM.AMT
            YFLG = 2
        END
        FT.PAY.POS = ''
        LOCATE FT.TXN.VAL IN FT.PAY.ACOR<1> SETTING FT.PAY.POS THEN
            FT.PAY.CNT += 1
            FT.PAY.AMT += FT.COMM.AMT
            YFLG = 2
        END
        GOSUB GET.FT.FIELD.1
    END
    RETURN

GET.FT.FIELD.1:
***************
    LOAN.REC.POS = ''; CC.CHG.POS = ''; FT.LCY.POS = ''; FT.FCY.POS = ''
    LOCATE FT.TXN.VAL IN LOAN.REC.CODES<1> SETTING LOAN.REC.POS THEN
        DB.LOAN.REC.CNT += 1
        DB.LOAN.REC.AMT += FT.COMM.AMT
        YFLG = 2
    END
    LOCATE FT.TXN.VAL IN CC.CHG.CODES<1> SETTING CC.CHG.POS THEN
        CC.CHG.CNT += 1
        CC.CHG.AMT += FT.COMM.AMT
        YFLG = 2
    END
    LOCATE FT.TXN.VAL IN FT.LCY.TYPE<1> SETTING FT.LCY.POS THEN
        FTBCO.CNT += 1
        FTBCO.AMT += FT.COMM.AMT
        YFLG = 2
    END
    LOCATE FT.TXN.VAL IN FT.FCY.TYPE<1> SETTING FT.FCY.POS THEN
        FT.BCOEXT.CNT += 1
        FT.BCOEXT.AMT += FT.COMM.AMT
        YFLG = 2
    END
    LOCATE R.FUNDS.TRANSFER<FT.LOCAL.REF,L.ACTUAL.VERSIO.POS> IN FT.VERSION<1> SETTING FT.VER.POS THEN
        GOSUB GET.INT.PAY.ATM
    END
    GOSUB FLAG.VALE
    RETURN

GET.FT.TXN.DB.RES.TRF:
**********************
    FT.TXN.POS = ''; DEB.POS = ''; RES.POS = ''
    LOCATE FT.TXN.VAL IN FT.TXN.CODE<1> SETTING FT.TXN.POS THEN
        FT.TXN.CNT += 1
        FT.TXN.AMT += FT.COMM.AMT
        YFLG = 2
    END
    LOCATE FT.TXN.VAL IN DEBTRF.TXN.CODES<1> SETTING DEB.POS THEN
        DEBTRF.CNT += 1
        DEBTRF.AMT += FT.COMM.AMT
        YFLG = 2
    END
    LOCATE FT.TXN.VAL IN RESTRF.TXN.CODES<1> SETTING RES.POS THEN
        RESTRF.CNT += 1
        RESTRF.AMT += FT.COMM.AMT
        YFLG = 2
    END
    RETURN

GET.INT.PAY.ATM:
****************
    CHQ.AC3.POS = ''
    LOCATE FT.TXN.VAL IN FT.CHQ.AC3<1> SETTING CHQ.AC3.POS THEN
        INT.PAY.CNT += 1
        INT.PAY.AMT += FT.COMM.AMT
        YFLG = 2
    END
    GOSUB FLAG.VALE
    RETURN

POPULATE.VALUES:
*--------------*
    R.DR.REG.REGN8.CONCAT<DR.REGN8.CONCAT.FIELD8> = PAID.VAL:'|':TAX.AMT
    R.DR.REG.REGN8.CONCAT<DR.REGN8.CONCAT.FIELD9> = CHQ.CNT:'|':CHQ.AMT
    R.DR.REG.REGN8.CONCAT<DR.REGN8.CONCAT.FIELD10> = TT.TXN.CNT:'|':TT.TXN.AMT
    R.DR.REG.REGN8.CONCAT<DR.REGN8.CONCAT.FIELD11> = FT.TXN.CNT:'|':FT.TXN.AMT
    R.DR.REG.REGN8.CONCAT<DR.REGN8.CONCAT.FIELD12> = DEBTRF.CNT - RESTRF.CNT:'|':DEBTRF.AMT - RESTRF.AMT
    R.DR.REG.REGN8.CONCAT<DR.REGN8.CONCAT.FIELD13> = DEB.PAY.CNT:'|':DEB.PAY.AMT
    R.DR.REG.REGN8.CONCAT<DR.REGN8.CONCAT.FIELD14> = FT.PAY.CNT + TT.PAY.CNT:'|':FT.PAY.AMT + TT.PAY.AMT
    R.DR.REG.REGN8.CONCAT<DR.REGN8.CONCAT.FIELD15> = ''     ;* This information is not available in T24
    R.DR.REG.REGN8.CONCAT<DR.REGN8.CONCAT.FIELD16> = DB.LOAN.REC.CNT:'|':DB.LOAN.REC.AMT
    R.DR.REG.REGN8.CONCAT<DR.REGN8.CONCAT.FIELD17> = CC.CHG.CNT:'|':CC.CHG.AMT
    R.DR.REG.REGN8.CONCAT<DR.REGN8.CONCAT.FIELD18> = FTBCO.CNT:'|':FTBCO.AMT
    R.DR.REG.REGN8.CONCAT<DR.REGN8.CONCAT.FIELD19> = TT.TRANSF.CNT + FT.BCOEXT.CNT:'|':TT.TRANSF.AMT + FT.BCOEXT.AMT
    R.DR.REG.REGN8.CONCAT<DR.REGN8.CONCAT.FIELD20> = INT.PAY.CNT:'|':INT.PAY.AMT
    R.DR.REG.REGN8.CONCAT<DR.REGN8.CONCAT.FIELD21> = ''     ;* This information is not available in T24
    R.DR.REG.REGN8.CONCAT<DR.REGN8.CONCAT.FIELD22> = NOT.USED.CNT:'|':NOT.USED.AMT
    R.DR.REG.REGN8.CONCAT<DR.REGN8.CONCAT.FIELD23> = ''     ;* This information is not available in T24
    R.DR.REG.REGN8.CONCAT<DR.REGN8.CONCAT.FIELD27> = ATM.CNT:'|':ATM.AMT
    R.DR.REG.REGN8.CONCAT<DR.REGN8.CONCAT.FIELD28> = SAV.CNT:'|':SAV.AMT
    R.DR.REG.REGN8.CONCAT<DR.REGN8.CONCAT.FIELD29> = CASH.WTDW.CNT:'|':CASH.WTDW.AMT:'|':YTOT.TAXAMT
    RETURN

CONCAT.WRITE:
*************
    CONCAT.ID = TAX.ACC
    CALL F.WRITE(FN.DR.REG.REGN8.CONCAT,CONCAT.ID,R.DR.REG.REGN8.CONCAT)
    RETURN

FLAG.VALE:
**********
    IF YFLG NE 2 THEN
        YFLG = 1
    END
    RETURN
END
