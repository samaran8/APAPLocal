*-----------------------------------------------------------------------------
* <Rating>75</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE LAPAP.VVR.VALIDATE.STATUS
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.CUSTOMER
    $INSERT I_F.ACCOUNT
    $INSERT TAM.BP I_F.REDO.CUST.PRD.LIST
    $INSERT I_System


    GOSUB INIT
    GOSUB UPD.STATUS
    RETURN

INIT:
    FN.CUST.PRD.LIST = 'F.REDO.CUST.PRD.LIST'
    F.CUST.PRD.LIST = ''
    CALL OPF(FN.CUST.PRD.LIST,F.CUST.PRD.LIST)
    FN.ACC = 'F.ACCOUNT'
    F.ACC = ''
    CALL OPF(FN.ACC,F.ACC)
    RETURN

UPD.STATUS:

    IF PGM.VERSION EQ ',REDO.STATUS.ALL.PF' OR PGM.VERSION EQ ',REDO.STATUS.ALL.MEN' THEN

        Y.STATUS = R.OLD(EB.CUS.CUSTOMER.STATUS)

        IF Y.STATUS EQ 3 THEN

            ETEXT = "PARA ELIMINAR ESTATUS DE FALLECIDO CONTACTE A OPERACIONES"
            CALL STORE.END.ERROR

        END

    END

    IF R.NEW(EB.CUS.CURR.NO) EQ '' THEN
        R.NEW(EB.CUS.CUSTOMER.STATUS) = '1'
    END
    IF COMI EQ '4' THEN
        CALL F.READ(FN.CUST.PRD.LIST,ID.NEW,R.CUST.PRD.LIST,F.CUST.PRD.LIST,CUS.ERR)
        Y.PRD.STATUS.LIST = R.CUST.PRD.LIST<PRD.PRD.STATUS>
        Y.PRD.PRODUCT.LIST = R.CUST.PRD.LIST<PRD.PRODUCT.ID>
        CHANGE VM TO FM IN Y.PRD.STATUS.LIST
        CHANGE VM TO FM IN Y.PRD.PRODUCT.LIST
        Y.PRD.QNTT = DCOUNT(Y.PRD.PRODUCT.LIST, FM)
        FOR A = 1 TO Y.PRD.QNTT STEP 1
            Y.CURR.PRD = Y.PRD.PRODUCT.LIST<A>
            CALL F.READ(FN.ACC,Y.CURR.PRD,R.ACC,F.ACC,ACC.ERR)
            IF R.ACC THEN
                Y.ACC.CUSTOMER = R.ACC<AC.CUSTOMER>
                Y.ACC.JOIN.HOL = R.ACC<AC.JOINT.HOLDER>
                IF Y.ACC.CUSTOMER EQ ID.NEW THEN
                    TEXT = "PRODUCTO NUM.:" : Y.CURR.PRD : ", ESTA ACTIVO."
                    E = TEXT
                    CALL STORE.END.ERROR
                    RETURN
                END
                FINDSTR ID.NEW IN Y.ACC.JOIN.HOL SETTING V.FLD, V.VAL THEN
                    TEXT = "PRODUCTO COMPARTIDO NUM.:" : Y.CURR.PRD : ", ESTA ACTIVO."
                    E = TEXT
                    CALL STORE.END.ERROR
                    RETURN
                END
            END
        NEXT A
*SI LLEGO A ESTE PUNTO, ES PORQUE NO HAY PRODUCTO ACTIVO EN REALIDAD...
*ELIMINO EL REGISTRO DE REDO.CUST.PRD.LIST PARA QUE NO HAYA TEMAS A FUTURO...
        CALL F.DELETE(FN.CUST.PRD.LIST,ID.NEW)
*LOCATE 'ACTIVE' IN Y.PRD.STATUS.LIST SETTING POS THEN
*    ETEXT = "EB-REDO.ACTIVE.PRD.FOUND"
*    CALL STORE.END.ERROR
*END
    END
    RETURN
END
