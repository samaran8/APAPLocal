*-----------------------------------------------------------------------------
* <Rating>-202</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE DR.REG.213IF01.UPDATE.CONCAT(REC.ID)
*-----------------------------------------------------------------------------
* Company Name   : APAP
* Developed By   :
* Program Name   : DR.REG.213IF01.UPDATE.CONCAT
* Date           : 2-May-2013
*-----------------------------------------------------------------------------
* Description:
*------------
* This multi-thread job is meant for to extact the Customer Details for
*-----------------------------------------------------------------------------
*
* Modification History :
* ----------------------
*   Date        Author             Modification Description
* 11-Aug-2014   V.P.Ashokkumar     PACS00309079 - Updated the field mapping and format
* 14-Oct-2014   V.P.Ashokkumar     PACS00309079 - Updated to filter AML transaction
* 01-Dec-2014   Ashokkumar.V.P     PACS00309080:- Added the section for REGN8 report.
* 21-Mar-2015   Ashokkumar.V.P     PACS00309079:- Added AA overpayment details.
*-----------------------------------------------------------------------------

    $INCLUDE T24.BP I_COMMON
    $INCLUDE T24.BP I_EQUATE
    $INCLUDE T24.BP I_BATCH.FILES
    $INCLUDE T24.BP I_TSA.COMMON
    $INCLUDE T24.BP I_F.DATES
    $INCLUDE T24.BP I_F.STMT.ENTRY
    $INCLUDE T24.BP I_F.CUSTOMER
    $INCLUDE T24.BP I_F.FUNDS.TRANSFER
    $INCLUDE T24.BP I_F.TELLER
    $INCLUDE T24.BP I_F.TRANSACTION
    $INCLUDE T24.BP I_F.CURRENCY
    $INCLUDE T24.BP I_F.ACCOUNT
    $INCLUDE T24.BP I_F.RELATION.CUSTOMER
    $INCLUDE LAPAP.BP I_DR.REG.213IF01.UPDATE.CONCAT.COMMON
    $INCLUDE REGREP.BP I_F.DR.REG.213IF01.PARAM
    $INCLUDE REGREP.BP I_F.DR.REG.213IF01.CONCAT
    $INCLUDE TAM.BP I_F.REDO.TRANSACTION.CHAIN
    $INCLUDE USPLATFORM.BP I_F.T24.FUND.SERVICES
    $INCLUDE REGREP.BP I_F.DR.REG.REGN8.PARAM
    $INCLUDE TAM.BP I_F.REDO.AA.OVERPAYMENT


    YREGN8.FLG = 0; Y.T24.FS.REF = ''
    BEGIN CASE
    CASE CONTROL.LIST<1,1> = 'ACCT.ENT.LWORK.DAY'
        GOSUB PROCESS.ACCT
    CASE CONTROL.LIST<1,1> = 'REDO.TRANSACTION'
        GOSUB PROCESS.AA
    CASE CONTROL.LIST<1,1> = 'REDO.AA.OVERPAY'
        GOSUB PROCESS.AA.OVERPAY
    END CASE
    RETURN

PROCESS.ACCT:
*************
    R.ACCT.ENT.LWORK.DAY = ''; ACCT.ENT.LWORK.DAY.ERR = ''; YACCT.ID = REC.ID; YSUSP.FLG = 0
    CALL F.READ(FN.ACCT.ENT.LWORK.DAY,REC.ID,R.ACCT.ENT.LWORK.DAY,F.ACCT.ENT.LWORK.DAY,ACCT.ENT.LWORK.DAY.ERR)
    IF R.ACCT.ENT.LWORK.DAY THEN
        CTR.STMT.ID = 1
        CNT.STMT.ID = DCOUNT(R.ACCT.ENT.LWORK.DAY,FM)
        LOOP
        WHILE CTR.STMT.ID LE CNT.STMT.ID
            R.STMT.ENTRY = ''; STMT.ENTRY.ERR = ''; AMT.FCY.VAL = ''; YCCY = ''; YTRANS.TYPE = ''
            AMT.LCY.VAL = ''; TXN.FT.TT = ''; YSTMT.CUST = ''; AMT.VAL = ''; RTE.FORM.VAL = ''
            STMT.ID = R.ACCT.ENT.LWORK.DAY<CTR.STMT.ID>
            CALL F.READ(FN.STMT.ENTRY,STMT.ID,R.STMT.ENTRY,F.STMT.ENTRY,STMT.ENTRY.ERR)
            YSTMT.CUST = R.STMT.ENTRY<AC.STE.CUSTOMER.ID>
            CONCAT.ID = YSTMT.CUST:'-':LAST.WRK.DATE
            GOSUB PROCESS.1
            CTR.STMT.ID += 1
        REPEAT
    END
    RETURN

PROCESS.1:
**********
    IF R.STMT.ENTRY THEN
        TXN.FT.TT = R.STMT.ENTRY<AC.STE.SYSTEM.ID>
        AMT.LCY.VAL = R.STMT.ENTRY<AC.STE.AMOUNT.LCY>
        AMT.FCY.VAL = R.STMT.ENTRY<AC.STE.AMOUNT.FCY>
        YSTMT.CUST = R.STMT.ENTRY<AC.STE.CUSTOMER.ID>
        TXN.REF = R.STMT.ENTRY<AC.STE.TRANS.REFERENCE>
        Y.STMT.TXN.CODE = R.STMT.ENTRY<AC.STE.TRANSACTION.CODE>
        YCCY = R.STMT.ENTRY<AC.STE.CURRENCY>
        GOSUB SUSP.ACCT.DET
        IF NOT(YSTMT.CUST) THEN
            RETURN
        END
        GOSUB L.RTE.FORM.CHK
        GOSUB FT.TT.TXN.VAL
        IF (TXN.FT.TT EQ 'FT' OR TXN.FT.TT EQ 'TT') AND TT.REC.STATUS NE 'REVE' THEN
            GOSUB GET.REGN8.PARAM
            YTRANS.TYPE = 'CASH'
            GOSUB CUST.REL.CHK
            GOSUB UPDATE.CONCAT.TABLE
        END
    END
    RETURN

SUSP.ACCT.DET:
**************
    IF NOT(NUM(REC.ID)) THEN
        YSUSP.FLG = 1
        YSTMT.CUST = ''
        Y.T24.FS.REF = R.STMT.ENTRY<AC.STE.THEIR.REFERENCE>
        GOSUB READ.TFS
        YACCT.ID = Y.L.FT.ADD.INFO.VAL
        GOSUB READ.ACCOUNT
        YSTMT.CUST = R.ACCOUNT<AC.CUSTOMER>
        CONCAT.ID = YSTMT.CUST:'-':LAST.WRK.DATE
    END
    RETURN

GET.REGN8.PARAM:
****************
    DB.CAT = ''
    GOSUB READ.ACCOUNT
    DB.CAT = R.ACCOUNT<AC.CATEGORY>
    GOSUB TT.ATM.CASH.VAL
    RETURN

FT.TT.TXN.VAL:
**************
    TXN.REF = FIELD(TXN.REF,'\',1)
    IF TXN.FT.TT EQ 'TT' THEN
        GOSUB READ.TELLER
        TT.REC.STATUS = R.TT<TT.TE.RECORD.STATUS>
        TT.TXN.VAL = R.TT<TT.TE.TRANSACTION.CODE>
    END ELSE
        GOSUB READ.FUND.TRANS
        TT.REC.STATUS = R.FT<FT.RECORD.STATUS>
        FT.TXN.VAL = R.FT<FT.TRANSACTION.TYPE>
    END
    RETURN

TT.ATM.CASH.VAL:
****************
    ATM.POS = 0; CURR.CAT.POS = 0; SAV.CAT.POS = 0; SAV.POS = ''
    WTDW.POS = 0; YREGN8.FLG = 0
    LOCATE FT.TXN.VAL IN ATM.CODES<1> SETTING ATM.POS THEN
        LOCATE DB.CAT IN CHK.CURR.CAT<1> SETTING CURR.CAT.POS THEN
            YREGN8.FLG = 1
        END
    END

    LOCATE FT.TXN.VAL IN ATM.CODES<1> SETTING SAV.POS THEN
        LOCATE DB.CAT IN CHK.SAV.CAT<1> SETTING SAV.CAT.POS THEN
            YREGN8.FLG = 1
        END
    END

    LOCATE TT.TXN.VAL IN TT.CASH.WTDW.CODES<1> SETTING WTDW.POS THEN
        LOCATE DB.CAT IN CHK.SAV.CAT<1> SETTING SAV.CAT.POS THEN
            YREGN8.FLG = 1
        END
    END
    RETURN

PROCESS.AA:
***********
    IF REC.ID[1,2] EQ 'TT' THEN
        RETURN
    END

    ERR.REDO.TRANSACTION.CHAIN = ''; R.REDO.TRANSACTION.CHAIN = ''; YTOTAL.CASH = 0; YTOTAL.CHECK = 0
    YSTMT.NO = ''; YACCT.ID = ''; YTRANS.TYPE = ''; YCCY = ''; YSTMT.NO.T = ''; YTEMP.AMT = 0; RTE.FORM.VAL = ''
    CALL F.READ(FN.REDO.TRANSACTION.CHAIN,REC.ID,R.REDO.TRANSACTION.CHAIN,F.REDO.TRANSACTION.CHAIN,ERR.REDO.TRANSACTION.CHAIN)
    YTOTAL.CASH = R.REDO.TRANSACTION.CHAIN<RTC.TOTAL.CASH>
    YTOTAL.CHECK = R.REDO.TRANSACTION.CHAIN<RTC.TOTAL.CHECK>
    YTRANS.TYPE = R.REDO.TRANSACTION.CHAIN<RTC.TRANS.TYPE>
    LOCATE 'CASH' IN YTRANS.TYPE<1,1> SETTING CA.POSN THEN
        YTRANS.TYPE = 'CASH'
    END
    YCCY = R.REDO.TRANSACTION.CHAIN<RTC.TRANS.CCY,1>
    TXN.REF = REC.ID
    GOSUB READ.FUND.TRANS
    TXN.REF = REC.ID
    YACCT.ID = R.FT<FT.CREDIT.ACCT.NO>
    GOSUB READ.ACCOUNT
    YSTMT.CUST = R.ACCOUNT<AC.CUSTOMER>
    IF NOT(YSTMT.CUST) THEN
        RETURN
    END
    YCOMP.CODE = R.ACCOUNT<AC.CO.CODE>
    YSTMT.NO.T = R.FT<FT.STMT.NOS>
    LOCATE YCOMP.CODE IN YSTMT.NO.T<1,1> SETTING POSN THEN
        YSTMT.NO = R.FT<FT.STMT.NOS,POSN+1>
        STMT.ID = YSTMT.NO:"0001"
    END ELSE
        YSTMT.NO = R.FT<FT.STMT.NOS,1>
        STMT.ID = YSTMT.NO:"0001"
        IF NOT(NUM(YSTMT.NO)) THEN
            YSTMT.NO = R.FT<FT.STMT.NOS,2>
            STMT.ID = YSTMT.NO:"0001"
        END
    END

    TXN.FT.TT = REC.ID[1,2]
    CONCAT.ID = YSTMT.CUST:'-':LAST.WRK.DATE
    GOSUB READ.STMT.ENTRY
    GOSUB L.RTE.FORM.CHK
    IF WTR.AML.CHECK NE 'Y' THEN
        STMT.ID = ''; AMT.FCY.VAL = ''; AMT.LCY.VAL = ''
        TXN.REF = REC.ID
        STMT.ID = YSTMT.NO:"0002"
        GOSUB READ.STMT.ENTRY
        GOSUB L.RTE.FORM.CHK
    END
    AMT.VAL = ABS(YTOTAL.CASH)
    GOSUB CUST.REL.CHK
    GOSUB UPDATE.CONCAT.TABLE
    RETURN

PROCESS.AA.OVERPAY:
*******************
    ERR.REDO.AA.OVERPAYMENT = ''; R.REDO.AA.OVERPAYMENT = ''; AMT.VAL = ''; YCCY = ''
    YTRANS.TYPE = ''; TXN.REF = ''; YACCT.ID = ''; YCOMP.CODE = ''; RTE.FORM.VAL = ''
    YSTMT.NO.T = ''; TT.TXN.VAL = ''; YREC.STATUS = ''; YTEMP.AMT = 0
    CALL F.READ(FN.REDO.AA.OVERPAYMENT,REC.ID,R.REDO.AA.OVERPAYMENT,F.REDO.AA.OVERPAYMENT,ERR.REDO.AA.OVERPAYMENT)
    YTRANS.TYPE = R.REDO.AA.OVERPAYMENT<REDO.OVER.PAYMENT.METHOD>
    TXN.REF = R.REDO.AA.OVERPAYMENT<REDO.OVER.TELLER.TXN.REF>
    YACCT.ID = R.REDO.AA.OVERPAYMENT<REDO.OVER.LOAN.NO>
    YCCY = R.REDO.AA.OVERPAYMENT<REDO.OVER.CURRENCY>
    AMT.VAL = R.REDO.AA.OVERPAYMENT<REDO.OVER.AMOUNT>
    YSTMT.CUST = R.REDO.AA.OVERPAYMENT<REDO.OVER.CUSTOMER.ID>
    GOSUB READ.ACCOUNT
    YCOMP.CODE = R.ACCOUNT<AC.CO.CODE>

    GOSUB READ.TELLER
    RTE.FORM.VAL = R.TT<TT.TE.LOCAL.REF,TT.L.RTE.FORM.POS>
    YREC.STATUS = R.TT<TT.TE.RECORD.STATUS>
    TT.TXN.VAL = R.TT<TT.TE.TRANSACTION.CODE>
    YSTMT.NO.T = R.TT<TT.TE.STMT.NO>
    YSTMT.NO = ''; STMT.ID = ''
    LOCATE YCOMP.CODE IN YSTMT.NO.T<1,1> SETTING POSN THEN
        YSTMT.NO = R.TT<TT.TE.STMT.NO,POSN+1>
        STMT.ID = YSTMT.NO:"0002"
    END ELSE
        YSTMT.NO = R.TT<TT.TE.STMT.NO,1>
        STMT.ID = YSTMT.NO:"0002"
        IF NOT(NUM(YSTMT.NO)) THEN
            YSTMT.NO = R.TT<TT.TE.STMT.NO,2>
            STMT.ID = YSTMT.NO:"0002"
        END
    END
    CONCAT.ID = YSTMT.CUST:'-':LAST.WRK.DATE
    WTR.AML.CHECK = 'Y'
    GOSUB CUST.REL.CHK
    GOSUB UPDATE.CONCAT.TABLE
    RETURN

READ.STMT.ENTRY:
****************
    R.STMT.ENTRY = ''; STMT.ENTRY.ERR = ''
    CALL F.READ(FN.STMT.ENTRY,STMT.ID,R.STMT.ENTRY,F.STMT.ENTRY,STMT.ENTRY.ERR)
    IF R.STMT.ENTRY THEN
        Y.STMT.TXN.CODE = R.STMT.ENTRY<AC.STE.TRANSACTION.CODE>
        AMT.LCY.VAL = R.STMT.ENTRY<AC.STE.AMOUNT.LCY>
        AMT.FCY.VAL = R.STMT.ENTRY<AC.STE.AMOUNT.FCY>
        YSTMT.CUST = R.STMT.ENTRY<AC.STE.CUSTOMER.ID>
    END
    RETURN

UPDATE.CONCAT.TABLE:
*------------------*

    R.DR.REG.213IF01.CONCAT = ''; DR.REG.213IF01.CONCAT.ERR = ''; YSTMT.VAL = ''; YAMT.AMT = ''; YTEMP.YSTMT.VAL = ''
    YSTMT.VAL = STMT.ID:'*':YACCT.ID:'*':Y.T24.FS.REF:'*':YTP.STMT.CUST
    CALL F.READ(FN.DR.REG.213IF01.CONCAT,CONCAT.ID,R.DR.REG.213IF01.CONCAT,F.DR.REG.213IF01.CONCAT,DR.REG.213IF01.CONCAT.ERR)
    IF R.DR.REG.213IF01.CONCAT THEN
        YTEMP.YSTMT.VAL = R.DR.REG.213IF01.CONCAT<DR.213IF01.CONCAT.STMT.ID>
        FINDSTR Y.T24.FS.REF IN YTEMP.YSTMT.VAL<1,1> SETTING LPOSN,LSM,LVM THEN
            RETURN
        END
        CNT.STMT = DCOUNT(YTEMP.YSTMT.VAL,VM)
        CTR.STMT = CNT.STMT + 1
        IF AMT.VAL GT 0 AND YTRANS.TYPE EQ 'CASH' THEN
            R.DR.REG.213IF01.CONCAT<DR.213IF01.CONCAT.STMT.ID,CTR.STMT> = YSTMT.VAL
        END
        R.DR.REG.213IF01.CONCAT<DR.213IF01.CONCAT.PEP.STMT.ID,CTR.STMT> = YSTMT.VAL
        IF YREGN8.FLG EQ 1 THEN
            R.DR.REG.213IF01.CONCAT<DR.213IF01.CONCAT.REGN8.STMT.ID,CTR.STMT> = YSTMT.VAL
        END
        IF WTR.AML.CHECK EQ 'Y' THEN
            YAMT.AMT = AMT.VAL - YTEMP.AMT
            R.DR.REG.213IF01.CONCAT<DR.213IF01.CONCAT.CR.AMOUNT> += YAMT.AMT
        END
        YPEP.CHK = R.DR.REG.213IF01.CONCAT<DR.213IF01.CONCAT.CUST.RELATION>
        RTE.FORM.VAL = R.DR.REG.213IF01.CONCAT<DR.213IF01.CONCAT.CUST.RTE.FORM>
    END ELSE
        IF AMT.VAL GT 0 AND YTRANS.TYPE EQ 'CASH' THEN
            R.DR.REG.213IF01.CONCAT<DR.213IF01.CONCAT.STMT.ID,1> = YSTMT.VAL
        END
        IF YREGN8.FLG EQ 1 THEN
            R.DR.REG.213IF01.CONCAT<DR.213IF01.CONCAT.REGN8.STMT.ID,1> = YSTMT.VAL
        END
        R.DR.REG.213IF01.CONCAT<DR.213IF01.CONCAT.PEP.STMT.ID,1> = YSTMT.VAL
        IF WTR.AML.CHECK EQ 'Y' THEN
            R.DR.REG.213IF01.CONCAT<DR.213IF01.CONCAT.CR.AMOUNT> = AMT.VAL - YTEMP.AMT
        END ELSE
            R.DR.REG.213IF01.CONCAT<DR.213IF01.CONCAT.CR.AMOUNT> = '0'
        END
        R.DR.REG.213IF01.CONCAT<DR.213IF01.CONCAT.CR.DATE> = LAST.WRK.DATE
        R.DR.REG.213IF01.CONCAT<DR.213IF01.CONCAT.CUST.RELATION> = YPEP.CHK
    END

    IF WTR.AML.CHECK EQ 'Y' THEN
        RTE.FORM.VAL = 'YES'
    END

    IF RTE.FORM.VAL EQ 'YES' THEN
        R.DR.REG.213IF01.CONCAT<DR.213IF01.CONCAT.CUST.RTE.FORM> = RTE.FORM.VAL
    END

    IF RTE.FORM.VAL EQ 'YES' OR YPEP.CHK NE '' OR YREGN8.FLG EQ '1' THEN
        CALL F.WRITE(FN.DR.REG.213IF01.CONCAT,CONCAT.ID,R.DR.REG.213IF01.CONCAT)
    END
    RETURN

GET.CURRENCY.RATE:
******************
    R.CURRENCY = ''; CURR.ERR = ''; CUR.AMLBUY.RATE = ''
    CALL CACHE.READ(FN.CURRENCY,YCCY,R.CURRENCY,CURR.ERR)
    CUR.AMLBUY.RATE = R.CURRENCY<EB.CUR.LOCAL.REF,L.CU.AMLBUY.RT.POS>
    IF AMT.FCY.VAL THEN
        AMT.VAL = AMT.FCY.VAL * CUR.AMLBUY.RATE
    END ELSE
        AMT.VAL = AMT.LCY.VAL
    END
    RETURN

CUST.REL.CHK:
*************
    CUSTOMER.ERR = ''; R.CUSTOMER = ''; YREL.CODE = ''; CU.PEPS.VAL = ''
    CALL F.READ(FN.CUS,YSTMT.CUST,R.CUSTOMER,F.CUS,CUSTOMER.ERR)
    IF NOT(R.CUSTOMER) THEN
        CALL EB.READ.HISTORY.REC(F.CUS.HIS,YSTMT.CUST,R.CUSTOMER,CUSTOMER.ERR)
    END
    GOSUB READ.RELATION.CUSTOMER
*    YREL.CODE = R.CUSTOMER<EB.CUS.RELATION.CODE>
    CU.PEPS.VAL = R.CUSTOMER<EB.CUS.LOCAL.REF,CUS.L.CU.PEPS.POS>
    IF CU.PEPS.VAL EQ 'SI' OR R.RELATION.CUSTOMER NE '' THEN
        YPEP.CHK = 'PEP'
    END
    RETURN

GET.TFS.VAL:
************
    YTEMP.AMT = 0
    CHECK.DEP = 'CHQDEP'
    FINDSTR CHECK.DEP IN YTRANSACT<1> SETTING YPOSN,YVM,YSM THEN
        YTEMP.AMT = R.T24.FUND.SERVICES<TFS.AMOUNT,YVM>
    END
    RETURN

L.RTE.FORM.CHK:
***************
    R.TRANSACTION = ''; TRANS.ERR = ''; WTR.AML.CHECK = ''; Y.T24.FS.REF = ''
    CALL F.READ(FN.TRANSACTION,Y.STMT.TXN.CODE,R.TRANSACTION,F.TRANSACTION,TRANS.ERR)
    WTR.AML.CHECK = R.TRANSACTION<AC.TRA.LOCAL.REF><1,L.TR.AML.CHECK.POS>

    IF WTR.AML.CHECK EQ 'Y' THEN
        GOSUB GET.CURRENCY.RATE
    END

    RTE.FORM.VAL = ''; YREC.STATUS = ''; YTRANSACT = ''
    IF TXN.FT.TT EQ 'FT' THEN
        TXN.REF = FIELD(TXN.REF,'\',1)
        GOSUB READ.FUND.TRANS
        RTE.FORM.VAL = R.FT<FT.LOCAL.REF,FT.L.RTE.FORM.POS>
        FT.TXN.VAL = R.FT<FT.TRANSACTION.TYPE>
        YREC.STATUS = R.FT<FT.RECORD.STATUS>
    END

    IF TXN.FT.TT EQ 'TT' THEN
        TXN.REF = FIELD(TXN.REF,'\',1)
        GOSUB READ.TELLER
        RTE.FORM.VAL = R.TT<TT.TE.LOCAL.REF,TT.L.RTE.FORM.POS>
        YREC.STATUS = R.TT<TT.TE.RECORD.STATUS>
        TT.TXN.VAL = R.TT<TT.TE.TRANSACTION.CODE>
        Y.T24.FS.REF = R.TT<TT.TE.LOCAL.REF,T24.FS.REF.POS>
    END

    IF YSUSP.FLG EQ 1 AND WTR.AML.CHECK EQ 'Y' THEN
        RTE.FORM.VAL = Y.RTE.FORM.VAL
        RETURN
    END

    IF Y.T24.FS.REF THEN
        GOSUB READ.TFS
        GOSUB TFS.RTE.CHK
        YTRANSACT = R.T24.FUND.SERVICES<TFS.TRANSACTION>
        GOSUB GET.TFS.VAL
    END
    RETURN

READ.FUND.TRANS:
****************
    ERRH.FT = ''; R.FT = ''
    CALL EB.READ.HISTORY.REC(F.FT.HST,TXN.REF,R.FT,ERRH.FT)
    RETURN

READ.TELLER:
************
    R.TT = ''; TT.ERR = ''; ERRH.TT = ''
    CALL F.READ(FN.TELLER,TXN.REF,R.TT,F.TELLER,TT.ERR)
    IF NOT(R.TT) THEN
        CALL EB.READ.HISTORY.REC(F.TT.HST,TXN.REF,R.TT,ERRH.TT)
    END
    RETURN

READ.ACCOUNT:
*************
    R.ACCOUNT = ''; ERR.ACCOUNT = ''; Y.ACCT.HST = ''; YTP.STMT.CUST = ''
    YJOINT.HOLD = ''; YRELATION = ''; YPEP.CHK = ''
    CALL F.READ(FN.ACCOUNT,YACCT.ID,R.ACCOUNT,F.ACCOUNT,ERR.ACCOUNT)
    IF NOT(R.ACCOUNT) THEN
        Y.ACCT.HST = YACCT.ID
        CALL EB.READ.HISTORY.REC(F.ACCOUNT.HST,Y.ACCT.HST,R.ACCOUNT,ERRH.ACCOUNT)
    END
    YRELATION = R.ACCOUNT<AC.RELATION.CODE>
    IF YRELATION EQ '500' OR YRELATION EQ '501' THEN
        YJOINT.HOLD = R.ACCOUNT<AC.JOINT.HOLDER>
        YTP.STMT.CUST = YSTMT.CUST
        YSTMT.CUST = YJOINT.HOLD
        GOSUB CUST.REL.CHK
        YSTMT.CUST = YTP.STMT.CUST
        CU.PEPS.VAL = ''; R.RELATION.CUSTOMER = ''
    END
    RETURN

***********************
READ.RELATION.CUSTOMER:
***********************
* In this para of the program, file RELATION.CUSTOMER is read
    R.RELATION.CUSTOMER  = '';    RELATION.CUSTOMER.ER = ''
    CALL F.READ(FN.RELATION.CUSTOMER,YSTMT.CUST,R.RELATION.CUSTOMER,F.RELATION.CUSTOMER,RELATION.CUSTOMER.ER)
    RETURN

READ.TFS:
*********
    ERR.T24.FUND.SERVICES = ''; R.T24.FUND.SERVICES = ''; ERRH.T24.FUND.SERVICES = ''
    Y.RTE.FORM.VAL = ''; Y.L.FT.ADD.INFO.VAL = ''
    CALL F.READ(FN.T24.FUND.SERVICES,Y.T24.FS.REF,R.T24.FUND.SERVICES,F.T24.FUND.SERVICES,ERR.T24.FUND.SERVICES)
    IF NOT(R.T24.FUND.SERVICES) THEN
        Y.T24.FS.REF.HST = Y.T24.FS.REF
        CALL EB.READ.HISTORY.REC(F.T24.FUND.SERVICES.HST,Y.T24.FS.REF.HIST,R.T24.FUND.SERVICES,ERRH.T24.FUND.SERVICES)
    END
    Y.RTE.FORM.VAL = R.T24.FUND.SERVICES<TFS.LOCAL.REF,TFS.L.RTE.FORM.POS>
    Y.L.FT.ADD.INFO.VAL = R.T24.FUND.SERVICES<TFS.LOCAL.REF,TFS.L.FT.ADD.INFO.POS>
    RTE.FORM.VAL = Y.RTE.FORM.VAL
    RETURN

TFS.RTE.CHK:
************
    IF Y.RTE.FORM.VAL[1,1] EQ 'Y' THEN
        WTR.AML.CHECK = 'Y'
        GOSUB GET.CURRENCY.RATE
    END
    RETURN
END
