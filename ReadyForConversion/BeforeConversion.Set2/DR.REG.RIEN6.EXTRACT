*-----------------------------------------------------------------------------
* <Rating>954</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE DR.REG.RIEN6.EXTRACT(REC.ID)
*----------------------------------------------------------------------------
* Company Name   : APAP
* Company Name   : APAP
* Developed By   : gangadhar@temenos.com
* Program Name   : DR.REG.RIEN6.EXTRACT.
* Date           : 3-May-2013
*----------------------------------------------------------------------------
* Description:
*------------
* This multi-thread job is meant for to extact the AZ.ACCOUNT in DOP and non DOP
*----------------------------------------------------------------------------
*
* Modification History :
* ----------------------
*   Date       Author              Modification Description
* 11/07/2014  V.P.Ashokkumar       PACS00312508 - Updated selection criteria, field length and fixed the field bug
* 15/10/2014  V.P.Ashokkumar       PACS00312508 - Updated new mapping changes for client minor and interest amount
* 20/05/2015  V.P.Ashokkumar       PACS00312508 - Added new fields to display in the report
*----------------------------------------------------------------------------

    $INCLUDE T24.BP I_COMMON
    $INCLUDE T24.BP I_EQUATE
    $INCLUDE T24.BP I_BATCH.FILES
    $INCLUDE T24.BP I_TSA.COMMON
    $INCLUDE T24.BP I_F.CUSTOMER
    $INCLUDE T24.BP I_F.ACCOUNT
    $INCLUDE T24.BP I_F.AZ.ACCOUNT
    $INCLUDE T24.BP I_F.COMPANY
    $INCLUDE T24.BP I_F.INDUSTRY
    $INCLUDE T24.BP I_F.GROUP.DATE
    $INCLUDE T24.BP I_F.GROUP.CREDIT.INT
    $INCLUDE T24.BP I_F.EB.CONTRACT.BALANCES
    $INCLUDE T24.BP I_F.RE.STAT.REP.LINE
    $INCLUDE T24.BP I_F.EB.LOOKUP
    $INCLUDE T24.BP I_F.CATEGORY
*
    $INCLUDE LAPAP.BP I_DR.REG.RIEN6.EXTRACT.COMMON
    $INCLUDE REGREP.BP I_F.DR.REG.RIEN6.PARAM
    $INCLUDE TAM.BP I_F.REDO.AZACC.DESC
*
    GOSUB PROCESS
    RETURN

PROCESS:
********
    IF NOT(NUM(REC.ID)) THEN
        RETURN
    END
    FLD.VAL1 = ''; FLD.VAL2 = ''; FLD.VAL3 = ''; FLD.VAL4 = ''; FLD.VAL5 = ''; FLD.VAL6 = ''; FLD.VAL7 = ''; FLD.VAL8 = ''; FLD.VAL9 = ''; FLD.VAL10 = ''
    FLD.VAL11 = ''; FLD.VAL12 = ''; FLD.VAL13 = ''; FLD.VAL14 = ''; FLD.VAL15 = ''; FLD.VAL16 = ''; FLD.VAL17 = ''; FLD.VAL18 = ''; FLD.VAL19 = ''; FLD.VAL20 = ''
    FLD.VAL21 = ''; FLD.VAL22 = ''; FLD.VAL23 = ''; FLD.VAL24 = ''; FLD.VAL25 = ''; FLD.VAL26 = ''; FLD.VAL27 = ''; FLD.VAL28 = ''; FLD.VAL29 = ''; FLD.VAL30 = ''
    FLD.VAL31 = ''; FLD.VAL32 = ''; FLD.VAL33 = ''; FLD.VAL34 = ''; FLD.VAL35 = ''; FLD.VAL36 = ''; FLD.VAL37 = ''; FLD.VAL38 = ''; FLD.VAL39 = ''; FLD.VAL40 = ''
    FLD.VAL41 = ''; FLD.VAL42 = ''; FLD.VAL43 = ''; R.AZ.ACCOUNT = ''; AZ.ACCOUNT.ERR = ''; R.ACCOUNT = ''; ACCOUNT.ERR = ''; ERR.AZ.ACCOUNT = ''; YH.REC.ID = ''
    ERR.ACCOUNT = ''; FINAL.MSG = ''; RECORD.STAT = ''; YAZ.OPEN.DATE = ''; CAT.VAL = ''; CCY.VAL = ''; CUS.ID = ''; YROLL.DTE = ''; R.CUSTOMER = ''; CUSTOMER.ERRR = ''
    YAC.ONLINE.ACTBAL = ''

    CALL F.READ(FN.AZ.ACCOUNT,REC.ID,R.AZ.ACCOUNT,F.AZ.ACCOUNT,AZ.ACCOUNT.ERR)
    IF NOT(R.AZ.ACCOUNT) THEN
        YH.REC.ID = REC.ID
        CALL EB.READ.HISTORY.REC(F.AZ.ACCOUNT.HST,YH.REC.ID,R.AZ.ACCOUNT,ERR.AZ.ACCOUNT)
    END
    IF NOT(R.AZ.ACCOUNT) THEN
        RETURN
    END
    CALL F.READ(FN.ACCOUNT,REC.ID,R.ACCOUNT,F.ACCOUNT,ACCOUNT.ERR)
    IF NOT(R.ACCOUNT) THEN
        YH.REC.ID = REC.ID
        CALL EB.READ.HISTORY.REC(F.ACCOUNT.HST,YH.REC.ID,R.ACCOUNT,ERR.ACCOUNT)
    END

    RECORD.STAT = R.ACCOUNT<AC.RECORD.STATUS>
    YAC.OPEN.DATE = R.ACCOUNT<AC.OPENING.DATE>
    YAC.ONLINE.ACTBAL = R.ACCOUNT<AC.ONLINE.ACTUAL.BAL>
    IF YAC.ONLINE.ACTBAL EQ '' THEN
        RETURN
    END
    CAT.VAL = R.AZ.ACCOUNT<AZ.CATEGORY>
    CCY.VAL = R.AZ.ACCOUNT<AZ.CURRENCY>
    CUS.ID = R.AZ.ACCOUNT<AZ.CUSTOMER>
    YROLL.DTE = R.AZ.ACCOUNT<AZ.ROLLOVER.DATE>
    CALL F.READ(FN.CUSTOMER,CUS.ID,R.CUSTOMER,F.CUSTOMER,CUSTOMER.ERR)
    GOSUB PROCESS.LCY
*
    IF CCY.VAL EQ LCCY THEN
        CALL F.WRITE(FN.DR.REG.RIEN6.WORKFILE, REC.ID, FINAL.MSG)
    END ELSE
        CALL F.WRITE(FN.DR.REG.RIEN6.WORKFILE.FCY,REC.ID,FINAL.MSG)
    END
    RETURN

GET.INT.PAY.PERIOD:
******************
    SCH.POS = ''; SCH.VAL = ''
    SCH.VAL = R.AZ.ACCOUNT<AZ.SCHEDULES>
    LOCATE 'I' IN R.AZ.ACCOUNT<AZ.TYPE.OF.SCHDLE,1> SETTING SCH.POS THEN
        SCH.TYPE = R.AZ.ACCOUNT<AZ.TYPE.OF.SCHDLE,SCH.POS>
        FRQ.VAL = R.AZ.ACCOUNT<AZ.FREQUENCY,SCH.POS>
        MAT.VAL = MAT.DATE
        BEGIN CASE
        CASE SCH.VAL EQ 'N'
            INT.PAY.PER = '99'
        CASE FRQ.VAL[9,3] EQ 'M12'
            INT.PAY.PER = '1'
        CASE FRQ.VAL[9,3] EQ 'M06'
            INT.PAY.PER = '2'
        CASE FRQ.VAL[9,3] EQ 'M04'
            INT.PAY.PER = '3'
        CASE FRQ.VAL[9,3] EQ 'M03'
            INT.PAY.PER = '4'
        CASE FRQ.VAL[9,3] EQ 'M02'
            INT.PAY.PER = '6'
        CASE FRQ.VAL[9,3] EQ 'M01'
            INT.PAY.PER = '12'
        CASE FRQ.VAL[9,3] EQ ''
            INT.PAY.PER = '99'
        CASE FRQ.VAL EQ MAT.VAL
            INT.PAY.PER = '99'
        END CASE
    END
    IF SCH.VAL EQ 'N' THEN
        INT.PAY.PER = '99'
    END
    RETURN

GET.RE.STAT.LINE:
*---------------*
    LINE.CONT = ''
    IF R.EB.CONTRACT.BALANCES THEN
        Y.ASSET.TYPE = 'CREDIT'
        Y.CONSOL.KEY = R.EB.CONTRACT.BALANCES<ECB.CONSOL.KEY>
	IF Y.CONSOL.KEY THEN
        Y.CONSOL.PART = FIELD(Y.CONSOL.KEY,'.',1,16)
        Y.IN.CONSOL.KEY = Y.CONSOL.PART:'.':Y.ASSET.TYPE
        Y.VARIABLE = ''; Y.RPRTS = ''; Y.LINES = ''
        CALL RE.CALCUL.REP.AL.LINE(Y.IN.CONSOL.KEY,Y.RPRTS,Y.LINES,Y.VARIABLE)
        Y.LINE = Y.RPRTS:'.':Y.LINES
        CALL F.READ(FN.RE.STAT.REP.LINE,Y.LINE,R.LINE,F.RE.STAT.REP.LINE,REP.ERR)
        LINE.CONT = R.LINE<RE.SRL.DESC,1>
	END
    END
    RETURN
*----------------------------------------------------------------------------
PROCESS.LCY:
***********
* PACS00312508 / S
    R.AC1 = ''; OUT.ARR = ''; YR.CUSTOMER = ''; YLCUS.IP.CLI = ''; FLD.VAL6 = ''
    YFLD1 = Y.TODAY[7,2]:Y.TODAY[5,2]:Y.TODAY[1,4]
    FLD.VAL1 = FMT(YFLD1,'L#8')
    YT.REC.ID = REC.ID
    FLD.VAL2 = FMT(R.AZ.ACCOUNT<AZ.LOCAL.REF,L.AZ.SHA1.CODE.POS>,'L#60')
    CALL DR.REG.GET.CUST.TYPE(R.CUSTOMER,OUT.ARR)
    FLD.VAL3 = FMT(OUT.ARR<2>,'L#30')
    FLD.VAL4 = FMT(R.DR.REG.RIEN6.PARAM<RIEN6.PARAM.CALIFICATION>,'L#10')
    FLD.VAL5 = FMT(R.DR.REG.RIEN6.PARAM<RIEN6.PARAM.IRB>,'L#5')
    FLD.VAL6 = FMT(R.AZ.ACCOUNT<AZ.LOCAL.REF,L.INV.FACILITY.POS>,'L#3')
    FLD.VAL7 = FMT(REC.ID,'L#10')
    VAL.DATE = R.AZ.ACCOUNT<AZ.VALUE.DATE>
    YFLD8 = YAC.OPEN.DATE[7,2]:'/':YAC.OPEN.DATE[5,2]:'/':YAC.OPEN.DATE[1,4]
    FLD.VAL8 = FMT(YFLD8,'L#10')
    MAT.DATE = R.AZ.ACCOUNT<AZ.MATURITY.DATE>
    Y.REGION = ''; YINT.ONLINE.VAL = '0'; Y.DAYS = ''
    IF LEN(VAL.DATE) EQ 8 AND LEN(MAT.DATE) EQ 8 THEN
        Y.DAYS = 'C'
        CALL CDD(Y.REGION,MAT.DATE,VAL.DATE,Y.DAYS)
    END
    YFLD9 =  ABS(Y.DAYS)
    FLD.VAL9 = FMT(YFLD9,'R%5')
    YFLD10 = MAT.DATE[7,2]:'/':MAT.DATE[5,2]:'/':MAT.DATE[1,4]
    FLD.VAL10 = FMT(YFLD10,'L#10')
    YFLD11 =  ABS(Y.DAYS)
    FLD.VAL11 = FMT(YFLD11,'R%5')
    FLD.VAL12 = FMT(YAC.ONLINE.ACTBAL,'R2%15')
    YFLD13 = R.DR.REG.RIEN6.PARAM<RIEN6.PARAM.BUY.PRICE>
    FLD.VAL13 = FMT(YFLD13,'R4%8')
    FLD.VAL14 = FMT(R.AZ.ACCOUNT<AZ.INTEREST.RATE>,'R2%6')
    FLD.VAL15 = FMT(R.AZ.ACCOUNT<AZ.INTEREST.RATE>,'R2%6')
    YTYPE.INT.PAY = ''; RATE.TYPE = ''
    YTYPE.INT.PAY = R.AZ.ACCOUNT<AZ.LOCAL.REF,L.TYPE.INT.PAY.POS>
    IF (YTYPE.INT.PAY EQ "Reinvested") THEN
        RATE.TYPE = 4
    END ELSE
        RATE.TYPE = 5
    END
    FLD.VAL16 = FMT(RATE.TYPE,'L#2')
    YT.REC.ID = ''; YT.REC.ID = REC.ID
    GOSUB GET.INT.PAY.DET
    FLD.VAL17 = FMT(INTEREST.PAYABLE,'R4%15')
    GOSUB GET.INT.PAY.PERIOD
    FLD.VAL18 = FMT(INT.PAY.PER,'R%2')
    FLD.VAL19 = FMT(R.DR.REG.RIEN6.PARAM<RIEN6.PARAM.REBUY.RESELL>,'R%1')
    FLD.VAL20 = FMT(R.DR.REG.RIEN6.PARAM<RIEN6.PARAM.REBUY.RESELL>,'R%1')
    FLD.VAL21 = FMT('','L#10')          ;* Blank as per APAP
    GOSUB GET.RE.STAT.LINE
    FLD.VAL22 = FMT(LINE.CONT,'L#30')
    FLD.VAL23 = FMT('','L#3') ;* Blank as per APAP
    FLD.VAL24 = FMT(OUT.ARR<1>,'L#2')
    YORG.MAT.DATE = R.AZ.ACCOUNT<AZ.ORIG.MAT.DATE>
    IF RECORD.STAT EQ 'CLOSED' THEN
        YFLD25 = Y.TODAY[7,2]:'/':Y.TODAY[5,2]:'/':Y.TODAY[1,4]
        FLD.VAL25 = FMT(YFLD25,'L#10')
    END ELSE
        FLD.VAL25 = FMT('','L#10')
    END
    FLD.VAL26 = FMT(R.DR.REG.RIEN6.PARAM<RIEN6.PARAM.CANCEL.PRICE>,'R4%15')
    GOSUB GET.CCY.CODE
    FLD.VAL27 = FMT(CCY.CODE,'R%2')
    FLD.VAL28 = FMT('','L#10')          ;* APAP doesn't have a revision date
    FLD.VAL29 = FMT(R.DR.REG.RIEN6.PARAM<RIEN6.PARAM.REPAY.FREQ>,'R%3')
    GOSUB GET.CALC.BASIC
    FLD.VAL30 = FMT(CALC.BASIS,'R%1')
    GOSUB GET.REL.CODE
    FLD.VAL31 = FMT(REL.VAL,'L#2')
    YLCUS.IP.CLI = R.CUSTOMER<EB.CUS.LOCAL.REF,L.TIP.CLI.POS>
    CUS.INDUST = R.CUSTOMER<EB.CUS.LOCAL.REF,L.APAP.INDUSTRY.POS>
    IF NOT(CUS.INDUST) AND YLCUS.IP.CLI EQ '511' THEN
        CUS.INDUST = '950004'
    END
    FLD.VAL32 = FMT(CUS.INDUST,'R%6')
    GOSUB GET.STATUS.VAL
    FLD.VAL33 = FMT(STAT.VAL,'L#1')
*    FINAL.MSG<-1> = R.COMPANY(EB.COM.NAME.ADDRESS)<1,3>
    LOC.CODE = ''; COMP.APP.ERR = ''; R.COMP.APP = ''
    LOC.CODE =  R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.LOCAL.POS>
    IF NOT(LOC.CODE) THEN
        Y.CO.CODE = R.CUSTOMER<EB.CUS.COMPANY.BOOK>
        CALL F.READ(FN.COMP.APP,Y.CO.CODE,R.COMP.APP,F.COMP.APP,COMP.APP.ERR)
        LOC.CODE = R.COMP.APP<EB.COM.LOCAL.REF,LCOMP.LOCALIDAD.POS>
    END
    FLD.VAL34 = FMT(LOC.CODE,'R%6')
    FLD.VAL35 = FMT(YLCUS.IP.CLI,'R%3')
*
    YWORK.AMT = ''; YINT.ONLINE.VAL = ''; RTMP.EB.CONTRACT.BALANCES = '' ; YONLINE.VAL = ''
    IF R.ACCOUNT<AC.LOCAL.REF,L.AC.REINVESTED.POS> EQ 'YES' THEN
        GOSUB READ.INT.ACCT
        YONLINE.VAL = R.AC1<AC.OPEN.ACTUAL.BAL>
        IF R.AC1 THEN
            YT.REC.ID = ''; YT.REC.ID = R.AZ.ACCOUNT<AZ.INTEREST.LIQU.ACCT>
            RTMP.EB.CONTRACT.BALANCES = R.EB.CONTRACT.BALANCES
            GOSUB GET.INT.PAY.DET
            YINT.ONLINE.VAL = YONLINE.VAL + INTEREST.PAYABLE
            FLD.VAL36 = FMT(YINT.ONLINE.VAL,'R2%15')        ;* Field 36 in the mapping
            GOSUB GET.INT.LIQ.AC.GL.CODE
            FLD.VAL37 = FMT(INT.LIQ.AC.GL.CODE,'L#30')      ;* Field 37 in the mapping
            R.EB.CONTRACT.BALANCES = RTMP.EB.CONTRACT.BALANCES
        END
    END ELSE
        FLD.VAL36 = FMT('0','R2%15')    ;* Field 36 in the mapping
        FLD.VAL37 = FMT('','L#30')      ;* Field 37 in the mapping
    END
* PACS00312508 / E
    GOSUB GET.GL.CODE.50000
    FLD.VAL38 = FMT(GL.CODE.50000,'L#30')         ;* Field 38 in the mapping
    GOSUB ADD.FIELD.39.43
    FINAL.MSG = FLD.VAL1:',':FLD.VAL2:',':FLD.VAL3:',':FLD.VAL4:',':FLD.VAL5:',':FLD.VAL6:',':FLD.VAL7:',':FLD.VAL8:',':FLD.VAL9:',':FLD.VAL10:',':FLD.VAL11:',':FLD.VAL12:',':FLD.VAL13:',':FLD.VAL14:',':FLD.VAL15:',':FLD.VAL16:',':FLD.VAL17:',':FLD.VAL18
    FINAL.MSG :=',':FLD.VAL19:',':FLD.VAL20:',':FLD.VAL21:',':FLD.VAL22:',':FLD.VAL23:',':FLD.VAL24:',':FLD.VAL25:',':FLD.VAL26:',':FLD.VAL27:',':FLD.VAL28:',':FLD.VAL29:',':FLD.VAL30:',':FLD.VAL31:',':FLD.VAL32:',':FLD.VAL33:',':FLD.VAL34:',':FLD.VAL35
    FINAL.MSG :=',':FLD.VAL36:',':FLD.VAL37:',':FLD.VAL38:',':FLD.VAL39:',':FLD.VAL40:',':FLD.VAL41:',':FLD.VAL42:',':FLD.VAL43
    RETURN

READ.INT.ACCT:
**************
    R.AC1 = '';ACC1.ERR = ''; YH.ACINT.ID = ''; ERR.ACC1 = ''
    AC1.ID = R.AZ.ACCOUNT<AZ.INTEREST.LIQU.ACCT>
    CALL F.READ(FN.ACCOUNT,AC1.ID,R.AC1,F.ACCOUNT,ACC1.ERR)
    IF NOT(R.AC1) THEN
        YH.ACINT.ID = AC1.ID
        CALL EB.READ.HISTORY.REC(F.ACCOUNT.HST,YH.ACINT.ID,R.AC1,ERR.ACC1)
    END
    RETURN

ADD.FIELD.39.43:
****************
    IF YROLL.DTE THEN
        FLD.VAL39 = FMT('V','L#2')
    END ELSE
        FLD.VAL39 = FMT('V','L#2')
    END

    IF CUS.INDUST GE '651000' AND CUS.INDUST LE '672204' THEN
        FLD.VAL40 = FMT('Y','L#1')
        IF CUS.INDUST EQ "651104" THEN
            FLD.VAL40 = FMT('M','L#1')
        END
    END ELSE
        FLD.VAL40 = FMT('M','L#1')
    END
    GOSUB GET.FIELD.41
    YFLD.CVAL = "NA"
    FLD.VAL42 = FMT(YFLD.CVAL,'L#4')
    FLD.VAL43 = FMT(YFLD.CVAL,'L#3')
    RETURN

GET.FIELD.41:
*************
    YTERM.CHK = '0'
    BEGIN CASE
    CASE YFLD9 LE '15'
        YTERM.CHK = "110"
    CASE YFLD9 GE '16' AND YFLD9 LE '30'
        YTERM.CHK = "120"
    CASE YFLD9 GE '31' AND YFLD9 LE '60'
        YTERM.CHK = "130"
    CASE YFLD9 GE '61' AND YFLD9 LE '90'
        YTERM.CHK = "140"
    CASE YFLD9 GE '91' AND YFLD9 LE '180'
        YTERM.CHK = "150"
    CASE YFLD9 GE '181' AND YFLD9 LE '365'
        YTERM.CHK = "160"
    CASE YFLD9 GE '366' AND YFLD9 LE '730'
        YTERM.CHK = "170"
    CASE YFLD9 GE '731' AND YFLD9 LE '1095'
        YTERM.CHK = "180"
    CASE YFLD9 GE '1096' AND YFLD9 LE '1460'
        YTERM.CHK = "190"
    CASE YFLD9 GE '1461' AND YFLD9 LE '1825'
        YTERM.CHK = "200"
    CASE YFLD9 GE '1826'
        YTERM.CHK = "210"
    END CASE
    FLD.VAL41 = FMT(YTERM.CHK,'R%5')
    RETURN

GET.GL.CODE.50000:
******************
GL.CODE.50000 = ''
    IF R.EB.CONTRACT.BALANCES THEN
        Y.ASSET.TYPE = '50000'
        Y.CONSOL.KEY = ''
        Y.CONSOL.KEY = R.EB.CONTRACT.BALANCES<ECB.CONSOL.KEY>
	IF Y.CONSOL.KEY THEN
        Y.CONSOL.PART = FIELD(Y.CONSOL.KEY,'.',1,16)
        Y.IN.CONSOL.KEY = Y.CONSOL.PART:'.':Y.ASSET.TYPE
        Y.VARIABLE = ''; Y.RPRTS = ''; Y.LINES = ''
        CALL RE.CALCUL.REP.AL.LINE(Y.IN.CONSOL.KEY,Y.RPRTS,Y.LINES,Y.VARIABLE)
        Y.LINE = ''; R.LINE = ''; REP.ERR = ''
        Y.LINE = Y.RPRTS:'.':Y.LINES
        CALL F.READ(FN.RE.STAT.REP.LINE,Y.LINE,R.LINE,F.RE.STAT.REP.LINE,REP.ERR)
        IF R.LINE THEN
            GL.CODE.50000 = R.LINE<RE.SRL.DESC,1>
        END
	END
    END
    RETURN
*----------------------------------------------------------------------------
GET.INT.LIQ.AC.GL.CODE:
***********************
    R.EB.CONTRACT.BALANCES1 = ''; EB.CONTRACT.BALANCES.ERR = ''; INT.LIQ.AC.GL.CODE = ''
    CALL F.READ(FN.EB.CONTRACT.BALANCES,AC1.ID,R.EB.CONTRACT.BALANCES1,F.EB.CONTRACT.BALANCES,EB.CONTRACT.BALANCES.ERR)
    IF R.EB.CONTRACT.BALANCES1 THEN
        Y.ASSET.TYPE = 'CREDIT'
        Y.CONSOL.KEY = ''
        Y.CONSOL.KEY = R.EB.CONTRACT.BALANCES1<ECB.CONSOL.KEY>
	IF Y.CONSOL.KEY THEN
        Y.CONSOL.PART = FIELD(Y.CONSOL.KEY,'.',1,16)
        Y.IN.CONSOL.KEY = Y.CONSOL.PART:'.':Y.ASSET.TYPE
        Y.VARIABLE = ''; Y.RPRTS = ''; Y.LINES = ''
        CALL RE.CALCUL.REP.AL.LINE(Y.IN.CONSOL.KEY,Y.RPRTS,Y.LINES,Y.VARIABLE)
        Y.LINE = ''; R.LINE = ''; REP.ERR = ''
        Y.LINE = Y.RPRTS:'.':Y.LINES
        CALL F.READ(FN.RE.STAT.REP.LINE,Y.LINE,R.LINE,F.RE.STAT.REP.LINE,REP.ERR)
        IF R.LINE THEN
            INT.LIQ.AC.GL.CODE = R.LINE<RE.SRL.DESC,1>
        END
	END
    END
    RETURN

GET.INT.PAY.DET:
****************
    R.EB.CONTRACT.BALANCES = ''; EB.CONTRACT.BALANCES.ERR = ''
    OPEN.BAL = ''; CRED.MVMT = ''; DEBT.MVMT = ''; INTEREST.PAYABLE = ''; VAL.CODE = ''
    CALL F.READ(FN.EB.CONTRACT.BALANCES,YT.REC.ID,R.EB.CONTRACT.BALANCES,F.EB.CONTRACT.BALANCES,EB.CONTRACT.BALANCES.ERR)
    IF R.EB.CONTRACT.BALANCES THEN
        YTYPE.SYSDATE = R.EB.CONTRACT.BALANCES<ECB.TYPE.SYSDATE>
        YTYPE = DCOUNT(YTYPE.SYSDATE,VM)
        YTOT.OPEN.BAL = ''
        FOR COUT = 1 TO YTYPE
            SYS.DATE = ''; YSYSTYPE = ''
            Y.TYPE.SYSDATE = R.EB.CONTRACT.BALANCES<ECB.TYPE.SYSDATE,COUT>
            YSYSTYPE = FIELD(Y.TYPE.SYSDATE,'-',1)
            SYS.DATE = FIELD(Y.TYPE.SYSDATE,'-',2)

            YOPEN.BALANCE = '' ; YCREDIT.MVMT = '' ; YAC.BAL  = ''; YDEBIT.MVMT = ''
            IF (YSYSTYPE EQ '50000' AND SYS.DATE <= Y.TODAY) THEN
                YOPEN.BALANCE = R.EB.CONTRACT.BALANCES<ECB.OPEN.BALANCE,COUT,1>
                YCREDIT.MVMT = R.EB.CONTRACT.BALANCES<ECB.CREDIT.MVMT,COUT,1>
                YDEBIT.MVMT = R.EB.CONTRACT.BALANCES<ECB.DEBIT.MVMT,COUT,1>
                YAC.BAL  = YOPEN.BALANCE + YCREDIT.MVMT + YDEBIT.MVMT
            END
            INTEREST.PAYABLE += YAC.BAL
        NEXT COUT
    END
    RETURN

GET.STATUS.VAL:
***************
    STAT1 = ''; STAT2 = ''; STAT.VAL = ''; STAT2.CNT = ''
    STAT1 = R.AZ.ACCOUNT<AZ.LOCAL.REF,L.AC.STATUS1.POS>
    STAT2 = R.AZ.ACCOUNT<AZ.LOCAL.REF,L.AC.STATUS2.POS>
    STAT2.CNT = DCOUNT(STAT2,SM)

    IF STAT2.CNT GE 2 THEN
        GOSUB GET.STATUS.MULTIVAL
        RETURN
    END

    BEGIN CASE
    CASE STAT2 EQ 'DECEASED'
        STAT.VAL = 'R'
    CASE STAT2 EQ 'GARNISHMENT'
        STAT.VAL = 'E'
    CASE STAT2 EQ 'GUARANTEE.STATUS'
        STAT.VAL = 'G'
    CASE STAT1 EQ 'ACTIVE' OR STAT1 EQ '6MINACTIVE' OR STAT1 EQ ''
        STAT.VAL = 'A'
    CASE STAT1 EQ 'ABANDONED'
        STAT.VAL = 'B'
    CASE STAT1 EQ '3YINACTIVE'
        STAT.VAL = 'I'
    END CASE
    RETURN

GET.STATUS.MULTIVAL:
********************
    D.POSN = ''; G.POSN = ''
    LOCATE 'DECEASED' IN STAT2<1,1,1> SETTING D.POSN THEN
        STAT.VAL = 'R'
    END ELSE
        LOCATE 'GARNISHMENT' IN STAT2<1,1,1> SETTING G.POSN THEN
            STAT.VAL = 'E'
        END ELSE
            STAT.VAL = 'G'
        END
    END
    RETURN

GET.REL.CODE:
*************
    REL.VAL = ''
    IF R.CUSTOMER THEN
        Y.RLN.CODE.VAL = R.CUSTOMER<EB.CUS.RELATION.CODE>
        Y.RL.DCNT = DCOUNT(Y.RLN.CODE.VAL,VM)
        Y.RL.CNT = '1'
        LOOP
        WHILE Y.RL.CNT LE Y.RL.DCNT
            Y.RLN.CODE = Y.RLN.CODE.VAL<1,Y.RL.CNT>
            LOCATE Y.RLN.CODE IN Y.LNK.TYP.NAME<1,1> SETTING Y.LNK.TYP.POS THEN
                REL.VAL = R.EB.LOOKUP.APP.LNK<EB.LU.DATA.VALUE,Y.LNK.TYP.POS>
            END
            IF REL.VAL THEN
                Y.RL.CNT =  Y.RL.DCNT
            END
            Y.RL.CNT += 1
        REPEAT
    END
    IF NOT(REL.VAL) THEN
        REL.VAL = 'NI'
    END
    RETURN

GET.CALC.BASIC:
**************
    GRP.DT.ID = CAT.VAL:CCY.VAL
    CALL F.READ(FN.GROUP.DATE,GRP.DT.ID,R.GROUP.DATE,F.GROUP.DATE,GROUP.DATE.ERR)
    GRP.CR.DATE = R.GROUP.DATE<AC.GRD.CREDIT.GROUP.DATE>
    GCI.ID = CAT.VAL:CCY.VAL:GRP.CR.DATE
    CALL F.READ(FN.GROUP.CREDIT.INT,GCI.ID,R.GROUP.CREDIT.INT,F.GROUP.CREDIT.INT,GROUP.CREDIT.INT.ERR)
    IF R.GROUP.CREDIT.INT<IC.GCI.INTEREST.DAY.BASIS> EQ 'A' THEN
        CALC.BASIS = '0'
    END ELSE
        CALC.BASIS = '1'
    END
    RETURN

GET.CCY.CODE:
************
    BEGIN CASE
    CASE CCY.VAL EQ LCCY
        CCY.CODE = '0'
    CASE CCY.VAL EQ 'USD'
        CCY.CODE = '1'
    CASE CCY.VAL EQ 'EUR'
        CCY.CODE = '2'
    CASE CCY.VAL EQ 'JPY'
        CCY.CODE = '3'
    END CASE
    RETURN

END
