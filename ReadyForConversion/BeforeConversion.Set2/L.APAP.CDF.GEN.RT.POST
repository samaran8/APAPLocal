*-----------------------------------------------------------------------------
* <Rating>-31</Rating>
*-----------------------------------------------------------------------------
SUBROUTINE L.APAP.CDF.GEN.RT.POST
    $INSERT T24.BP I_COMMON
    $INSERT T24.BP I_EQUATE
    $INSERT T24.BP I_F.ACCOUNT
    $INSERT TAM.BP I_F.REDO.CARD.RENEWAL
    $INSERT ATM.BP I_F.ATM.REVERSAL
    $INSERT TAM.BP I_F.REDO.LY.POINTS
    $INSERT T24.BP I_F.USER
    $INSERT T24.BP I_F.COMPANY
    $INSERT BP I_F.ST.L.APAP.CDF.LOG

    *--GOSUB DO.LOG
    GOSUB INSERT.DATOS
    RETURN

DO.LOG:
    GOSUB GET.DATOS

    Y.TRANS.ID.2  = Y.KEY.LOG
    Y.APP.NAME.2  = "ST.L.APAP.CDF.LOG"
    Y.VER.NAME.2  = Y.APP.NAME.2 :",IN"
    Y.FUNC.2   = "I"
    Y.PRO.VAL.2  = "PROCESS"
    Y.GTS.CONTROL.2 = ""
    Y.NO.OF.AUTH.2  = ""
    FINAL.OFS.2  = ""
    OPTIONS.2   = ""
    Y.CAN.NUM.2  = 0
    Y.CAN.MULT.2  = ""
    R.CDF.FIN   = ""
    R.CDF.FIN<1>  = SUBSTRINGS(Y.KEY.LOG,4,6) 
    R.CDF.FIN<2>  = Y.TOTAL.TXN         ;*TOTAL.TXNS
    R.CDF.FIN<3>  = Y.TOTAL.CARDS       ;*TOTAL.CARDS
    R.CDF.FIN<4>  = INT(Y.TOTAL.CERITOS.GEN)      ;*TOTAL.CERITOS.GEN
    
    CALL OFS.BUILD.RECORD(Y.APP.NAME.2,Y.FUNC.2,Y.PRO.VAL.2,Y.VER.NAME.2,Y.GTS.CONTROL.2,Y.NO.OF.AUTH.2,Y.TRANS.ID.2,R.CDF.FIN,FINAL.OFS.2)
    CALL OFS.GLOBUS.MANAGER("DIARY.OFS", FINAL.OFS.2)
    CALL JOURNAL.UPDATE('')
RETURN

INSERT.DATOS:
    
    Y.DATE = TIMEDATE()

    GOSUB GET.DATOS

    FN.TABLE = "F.ST.L.APAP.CDF.LOG"
    FV.CDF   = ""
    R.TABLE = ""

    CALL OPF(FN.TABLE,FV.CDF)

    R.TABLE<ST.L.A51.RUN.PERIOD> = SUBSTRINGS(Y.KEY.LOG,4,6)
    R.TABLE<ST.L.A51.TOTAL.TXN> = Y.TOTAL.TXN
    R.TABLE<ST.L.A51.TOTAL.CARDS> = Y.TOTAL.CARDS
    R.TABLE<ST.L.A51.TOTAL.CERITOS.GEN> = INT(Y.TOTAL.CERITOS.GEN) 

    R.TABLE<ST.L.A51.INPUTTER> = OPERATOR
    R.TABLE<ST.L.A51.AUTHORISER> = OPERATOR
    R.TABLE<ST.L.A51.CO.CODE> = ID.COMPANY
    R.TABLE<ST.L.A51.DEPT.CODE> =  R.USER<EB.USE.DEPARTMENT.CODE>

    R.TABLE<ST.L.A51.OVERRIDE> = ""
    R.TABLE<ST.L.A51.CURR.NO> = 1

    DATE.STAMP = OCONV(DATE(), 'D4-')
    TIME.STAMP = TIMEDATE()
    Y.DATE.TIME  = DATE.STAMP[9,2]:DATE.STAMP[1,2]:DATE.STAMP[4,2]:TIME.STAMP[1,2]:TIME.STAMP[4,2]

    R.TABLE<ST.L.A51.DATE.TIME> = Y.DATE.TIME
    R.TABLE<ST.L.A51.AUDIT.DATE.TIME> = Y.DATE.TIME

    CALL F.WRITE(FN.TABLE,Y.KEY.LOG,R.TABLE)
    
    ID = '11111'
    CALL JOURNAL.UPDATE(ID)
RETURN


GET.DATOS:
    
    FN.COUNT = "F.LAPAP.LOG.TC"
    FV.COUNT = "" 
    R.COUNT = ""
    ERR.COUNT = ""
    CALL OPF(FN.COUNT,FV.COUNT)
    
    SEL.CMD.COUNT = "SELECT " : FN.COUNT
    CALL EB.READLIST(SEL.CMD.COUNT,SEL.COUNT.LIST,"",NO.OF.RECS.COUNT,SEL.COUNT.ERROR)

    IF SEL.COUNT.LIST EQ '' THEN
       Y.TOTAL.CARDS = 0
       Y.TOTAL.TXN = 0
       Y.TOTAL.CERITOS.GEN = 0
      
       RETURN
    END

    *--Como hago esto sin hacer While
    LOOP REMOVE Y.COUNT.ID FROM SEL.COUNT.LIST SETTING Y.COUNT.POS
    WHILE Y.COUNT.ID DO
        CALL F.READ(FN.COUNT,Y.COUNT.ID,R.COUNT,FV.COUNT,ERR.COUNT) 

        IF R.COUNT NE '' THEN
            Y.DATOS = CHANGE(R.COUNT,"|",FM)

            Y.KEY.LOG = Y.DATOS<1>
            Y.TOTAL.CARDS += Y.DATOS<2> 
            Y.TOTAL.TXN += Y.DATOS<3>
            Y.TOTAL.CERITOS.GEN += Y.DATOS<4>  
        END
    REPEAT
    
RETURN

END
