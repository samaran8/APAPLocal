*-----------------------------------------------------------------------------
* <Rating>246</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE REDO.B.AA.LOAN.UNC.BAL(AA.ARR.LIST)

    $INCLUDE T24.BP I_COMMON
    $INCLUDE T24.BP I_EQUATE
    $INCLUDE T24.BP I_F.COMPANY
    $INCLUDE T24.BP I_F.ACCOUNT
    $INCLUDE T24.BP I_F.AA.ARRANGEMENT.ACTIVITY
    $INCLUDE T24.BP I_F.AA.ARRANGEMENT
    $INCLUDE T24.BP I_F.FUNDS.TRANSFER
    $INCLUDE T24.BP I_F.FT.TXN.TYPE.CONDITION
    $INCLUDE T24.BP I_F.AA.ACCOUNT.DETAILS
    $INCLUDE LAPAP.BP I_REDO.B.AA.LOAN.UNC.BAL.COMMON


    GOSUB INIT
    GOSUB PROCESS
    RETURN

INIT:
*****
    ERR.FUNDS.TRANSFER.H = ''; R.FUNDS.TRANSFER.H = ''; ERR.FUNDS.TRANSFER = ''
    YTXN.REF = ''; TXN.AMT = 0; ORG.AMT = 0; YFT.TXN.ID = ''; YFLD5 = ''
    ERR.AA.ARRANGEMENT = ''; R.AA.ARRANGEMENT = ''; DAT.BALANCES = 0
    YFTTC = ''; YFLD1 = ''; YFLD2 = ''; YFLD3 = ''; YFLD4 = ''; ERR.FTTC = ''
    ERR.COMPANY = ''; RV.COMPANY = ''; YUNC.VAL = 0; R.FTTC = ''
    R.DR.REG.UNC.BAL.WORKFILE = ''; YACTIVT.REF = ''; YLT.ACTIVITY = ''
    ERR.ACCOUNT = ''; R.ACCOUNT = ''; YPOST.REST = ''
    RETURN

PROCESS:
********
    BALANCE.TO.CHECK = 'UNCACCOUNT'
    YACTIVT.REF = AA.ARR.LIST
    GOSUB READ.AA.ACTIVITY
    AA.ARR.ID = R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.ARRANGEMENT>
    CALL F.READ(FN.AA.ARRANGEMENT,AA.ARR.ID,R.AA.ARRANGEMENT,F.AA.ARRANGEMENT,ERR.AA.ARRANGEMENT)
    YACCT.ID = R.AA.ARRANGEMENT<AA.ARR.LINKED.APPL.ID>
    YSTART.DTE = R.AA.ARRANGEMENT<AA.ARR.START.DATE>

    END.DATE = YLST.TODAY
    CALL AC.GET.ECB.BALANCE(YACCT.ID, BALANCE.TO.CHECK, "", END.DATE, DAT.BALANCES, "")
* The record with negative balance will not be reflected.
    IF (DAT.BALANCES EQ 0 OR DAT.BALANCES EQ '' OR DAT.BALANCES LT 0) THEN RETURN
    YTXN.REF = R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.TXN.CONTRACT.ID>
    YACTIVT.REF = YTXN.REF
    GOSUB READ.AA.ACTIVITY
    YLT.ACTIVITY = R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.ACTIVITY>
    LOCATE YLT.ACTIVITY IN Y.TXNCDE.VAL.ARR<1,1> SETTING TXNACT.POS ELSE
        RETURN
    END

    CALL F.READ(FN.ACCOUNT,YACCT.ID,R.ACCOUNT,F.ACCOUNT,ERR.ACCOUNT)
    IF NOT(R.ACCOUNT) THEN RETURN
    YPOST.REST = R.ACCOUNT<AC.POSTING.RESTRICT>
    IF YPOST.REST EQ '75' OR YPOST.REST EQ '90' THEN RETURN
    TXN.AMT  = R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.TXN.AMOUNT>
    ORG.AMT = R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.ORIG.TXN.AMT>
    YFT.TXN.ID = R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.TXN.CONTRACT.ID>

    ERR.AA.ACCOUNT.DETAILS = ''; R.AA.ACCOUNT.DETAILS = ''; YAGING = ''
    CALL F.READ(FN.AA.ACCOUNT.DETAILS,AA.ARR.ID,R.AA.ACCOUNT.DETAILS,F.AA.ACCOUNT.DETAILS,ERR.AA.ACCOUNT.DETAILS)
    YAGING = R.AA.ACCOUNT.DETAILS<AA.AD.ARR.AGE.STATUS>
    YFT.TXN.ID.H = YFT.TXN.ID
    CALL EB.READ.HISTORY.REC(F.FUNDS.TRANSFER.H,YFT.TXN.ID.H,R.FUNDS.TRANSFER.H,ERR.FUNDS.TRANSFER.H)
    IF ERR.FUNDS.TRANSFER.H THEN
        CALL F.READ(FN.FUNDS.TRANSFER,YFT.TXN.ID,R.FUNDS.TRANSFER.H,F.FUNDS.TRANSFER,ERR.FUNDS.TRANSFER)
    END

    YFTTC = R.FUNDS.TRANSFER.H<FT.TRANSACTION.TYPE>
    CALL F.READ(FN.FTTC,YFTTC,R.FTTC,F.FTTC,ERR.FTTC)
    YFLD4 = R.FTTC<FT6.DESCRIPTION,LNGG>
    IF NOT(YFLD4) THEN
        YFLD4 = R.FTTC<FT6.DESCRIPTION,1>
    END
    YFLD1 = R.FUNDS.TRANSFER.H<FT.CREDIT.CUSTOMER>
    COMP.ID = R.FUNDS.TRANSFER.H<FT.CO.CODE>
    CALL F.READ(FN.COMPANY,COMP.ID,RV.COMPANY,FV.COMPANY,ERR.COMPANY)
    YFLD2 = RV.COMPANY<EB.COM.LOCAL.REF,L.CO.BOOK.POS>

    YFLD3 = R.FUNDS.TRANSFER.H<FT.CREDIT.VALUE.DATE>
    YFLD5 = R.FUNDS.TRANSFER.H<FT.CREDIT.AMOUNT>
    IF NOT(YFLD5) THEN
        YFLD5 = R.FUNDS.TRANSFER.H<FT.DEBIT.AMOUNT>
    END
    GOSUB SCHEDULE.PROCESS
*   YUNC.VAL = ORG.AMT - TXN.AMT
*    IF YUNC.VAL NE 0 THEN
    IF YAGING NE 'CUR' OR SCHEDULE.DATE EQ '' THEN
        SCHEDULE.DATE = TP.SCHEDULE.DATE
    END
    IF NOT(YFLD5) THEN
       RETURN
    END
*   IF YFLD3 GE TP.SCHEDULE.DATE THEN
    R.DR.REG.UNC.BAL.WORKFILE<1> = YFLD2:',':YFLD4:',':YACCT.ID:',':YFLD1:',':YFLD3:',':YFT.TXN.ID:',':YFLD5:',':DAT.BALANCES:',':SCHEDULE.DATE:',':YLT.ACTIVITY:',':AA.ARR.ID
    R.DR.REG.UNC.BAL.WORKFILE<2> = DAT.BALANCES
    R.DR.REG.UNC.BAL.WORKFILE<3> = YFLD5
    GOSUB WRITE.FILE
*   END
    RETURN

WRITE.FILE:
***********
    AA.ARR.LIST1 = AA.ARR.ID:'-':YFLD3:'-':AA.ARR.LIST
    CALL F.WRITE(FN.DR.REG.UNC.BAL.WORKFILE,AA.ARR.LIST1,R.DR.REG.UNC.BAL.WORKFILE)
    RETURN

SCHEDULE.PROCESS:
*****************
    R.REDO.AA.SCHEDULE = ''; REDO.AA.SCHEDULE.ERR = ''; DUE.DATES = ''; TP.SCHEDULE.DATE = ''; SCHEDULE.DATE = ''; TOT.DUE.DATE = 0
    CALL F.READ(FN.REDO.AA.SCHEDULE,AA.ARR.ID,R.REDO.AA.SCHEDULE,F.REDO.AA.SCHEDULE,REDO.AA.SCHEDULE.ERR)
    IF R.REDO.AA.SCHEDULE THEN
        DUE.DATES = RAISE(R.REDO.AA.SCHEDULE<2>)
    END

    TOT.DUE.DATE=DCOUNT(DUE.DATES,FM)
    J = 1
    LOOP
    WHILE J LE TOT.DUE.DATE
        SCHEDULE.DATE = DUE.DATES<J>
        IF SCHEDULE.DATE LT TODAY THEN
            J +=1
            TP.SCHEDULE.DATE = SCHEDULE.DATE
            CONTINUE
        END

        J = TOT.DUE.DATE + 1
    REPEAT
    RETURN

READ.AA.ACTIVITY:
*****************
    ERR.AA.ARRANGEMENT.ACTIVITY = ''; R.AA.ARRANGEMENT.ACTIVITY = ''
    CALL F.READ(FN.AA.ARRANGEMENT.ACTIVITY,YACTIVT.REF,R.AA.ARRANGEMENT.ACTIVITY,F.AA.ARRANGEMENT.ACTIVITY,ERR.AA.ARRANGEMENT.ACTIVITY)

    RETURN
END
