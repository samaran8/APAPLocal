*----------------------------------------------------------------------------- 
* <Rating>-211</Rating>
*----------------------------------------------------------------------------- 
    SUBROUTINE L.APAP.GET.LOAN.INFO(R.DATA) 
*----------------------------------------------------------------------------- 
*DESCRIPTION: 
*-----------------------------------------------------------------------------
* This is an Nofile routine to get some data for enquiry ENQ.L.APAP.GET.LOAN.INFO 
* related to C.3 IVR Interface. 

* Input/Output: 
*-------------- 
* IN : LINKED.APPL.ID 
* OUT : R.DATA (ALL DATA) 
*--------------- 
*----------------------------------------------------------------------------- 
* Modification History : 
* Date            Who                   Reference               Description 
* 30-05-2019      ANTHONY MARTINEZ      APAPMOVIL               CREATION
*----------------------------------------------------------------------------- 
*<region name= Inserts> 
    $INCLUDE T24.BP I_COMMON
    $INCLUDE T24.BP I_EQUATE
    $INCLUDE T24.BP I_ENQUIRY.COMMON
    $INCLUDE T24.BP I_F.AA.ACCOUNT.DETAILS
    $INCLUDE T24.BP I_F.AA.ARRANGEMENT
    $INCLUDE T24.BP I_F.CURRENCY
    $INCLUDE T24.BP I_F.CUSTOMER
    $INCLUDE T24.BP I_F.AA.ACTIVITY.HISTORY
    $INCLUDE T24.BP I_F.AA.ARRANGEMENT.ACTIVITY
    $INCLUDE T24.BP I_F.AA.INTEREST.ACCRUALS
    $INCLUDE T24.BP I_F.AA.TERM.AMOUNT
    $INCLUDE T24.BP I_F.AA.BILL.DETAILS
    $INCLUDE T24.BP I_F.AA.OVERDUE
    $INCLUDE T24.BP I_F.ACCOUNT
* </region> 
*----------------------------------------------------------------------------- 
 
    GOSUB INIT 
    GOSUB OPENFILES 
    GOSUB PROCESS 
    RETURN

***** 
INIT: 
*****
 
    Y.LINKED.APPL.ID.POS = "" 
    Y.LINKED.APPL.ID = "" 
    Y.DATACOUNT = "" 
    Y.RESULT = "" 
    AA.ARRANGEMENT.ERR = "" 
    Y.AA.ARRANGEMENT.REC = "" 
    Y.AA.ID = "" 
    Y.CURRENCY = "" 
    Y.CURRENCY.REC = "" 
    CURRENCY.ERR = "" 
    Y.AA.ACCOUNT.DETAILS.REC = "" 
    AA.ACCOUNT.DETAILS.ERR = "" 
 
    LOC.REF.POS = "" 
    LOC.REF.APP = "AA.PRD.DES.OVERDUE" 
    LOC.REF.FIELD = "L.LOAN.STATUS.1" 
    CALL GET.LOC.REF(LOC.REF.APP,LOC.REF.FIELD,LOC.REF.POS) 
    POS.L.LOAN.STATUS = LOC.REF.POS<1,1> 
 
    RETURN 
 
********* 
OPENFILES: 
********* 
*Open files AA.ARRANGEMENT and AA.ACCOUNT.DETAILS 
 
    FN.AA.ARRANGEMENT = "F.AA.ARRANGEMENT" 
    F.AA.ARRANGEMENT = "" 
    CALL OPF(FN.AA.ARRANGEMENT,F.AA.ARRANGEMENT) 
 
    FN.AA.ACCOUNT.DETAILS = "F.AA.ACCOUNT.DETAILS" 
    F.AA.ACCOUNT.DETAILS = "" 
    CALL OPF(FN.AA.ACCOUNT.DETAILS,F.AA.ACCOUNT.DETAILS) 
 
    FN.CURRENCY = "F.CURRENCY" 
    F.CURRENCY = "" 
    CALL OPF(FN.CURRENCY,F.CURRENCY) 
 
    FN.CUSTOMER = "F.CUSTOMER" 
    F.CUSTOMER = "" 
    CALL OPF(FN.CUSTOMER,F.CUSTOMER) 
 
    FN.AA.ACT.HISTORY = "F.AA.ACTIVITY.HISTORY" 
    F.AA.ACT.HISTORY  = "" 
    CALL OPF(FN.AA.ACT.HISTORY,F.AA.ACT.HISTORY) 
 
    FN.AA.ARR.ACTIVITY = "F.AA.ARRANGEMENT.ACTIVITY" 
    F.AA.ARR.ACTIVITY = "" 
    CALL OPF(FN.AA.ARR.ACTIVITY,F.AA.ARR.ACTIVITY) 
 
    FN.AA.INT.ACCRUALS = "F.AA.INTEREST.ACCRUALS" 
        F.AA.INT.ACCRUALS = "" 
        CALL OPF(FN.AA.INT.ACCRUALS, F.AA.INT.ACCRUALS) 
        FN.AA.ARR.TERM.AMOUNT = "F.AA.ARR.TERM.AMOUNT" 
        F.AA.ARR.TERM.AMOUNT = "" 
        CALL OPF(FN.AA.ARR.TERM.AMOUNT,F.AA.ARR.TERM.AMOUNT) 
        FN.ACCOUNT = "F.ACCOUNT" 
        F.ACCOUNT = "" 
        CALL OPF(FN.ACCOUNT,F.ACCOUNT) 
        RETURN 
******** 
PROCESS: 
******** 
    LOCATE "LINKED.APPL.ID" IN D.FIELDS<1> SETTING LINKED.APPL.ID.POS THEN 
        Y.LINKED.APPL.ID = D.RANGE.AND.VALUE<LINKED.APPL.ID.POS> 
        COMI = Y.LINKED.APPL.ID 
        CALL IN2POSANT(19,'') 
        Y.LINKED.APPL.ID = COMI 
    END 
    R.ACCOUNT = ""; ACC.ERR = "" 
    CALL F.READ(FN.ACCOUNT,Y.LINKED.APPL.ID,R.ACCOUNT,F.ACCOUNT,ACC.ERR) 
    IF R.ACCOUNT THEN 
        Y.ARR.ID = R.ACCOUNT<AC.ARRANGEMENT.ID> 
    END ELSE 
        Y.RESULT = "2**************" 
        RETURN 
    END 
    GOSUB GET.ARR.DETAILS 
    GOSUB GET.CUST.DETAILS 
    GOSUB GET.ACCT.HIS.DETAILS 
    Y.AA.RATE = "N/A" 
    Y.ARR.ID2 = Y.ARR.ID:"-PRINCIPALINT" 
    CALL F.READ(FN.AA.INT.ACCRUALS,Y.ARR.ID2,R.AA.INT.ACCRUALS,F.AA.INT.ACCRUALS,"") 
    IF R.AA.INT.ACCRUALS THEN 
        Y.AA.RATE = "-" 
        Y.AA.RATE = R.AA.INT.ACCRUALS<AA.INT.ACC.RATE,1> 
    END 
    Y.RESULT := Y.AA.RATE:"*" 
    Y.AA.BAL = "N/A" 
    CALL REDO.GET.TOTAL.OUTSTANDING.SIN.UNC.UND(Y.ARR.ID,Y.PROP.AMT,Y.TOTAL.AMT) 
    Y.AA.BAL = FIELD(Y.PROP.AMT,FM,1) 
    Y.RESULT := Y.AA.BAL:"*" 
    GOSUB GET.PAY.DETAILS 
    Y.AA.TERM = "N/A" 
    ID.TERM.AMOUNT = Y.ARR.ID:'-COMMITMENT-':Y.START.DATE:'.1' 
    CALL F.READ(FN.AA.ARR.TERM.AMOUNT,ID.TERM.AMOUNT,R.AA.ARR.TERM.AMOUNT,F.AA.ARR.TERM.AMOUNT,"") 
    
    IF R.AA.ARR.TERM.AMOUNT THEN 
        Y.AA.TERM = "-" 
        Y.AA.TERM = R.AA.ARR.TERM.AMOUNT<AA.AMT.TERM> 
    END

    Y.RESULT := Y.AA.TERM:"*" 
    Y.RESULT := Y.AA.BILL.PAY.DATE.FIRST:"*" 
    Y.RESULT := Y.AA.BILL.PAY.DATE.LAST:"*" 
    Y.RESULT := Y.AA.REPAY.AMT:"*"
 
    Y.RESULT := Y.AA.BILL.DUE:"*" 
    Y.AA.LOAN.STATUS = "N/A" 
    PROP.CLASS = 'OVERDUE' 
    PROPERTY = '' 
    R.Condition = '' 
    ERR.MSG = '' 
    EFF.DATE = '' 
    CALL REDO.CRR.GET.CONDITIONS(Y.ARR.ID,EFF.DATE,PROP.CLASS,PROPERTY,R.Condition,ERR.MSG) 
    Y.AA.LOAN.STATUS = R.Condition<AA.OD.LOCAL.REF,POS.L.LOAN.STATUS>

    CALL GET.LOC.REF("AA.ARR.OVERDUE", "L.LOAN.COND", L.LOAN.COND.POS)

    Y.RESULT := Y.AA.LOAN.STATUS:"*" 
    Y.RESULT := Y.AA.PRODUCT:"*" 
    Y.RESULT := Y.AA.DATE.LAST.PAY:"*"
    Y.RESULT := R.Condition<AA.OD.LOCAL.REF, L.LOAN.COND.POS> : "*"

    R.DATA = Y.RESULT
    
    RETURN

**************** 
GET.PAY.DETAILS: 
**************** 
    Y.AA.MNTPAY = "N/A" 
    Y.AA.BILL.DUE.AMT = "N/A" 
    Y.AA.BILL.PAY.DATE.FIRST = "N/A" 
    Y.AA.BILL.PAY.DATE.LAST = "N/A" 
    Y.AA.BILL.DUE = "N/A" 
    CALL F.READ(FN.AA.ACCOUNT.DETAILS,Y.ARR.ID,Y.AA.ACCOUNT.DETAILS.REC,F.AA.ACCOUNT.DETAILS,AA.ACCOUNT.DETAILS.ERR) 
    IF Y.AA.ACCOUNT.DETAILS.REC THEN 
        Y.BILL.PAY.DATES = Y.AA.ACCOUNT.DETAILS.REC<AA.AD.BILL.PAY.DATE> 
        Y.DATACOUNT = DCOUNT(Y.BILL.PAY.DATES,VM) 
        Y.AA.BILL.PAY.DATE.FIRST = FIELD(Y.BILL.PAY.DATES,VM,1) 
        Y.AA.BILL.PAY.DATE.LAST = FIELD(Y.BILL.PAY.DATES,VM,Y.DATACOUNT) 
        GOSUB GET.PAY.DETAILS.2 
    END 
    Y.RESULT := Y.AA.MNTPAY:"*" 
    Y.RESULT := Y.AA.BILL.DUE.AMT:"*" 
 
    RETURN 
****************** 
GET.PAY.DETAILS.2: 
****************** 
    Y.AA.MNTPAY = 0 
    Y.AA.BILL.DUE = 0 
    Y.AA.BILL.DUE.AMT = 0 
    I = 1 
    LOOP 
    WHILE I LE Y.DATACOUNT 
        BILL.REFERENCE = Y.AA.ACCOUNT.DETAILS.REC<AA.AD.BILL.ID,I> 
        IF Y.AA.ACCOUNT.DETAILS.REC<AA.AD.BILL.TYPE,I,1> EQ 'PAYMENT' AND Y.AA.ACCOUNT.DETAILS.REC<AA.AD.SET.STATUS,I,1> EQ 'UNPAID' THEN
            Y.AA.BILL.DUE++ 
            CALL AA.GET.BILL.DETAILS(Y.ARR.ID, BILL.REFERENCE, BILL.DETAILS, RET.ERROR) 
            Y.PROPERTY.TOT = DCOUNT(BILL.DETAILS<AA.BD.PROPERTY>,VM) 
            GOSUB SUM.AMOUNTS 
            LOCATE 'PRMORA' IN BILL.DETAILS<AA.BD.PROPERTY,1> SETTING Y.POS THEN 
                Y.AA.BILL.DUE.AMT += SUM(BILL.DETAILS<AA.BD.OS.PROP.AMOUNT,Y.POS>) 
            END 
        END 
        I++ 
    REPEAT 
    RETURN 
************ 
SUM.AMOUNTS: 
************ 
    K = 1 
    LOOP 
    WHILE K LE Y.PROPERTY.TOT 
        Y.AA.MNTPAY += SUM(BILL.DETAILS<AA.BD.OS.PROP.AMOUNT,K>) 
        K++ 
    REPEAT 
    RETURN 
**************** 
GET.ARR.DETAILS: 
**************** 
   Y.CURRENCY = "N/A" 
   Y.AA.PRODUCT = "N/A" 
   IF Y.ARR.ID NE "" THEN 
       CALL F.READ(FN.AA.ARRANGEMENT,Y.ARR.ID,Y.AA.ARRANGEMENT.REC,F.AA.ARRANGEMENT,AA.ARRANGEMENT.ERR) 
       IF Y.AA.ARRANGEMENT.REC THEN 
           Y.RESULT = "1*" 
           Y.CURRENCY = Y.AA.ARRANGEMENT.REC<AA.ARR.CURRENCY> 
           CALL F.READ(FN.CURRENCY,Y.CURRENCY,Y.CURRENCY.REC,F.CURRENCY,CURRENCY.ERR) 
            IF Y.CURRENCY.REC THEN 
                Y.CURRENCY = Y.CURRENCY.REC<EB.CUR.CCY.NAME> 
            END 
            Y.AA.PRODUCT = Y.AA.ARRANGEMENT.REC<AA.ARR.PRODUCT> 
            Y.AA.CUSTOMER = Y.AA.ARRANGEMENT.REC<AA.ARR.CUSTOMER> 
            Y.START.DATE = Y.AA.ARRANGEMENT.REC<AA.ARR.START.DATE> 
        END ELSE 
            Y.RESULT = "2**************" 
            RETURN 
        END 
    END ELSE 
        Y.RESULT = "2**************" 
        RETURN 
    END 
 
    RETURN 
***************** 
GET.CUST.DETAILS: 
***************** 
    Y.RESULT := Y.CURRENCY:"*" 
    Y.AA.PRIM.OWNER = "N/A" 
    R.CUSTOMER = ""; CUS.ERR = "" 
    CALL F.READ(FN.CUSTOMER,Y.AA.CUSTOMER,R.CUSTOMER,F.CUSTOMER,CUS.ERR) 
    IF R.CUSTOMER THEN 
        Y.AA.PRIM.OWNER = R.CUSTOMER<EB.CUS.GIVEN.NAMES>:" ":R.CUSTOMER<EB.CUS.FAMILY.NAME> 
    END 
    Y.RESULT := Y.AA.PRIM.OWNER:"*" 
    RETURN 
********************* 
GET.ACCT.HIS.DETAILS: 
********************* 
    Y.AA.EFFDATE = "N/A" 
    Y.ACTIVITY = "LENDING-DISBURSE-COMMITMENT" 
    Y.AA.REPAY.AMT = "N/A" 
    Y.ACT.REPAY = "APPLYPAYMENT" 
    R.AA.ACT.HISTORY = "" 
    CALL F.READ(FN.AA.ACT.HISTORY,Y.ARR.ID,R.AA.ACT.HISTORY,F.AA.ACT.HISTORY,"") 
    IF R.AA.ACT.HISTORY THEN 
        Y.CONT = DCOUNT(R.AA.ACT.HISTORY<AA.AH.EFFECTIVE.DATE>,VM) 
        Y.AA.EFFDATE = "-" 
        GOSUB GET.DISBURSE.DATE 
        Y.AA.REPAY.AMT = 0 
        Y.LAST.PAY = 0 
        I = 1 
        LOOP 
        WHILE I LE Y.CONT 
            Y.ACT.TO.EVAL.TOT = R.AA.ACT.HISTORY<AA.AH.ACTIVITY,I> 
            Y.ACT.TO.EVAL.CNT = DCOUNT(Y.ACT.TO.EVAL.TOT,SM) 
            GOSUB GET.ACCT.HIS.DETAILS.2 
            I++ 
        REPEAT 
    END 
    Y.RESULT := Y.AA.EFFDATE:"*" 
    RETURN 
*********************** 
GET.ACCT.HIS.DETAILS.2: 
*********************** 
    J = 1 
    LOOP 
    WHILE J LE Y.ACT.TO.EVAL.CNT 
        Y.ACT.TO.EVAL = FIELD(Y.ACT.TO.EVAL.TOT,SM,J) 
        Y.ACT.TO.EVAL = FIELD(Y.ACT.TO.EVAL,"-",2) 
        IF Y.ACT.TO.EVAL EQ Y.ACT.REPAY THEN 
            Y.LAST.PAY++ 
            IF Y.LAST.PAY EQ 1 THEN 
                Y.AA.DATE.LAST.PAY = R.AA.ACT.HISTORY<AA.AH.EFFECTIVE.DATE,I> 
                Y.AA.REPAY.AMT = R.AA.ACT.HISTORY<AA.AH.ACTIVITY.AMT,I,J> 
            END 
        END 
        J++ 
    REPEAT 
    RETURN 
*----------------- 
GET.DISBURSE.DATE: 
*----------------- 
    I = 1 
    LOOP 
    WHILE I LE Y.CONT 
        Y.ACTIVITY.TOT = R.AA.ACT.HISTORY<AA.AH.ACTIVITY,I> 
        Y.ACTIVITY.TOT.CNT = DCOUNT(Y.ACTIVITY.TOT,SM) 
        J = 1 
        LOOP 
        WHILE J LE Y.ACTIVITY.TOT.CNT 
            Y.ACT.TO.EVAL = FIELD(Y.ACTIVITY.TOT,SM,J) 
            IF Y.ACTIVITY EQ Y.ACT.TO.EVAL THEN 
                Y.AA.EFFDATE = R.AA.ACT.HISTORY<AA.AH.EFFECTIVE.DATE,I> 
                RETURN 
            END 
            J++ 
        REPEAT 
        I++ 
    REPEAT 
    Y.ACTIVITY = "LENDING-TAKEOVER-ARRANGEMENT" 
    I = 1 
    LOOP 
    WHILE I LE Y.CONT 
        J = 1 
        LOOP 
        WHILE J LE Y.ACTIVITY.TOT.CNT 
            Y.ACT.TO.EVAL = FIELD(Y.ACTIVITY.TOT,SM,J) 
            IF Y.ACTIVITY EQ Y.ACT.TO.EVAL THEN 
                Y.AA.ACT.ID = R.AA.ACT.HISTORY<AA.AH.ACTIVITY.REF,I,J> 
                R.AA.ARR.ACTIVITY = ""; ARR.ERR = "" 
                CALL F.READ(FN.AA.ARR.ACTIVITY,Y.AA.ACT.ID,R.AA.ARR.ACTIVITY,F.AA.ARR.ACTIVITY,ARR.ERR) 
                IF R.AA.ARR.ACTIVITY THEN 
                    Y.AA.EFFDATE = R.AA.ARR.ACTIVITY<AA.ARR.ACT.ORIG.CONTRACT.DATE> 
                    RETURN 
                END 
            END 
            J++ 
        REPEAT 
        I++ 
    REPEAT 
    RETURN 
END
