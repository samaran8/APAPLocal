*-----------------------------------------------------------------------------
* <Rating>27</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE LAPAP.ID.GET.VOTANTE.RT
    $INSERT T24.BP I_COMMON
    $INSERT T24.BP I_EQUATE
    $INSERT T24.BP I_F.CUSTOMER
    $INSERT T24.BP I_F.ACCOUNT
    $INSERT BP I_F.ST.L.APAP.ASAMBLEA.VOTANTE
    $INSERT BP I_F.ST.L.APAP.ASAMBLEA.PARTIC
    $INSERT BP I_F.ST.L.APAP.ASAMBLEA.PARAM
    $INSERT BP I_F.ST.L.APAP.ASAMBLEA.RELAC


    GOSUB INITIAL
    GOSUB OPENER
    GOSUB GET.PARAMS
    GOSUB PROCESS.1

    CALL REBUILD.SCREEN

    RETURN
INITIAL:
    Y.CEDULA = ID.NEW         ;*COMI

    FN.CUS = "FBNK.CUSTOMER"
    FV.CUS = ""
    FN.ACC = "FBNK.ACCOUNT"
    FV.ACC = ""
    FN.PA = "FBNK.ST.L.APAP.ASAMBLEA.PARTIC"
    FV.PA = ""
    FN.VOT = "FBNK.ST.L.APAP.ASAMBLEA.VOTANTE"
    FV.VOT = ""
    FN.PRM = "FBNK.ST.L.APAP.ASAMBLEA.PARAM"
    FV.PRM = ""
    FN.REL = "FBNK.ST.L.APAP.ASAMBLEA.RELAC"
    FV.REL = ""

    RETURN

OPENER:
    CALL OPF(FN.CUS,FV.CUS)
    CALL OPF(FN.ACC,FV.ACC)
    CALL OPF(FN.PA,FV.PA)
    CALL OPF(FN.VOT,FV.VOT)
    CALL OPF(FN.PRM,FV.PRM)
    CALL OPF(FN.REL,FV.REL)

    RETURN
GET.PARAMS:
    CALL F.READ(FN.PRM, "1", R.PRM1, FV.PRM, PRM1.ERR)
    Y.PA.BALANCE.INICIO = R.PRM1<ST.L.A95.VALOR>
    CALL F.READ(FN.PRM, "2", R.PRM2, FV.PRM, PRM2.ERR)
    Y.PA.BALANCE.PROMEDIO = R.PRM2<ST.L.A95.VALOR>
    CALL F.READ(FN.PRM, "3", R.PRM3, FV.PRM, PRM3.ERR)
    Y.PA.MAXIMO.BOLETOS = R.PRM3<ST.L.A95.VALOR>
    CALL F.READ(FN.PRM, "4", R.PRM4, FV.PRM, PRM4.ERR)
    Y.PA.FECHA.ASAMBLEA = R.PRM4<ST.L.A95.VALOR>
    Y.ANO = Y.PA.FECHA.ASAMBLEA[1,4]
    Y.ANO.ACT = Y.ANO - 1

    RETURN

PROCESS.1:
    CUENTAS.ARR = ''
    VOTOS = 0
    OBSERVACIONES.ARR = ''
    IF V$FUNCTION EQ 'I' THEN

*SI ES UN REGISTRO NUEVO PROCEDO A REALIZAR EL PROCESO...
        IF R.OLD(1) EQ '' THEN
            SEL.CMD = "SELECT " : FN.CUS : " WITH L.CU.CIDENT EQ " : Y.CEDULA : " SAMPLE 1"
            CALL EB.READLIST(SEL.CMD,SEL.LIST,'',NO.OF.RECS,SEL.ERR)
            LOOP REMOVE CUSTOMER.ID FROM SEL.LIST SETTING STMT.POS

            WHILE CUSTOMER.ID DO
                GOSUB READ.CUSTOMER
                CODIGO.CLIENTE = CUSTOMER.ID
            REPEAT

            IF R.CUS EQ '' THEN
                RETURN
            END
            SEL.CMD2 = "SELECT " : FN.PA : " WITH CLIENTE EQ " : CODIGO.CLIENTE
            CALL EB.READLIST(SEL.CMD2,SEL.LIST2,'',NO.OF.RECS2,SEL.ERR2)
            LOOP REMOVE CUENTAPART.ID FROM SEL.LIST2 SETTING CUENTAPART.POS

            WHILE CUENTAPART.ID DO
                GOSUB READ.PARTCIPANTE
            REPEAT
*BUSCAMOS LAS RELACIONES 500 Y 501 PARA ESTE CLIENTE.
            GOSUB OBTENER.RELACIONES


            CHANGE @FM TO @VM IN CUENTAS.ARR

            R.NEW(ST.L.AV.CUENTAS) = CUENTAS.ARR
            IF VOTOS GT Y.PA.MAXIMO.BOLETOS THEN
                VOTOS = Y.PA.MAXIMO.BOLETOS
            END
            R.NEW(ST.L.AV.VOTOS) = VOTOS
            R.NEW(ST.L.AV.OBSERVACIONES) = OBSERVACIONES.ARR

        END
    END

    RETURN

READ.CUSTOMER:
    CALL F.READ(FN.CUS, CUSTOMER.ID, R.CUS, FV.CUS, CUS.ERR)

    R.NEW(ST.L.AV.CODIGO.CLIENTE) = CUSTOMER.ID
    R.NEW(ST.L.AV.NOMBRE) = R.CUS<EB.CUS.GIVEN.NAMES>
    R.NEW(ST.L.AV.APELLIDO1) = R.CUS<EB.CUS.FAMILY.NAME>
    R.NEW(ST.L.AV.APELLIDO2) = ""
*DEBUG
    RETURN

READ.PARTCIPANTE:
    CALL F.READ(FN.PA, CUENTAPART.ID, R.PA, FV.PA, PA.ERR)
*DEBUG
    IF R.PA NE '' THEN
        INDICADOR.PARTIC = R.PA<ST.L.APA.CUENTA.PARTICIPO>
        IF INDICADOR.PARTIC EQ 'SI' THEN
            OBSERVACIONES.ARR<-1> = "CUENTA: " : R.PA<ST.L.APA.CUENTA> : ", REPRESENTADA POR CLIENTE: " : R.PA<ST.L.APA.CLIENTE.PARTICIPO>
        END ELSE
            Y.CUENTA = R.PA<ST.L.APA.CUENTA>
            Y.VOTOS.CUENTA = R.PA<ST.L.APA.VOTOS.CUENTA>
            Y.CLIENTE.CUENTA = R.PA<ST.L.APA.CLIENTE>

            CUENTAS.ARR<-1> = Y.CUENTA
            VOTOS += Y.VOTOS.CUENTA
        END
    END

    RETURN

OBTENER.RELACIONES:
    ARREGLO.SEC = ""
    SEL.CMD3 = "SELECT " : FN.REL : " WITH CODIGO.CLIENTE EQ " : CODIGO.CLIENTE
    CALL EB.READLIST(SEL.CMD3,SEL.LIST3,'',NO.OF.RECS3,SEL.ERR3)
    LOOP REMOVE CUENTA.REL FROM SEL.LIST3 SETTING MANCOMU.POS

    WHILE CUENTA.REL DO
        CALL F.READ(FN.REL, CUENTA.REL, R.REL, FV.REL, REL.ERR)
        Y.REL.CLIENTE = R.REL<ST.L.AR.CODIGO.CLIENTE>
        Y.CODIGO.RELACION = R.REL<ST.L.AR.CODIGO.RELACION>
        Y.QNT = DCOUNT(Y.REL.CLIENTE,@VM)
        FOR A = 1 TO Y.QNT STEP 1
            Y.CLIENTE.ACTUAL = Y.REL.CLIENTE<1,A>
            IF Y.CLIENTE.ACTUAL EQ CODIGO.CLIENTE THEN
                IF Y.CODIGO.RELACION<1,A> EQ 500 OR Y.CODIGO.RELACION<1,A> EQ 501 THEN
                    ARREGLO.SEC<-1> = CUENTA.REL
                    SEL.CMD4 = "SELECT " : FN.PA : " WITH @ID LIKE " : CUENTA.REL : "... SAMPLE 1"
                    CALL EB.READLIST(SEL.CMD4,SEL.LIST4,'',NO.OF.RECS4,SEL.ERR4)
                    LOOP REMOVE CUENTA.REL.ID FROM SEL.LIST4 SETTING STMT.POS4
                    WHILE CUENTA.REL.ID DO
                        CUENTAPART.ID = CUENTA.REL.ID
                        GOSUB READ.PARTCIPANTE
                    REPEAT
*CUENTAS.ARR<-1> = CUENTA.REL
                END
            END
        NEXT A
    REPEAT
*DEBUG
    RETURN

END
