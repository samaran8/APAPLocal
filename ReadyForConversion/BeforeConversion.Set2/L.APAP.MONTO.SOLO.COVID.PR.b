*-----------------------------------------------------------------------------
* <Rating>-92</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE L.APAP.MONTO.SOLO.COVID.PR (Y.AAA.ID,Y.SALIDAD)
    $INSERT T24.BP I_COMMON
    $INSERT T24.BP I_EQUATE
    $INSERT T24.BP I_F.AA.ARRANGEMENT.ACTIVITY
    $INSERT  BP I_F.ST.L.APAP.PRELACION.COVI19.DET
    $INSERT T24.BP I_F.AA.BILL.DETAILS
    $INSERT T24.BP I_F.AA.ACCOUNT.DETAILS
    $INSERT BP I_F.ST.L.APAP.COVI.PRELACIONIII
    $INSERT T24.BP I_F.AA.ACTIVITY.BALANCES
    $INSERT T24.BP I_F.AA.ACTIVITY
    GOSUB APLICACIONES
    GOSUB PROCESS
    RETURN

APLICACIONES:
    FN.AAA = 'F.AA.ARRANGEMENT.ACTIVITY'
    FV.AAA = ''
    CALL OPF (FN.AAA,FV.AAA)

    FN.L.APAP.PRELACION.COVI19.DET = 'F.ST.L.APAP.PRELACION.COVI19.DET'
    FV.L.APAP.PRELACION.COVI19.DET = ''
    CALL OPF (FN.L.APAP.PRELACION.COVI19.DET,FV.L.APAP.PRELACION.COVI19.DET)

    FN.AA.ACCOUNT.DETAILS = 'F.AA.ACCOUNT.DETAILS';
    FV.AA.ACCOUNT.DETAILS = '';
    CALL OPF (FN.AA.ACCOUNT.DETAILS,FV.AA.ACCOUNT.DETAILS)

    FN.AA.BILL.DETAILS = "F.AA.BILL.DETAILS"
    FV.AA.BILL.DETAILS = ""
    CALL OPF (FN.AA.BILL.DETAILS,FV.AA.BILL.DETAILS)

    FN.PRELACIONIII$HIS = 'F.ST.L.APAP.COVI.PRELACIONIII$HIS'
    F.PRELACIONIII$HIS = ''
    CALL OPF (FN.PRELACIONIII$HIS,F.PRELACIONIII$HIS)

    FN.PRELACIONIII.LIVE = 'F.ST.L.APAP.COVI.PRELACIONIII'
    F.PRELACIONIII.LIVE = ''
    CALL OPF (FN.PRELACIONIII.LIVE,F.PRELACIONIII.LIVE)

    FN.AA.BILL.DETAILS.HIST = "F.AA.BILL.DETAILS.HIST"
    FV.AA.BILL.DETAILS.HIST = ""
    CALL OPF (FN.AA.BILL.DETAILS.HIST,FV.AA.BILL.DETAILS.HIST)
    FN.AA.ACT.BAL = 'F.AA.ACTIVITY.BALANCES'; F.AA.ACT.BAL = ''
    CALL OPF(FN.AA.ACT.BAL,F.AA.ACT.BAL)

    RETURN

PROCESS:
    Y.MONTO.CUOTA.PR = 0 ; Y.TODAY = TODAY;
    GOSUB GET.AAA.INFO
    GOSUB GET.DETAILS.PAY
    IF NOT (R.L.APAP.PRELACION.COVI19.DET) THEN
        GOSUB READ.LIVE.PRELACION
*GOSUB READ.HISTORICO.PRELACION
        GOSUB READ.AA.ACCOUNT.DETAILS
    END
    Y.SALIDAD = Y.MONTO.CUOTA.PR
    RETURN

READ.LIVE.PRELACION:
*******************
    CALL F.READ(FN.PRELACIONIII.LIVE,Y.ARR.ID,R.PRELACIONIII.LIVE,F.PRELACIONIII.LIVE,ERROR.PRELACIONIII.LIVE)
    Y.ST.L.A76.CURR.NO = R.PRELACIONIII.LIVE<ST.L.A76.CURR.NO>
    Y.FECHA.COVID19 = R.PRELACIONIII.LIVE<ST.L.A76.FECHA>
    RETURN
READ.HISTORICO.PRELACION:
*************************
    IF Y.ST.L.A76.CURR.NO EQ 1 THEN
        RETURN
    END
    Y.HIS.ARRANGEMENT =  Y.ARR.ID:";1":
    SEL.CMD1 = '' ; SEL.LIST1 = '' ; NO.OF.REC1 = '' ; RET.CODE1 = ''; Y.FECHA.COVID19 = '' ; Y.FECHA  = ''
    SEL.CMD1 = "SELECT ":FN.PRELACIONIII$HIS:" FECHA WITH @ID EQ ":Y.HIS.ARRANGEMENT
    CALL EB.READLIST(SEL.CMD1,SEL.LIST1,'',NO.OF.REC1,RET.CODE1)
    LOOP
        REMOVE Y.FECHA FROM SEL.LIST1 SETTING PRE.POST1
    WHILE  Y.FECHA  DO
        Y.FECHA.COVID19 = Y.FECHA
    REPEAT

    RETURN
GET.DETAILS.PAY:
***************
    CALL F.READ(FN.L.APAP.PRELACION.COVI19.DET,Y.CONTRACT.ID,R.L.APAP.PRELACION.COVI19.DET,FV.L.APAP.PRELACION.COVI19.DET,ERRO.DETAILS)
    RETURN

GET.AAA.INFO:
*************
    CALL F.READ(FN.AAA,Y.AAA.ID,R.AAA,FV.AAA,ERROR.AA)
    Y.CONTRACT.ID = R.AAA<AA.ARR.ACT.TXN.CONTRACT.ID>
    Y.SYSTEM.ID =   R.AAA<AA.ARR.ACT.TXN.SYSTEM.ID>
    Y.ARR.ID =     R.AAA<AA.ARR.ACT.ARRANGEMENT>
    Y.ACTIVITY = R.AAA<AA.ARR.ACT.ACTIVITY>
    Y.FECHA.EFECTIVIDAD =  R.AAA<AA.ARR.ACT.EFFECTIVE.DATE>
    Y.CHILD.ACTIVITY =    R.AAA<AA.ARR.ACT.CHILD.ACTIVITY>
    RETURN
READ.AA.ACCOUNT.DETAILS:
***********************

    IF Y.ACTIVITY EQ 'LENDING-APPLYPAYMENT-RP.PAYOFF.CHQ' OR Y.ACTIVITY EQ 'LENDING-APPLYPAYMENT-RP.PAYOFF' THEN
        GOSUB GET.BILL.BALANCE.CANCELACION
        RETURN
    END
    Y.REPAY.REFERENCE = ''; R.AA.ACCOUNT.DETAILS = ''; ERROR.ACCOUNT.DETAILS = ''; Y.ID.REAY = Y.AAA.ID:"-":Y.FECHA.EFECTIVIDAD
    Y.ARREGLO.BILL = ''; Y.CNT = 0; Y.TOTAL = 0;
    CALL F.READ(FN.AA.ACCOUNT.DETAILS,Y.ARR.ID,R.AA.ACCOUNT.DETAILS,FV.AA.ACCOUNT.DETAILS,ERROR.ACCOUNT.DETAILS)
    IF R.AA.ACCOUNT.DETAILS THEN
        Y.REPAY.REFERENCE = R.AA.ACCOUNT.DETAILS<AA.AD.REPAY.REFERENCE>
        Y.RPY.BILL.ID = R.AA.ACCOUNT.DETAILS<AA.AD.RPY.BILL.ID>
        LOCATE Y.ID.REAY IN Y.REPAY.REFERENCE<1,1> SETTING PRO.POS THEN
            Y.ARREGLO.BILL = Y.RPY.BILL.ID<1,PRO.POS>
            Y.ARREGLO.BILL = CHANGE(Y.ARREGLO.BILL,VM,FM)
            Y.ARREGLO.BILL = CHANGE(Y.ARREGLO.BILL,SM,FM)
            Y.CNT = DCOUNT(Y.ARREGLO.BILL,FM)
            FOR I = 1 TO Y.CNT
                Y.BIIL.ID = Y.ARREGLO.BILL<I>
                Y.VALOR = ''
                GOSUB READ.AA.BILL.DETAILS
                Y.TOTAL = Y.TOTAL + Y.MONTO.PRINCIPAL;
            NEXT I
            IF Y.TOTAL GT 0 THEN
                Y.MONTO.CUOTA.PR = Y.TOTAL;
            END
        END
    END
    RETURN

READ.AA.BILL.DETAILS:
********************
    Y.OR.PR.AMT = 0; Y.OS.PR.AMT = 0; Y.MONTO.PRINCIPAL = 0;
    CALL F.READ(FN.AA.BILL.DETAILS,Y.BIIL.ID,R.AA.BILL.DETAILS,FV.AA.BILL.DETAILS,ERROR.BILL.DETAILS)

    IF NOT (R.AA.BILL.DETAILS) THEN
        CALL F.READ(FN.AA.BILL.DETAILS.HIST,Y.BIIL.ID,R.AA.BILL.DETAILS,FV.AA.BILL.DETAILS.HIST,ERROR.BILL.DETAILS)

    END

    Y.PAYMENT.DATE = R.AA.BILL.DETAILS<AA.BD.PAYMENT.DATE>
    IF Y.PAYMENT.DATE GT Y.FECHA.COVID19 THEN
        Y.PAYMENT.TYPE = R.AA.BILL.DETAILS<AA.BD.PAYMENT.TYPE>
        Y.BILL.TYPE =  R.AA.BILL.DETAILS<AA.BD.BILL.TYPE>
        Y.PAY.PROPERTY = R.AA.BILL.DETAILS<AA.BD.PAY.PROPERTY>
        Y.OR.PR.AMT = R.AA.BILL.DETAILS<AA.BD.OR.PR.AMT>
        Y.OS.PR.AMT = R.AA.BILL.DETAILS<AA.BD.OS.PR.AMT>
        Y.OR.PROP.AMOUNT = R.AA.BILL.DETAILS<AA.BD.OR.PROP.AMOUNT>
        LOCATE 'CONSTANTE' IN Y.PAYMENT.TYPE<1,1> SETTING BILL.POS THEN
            Y.PAY.PROPERTY = Y.PAY.PROPERTY<1,BILL.POS>
            LOCATE 'PRINCIPALINT' IN Y.PAY.PROPERTY <1,1,1> SETTING BILL.POS1 THEN
                Y.OR.PR.AMT = Y.OR.PR.AMT<1,BILL.POS,BILL.POS1>
                Y.OS.PR.AMT = Y.OS.PR.AMT<1,BILL.POS,BILL.POS1>
                IF Y.OS.PR.AMT EQ 0  AND Y.OR.PR.AMT GT 0 THEN
                    Y.MONTO.PRINCIPAL = Y.OR.PR.AMT;
                END
            END
        END ELSE
            GOSUB GET.AA.BILL.SOLO.INTERES
        END
    END
    RETURN


GET.AA.BILL.SOLO.INTERES:
************************
    LOCATE 'SOLO.INTERES' IN Y.PAYMENT.TYPE<1,1> SETTING BILL.POS THEN
        Y.PAY.PROPERTY = Y.PAY.PROPERTY<1,BILL.POS>
        LOCATE 'PRINCIPALINT' IN Y.PAY.PROPERTY <1,1,1> SETTING BILL.POS1 THEN
            Y.OR.PR.AMT = Y.OR.PR.AMT<1,BILL.POS,BILL.POS1>
            Y.OS.PR.AMT = Y.OS.PR.AMT<1,BILL.POS,BILL.POS1>
            IF Y.OS.PR.AMT EQ 0  AND Y.OR.PR.AMT GT 0 THEN
                Y.MONTO.PRINCIPAL = Y.OR.PR.AMT;
            END
        END
    END

    RETURN

GET.BILL.BALANCE.CANCELACION:
****************************
    SEL.CMD = ''; SEL.LIST = ''; NO.OF.RECS = ''; RET.CODE = '';
    SEL.CMD = "SELECT ":FN.AA.BILL.DETAILS:" WITH ARRANGEMENT.ID EQ ":Y.ARR.ID:
    SEL.CMD := " AND PAYMENT.DATE EQ ":Y.FECHA.EFECTIVIDAD:" AND PAYMENT.METHOD EQ 'INFO' AND BILL.TYPE EQ 'PAYOFF'"
    CALL EB.READLIST(SEL.CMD,SEL.LIST,'',NO.OF.RECS,RET.CODE)

    IF DCOUNT(SEL.LIST,FM) EQ 0 THEN
        SEL.CMD = ''; SEL.LIST = ''; NO.OF.RECS = ''; RET.CODE = '';
        SEL.CMD = "SELECT ":FN.AA.BILL.DETAILS.HIST:" WITH ARRANGEMENT.ID EQ ":Y.ARR.ID:
        SEL.CMD := " AND PAYMENT.DATE EQ ":Y.FECHA.EFECTIVIDAD:" AND PAYMENT.METHOD EQ 'INFO' AND BILL.TYPE EQ 'PAYOFF'"
        CALL EB.READLIST(SEL.CMD,SEL.LIST,'',NO.OF.RECS,RET.CODE)

    END

    LOOP
        REMOVE Y.BIIL.ID FROM SEL.LIST SETTING BILL.POS
    WHILE Y.BIIL.ID:BILL.POS DO
        CALL F.READ(FN.AA.BILL.DETAILS,Y.BIIL.ID,R.AA.BILL.DETAILS,FV.AA.BILL.DETAILS,ERROR.BILL.DETAILS)
        IF NOT (R.AA.BILL.DETAILS) THEN
            CALL F.READ(FN.AA.BILL.DETAILS.HIST,Y.BIIL.ID,R.AA.BILL.DETAILS,FV.AA.BILL.DETAILS.HIST,ERROR.BILL.DETAILS)
        END
        Y.PAYMENT.DATE = R.AA.BILL.DETAILS<AA.BD.PAYMENT.DATE>
        IF Y.PAYMENT.DATE GT Y.FECHA.COVID19 THEN
            Y.OR.PROP.AMOUNT = R.AA.BILL.DETAILS<AA.BD.OR.PROP.AMOUNT>
            Y.PROPERTY = R.AA.BILL.DETAILS<AA.BD.PROPERTY>
            LOCATE 'PRINCIPALINT' IN Y.PROPERTY <1,1> SETTING BILL.PAYOFF1 THEN
                Y.MONTO.CUOTA.PR = Y.OR.PROP.AMOUNT<1,BILL.PAYOFF1>
            END
        END

    REPEAT
    IF Y.MONTO.CUOTA.PR EQ 0 THEN
        GOSUB GET.AA.ACT.BAL
    END
    RETURN

GET.AA.ACT.BAL:
    R.AA.ACT.BAL = ''; ERROR.AA.ACT.BAL = ''; Y.ACTIVITY.REF = ''; Y.BAL.PROPERTY = '';Y.BAL.PROPERTY.AMT = '';
    Y.CNT.1 = 0; I = 0;
    CALL F.READ (FN.AA.ACT.BAL,Y.ARR.ID,R.AA.ACT.BAL,F.AA.ACT.BAL,ERROR.AA.ACT.BAL)
    Y.ACTIVITY.REF = R.AA.ACT.BAL<AA.ACT.BAL.ACTIVITY.REF>
    Y.BAL.PROPERTY = R.AA.ACT.BAL<AA.ACT.BAL.PROPERTY>
    Y.BAL.PROPERTY.AMT = R.AA.ACT.BAL<AA.ACT.BAL.PROPERTY.AMT>
    Y.CHILD.ACTIVITY = CHANGE(Y.CHILD.ACTIVITY,SM,FM)
    Y.CHILD.ACTIVITY = CHANGE(Y.CHILD.ACTIVITY,VM,FM)
    Y.CNT.1 = DCOUNT(Y.CHILD.ACTIVITY,FM)
    FOR I = 1 TO Y.CNT.1
        Y.HIJA =  Y.CHILD.ACTIVITY<I>
        LOCATE Y.HIJA IN Y.ACTIVITY.REF <1,1> SETTING REF.PAYOFF1 THEN
            Y.PROPIEDADES = Y.BAL.PROPERTY<1,REF.PAYOFF1>
            LOCATE 'PRINCIPALINT.ACCPRINCIPALINT' IN Y.PROPIEDADES<1,1,1> SETTING REF.PAYOFF2 THEN
                Y.MONTO.CUOTA.PR += Y.BAL.PROPERTY.AMT<1,REF.PAYOFF1,REF.PAYOFF2>
            END
        END
    NEXT I


    RETURN

END
