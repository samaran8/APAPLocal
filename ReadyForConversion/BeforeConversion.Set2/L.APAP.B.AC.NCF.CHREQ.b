*-----------------------------------------------------------------------------
* <Rating>139</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE L.APAP.B.AC.NCF.CHREQ(STMT.ID)

*
* Client Name : APAP
* Description: Routine to generate / issue the NCF for AC.CHARGE.REQUEST table
* Dev By : Ashokkumar
*

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.AC.CHARGE.REQUEST
    $INSERT I_F.STMT.ENTRY
    $INSERT I_F.ACCOUNT
    $INSERT TAM.BP I_F.REDO.NCF.ISSUED
    $INSERT TAM.BP I_F.REDO.L.NCF.STATUS
    $INSERT TAM.BP I_F.REDO.PENDING.CHARGE
    $INSERT TAM.BP I_F.REDO.REPAYMENT.CHARGE
    $INSERT LAPAP.BP I_L.APAP.B.AC.NCF.CHREQ.COMMON


    GOSUB PROCESS
    RETURN

PROCESS:
********
    YCUSTOMER.ID = ''; YBOOKING.DATE = ''; YAMT.LCY = ''; YTRANS.REF = ''; YDTE = ''; REPAYMENT.ID = ''
    YDTE = FIELD(STMT.ID,'-',2)

    BEGIN CASE
    CASE LEN(STMT.ID) GT 20
        ERR.STMT.ENTRY = ''; R.STMT.ENTRY = ''
        CALL F.READ(FN.STMT.ENTRY,STMT.ID,R.STMT.ENTRY,F.STMT.ENTRY,ERR.STMT.ENTRY)
        YTRANS.REF = R.STMT.ENTRY<AC.STE.OUR.REFERENCE>
        IF YTRANS.REF EQ '' THEN
            YTRANS.REF = R.STMT.ENTRY<AC.STE.TRANS.REFERENCE>
            YTRANS.REF = FIELD(YTRANS.REF,'\',1)
        END
        IF YTRANS.REF[1,3] NE 'CHG' THEN RETURN

        YAMT.LCY = R.STMT.ENTRY<AC.STE.AMOUNT.LCY>
        YCUSTOMER.ID = R.STMT.ENTRY<AC.STE.CUSTOMER.ID>
        YBOOKING.DATE = R.STMT.ENTRY<AC.STE.BOOKING.DATE>
        GOSUB SUB.PROCESS

    CASE STMT.ID[1,3] EQ 'CHG'
        YTRANS.REF = STMT.ID
        GOSUB SUB.PROCESS

    CASE YDTE NE ''
        ERR.REDO.REPAYMENT.CHARGE = ''; R.REDO.REPAYMENT.CHARGE = ''; RP.TXN = ''
        CALL F.READ(FN.REDO.REPAYMENT.CHARGE,STMT.ID,R.REDO.REPAYMENT.CHARGE,F.REDO.REPAYMENT.CHARGE,ERR.REDO.REPAYMENT.CHARGE)
        RP.TXN = R.REDO.REPAYMENT.CHARGE<REPAY.CHG.TRANSACTION>

        LOOP
            REMOVE REPAYMENT.ID FROM RP.TXN SETTING RP.POSN
        WHILE REPAYMENT.ID:RP.POSN

            IF REPAYMENT.ID[1,3] NE 'CHG' THEN CONTINUE
            YTRANS.REF = REPAYMENT.ID
            STMT.ID = REPAYMENT.ID
            GOSUB SUB.PROCESS
        REPEAT
    END CASE

    RETURN

SUB.PROCESS:
************
    CRT YTRANS.REF
    ERR.AC.CHARGE.REQUEST = ''; R.AC.CHARGE.REQUEST = ''; YDB.ACCT = ''; YCHQ.AMT = ''
    CALL F.READ(FN.AC.CHARGE.REQUEST,YTRANS.REF,R.AC.CHARGE.REQUEST,F.AC.CHARGE.REQUEST,ERR.AC.CHARGE.REQUEST)
    IF NOT(R.AC.CHARGE.REQUEST) THEN
        YTRANS.REFH = ''; YTRANS.REFH = YTRANS.REF
        R.AC.CHARGE.REQUEST = ''; AC.CHARGE.REQUEST.ERR = ''
        CALL EB.READ.HISTORY.REC(F.AC.CHARGE.REQUEST.H,YTRANS.REFH,R.AC.CHARGE.REQUEST,AC.CHARGE.REQUEST.ERR)
    END
    YDB.ACCT = R.AC.CHARGE.REQUEST<CHG.DEBIT.ACCOUNT>

    IF STMT.ID[1,3] EQ 'CHG' THEN
        YBOOKING.DATE = R.AC.CHARGE.REQUEST<CHG.CHARGE.DATE>
        YCHQ.AMT = R.AC.CHARGE.REQUEST<CHG.CHARGE.AMOUNT,1>

        ERR.ACCOUNT = ''; R.ACCOUNT = ''
        CALL F.READ(FN.ACCOUNT,YDB.ACCT,R.ACCOUNT,F.ACCOUNT,ERR.ACCOUNT)
        YCUSTOMER.ID = R.ACCOUNT<AC.CUSTOMER>
        YAMT.LCY = YCHQ.AMT
    END
    IF REPAYMENT.ID NE '' THEN
        YBOOKING.DATE = YLSTDAY
    END

    YTP.YAMT.LCY = ''; YTP.YAMT.LCY = ABS(YAMT.LCY)
    NCF.ISSUE.ID = ''
    NCF.ISSUE.ID = YCUSTOMER.ID:'.':YBOOKING.DATE:'.':YTRANS.REF

    R.REDO.PENDING.CHARGE = ''; PEND.CHRG = ''
    CALL F.READ(FN.PENDING.CHARGE,YDB.ACCT,R.REDO.PENDING.CHARGE,F.PENDING.CHARGE,PEND.CHRG)
    IF R.REDO.PENDING.CHARGE THEN
        FINDSTR YTRANS.REF IN R.REDO.PENDING.CHARGE<PEN.CHG.TRANSACTION> SETTING POS1,YSM,YVM THEN
            YAMT = ''; YAMT = R.REDO.PENDING.CHARGE<PEN.CHG.AMOUNT,YSM,YVM>
            IF YAMT EQ YTP.YAMT.LCY THEN
	        GOSUB REVERSE.NCF.TABLE
                RETURN
            END
        END
    END
    IF YAMT.LCY GT 0 THEN
        Y.CNT = '1'; NCF.NUMBER = ''
        CALL REDO.NCF.PERF.RTN(Y.CNT,NCF.NUMBER)
        NCF.NUMBER = NCF.NUMBER<1>
        GOSUB UPDATE.TABLE
    END

    IF YAMT.LCY LT 0 THEN
        GOSUB REVERSE.NCF.TABLE
    END
    RETURN

UPDATE.TABLE:
**************
    GOSUB READ.NCF.ISSUE
    IF NOT(R.REDO.NCF.ISSUED) THEN
        R.REDO.NCF.ISSUED<ST.IS.TXN.ID> = YTRANS.REF
        IF STMT.ID[1,3] NE 'CHG' THEN
            R.REDO.NCF.ISSUED<ST.IS.TAX.AMOUNT> = "DOP ":YAMT.LCY
        END ELSE
            R.REDO.NCF.ISSUED<ST.IS.CHARGE.AMOUNT> = "DOP ":YAMT.LCY
        END
        R.REDO.NCF.ISSUED<ST.IS.DATE> = YBOOKING.DATE
        R.REDO.NCF.ISSUED<ST.IS.CUSTOMER> = YCUSTOMER.ID
        R.REDO.NCF.ISSUED<ST.IS.ACCOUNT> = YDB.ACCT
        R.REDO.NCF.ISSUED<ST.IS.NCF> = NCF.NUMBER
    END ELSE
        IF STMT.ID[1,3] EQ 'CHG' THEN
            IF R.REDO.NCF.ISSUED<ST.IS.CHARGE.AMOUNT> NE '' THEN
                RETURN
            END
            R.REDO.NCF.ISSUED<ST.IS.CHARGE.AMOUNT> = "DOP ":YAMT.LCY
        END ELSE
            IF R.REDO.NCF.ISSUED<ST.IS.TAX.AMOUNT> NE '' THEN
                RETURN
            END
            R.REDO.NCF.ISSUED<ST.IS.TAX.AMOUNT> = "DOP ":YAMT.LCY
        END
        R.REDO.NCF.ISSUED<ST.IS.DATE> = YBOOKING.DATE
        R.REDO.NCF.ISSUED<ST.IS.CUSTOMER> = YCUSTOMER.ID
        R.REDO.NCF.ISSUED<ST.IS.NCF,-1> = NCF.NUMBER
    END
    CALL F.WRITE(FN.REDO.NCF.ISSUED,NCF.ISSUE.ID,R.REDO.NCF.ISSUED)


    GOSUB READ.NCF.STAT
    IF NOT(R.REDO.L.NCF.STATUS) THEN
        R.REDO.L.NCF.STATUS<NCF.ST.TRANSACTION.ID> = YTRANS.REF
        R.REDO.L.NCF.STATUS<NCF.ST.CUSTOMER.ID> = YCUSTOMER.ID
        R.REDO.L.NCF.STATUS<NCF.ST.DATE>= YBOOKING.DATE
        IF STMT.ID[1,3] NE 'CHG' THEN
            R.REDO.L.NCF.STATUS<NCF.ST.TAX.AMOUNT> = "DOP ":YAMT.LCY
        END ELSE
            R.REDO.L.NCF.STATUS<NCF.ST.CHARGE.AMOUNT> = "DOP ":YAMT.LCY
        END
        R.REDO.L.NCF.STATUS<NCF.ST.NCF> = NCF.NUMBER
        R.REDO.L.NCF.STATUS<NCF.ST.STATUS> = 'AVAILABLE'
    END ELSE
        R.REDO.L.NCF.STATUS<NCF.ST.DATE>= YBOOKING.DATE
        IF STMT.ID[1,3] NE 'CHG' THEN
            R.REDO.L.NCF.STATUS<NCF.ST.TAX.AMOUNT> = "DOP ":YAMT.LCY
        END ELSE
            R.REDO.L.NCF.STATUS<NCF.ST.CHARGE.AMOUNT> = "DOP ":YAMT.LCY
        END
        R.REDO.L.NCF.STATUS<NCF.ST.NCF,-1> = NCF.NUMBER
        R.REDO.L.NCF.STATUS<NCF.ST.STATUS> = 'AVAILABLE'
    END
    CALL F.WRITE(FN.REDO.L.NCF.STATUS,NCF.ISSUE.ID,R.REDO.L.NCF.STATUS)
    RETURN

READ.NCF.ISSUE:
***************
    ISSUE.MSG = ''; R.REDO.NCF.ISSUED = ''
    CALL F.READ(FN.REDO.NCF.ISSUED,NCF.ISSUE.ID,R.REDO.NCF.ISSUED,F.REDO.NCF.ISSUED,ISSUE.MSG)
    RETURN

READ.NCF.STAT:
***************
    R.REDO.L.NCF.STATUS = ''; STATUS.MSG = ''
    CALL F.READ(FN.REDO.L.NCF.STATUS,NCF.ISSUE.ID,R.REDO.L.NCF.STATUS,F.REDO.L.NCF.STATUS,STATUS.MSG)
    RETURN

REVERSE.NCF.TABLE:
******************
    YDEL.VAL = 0; Y.NCF = ''; YCHRG.AMT = ''; YTAX.AMT = ''
    GOSUB READ.NCF.ISSUE
    Y.NCF = R.REDO.NCF.ISSUED<ST.IS.NCF>
    YCHRG.AMT = R.REDO.NCF.ISSUED<ST.IS.CHARGE.AMOUNT>
    IF YCHRG.AMT THEN
        YCHRG.AMT = FIELD(YCHRG.AMT,' ',2,1)
    END
    YTAX.AMT = R.REDO.NCF.ISSUED<ST.IS.TAX.AMOUNT>
    IF YTAX.AMT THEN
        YTAX.AMT = FIELD(YTAX.AMT,' ',2,1)
    END

    Y.CNT.NCF = DCOUNT(Y.NCF,VM)
    IF Y.CNT.NCF GT '1' THEN
        IF YTAX.AMT AND YAMT.LCY EQ YTAX.AMT THEN
            R.REDO.NCF.ISSUED<ST.IS.TAX.AMOUNT> = ''
        END
        IF YCHRG.AMT AND YAMT.LCY EQ YCHRG.AMT THEN
            R.REDO.NCF.ISSUED<ST.IS.CHARGE.AMOUNT> = ''
        END

        R.REDO.NCF.ISSUED<ST.IS.NCF> = R.REDO.NCF.ISSUED<ST.IS.NCF,1>
        CALL F.WRITE(FN.REDO.NCF.ISSUED,NCF.ISSUE.ID,R.REDO.NCF.ISSUED)
    END ELSE
        IF R.REDO.NCF.ISSUED THEN
            NCF.NUMBER = Y.NCF
            CALL F.DELETE(FN.REDO.NCF.ISSUED,NCF.ISSUE.ID)
            YDEL.VAL = 1
        END
    END

    GOSUB READ.NCF.STAT
    IF R.REDO.L.NCF.STATUS THEN
        IF YTAX.AMT AND YAMT.LCY EQ YTAX.AMT THEN
            R.REDO.L.NCF.STATUS<NCF.ST.TAX.AMOUNT> = ''
        END
        IF YCHRG.AMT AND YAMT.LCY EQ YCHRG.AMT THEN
            R.REDO.L.NCF.STATUS<NCF.ST.CHARGE.AMOUNT> = ''
        END
        R.REDO.L.NCF.STATUS<NCF.ST.NCF,1> = ''
        R.REDO.L.NCF.STATUS<NCF.ST.STATUS> = 'UNAVAILABLE'
        CALL F.WRITE(FN.REDO.L.NCF.STATUS,NCF.ISSUE.ID,R.REDO.L.NCF.STATUS)
    END

    IF YDEL.VAL EQ 1 THEN
        GET.NCF.REC = "OTHERS.99"
        R.REDO.AA.NCF.IDS = ''; R.REDO.AA.NCF.IDS.ERR = ''; RETRY.VAR = "E"
        CALL F.READU(FN.REDO.AA.NCF.IDS,GET.NCF.REC,R.REDO.AA.NCF.IDS,F.REDO.AA.NCF.IDS,R.REDO.AA.NCF.IDS.ERR,RETRY.VAR)
        R.REDO.AA.NCF.IDS<-1> = NCF.NUMBER
        CALL F.WRITE(FN.REDO.AA.NCF.IDS,GET.NCF.REC,R.REDO.AA.NCF.IDS)
    END
    RETURN

END
