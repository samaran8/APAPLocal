*-----------------------------------------------------------------------------
* <Rating>-21</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE REDO.B.AA.CP.OVERPAY(OVERPAY.ID)

*-------------------------------------------------
*Description: This batch routine is to post the FT OFS messages for overpayment
*             and also to credit the interest in loan..
* Dev by: V.P.Ashokkumar
* Date  : 10/10/2016
*-------------------------------------------------
    $INCLUDE T24.BP I_COMMON
    $INCLUDE T24.BP I_EQUATE
    $INCLUDE T24.BP I_F.DATES
    $INCLUDE T24.BP I_AA.LOCAL.COMMON
    $INCLUDE T24.BP I_F.AA.PAYMENT.SCHEDULE
    $INCLUDE T24.BP I_F.AA.ARRANGEMENT.ACTIVITY
    $INCLUDE T24.BP I_F.ACCOUNT
    $INCLUDE T24.BP I_BATCH.FILES
    $INCLUDE LAPAP.BP I_F.REDO.AA.CP.OVERPAYMENT
    $INCLUDE TAM.BP I_F.REDO.AA.OVERPAYMENT
    $INCLUDE LAPAP.BP I_REDO.B.AA.OVERPAY.COMMON


    GOSUB PROCESS
    RETURN

PROCESS:
********

    BEGIN CASE
    CASE CONTROL.LIST<1,1> EQ 'NEW.OVERPAY'
        ERR.REDO.AA.CP.OVERPAYMENT = ''; R.REDO.AA.CP.OVERPAYMENT = ''
        CALL F.READ(FN.REDO.AA.CP.OVERPAYMENT,OVERPAY.ID,R.REDO.AA.CP.OVERPAYMENT,F.REDO.AA.CP.OVERPAYMENT,ERR.REDO.AA.CP.OVERPAYMENT)
        IF NOT(R.REDO.AA.CP.OVERPAYMENT) THEN
            RETURN
        END

        Y.AA.ID = R.REDO.AA.CP.OVERPAYMENT<REDO.AA.CP.LOAN.NO>
        IF Y.AA.ID THEN
            GOSUB SUB.PROCESS
* Delete the already processed Extrordinary payment record.
            CALL F.DELETE(FN.REDO.AA.CP.OVERPAYMENT,OVERPAY.ID)
        END

    CASE CONTROL.LIST<1,1> EQ 'OLD.OVERPAY'
        R.REDO.AA.OVERPAYMENT = ''; OVER.ERR = ''
        CALL F.READ(FN.REDO.AA.OVERPAYMENT,OVERPAY.ID,R.REDO.AA.OVERPAYMENT,F.REDO.AA.OVERPAYMENT,OVER.ERR)
        IF NOT(R.REDO.AA.OVERPAYMENT) THEN
            RETURN
        END
        Y.LOAN.ACC = R.REDO.AA.OVERPAYMENT<REDO.OVER.LOAN.NO>
        IF NOT(Y.LOAN.ACC) THEN
            RETURN
        END

        ERR.ACCOUNT = ''; R.ACCOUNT = ''
        CALL F.READ(FN.ACCOUNT,Y.LOAN.ACC,R.ACCOUNT,F.ACCOUNT,ERR.ACCOUNT)
        Y.AA.ID = R.ACCOUNT<AC.ARRANGEMENT.ID>
        IF Y.AA.ID THEN
            GOSUB SUB.PROCESS
        END
    END CASE
    RETURN

SUB.PROCESS:
************
    PROCESS.MSG = ''
    Y.ACT = 'LENDING-CHANGE-REPAYMENT.SCHEDULE'
    Y.AAA.REQ<AA.ARR.ACT.ARRANGEMENT> = Y.AA.ID
    Y.AAA.REQ<AA.ARR.ACT.ACTIVITY> = Y.ACT
    Y.AAA.REQ<AA.ARR.ACT.EFFECTIVE.DATE> = TODAY
    Y.AAA.REQ<AA.ARR.ACT.PROPERTY,1> = 'REPAYMENT.SCHEDULE'

    APP.NAME = 'AA.ARRANGEMENT.ACTIVITY'
    IN.FUNCTION = 'I'
    VERSION.NAME = 'AA.ARRANGEMENT.ACTIVITY,ZERO.AUTH'
    CALL OFS.BUILD.RECORD(APP.NAME, IN.FUNCTION, "PROCESS", VERSION.NAME, "", "0", AAA.ID, Y.AAA.REQ, PROCESS.MSG)

    OFS.MSG.ID = ''; OFS.ERR = ''
    OFS.SOURCE = 'TRIGGER.INSURANCE'
    CALL OFS.POST.MESSAGE(PROCESS.MSG,OFS.MSG.ID,OFS.SOURCE,OFS.ERR)

    RETURN
END
