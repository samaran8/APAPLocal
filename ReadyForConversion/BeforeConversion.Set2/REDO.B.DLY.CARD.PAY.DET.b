*-----------------------------------------------------------------------------
* <Rating>-52</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE REDO.B.DLY.CARD.PAY.DET(VISION.ID)

*******************************************************************************************
* Description: The REPORT to capture the vision plus transaction posted on last working day.
* Dev By:V.P.Ashokkumar
*
*******************************************************************************************
    $INCLUDE T24.BP I_COMMON
    $INCLUDE T24.BP I_EQUATE
    $INCLUDE T24.BP I_F.FUNDS.TRANSFER
    $INCLUDE T24.BP I_F.TELLER
    $INCLUDE T24.BP I_F.DATES
    $INCLUDE TAM.BP I_F.REDO.VISION.PLUS.TXN
    $INCLUDE TAM.BP I_F.REDO.H.REPORTS.PARAM
    $INCLUDE LAPAP.BP I_REDO.B.DLY.CARD.PAY.DET.COMMON


    GOSUB INIT
    GOSUB PROCESS
    RETURN

PROCESS:
********
    ERR.REDO.VISION.PLUS.TXN = ''; R.REDO.VISION.PLUS.TXN = ''
    CALL F.READ(FN.REDO.VISION.PLUS.TXN,VISION.ID,R.REDO.VISION.PLUS.TXN,F.REDO.VISION.PLUS.TXN,ERR.REDO.VISION.PLUS.TXN)

    YCARD.NUM = R.REDO.VISION.PLUS.TXN<VP.TXN.CARDHOLDER.NUM>
    YACCT.NUM = R.REDO.VISION.PLUS.TXN<VP.TXN.DEBIT.ACCT>
    YCARD.CCY = R.REDO.VISION.PLUS.TXN<VP.TXN.CURRENCY>
    YCARD.AMT = R.REDO.VISION.PLUS.TXN<VP.TXN.TRANS.AMOUNT>
    YAUTH.CODE = R.REDO.VISION.PLUS.TXN<VP.TXN.TRANS.AUTH>
    YCOMP.CODE = R.REDO.VISION.PLUS.TXN<VP.TXN.CHANNEL>
    IF NOT(YCOMP.CODE) THEN
        YCOMP.CODE = R.REDO.VISION.PLUS.TXN<VP.TXN.BRANCH>
    END
    YAUTH.DATE = R.REDO.VISION.PLUS.TXN<VP.TXN.POSTING.DATE>
    YREF.NUM = R.REDO.VISION.PLUS.TXN<VP.TXN.TXN.REF>
    YCASH = R.REDO.VISION.PLUS.TXN<VP.TXN.CASH.AMT>
    YCHEQUE = R.REDO.VISION.PLUS.TXN<VP.TXN.CHEQUE.AMT>
    IF YCASH THEN
        YPAY.TYPE = 'EFECTIVO'
    END ELSE
        YPAY.TYPE = 'CHEQUE'
    END

    IF YREF.NUM[1,2] EQ 'TT' THEN
        YREF.ID = YREF.NUM
        GOSUB READ.TT
    END

    IF YREF.NUM[1,2] EQ 'FT' THEN
        YREF.ID = YREF.NUM
        GOSUB READ.FT
    END
    IF NOT(R.FUNDS.TRANSFER) AND NOT(R.TELLER) ELSE
        GOSUB WRITE.CONCAT
    END
    RETURN

WRITE.CONCAT:
*************
    YFIN.ENQ = YCARD.NUM:',':YACCT.NUM:',':YCARD.CCY:',':YCARD.AMT:',':YAUTH.CODE:',':YCOMP.CODE:',':YAUTH.DATE:',':YAUTH.TIME:',':YREF.NUM:',':YCAJERO:',':YPAY.TYPE
    CALL F.WRITE(FN.DR.OPER.VPLUS.WORKFILE,VISION.ID,YFIN.ENQ)
    RETURN

INIT:
*****
    YCARD.NUM = ''; YACCT.NUM = ''; YPAY.TYPE = ''; YAUTH.CODE = ''; YCARD.AMT = ''; R.TELLER = ''
    YCARD.CCY = ''; YCOMP.CODE = ''; YAUTH.DATE = ''; YAUTH.TIME = ''; TELLER.ERR = ''
    ERR.FUNDS.TRANSFER = ''; R.FUNDS.TRANSFER = ''; TXN.TYP = ''; YREF.ID  = ''; YREF.ID.HST = ''
    YCARD.TYPE = ''; YCAJERO = ''; YREF.NUM = ''; YCASH = ''; YCHEQUE = ''; YFIN.ENQ = ''
    RETURN

READ.FT:
********
    CALL F.READ(FN.FUNDS.TRANSFER,YREF.ID,R.FUNDS.TRANSFER,F.FUNDS.TRANSFER,FUNDS.TRANSFER.ERR)
    IF NOT(R.FUNDS.TRANSFER) THEN
        YREF.ID.HST = YREF.ID; FUNDS.TRANSFER.ERR = ''
        CALL EB.READ.HISTORY.REC(F.FUNDS.TRANSFER.HST,YREF.ID.HST,R.FUNDS.TRANSFER,FUNDS.TRANSFER.ERR)
    END
    YAUTH.TIME = R.FUNDS.TRANSFER<FT.DATE.TIME>[7,4]
*    YCAJERO = R.FUNDS.TRANSFER<FT.LOCAL.REF,L.FT.INP.USER.ID.POS>
    RETURN

READ.TT:
********
    CALL F.READ(FN.TELLER,YREF.ID,R.TELLER,F.TELLER,TELLER.ERR)
    IF NOT(R.TELLER) THEN
        YREF.ID.HST = YREF.ID; TELLER.ERR = ''
        CALL EB.READ.HISTORY.REC(F.TELLER.HST,YREF.ID.HST,R.TELLER,TELLER.ERR)
    END
    YAUTH.TIME = R.TELLER<TT.TE.DATE.TIME>[7,4]
*    YCAJERO = R.TELLER<TT.TE.LOCAL.REF,L.TT.INP.USER.ID.POS>
    YCAJERO = R.TELLER<TT.TE.TELLER.ID.1>
    RETURN

END
