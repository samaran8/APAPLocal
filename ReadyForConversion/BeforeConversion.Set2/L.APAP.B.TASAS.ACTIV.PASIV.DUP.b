*-----------------------------------------------------------------------------
* <Rating>-55</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE L.APAP.B.TASAS.ACTIV.PASIV.DUP(REC.ID)

*
* Client Name   : APAP
* Develop By    : Ashokkumar
* Description   : The routine to generate the Activasa and Pasivas report AR010.
*

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_BATCH.FILES
    $INSERT I_TSA.COMMON
    $INSERT I_F.ACCOUNT
    $INSERT I_F.AZ.ACCOUNT
    $INSERT I_F.CUSTOMER
    $INSERT I_F.AA.ARRANGEMENT
    $INSERT I_F.AZ.PRODUCT.PARAMETER
    $INSERT I_F.BASIC.INTEREST
    $INSERT I_F.GROUP.DATE
    $INSERT I_F.ACCOUNT.CREDIT.INT
    $INSERT I_F.PERIODIC.INTEREST
    $INSERT I_F.GROUP.CREDIT.INT
    $INSERT I_DAS.COMMON
    $INSERT I_DAS.BASIC.INTEREST
    $INSERT I_F.AA.INTEREST
    $INSERT I_F.AA.TERM.AMOUNT
    $INSERT I_F.COLLATERAL
    $INSERT I_F.AA.ACTIVITY.HISTORY
    $INSERT I_F.AA.ACCOUNT
    $INSERT I_F.AA.CUSTOMER
    $INSERT I_F.AA.OVERDUE
    $INSERT I_F.COMPANY
    $INSERT I_L.APAP.B.TASAS.ACTIV.PASIV.DUP.COMMON
    $INSERT I_F.REDO.APAP.INSTIT.FINANC.PARAM


    GOSUB MAIN.PROCESS
    RETURN

MAIN.PROCESS:
*************
    YGRP.ARRY = ''; YYR.MTH = 12
    BEGIN CASE
    CASE CONTROL.LIST<1,1> EQ "SELECT.AZ"
        CALL F.READ(FN.AZ.ACCOUNT,REC.ID,R.AZ.ACCOUNT,F.AZ.ACCOUNT,AZ.ACCOUNT.ERR)
        GOSUB READ.ACCOUNT.DET
        CUS.ID = R.ACCOUNT<AC.CUSTOMER>
        GOSUB PROCESS.AZ

    CASE CONTROL.LIST<1,1> EQ "SELECT.AC"
        GOSUB READ.ACCOUNT.DET
        CUS.ID = R.ACCOUNT<AC.CUSTOMER>
        IF NOT(CUS.ID) THEN RETURN
        CUS.OPENING.DATE = R.ACCOUNT<AC.OPENING.DATE>
        CCY.VAL = R.ACCOUNT<AC.CURRENCY>
        YCAT.ID = R.ACCOUNT<AC.CATEGORY>
        AC.OPEN.BAL = R.ACCOUNT<AC.OPEN.CLEARED.BAL>
        IF (AC.OPEN.BAL EQ 0 OR AC.OPEN.BAL EQ 0.00 OR AC.OPEN.BAL EQ '') THEN
            RETURN
        END
        LOCATE YCAT.ID IN CAT.LIST3<1> SETTING CAT.POSN ELSE
            RETURN
        END
        GOSUB PROCESS.AC

    CASE CONTROL.LIST<1,1> EQ "SELECT.AA"
        YTP.RECID = ''; R.AA.ARRANGEMENT = ''; AA.ARRANGEMENT.ERR = ''; STRT.DATE = ''; AMT.VAL = 0; YACCT.ID = ''
        CALL F.READ(FN.AA.ARRANGEMENT,REC.ID,R.AA.ARRANGEMENT,F.AA.ARRANGEMENT,AA.ARRANGEMENT.ERR)
        GOSUB PROCESS.AA

    END CASE
    IF YGRP.ARRY THEN
        CALL F.WRITE(FN.DR.REG.PASIVAS.ACTIV.DUP, YTP.REC, YGRP.ARRY)
    END
    RETURN

PROCESS.AC:
***********
    GOSUB GET.CUSTOMER.DET
    INT.RATE = ''; COMI = ''
    CALL DR.REP.AC.INT.CALC(REC.ID,R.ACCOUNT,INT.RATE)

    IF COMI EQ 2 THEN
        YTAS.PREF = 'N'
    END ELSE
        YTAS.PREF = 'S'
    END

    IF CUS.FAX.1 NE '' THEN
        YTAS.PREF = 'EM'
    END
    CUS.OPENING.DATE1 = CUS.OPENING.DATE[7,2]:'/':CUS.OPENING.DATE[5,2]:'/':CUS.OPENING.DATE[1,4]

    IF CUS.OPENING.DATE EQ LAST.WORK.DAY THEN
        YTP.REC = '225':CCY.VAL:INT.RATE:'-':REC.ID
        YGRP.ARRY = FMT(REC.ID,'L#25'):"|":FMT(CUS.OPENING.DATE1,'L#10'):"|":FMT('N','L#2'):"|2|25|00|":FMT('NA','L#3'):"|":FMT('NA','L#100'):"|":FMT('NA','L#15'):"|1|":FMT(CUS.OPENING.DATE1,'L#10'):"|":FMT(CUS.OPENING.DATE1,'L#10'):"|":FMT(CCY.VAL,'L#3'):"|":FMT(AC.OPEN.BAL,'R2%17'):"|000000|00|":FMT(INT.RATE,'R2%5'):"|":FMT('N','L#2'):"|NA|NA|1|0|":FMT('0','R2%5'):"|01|01|":FMT('NA','L#100'):"|":FMT('0','R2%10'):"|":FMT('0','R2%17'):"|0|0|":FMT('1','R%8'):"|":FMT(YFLD32,'L#2'):"|":FMT(YFLD33,'R%3'):"|"
    END ELSE
        YTP.REC = '225ACDUMMY':CCY.VAL:INT.RATE:'-':REC.ID
        YGRP.ARRY = FMT(REC.ID,'L#25'):"|":FMT(CUS.OPENING.DATE1,'L#10'):"|":FMT('N','L#2'):"|2|25|00|":FMT('NA','L#3'):"|":FMT('NA','L#100'):"|":FMT('NA','L#15'):"|1|":FMT(CUS.OPENING.DATE1,'L#10'):"|":FMT(CUS.OPENING.DATE1,'L#10'):"|":FMT(CCY.VAL,'L#3'):"|":FMT(AC.OPEN.BAL,'R2%17'):"|000000|00|":FMT(INT.RATE,'R2%5'):"|":FMT('N','L#2'):"|NA|NA|1|0|":FMT('0','R2%5'):"|01|01|":FMT('NA','L#100'):"|":FMT('0','R2%10'):"|":FMT('0','R2%17'):"|0|0|":FMT('1','R%8'):"|":FMT(YFLD32,'L#2'):"|":FMT(YFLD33,'R%3'):"|"

    END
    RETURN

PROCESS.AZ:
***********
    AZ.OPEN.BAL = 0
    GOSUB GET.CUSTOMER.DET
    CCY.VAL = R.AZ.ACCOUNT<AZ.CURRENCY>
    VAL.DATE = R.AZ.ACCOUNT<AZ.VALUE.DATE>
    CUS.ID = R.AZ.ACCOUNT<AZ.CUSTOMER>
    YCAT.ID = R.AZ.ACCOUNT<AZ.CATEGORY>
    YCREATE.DATE = R.AZ.ACCOUNT<AZ.CREATE.DATE>
    YROLL.DTE = R.AZ.ACCOUNT<AZ.ROLLOVER.DATE>
    YMATURITY.DATE = R.AZ.ACCOUNT<AZ.MATURITY.DATE>
    YINTEREST.RATE = R.AZ.ACCOUNT<AZ.ORIG.INTEREST.RATE>
    AMT.VAL = R.AZ.ACCOUNT<AZ.PRINCIPAL>
    YREINVST = R.AZ.ACCOUNT<AZ.LOCAL.REF,L.AZ.REIVSD.INT.POSN>
    YL.AZ.DEP.NAME = R.AZ.ACCOUNT<AZ.LOCAL.REF,L.AZ.DEP.NAME.POSN>
    YCO.CODE = R.AZ.ACCOUNT<AZ.CO.CODE>
    GOSUB GET.COMPANY
    AZ.OPEN.BAL = AMT.VAL + YREINVST
    YDATE = VAL.DATE
    YDATE1 = YMATURITY.DATE
    GOSUB DATE.30.CHK

    YMTH.INT = ''; YINT.NPV = ''
    Y.INTEREST.RATE = YINTEREST.RATE / 100
    YMTH.INT = Y.INTEREST.RATE / YYR.MTH
    YINT.NPV = ((1+YMTH.INT)^YYR.MTH) - 1
    YINT.NPV = ABS(YINT.NPV)
    YINT.NPV = YINT.NPV * 100

    GOSUB EVAL.PROD.INT.RATE

    IF YL.AP.ABB.DEPO EQ 'CF' THEN
        YSECTOR = '30'
    END
    IF YL.AP.ABB.DEPO EQ 'DP' THEN
        YSECTOR  = '31'
    END
    IF YCAT.ID EQ '6604' THEN
        YSECTOR = '33'
    END

    YTP.REC = '2':YSECTOR:CCY.VAL:'-':REC.ID
    YGRP.ARRY = REC.ID:"|":YCREATE.DATE:"|":VAL.DATE:"|":YCAT.ID:"|":YL.AZ.DEP.NAME:"||":CUS.FAX.1:"/":YCUS.SECTOR:"|":YCONTRA.PARTE:"|":YIDENT:"|":YTIPO:"|":CUS.DATE.OF.BIRTH:"/":CUS.LEGAL.DATE:"|":YMATURITY.DATE:"|":CCY.VAL:"|":FMT(AZ.OPEN.BAL,'R2%17'):"|":VAL.DATE:"/":YMATURITY.DATE:"|":VAL.DATE:"/":YMATURITY.DATE:"|":FMT(YINTEREST.RATE,'R2%5'):"|":CUS.FAX.1:"|TF|":VAL.DATE:"/":YMATURITY.DATE:"|NA|0|":FMT(YINT.NPV,'R2%5'):"|":YEB.COM.NAME.ADDRESS.4:"|":YEB.COM.NAME.ADDRESS.3:"|NA|":FMT('0','R2%10'):"|":FMT('0','R2%17'):"|0|0|":FMT('1','R%8'):"|":YCUS.SECTOR:"|":YFLD33:"|"
    RETURN

PROCESS.AA:
***********
    PROD.GRP = R.AA.ARRANGEMENT<AA.ARR.PRODUCT.GROUP>
    CCY.VAL = R.AA.ARRANGEMENT<AA.ARR.CURRENCY>
    CUS.ID = R.AA.ARRANGEMENT<AA.ARR.CUSTOMER>
    STRT.DATE = R.AA.ARRANGEMENT<AA.ARR.START.DATE>
    YACCT.ID = R.AA.ARRANGEMENT<AA.ARR.LINKED.APPL.ID>
    Y.PRODUCT = R.AA.ARRANGEMENT<AA.ARR.PRODUCT>

    GOSUB GET.CUSTOMER.DET

    idPropertyClass = "TERM.AMOUNT"
    idProperty = ''
    GOSUB ARR.CONDITIONS
    IF returnConditions THEN
        R.AA.TERM = RAISE(returnConditions)
        Y.MONTO = R.AA.TERM<AA.AMT.AMOUNT>
        Y.MAT.DATE = R.AA.TERM<AA.AMT.MATURITY.DATE>
        YTERM.LT = R.AA.TERM<AA.AMT.TERM>
        YTERM.LEN = LEN(YTERM.LT) - 1
        YTERM = YTERM.LT[1,YTERM.LEN]
        COLL.ID = R.AA.TERM<AA.AMT.LOCAL.REF,L.AA.COL.POSN,1>
    END

    Y.REGION = ''; Y.DAYS = ''
    IF Y.MAT.DATE AND STRT.DATE THEN
        YDATE = STRT.DATE
        YDATE1 = Y.MAT.DATE
        GOSUB DATE.30.CHK
    END

    idPropertyClass = "OVERDUE"
    idProperty = ''; YACT.OD.STATUS1 = ''
    GOSUB ARR.CONDITIONS
    IF returnConditions THEN
        R.AA.OVERDUE = RAISE(returnConditions)
        YACT.OD.STATUS1 = R.AA.OVERDUE<AA.OD.LOCAL.REF,OD.L.LOAN.STATUS1.POS>
    END

    idPropertyClass = "CUSTOMER"
    idProperty = ''
    GOSUB ARR.CONDITIONS
    IF returnConditions THEN
        R.AA.CUSTOMER = RAISE(returnConditions)
        YL.AA.CAMP.TY = R.AA.CUSTOMER<AA.CUS.LOCAL.REF,L.AA.CAMP.TY.POS>
    END

    idPropertyClass = "ACCOUNT"
    idProperty = ''; Y.AA.LOAN = ''
    GOSUB ARR.CONDITIONS
    IF returnConditions THEN
        R.AA.ACCOUNT = RAISE(returnConditions)
        Y.AA.LOAN = R.AA.ACCOUNT<AA.AC.LOCAL.REF,L.AA.LOAN.DSN.POS>
        YCATEGORY = R.AA.ACCOUNT<AA.AC.CATEGORY>
    END

    idPropertyClass = 'INTEREST'
    idProperty = 'PRINCIPALINT'
    GOSUB ARR.CONDITIONS
    IF returnConditions THEN
        R.AA.INTEREST = RAISE(returnConditions)
        YRV.FREQ = R.AA.INTEREST<AA.INT.LOCAL.REF,L.AA.RT.RV.FREQ.POSN>
        Y.EFC.RATE = R.AA.INTEREST<AA.INT.EFFECTIVE.RATE>
    END

    BEGIN CASE
    CASE PROD.GRP EQ 'COMERCIAL'
        YSECTOR = '36'
    CASE PROD.GRP EQ 'CONSUMO'
        YSECTOR = '37'
    CASE PROD.GRP EQ 'HIPOTECARIO'
        YSECTOR = '38'
    CASE PROD.GRP EQ 'LINEAS.DE.CREDITO'
        FINDSTR "COM" IN Y.PRODUCT SETTING YFM,YSM,YVM THEN
            YSECTOR = "36"
        END
        FINDSTR "CONS" IN Y.PRODUCT SETTING YFM,YSM,YVM THEN
            YSECTOR = "37"
        END
    END CASE

    GOSUB GET.NPV.CALC

    IF COLL.ID EQ '' THEN
        GOSUB COLL.GET.DETAILS
    END

    COLLATERAL.ERR = ''; R.COLLATERAL = ''; YTPO.EFIC = ''; YTIPO.STAT = ''
    YL.COL.BLD.AREA = ''; YL.COL.TOT.VALUA = ''; YL.COL.DEP.VALUE = ''; YCOL.ADDR = ''
    CALL F.READ(FN.COLLATERAL,COLL.ID,R.COLLATERAL,F.COLLATERAL,COLLATERAL.ERR)
    YCOLT.TYPE = R.COLLATERAL<COLL.COLLATERAL.TYPE>
    YCOL.ADDR = R.COLLATERAL<COLL.ADDRESS>
    IF NOT(YCOL.ADDR) THEN
        YCOL.ADDR = 'NA'
    END
    IF YCOL.ADDR THEN
        CHANGE SM TO ' ' IN YCOL.ADDR
        CHANGE VM TO ' ' IN YCOL.ADDR
    END

    YL.COL.BLD.AREA = R.COLLATERAL<COLL.LOCAL.REF,L.COL.BLD.AREA.POSN>
    YL.COL.TOT.VALUA = R.COLLATERAL<COLL.LOCAL.REF,L.COL.TOT.VALUA.POSN>
    YL.COL.DEP.VALUE = R.COLLATERAL<COLL.LOCAL.REF,L.COL.DEP.VALUE.POSN>
    CHANGE SM TO ' ' IN YL.COL.BLD.AREA
    CHANGE VM TO ' ' IN YL.COL.BLD.AREA

    YTP.REC = '1':YSECTOR:CCY.VAL:'-':REC.ID
    YGRP.ARRY = YACCT.ID:"|":STRT.DATE:"|":YACT.OD.STATUS1:"|":YCATEGORY:"|":PROD.GRP:"|":Y.AA.LOAN:"|":CUS.FAX.1:"/":YCUS.SECTOR:"/":YL.TIP.CLI:"|":YCONTRA.PARTE:"|":FMT(YIDENT,'L#15'):"|":YTIPO:"|":YCUS.DATE.OF.BIRTH:"|":Y.MAT.DATE:"|":CCY.VAL:"|":FMT(Y.MONTO,'R2%17'):"|":YTERM.LT:"|":STRT.DATE:"/":Y.MAT.DATE:"|":FMT(Y.EFC.RATE,'R2%5'):"|":YL.AA.CAMP.TY:"|TF|":YRV.FREQ:"|1|0|":FMT(YINT.NPV,'R2%5'):"|":YEB.COM.NAME.ADDRESS.4:"|":YCO.CODE:"|":YCOL.ADDR:"|":YL.COL.BLD.AREA:"|":FMT(YL.COL.TOT.VALUA,'R2%17'):"|":YL.COL.DEP.VALUE:"/":Y.AA.LOAN:"|":YCOLT.TYPE:"|":FMT('1','R%8'):"|":YCUS.SECTOR:"|":YFLD33:"|"
    RETURN

GET.NPV.CALC:
**************
    SIMULATION.REF = ''; NO.RESET = ''; TOT.PAYMENT = ''; DUE.DATES = ''; DUE.TYPES = ''; DUE.DEFER.DATES = '' ; DATE.RANGE = ''  ; yTOT.PAYMENT = 0
    DUE.METHODS = ''; DUE.TYPE.AMTS = ''; DUE.PROPS = ''; DUE.PROP.AMTS = ''; DUE.OUTS = ''; BALANCES.TO.CHECK = ''; TOT.BALANCES = '' ; TOTAL.REVENU  = 0
    CALL AA.SCHEDULE.PROJECTOR(REC.ID, SIMULATION.REF, NO.RESET, DATE.RANGE, TOT.PAYMENT, DUE.DATES, DUE.DEFER.DATES, DUE.TYPES, DUE.METHODS,DUE.TYPE.AMTS, DUE.PROPS, DUE.PROP.AMTS, DUE.OUTS)
    yTOT.PAYMENT = SUM(TOT.PAYMENT)

    Y.IDS.DETAILS<1> = REC.ID
    Y.IDS.DETAILS<2> = "YES"
    CALL REDO.GET.DISBURSEMENT.DETAILS(Y.IDS.DETAILS,R.DISB.DETAILS,Y.COMMITED.AMT,Y.PEND.DISB)
    Y.TOT.DIS.AMT = ABS(R.DISB.DETAILS<3>)
    TOTAL.REVENU =  Y.TOT.DIS.AMT - yTOT.PAYMENT

    TOT.IRR = ''; YINT.NPV = ''
    YEFC.RATE = ''; YEFC.RATE = Y.EFC.RATE / YYR.MTH
    YINT.NPV = (ABS(TOTAL.REVENU) / ABS(Y.TOT.DIS.AMT) * 10)
    YINT.NPV = YINT.NPV + Y.EFC.RATE
    RETURN

GET.COMPANY:
************
    ERR.COMPANY = ''; RV.COMPANY = ''; YEB.COM.NAME.ADDRESS.3 = ''
    YEB.COM.NAME.ADDRESS.4 = ''
    CALL F.READ(FN.COMPANY,YCO.CODE,RV.COMPANY,F.COMPANY,ERR.COMPANY)
    YEB.COM.NAME.ADDRESS.3 = RV.COMPANY<EB.COM.NAME.ADDRESS,3>
    YEB.COM.NAME.ADDRESS.4 = RV.COMPANY<EB.COM.NAME.ADDRESS,4>
    RETURN

ARR.CONDITIONS:
***************
    ArrangementID = REC.ID ; effectiveDate = LAST.WORK.DAY; returnIds = ''; R.CONDITION =''; returnConditions = ''; returnError = ''
    CALL AA.GET.ARRANGEMENT.CONDITIONS(ArrangementID, idPropertyClass, idProperty, effectiveDate, returnIds, returnConditions, returnError)
    RETURN

COLL.GET.DETAILS:
*****************
    REQD.MODE = ''; EFF.DATE = STRT.DATE; R.AA.ACTIVITY.HISTORY = ''; YACT.DTE.ID = ''; ID.COM3 = ''
    CALL AA.READ.ACTIVITY.HISTORY(REC.ID, REQD.MODE, EFF.DATE, R.AA.ACTIVITY.HISTORY)
    IF R.AA.ACTIVITY.HISTORY THEN
        YACT.ID.ARR = R.AA.ACTIVITY.HISTORY<AA.AH.ACTIVITY.ID>
    END
    YACT.DTE.ID = ''
    LOCATE "LENDING-TAKEOVER-ARRANGEMENT" IN YACT.ID.ARR<1,1> SETTING CHG.POSN.1 THEN
        YACT.DTE.ID = R.AA.ACTIVITY.HISTORY<AA.AH.ACT.DATE,CHG.POSN.1,1>
        TERM.AMT.ID = REC.ID:'-TERM.AMOUNT-':YACT.DTE.ID:'.1'
        GOSUB READ.TERM.AMT
    END ELSE

        YACT.DTE.ID = ''
        LOCATE "LENDING-NEW-ARRANGEMENT" IN YACT.ID.ARR<1,1> SETTING CHG.POSN.2 THEN
            YACT.DTE.ID = R.AA.ACTIVITY.HISTORY<AA.AH.ACT.DATE,CHG.POSN.2,1>
            TERM.AMT.ID = REC.ID:'-TERM.AMOUNT-':YACT.DTE.ID:'.1'
            GOSUB READ.TERM.AMT
        END
    END
    COLL.ID = R.AA.TERM.AMOUNT<AA.AMT.LOCAL.REF,L.AA.COL.POSN,1>
    RETURN

READ.TERM.AMT:
**************
    AA.ARR.TERM.AMOUNT.ERR = ''; R.AA.TERM.AMOUNT = ''
    CALL F.READ(FN.AA.ARR.TERM.AMOUNT,TERM.AMT.ID,R.AA.TERM.AMOUNT,F.AA.ARR.TERM.AMOUNT,AA.ARR.TERM.AMOUNT.ERR)
    RETURN

GET.CUSTOMER.DET:
*****************
    R.CUSTOMER = ''; ERR.CUSTOMER = ''; YLEGAL.ISS.DATE = ''; YCUS.INDUT = ''; YCUS.SECTOR = ''
    YFLD32 = ''; YFLD33 = ''; L.TIP.CLI = ''
    CALL F.READ(FN.CUSTOMER,CUS.ID,R.CUSTOMER,F.CUSTOMER,ERR.CUSTOMER)
    CU.TIPO.CL = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.TIPO.CL.POSN>
    YCUS.CIDENT = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.CIDENT.POSN>
    YCUS.RNC = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.RNC.POSN>
    YCUS.FOREIGN = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.PASS.NAT.POSN>
    Y.L.CU.ACTANAC = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.ACTANAC.POSN>
    Y.L.CU.NOUNICO = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.NOUNICO.POSN>
    CUS.GIVEN.NAMES = R.CUSTOMER<EB.CUS.GIVEN.NAMES>
    CUS.FAMILY.NAME = R.CUSTOMER<EB.CUS.FAMILY.NAME>
    CUS.TEXT = R.CUSTOMER<EB.CUS.TEXT>
    CUS.NAME.1 = R.CUSTOMER<EB.CUS.NAME.1>
    CUS.NAME.2 = R.CUSTOMER<EB.CUS.NAME.2>
    CUS.FAX.1 = R.CUSTOMER<EB.CUS.FAX.1>
    YL.LOCALIDAD = R.CUSTOMER<EB.CUS.LOCAL.REF,L.LOCALIDAD.POSN>
    YCUS.INDUT = R.CUSTOMER<EB.CUS.LOCAL.REF,L.APAP.INDUSTRY.POSN>
    YCO.CODE = R.CUSTOMER<EB.CUS.CO.CODE>
    YCUS.SECTOR = R.CUSTOMER<EB.CUS.SECTOR>
    CUS.LEGAL.DATE = R.CUSTOMER<EB.CUS.LEGAL.ISS.DATE>
    CUS.DATE.OF.BIRTH = R.CUSTOMER<EB.CUS.DATE.OF.BIRTH>
    YL.TIP.CLI = R.CUSTOMER<EB.CUS.LOCAL.REF,L.TIP.CLI.POSN>

    IF CU.TIPO.CL EQ 'PERSONA JURIDICA' THEN
        YCONTRA.PARTE = CUS.NAME.1:' ':CUS.NAME.2
        YCUS.DATE.OF.BIRTH = CUS.LEGAL.DATE
    END ELSE
        YCONTRA.PARTE = CUS.GIVEN.NAMES:' ':CUS.FAMILY.NAME
        YCUS.DATE.OF.BIRTH = CUS.DATE.OF.BIRTH
    END

    BEGIN CASE
    CASE YCUS.CIDENT NE ''
        YIDENT = YCUS.CIDENT
        YTIPO = "L.CU.CIDENT"
    CASE YCUS.RNC NE ''
        YIDENT = YCUS.RNC
        YTIPO = "L.CU.RNC"
        GOSUB READ.INST.PARAM
    CASE Y.L.CU.ACTANAC NE ''
        YIDENT = Y.L.CU.ACTANAC
        YTIPO = "L.CU.ACTNAC"
    CASE Y.L.CU.NOUNICO NE ''
        YIDENT = Y.L.CU.NOUNICO
        YTIPO = "L.CU.NOUNICO"
    CASE YCUS.FOREIGN NE ''
        YIDENT = YCUS.FOREIGN
        YTIPO = "L.CU.PASS.NAT"
    END CASE
    RETURN

READ.ACCOUNT.DET:
*****************
    R.ACCOUNT = ''; ACCOUNT.ERR = ''
    CALL F.READ(FN.ACCOUNT,REC.ID,R.ACCOUNT,F.ACCOUNT,ACCOUNT.ERR)
    RETURN

READ.INST.PARAM:
****************
    R.REDO.APAP.INSTIT.FINANC.PARAM = ''; ERR.REDO.APAP.INSTIT.FINANC.PARAM = ''
    CALL F.READ(FN.REDO.APAP.INSTIT.FINANC.PARAM,YCUS.RNC,R.REDO.APAP.INSTIT.FINANC.PARAM,F.REDO.APAP.INSTIT.FINANC.PARAM,ERR.REDO.APAP.INSTIT.FINANC.PARAM)
    IF R.REDO.APAP.INSTIT.FINANC.PARAM THEN
        YFLD32 = R.REDO.APAP.INSTIT.FINANC.PARAM<INST.FIN.TIPO.INSTITUTION>
        YFLD33 = R.REDO.APAP.INSTIT.FINANC.PARAM<INST.FIN.NUMERO.INSTITUTION>
    END
    RETURN

DATE.30.CHK:
*************
    YACT.MONTH = 0; Y.NO.OF.MONTHS = 0; Y.MNTH = ''; Y.DAYS = ''; Y.GDAYS = ''
    TMP.YDATE = ''; TMP.YDATE1 = ''; Y.YEAR = 0
    IF LEN(YDATE) <> 8 OR LEN(YDATE1) <> 8 THEN
        Y.DAYS = 0
        RETURN
    END
    IF YDATE[1,4] > YDATE1[1,4] THEN
        TMP.YDATE1 = YDATE1
        TMP.YDATE = YDATE
        YDATE1 = TMP.YDATE
        YDATE = TMP.YDATE1
    END
    Y.GDAYS = 'C'
    CALL EB.NO.OF.MONTHS(YDATE,YDATE1,Y.NO.OF.MONTHS)
    Y.MNTH = Y.NO.OF.MONTHS:'M'
    CALL CALENDAR.DAY(YDATE,'+',Y.MNTH)
    CALL CDD('',Y.MNTH,YDATE1,Y.GDAYS)
    IF Y.NO.OF.MONTHS THEN
        YACT.MONTH = Y.NO.OF.MONTHS * 30
    END
    Y.DAYS =  YACT.MONTH + Y.GDAYS
    Y.YEAR = Y.NO.OF.MONTHS/12
    TERM.IN.DAYS = ABS(Y.DAYS)
    RETURN

EVAL.PROD.INT.RATE:
*******************
    PROD.INT.RATE = "" ; RTE.SPR = ''; PI.KEY = ''; COND.GRP = '';GD.ID = ''
    CAT.VAL = R.AZ.ACCOUNT<AZ.ALL.IN.ONE.PRODUCT>
    COND.GRP = R.ACCOUNT<AC.CONDITION.GROUP>
    GD.ID = COND.GRP:CCY.VAL
    GOSUB READ.GROUP.DTE
    R.AZ.PRODUCT.PARAMETER = ''; AZ.PRODUCT.PARAMETER.ERR = ''; FIX.RATE = ''; RTE.KEY = ''; RTE.OPR = ''; YL.AP.ABB.DEPO = ''
    CALL F.READ(FN.AZ.PRODUCT.PARAMETER,CAT.VAL,R.AZ.PRODUCT.PARAMETER,F.AZ.PRODUCT.PARAMETER,AZ.PRODUCT.PARAMETER.ERR)
    FIX.RATE = R.AZ.PRODUCT.PARAMETER<AZ.APP.INT.FIXED.RATE>
    RTE.KEY = R.AZ.PRODUCT.PARAMETER<AZ.APP.RATE.KEY>
    RTE.OPR = R.AZ.PRODUCT.PARAMETER<AZ.APP.RATE.OPERAND>
    RTE.SPR = R.AZ.PRODUCT.PARAMETER<AZ.APP.RATE.SPREAD>
    PI.KEY = R.AZ.PRODUCT.PARAMETER<AZ.APP.PERIODIC.RATE.KEY>
    YL.AP.ABB.DEPO = R.AZ.PRODUCT.PARAMETER<AZ.APP.LOCAL.REF,L.AP.ABB.DEPO.POSN>

    BEGIN CASE
    CASE FIX.RATE
        PROD.INT.RATE = FIX.RATE

    CASE RTE.KEY
        KEY.PART = ''; TABLE.SUFFIX = ''; BI.ID = ''
        KEY.PART = RTE.KEY:CCY.VAL
        ID.LIST = dasBasicInterestIdLikeByDsndId
        THE.ARGS = KEY.PART:'...'
        CALL DAS ('BASIC.INTEREST', ID.LIST, THE.ARGS, TABLE.SUFFIX)
        BI.ID = ID.LIST<1>
        GOSUB READ.BASIC.INT
        BEGIN CASE
        CASE RTE.OPR EQ 'ADD'
            PROD.INT.RATE = BI.INT.RATE + RTE.SPR
        CASE RTE.OPR EQ 'SUBTRACT'
            PROD.INT.RATE = BI.INT.RATE - RTE.SPR
        CASE 1
            PROD.INT.RATE = BI.INT.RATE
        END CASE

    CASE PI.KEY
        PI.ID = ''; OFF.RATE = ''; CNT.OFF.RATE = ''; REST.PER = ''
        PI.ID = PI.KEY:CCY.VAL:LAST.WORK.DAY
        GOSUB READ.PERD.INT
        IF NOT(R.PERIODIC.INTEREST) THEN
            PI.ID = PI.KEY:CCY.VAL:GRP.CR.DATE
            GOSUB READ.PERD.INT
        END
        REST.PER = R.PERIODIC.INTEREST<PI.REST.PERIOD>
        REST.POS = ''
        LOCATE 'R' IN REST.PER<1,1> SETTING REST.POS THEN
            OFF.RATE = R.PERIODIC.INTEREST<PI.OFFER.RATE,REST.POS>
            CNT.OFF.RATE = DCOUNT(OFF.RATE,SM)
            PROD.INT.RATE = R.PERIODIC.INTEREST<PI.OFFER.RATE,REST.POS,CNT.OFF.RATE>
        END
    END CASE
    RETURN

READ.PERD.INT:
**************
    R.PERIODIC.INTEREST = ''; PERIODIC.INTEREST.ERR = ''
    CALL F.READ(FN.PERIODIC.INTEREST,PI.ID,R.PERIODIC.INTEREST,F.PERIODIC.INTEREST,PERIODIC.INTEREST.ERR)
    RETURN

READ.GROUP.DTE:
***************
    R.GROUP.DATE = ''; GROUP.DATE.ERR = ''; GRP.CR.DATE = ''
    CALL F.READ(FN.GROUP.DATE,GD.ID,R.GROUP.DATE,F.GROUP.DATE,GROUP.DATE.ERR)
    GRP.CR.DATE = R.GROUP.DATE<AC.GRD.CREDIT.GROUP.DATE>
    RETURN

READ.BASIC.INT:
***************
    R.BASIC.INTEREST = ''; BASIC.INTEREST.ERR = ''; BI.INT.RATE = ''
    CALL F.READ(FN.BASIC.INTEREST,BI.ID,R.BASIC.INTEREST,F.BASIC.INTEREST,BASIC.INTEREST.ERR)
    BI.INT.RATE = R.BASIC.INTEREST<EB.BIN.INTEREST.RATE>
    RETURN

END
