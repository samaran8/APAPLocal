*-----------------------------------------------------------------------------
* <Rating>70</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE LAPAP.CHARGE.MIN.BAL(Y.CUSTOMER.ID)

    $INSERT T24.BP I_COMMON
    $INSERT T24.BP I_EQUATE
    $INSERT T24.BP I_F.CUSTOMER
    $INSERT T24.BP I_F.CUSTOMER.ACCOUNT
    $INSERT T24.BP I_TSA.COMMON
    $INSERT T24.BP I_AA.LOCAL.COMMON
    $INSERT T24.BP I_F.COMPANY
    $INSERT T24.BP I_F.ACCOUNT
    $INSERT T24.BP I_F.DATES
    $INSERT T24.BP I_F.AC.CHARGE.REQUEST
    $INCLUDE BP I_F.LAPAP.CHARGE.BAL.MIN.PARAM
    $INSERT LAPAP.BP I_LAPAP.CHARGE.MIN.BAL.COMMON

    GOSUB MAIN
    RETURN

*****
MAIN:
*****
    R.CUSTOMER = ''; CUSTOMER.ERR = ''
    CALL F.READ(FN.CUSTOMER,Y.CUSTOMER.ID,R.CUSTOMER,F.CUSTOMER,CUSTOMER.ERR)
    CUS.SEG.POS= '';
    CALL GET.LOC.REF("CUSTOMER","L.CU.SEGMENTO",CUS.SEG.POS)
    Y.SEGMENTO              = R.CUSTOMER<EB.CUS.LOCAL.REF,CUS.SEG.POS>

    GOSUB READ.CUSTOMER.ACCOUNT

    RETURN

**********************
READ.CUSTOMER.ACCOUNT:
**********************

    R.CUSTOMER.ACC = ''; CUSTOMER.ACC.ERR = ''
    CALL F.READ(FN.CUSTOMER.ACCOUNT,Y.CUSTOMER.ID,R.CUSTOMER.ACC,F.CUSTOMER.ACCOUNT,CUSTOMER.ACC.ERR)

    Y.ACC.NUM                = R.CUSTOMER.ACC
    Y.ACC.NUM.COUNT          = DCOUNT(Y.ACC.NUM,FM)

    Y.APLICA        = '';
    i = 0;
    FOR i = 1 TO Y.ACC.NUM.COUNT
        CRT Y.ACC.NUM<i>
        Y.ACCOUNT.NO           = Y.ACC.NUM<i>

        R.ACCOUNT =''; ACCOUNT.ERR = ''
        CALL F.READ(FN.ACCOUNT, Y.ACCOUNT.NO, R.ACCOUNT,F.ACCOUNT,ACCOUNT.ERR)
        Y.ARRANGEMENT.ID    = R.ACCOUNT<AC.ARRANGEMENT.ID>
        Y.CATEGORY          = R.ACCOUNT<AC.CATEGORY>
        Y.START.BAL         = R.ACCOUNT<AC.START.YEAR.BAL>

        ACC.STATUS.POS  =  ""
        CALL GET.LOC.REF("ACCOUNT","L.AC.STATUS",ACC.STATUS.POS)
        Y.STATUS              = R.ACCOUNT<AC.LOCAL.REF,ACC.STATUS.POS>

        FINDSTR Y.STATUS IN Y.STATUS.COM SETTING V.FLD, V.VAL THEN
            IF Y.CATEGORY GE "6000" AND Y.CATEGORY LE "6012" OR Y.CATEGORY GE "6020" AND Y.CATEGORY LE "6599" THEN
                IF Y.CATEGORY EQ "6023" OR Y.ARRANGEMENT.ID NE "" THEN
                    Y.APLICA        = 'FALSE'
                    CALL OCOMO("ACCOUNT DOES NOT APPLIES FOR PROCESSING, ACC. NO.: " : Y.ACCOUNT.NO :" FOR CATEGORY 6023")
                END ELSE
                    Y.APLICA            = "TRUE"
                    CALL OCOMO("ACCOUNT APPLIES FOR PROCESSING, ACC. NO.: " : Y.ACCOUNT.NO)
                    GOSUB MIN.PROCESS
                END
            END
        END ELSE
            CALL OCOMO("STATUS NOT FOUND IN ARRAY FOR ACC. NO.:": Y.ACCOUNT.NO)
        END

    NEXT i
    RETURN

*************
MIN.PROCESS:
*************

    IF Y.APLICA EQ "TRUE" THEN
        R.CHARGE.PARAM =''; CHARGE.PARAM.ERR = ''
        CALL F.READ(FN.ST.LAPAP.CHARGE.BAL.MIN.PARAM,Y.SEGMENTO,R.CHARGE.PARAM,F.ST.L.APAP.CHARGE.BAL.MIN.PARAM,CHARGE.PARAM.ERR)
        IF NOT (R.CHARGE.PARAM) THEN
        END

        Y.BALANCES.MIN = R.CHARGE.PARAM<ST.LAP33.BALANCE.PROMEDIO>
        Y.CHARGE.CODE = R.CHARGE.PARAM<ST.LAP33.CODIGO.CARGO>

        Y.AVG.BAL = ''
        CALL LAPAP.ACCT.AVG.AMT.RT(Y.ACCOUNT.NO,Y.YEAR.ACT,Y.MES.ACT,Y.AVG.BAL)

        IF Y.AVG.BAL LT Y.BALANCES.MIN THEN
            CALL OCOMO("ACCOUNT APPLIES FOR CHARGE, ACC. NO.: " : Y.ACCOUNT.NO)
            Y.VER.NAME                          = 'AC.CHARGE.REQUEST,MIN.BAL';
            Y.APP.NAME                          = 'AC.CHARGE.REQUEST';
            Y.FUNC                              = 'I';
            Y.PRO.VAL                           = "PROCESS";
            Y.GTS.CONTROL                       = "";
            Y.NO.OF.AUTH                        = "";
            FINAL.OFS                           = "";
            Y.TRANS.ID                          = "";

            R.CHARGE<CHG.CHARGE.CODE>           = Y.CHARGE.CODE
            R.CHARGE<CHG.DEBIT.ACCOUNT>         = Y.ACCOUNT.NO

            CALL OFS.BUILD.RECORD(Y.APP.NAME,Y.FUNC,Y.PRO.VAL,Y.VER.NAME,Y.GTS.CONTROL,Y.NO.OF.AUTH,Y.TRANS.ID,R.CHARGE,FINAL.OFS)

            OFS.MSG.ID = ''; OFS.SOURCE.ID = "DM.OFS.SRC.VAL"; OPTIONS = ''
            CALL OFS.POST.MESSAGE(FINAL.OFS,OFS.MSG.ID,OFS.SOURCE.ID,OPTIONS)
            CALL JOURNAL.UPDATE(Y.CARD.REQUEST.ID)
        END
    END
    RETURN
END
