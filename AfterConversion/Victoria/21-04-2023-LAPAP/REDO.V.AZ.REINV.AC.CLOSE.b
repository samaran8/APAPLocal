$PACKAGE APAP.LAPAP
SUBROUTINE REDO.V.AZ.REINV.AC.CLOSE
*
* Description: This auth routine is attached to the version- 'AZ.ACCOUNT,NOR.PRECLOSURE.AUTH'
*              to close the non-reinvested deposit base account.
* Dev By     : V.P.Ashokkumar
******************************************************************************
*MODIFICATION HISTORY:
*
*DATE           WHO                 REFERENCE               DESCRIPTION
*21-APR-2023    CONVERSION TOOL     R22 AUTO CONVERSION     INSERT FILE MODIFIED
*21-APR-2023    VICTORIA S          R22 MANUAL CONVERSION   NO CHANGE
*21-APR-2023    CONVERSION TOOL     R22 AUTO CONVERSION     FM TO @FM,VM TO @VM
*-----------------------------------------------------------------------------
    $INSERT I_COMMON ;*R22 AUTO CONVERSION START
    $INSERT I_EQUATE
    $INSERT I_F.ACCOUNT
    $INSERT I_F.AZ.ACCOUNT
    $INSERT I_F.ACCOUNT.CLOSURE ;*R22 AUTO CONVERSION END


    GOSUB INIT
    GOSUB PROCESS
RETURN

INIT:
*****
    FN.ACCOUNT = 'F.ACCOUNT'; F.ACCOUNT = ''
    CALL OPF(FN.ACCOUNT,F.ACCOUNT)
    FN.ACCOUNT.H = 'F.ACCOUNT$HIS'; F.ACCOUNT.H = ''
    CALL OPF(FN.ACCOUNT.H,F.ACCOUNT.H)
    FN.AZ.ACCOUNT = 'F.AZ.ACCOUNT'; F.AZ.ACCOUNT = ''
    CALL OPF(FN.AZ.ACCOUNT,F.AZ.ACCOUNT)
    FN.AZ.ACCOUNT.H = 'F.AZ.ACCOUNT$HIS'; F.AZ.ACCOUNT.H = ''
    CALL OPF(FN.AZ.ACCOUNT.H,F.AZ.ACCOUNT.H)

    LOC.REF.APP = 'ACCOUNT.CLOSURE':@FM:'AZ.ACCOUNT':@FM:'ACCOUNT' ;*R22 AUTO CONVERSION
    LOC.REF.FIELD = 'L.AC.AZ.ACC.REF':@VM:'L.AC.CAN.REASON':@VM:'L.AC.OTH.REASON':@FM:'L.AC.CAN.REASON':@VM:'L.AC.OTH.REASON':@FM:'L.AC.AZ.ACC.REF':@VM:'L.AC.REINVESTED' ;*R22 AUTO CONVERSION
    YLOC.REF = ''
    CALL MULTI.GET.LOC.REF(LOC.REF.APP,LOC.REF.FIELD,YLOC.REF)
    POS.AZ.ACC.REF.CLOSE    = YLOC.REF<1,1>
    POS.AC.CAN.REASON.CLOSE = YLOC.REF<1,2>
    POS.AC.OTH.REASON.CLOSE = YLOC.REF<1,3>
    POS.AC.CAN.REASON.AZ    = YLOC.REF<2,1>
    POS.AC.OTH.REASON.AZ    = YLOC.REF<2,2>
    POS.AZ.ACC.REF          = YLOC.REF<3,1>
    L.AC.REINVESTED.POS     = YLOC.REF<3,2>

    TOD.DATE = TODAY
    AZ.OFS.SOURCE = 'ACCT.REINV.OFS'
RETURN

PROCESS:
********
    YL.AC.AZ.ACC.REF = ''; YCATEG = ''; YWORKBAL = ''
    YOFS.COMPANY = ''; YREINV.VAL = ''; YFLG = 0
    ACCT.ID = ID.NEW
    GOSUB READ.ACCOUNT
    YCATEG = R.ACCOUNT<AC.CATEGORY>
    IF (YCATEG GE '6013' AND YCATEG LE '6020') THEN
        AZ.ACCT.ID = R.NEW(AC.ACL.LOCAL.REF)<1,POS.AZ.ACC.REF.CLOSE>
        IF NOT(AZ.ACCT.ID) THEN
            RETURN
        END
        GOSUB READ.AZ.ACCT
        IF R.AZ.ACCOUNT THEN
            RETURN
        END
        YL.AC.AZ.ACC.REF.H = AZ.ACCT.ID
        R.AZ.ACCOUNT = ''; AZ.ERR = ''
        CALL EB.READ.HISTORY.REC(F.AZ.ACCOUNT.H,YL.AC.AZ.ACC.REF.H,R.AZ.ACCOUNT,AZ.ERR)
        YOFS.COMPANY = R.ACCOUNT<AZ.CO.CODE>
        YREINV.VAL = R.ACCOUNT<AC.LOCAL.REF,L.AC.REINVESTED.POS>
        TRANSACTION.ID1 = AZ.ACCT.ID
        GOSUB OFS.PROCESS
    END
RETURN

OFS.PROCESS:
************
* Capital date should be set for account with balance.
    R.ACCOUNT = ''; ACCT.ERR = ''; YWORKBAL =''
    CALL F.READ(FN.ACCOUNT,AZ.ACCT.ID,R.ACCOUNT,F.ACCOUNT,ACCT.ERR)
    YWORKBAL = R.ACCOUNT<AC.WORKING.BALANCE>
    IF YWORKBAL AND YWORKBAL NE 0 THEN
        R.ACCOUNT.CLOSURE<AC.ACL.CAPITAL.DATE> = TODAY
    END
    IF R.AZ.ACCOUNT<AZ.LOCAL.REF,POS.AC.CAN.REASON.AZ> THEN
        R.ACCOUNT.CLOSURE<AC.ACL.LOCAL.REF,POS.AC.CAN.REASON.CLOSE> = R.AZ.ACCOUNT<AZ.LOCAL.REF,POS.AC.CAN.REASON.AZ>
    END
    IF R.AZ.ACCOUNT<AZ.LOCAL.REF,POS.AC.OTH.REASON.AZ> THEN
        R.ACCOUNT.CLOSURE<AC.ACL.LOCAL.REF,POS.AC.OTH.REASON.CLOSE> = R.AZ.ACCOUNT<AZ.LOCAL.REF,POS.AC.OTH.REASON.AZ>
    END
    R.ACCOUNT.CLOSURE<AC.ACL.POSTING.RESTRICT> = '90'
    R.ACCOUNT.CLOSURE<AC.ACL.CLOSE.MODE> = 'AUTO'
    R.ACCOUNT.CLOSURE<AC.ACL.CLOSE.ONLINE> = 'Y'
    R.ACCOUNT.CLOSURE<AC.ACL.LOCAL.REF,POS.AZ.ACC.REF.CLOSE> = AZ.ACCT.ID

    APPLICATION.NAME = 'ACCOUNT.CLOSURE'
    OFS.FUNCTION1 = 'I'
    PROCESS1 = 'PROCESS'
    OFS.VERSION1 = ''
    GTSMODE1 = ''
    NO.OF.AUTH1 = '0'
    OFS.RECORD1 = ''
    VERSION1 = 'ACCOUNT.CLOSURE,REDO.TELLER'
    MSG.ID1 = ''
    OPTION1 = ''
    CALL OFS.BUILD.RECORD(APPLICATION.NAME,OFS.FUNCTION1,PROCESS1,VERSION1,GTSMODE1,NO.OF.AUTH1,TRANSACTION.ID1,R.ACCOUNT.CLOSURE,OFS.ACC)
    MSG.ID = ''; ERR.OFS = ''
    CALL OFS.POST.MESSAGE(OFS.ACC,MSG.ID,AZ.OFS.SOURCE,ERR.OFS)
RETURN

READ.ACCOUNT:
*************
    ACCT.ERR = ''; R.ACCOUNT = ''; AC.ERR = ''
    CALL F.READ(FN.ACCOUNT,ACCT.ID,R.ACCOUNT,F.ACCOUNT,ACCT.ERR)
    IF NOT(R.ACCOUNT) THEN
        ACCT.ID.H = ACCT.ID
        CALL EB.READ.HISTORY.REC(F.ACCOUNT.H,ACCT.ID.H,R.ACCOUNT,AC.ERR)
    END
RETURN

READ.AZ.ACCT:
*************
    AZ.ERR = ''; R.AZ.ACCOUNT = ''
    CALL F.READ(FN.AZ.ACCOUNT,AZ.ACCT.ID,R.AZ.ACCOUNT,F.AZ.ACCOUNT,AZ.ERR)
RETURN
END
