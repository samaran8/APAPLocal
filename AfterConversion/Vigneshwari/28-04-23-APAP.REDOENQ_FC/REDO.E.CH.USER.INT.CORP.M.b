$PACKAGE APAP.REDOENQ
SUBROUTINE REDO.E.CH.USER.INT.CORP.M(R.DATA)

*-----------------------------------------------------------------------------
*DESCRIPTION:
*------------
* This is an Nofile routine to get some data for enquiry REDO.E.CH.USER.INT.CORP.M
* related to corporate channel users
*
* Input/Output:
*--------------
* IN : USER.ID / CUSTOMER.ID
* OUT : R.DATA (ALL DATA)
*---------------
*-----------------------------------------------------------------------------
* Modification History :
* Date Who Reference Description
* 04-JUL-2012 RMONDRAGON ODR-2011-06-0243 FIRST VERSION
* 26-JUL-2012 RMONDRAGON ODR-2011-06-0243 SECOND VERSION
* 20-MAR-2013 RMONDRAGON ODR-2011-06-0243 THIRD VERSION
*  DATE             WHO                   REFERENCE                  
* 10-APRIL-2023      Conversion Tool       R22 Auto Conversion  - F.READ to CACHE.READ , FM to @FM and VM to @VM
* 10-APRIL-2023      Harsha                R22 Manual Conversion - No changes                             
*------------------------------------------------------------------------
*-----------------------------------------------------------------------------
* <region name= Inserts>
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_ENQUIRY.COMMON
    $INSERT I_F.EB.EXTERNAL.USER
    $INSERT I_F.AA.ARRANGEMENT
    $INSERT I_F.CUSTOMER
    $INSERT I_F.CUST.DOCUMENT
    $INSERT I_F.DOCUMENT.STATUS

    $INSERT I_F.REDO.CH.PROFILE
* </region>
*-----------------------------------------------------------------------------

    GOSUB INIT
    GOSUB OPENFILES
    GOSUB PROCESS

RETURN

*****
INIT:
*****

    Y.BY.RNC = 'N'

    LREF.APP = 'EB.EXTERNAL.USER'
    LREF.FIELDS = 'PROD.USED'
    LREF.POS = ''
    CALL GET.LOC.REF(LREF.APP,LREF.FIELDS,LREF.POS)
    PROD.USED.POS = LREF.POS<1,1>

RETURN

*********
OPENFILES:
*********

    FN.EB.EXTERNAL.USER = 'F.EB.EXTERNAL.USER'
    F.EB.EXTERNAL.USER = ''
    CALL OPF(FN.EB.EXTERNAL.USER,F.EB.EXTERNAL.USER)

    FN.REDO.CH.PROFILE = 'F.REDO.CH.PROFILE'
    F.REDO.CH.PROFILE = ''
    CALL OPF(FN.REDO.CH.PROFILE,F.REDO.CH.PROFILE)

    FN.AA.ARR.USER.RIGHTS = 'F.AA.ARR.USER.RIGHTS'
    F.AA.ARR.USER.RIGHTS = ''
    CALL OPF(FN.AA.ARR.USER.RIGHTS,F.AA.ARR.USER.RIGHTS)

    FN.AA.ARRANGEMENT = 'F.AA.ARRANGEMENT'
    F.AA.ARRANGEMENT = ''
    CALL OPF(FN.AA.ARRANGEMENT,F.AA.ARRANGEMENT)

    FN.CUSTOMER = 'F.CUSTOMER'
    F.CUSTOMER = ''
    CALL OPF(FN.CUSTOMER,F.CUSTOMER)

    FN.CUST.DOCUMENT = 'F.CUST.DOCUMENT'
    F.CUST.DOCUMENT = ''
    CALL OPF(FN.CUST.DOCUMENT,F.CUST.DOCUMENT)

    FN.DOCUMENT.STATUS = 'F.DOCUMENT.STATUS'
    F.DOCUMENT.STATUS = ''
    CALL OPF(FN.DOCUMENT.STATUS,F.DOCUMENT.STATUS)

    FN.CORPORATE.USER.LIST = 'F.CORPORATE.USER.LIST'
    F.CORPORATE.USER.LIST = ''
    CALL OPF(FN.CORPORATE.USER.LIST,F.CORPORATE.USER.LIST)

    FN.CUSTOMER.L.CU.RNC = 'F.CUSTOMER.L.CU.RNC'
    F.CUSTOMER.L.CU.RNC = ''
    CALL OPF(FN.CUSTOMER.L.CU.RNC,F.CUSTOMER.L.CU.RNC)

RETURN

********
PROCESS:
********


    LOCATE 'USER.ID' IN D.FIELDS<1> SETTING USER.ID.POS THEN
        Y.USER.ID = D.RANGE.AND.VALUE<USER.ID.POS>
    END

    LOCATE 'CUSTOMER.ID' IN D.FIELDS<1> SETTING CUSTOMER.ID.POS THEN
        Y.CUSTOMER.ID = D.RANGE.AND.VALUE<CUSTOMER.ID.POS>
    END

    LOCATE 'RNC' IN D.FIELDS<1> SETTING RNC.ID.POS THEN
        Y.RNC.ID = D.RANGE.AND.VALUE<RNC.ID.POS>
    END


    IF Y.USER.ID NE '' AND Y.CUSTOMER.ID EQ '' AND Y.RNC.ID EQ '' THEN
        Y.SEL.CMD = 'SSELECT ':FN.EB.EXTERNAL.USER:' WITH @ID EQ ':Y.USER.ID:' AND CHANNEL EQ INTERNET AND PROD.USED LIKE CORP...'
        CALL EB.READLIST(Y.SEL.CMD,Y.SEL.CMD.LIST,'',Y.SEL.CMD.LIST.NO,SEL.CMD.ERR)
        Y.SEL.FINAL.LIST = Y.SEL.CMD.LIST
        Y.USER.TOT.CNT =DCOUNT(Y.SEL.FINAL.LIST,@FM)
        GOSUB GET.CORP.USR
    END

    IF Y.CUSTOMER.ID NE '' AND Y.USER.ID EQ '' AND Y.RNC.ID EQ '' THEN
        CALL F.READ(FN.CORPORATE.USER.LIST,Y.CUSTOMER.ID,R.CORPORATE.USER.LIST,F.CORPORATE.USER.LIST,CUL.ERR)
        IF R.CORPORATE.USER.LIST THEN
            Y.SEL.FINAL.LIST = R.CORPORATE.USER.LIST
            Y.USER.TOT.CNT =DCOUNT(Y.SEL.FINAL.LIST,@FM)
            GOSUB GET.CORP.USR
        END
    END

    IF Y.RNC.ID NE '' AND Y.USER.ID EQ '' THEN
        Y.SEL.CMD.INT = 'SSELECT ':FN.CUSTOMER:' WITH L.CU.RNC EQ ':Y.RNC.ID
        CALL EB.READLIST(Y.SEL.CMD.INT,Y.SEL.CMD.LIST.INT,'',Y.SEL.CMD.LIST.NO.INT,SEL.CMD.INT.ERR)
        IF Y.SEL.CMD.LIST.NO.INT EQ 1 THEN
            Y.CUS.ID = Y.SEL.CMD.LIST.INT<1>
            IF Y.CUSTOMER.ID THEN
                IF Y.CUSTOMER.ID NE Y.CUS.ID THEN
                    RETURN
                END
            END
            CALL F.READ(FN.CORPORATE.USER.LIST,Y.CUS.ID,R.CORPORATE.USER.LIST,F.CORPORATE.USER.LIST,CUL.ERR)
            IF R.CORPORATE.USER.LIST THEN
                Y.SEL.FINAL.LIST = R.CORPORATE.USER.LIST
                Y.USER.TOT.CNT =DCOUNT(Y.SEL.FINAL.LIST,@FM)
                GOSUB GET.CORP.USR
            END
        END ELSE
            RETURN
        END
    END

    IF Y.USER.ID NE '' THEN
        IF Y.RNC.ID NE '' THEN
            Y.SEL.CMD = 'SSELECT ':FN.CUSTOMER:' WITH L.CU.RNC EQ ':Y.RNC.ID
            CALL EB.READLIST(Y.SEL.CMD,Y.SEL.CMD.LIST,'',Y.SEL.CMD.LIST.NO,SEL.CMD.ERR)
            Y.CUSTOMER.ID = Y.SEL.CMD.LIST
        END

        CALL F.READ(FN.CORPORATE.USER.LIST,Y.CUSTOMER.ID,R.CORPORATE.USER.LIST,F.CORPORATE.USER.LIST,CUL.ERR)
        IF R.CORPORATE.USER.LIST THEN
            Y.SEL.FINAL.LIST = R.CORPORATE.USER.LIST
            Y.USER.TOT.CNT =DCOUNT(Y.SEL.FINAL.LIST,@FM)
            GOSUB GET.CORP.USR
        END

    END

    IF Y.USER.ID EQ '' AND Y.CUSTOMER.ID EQ '' AND Y.RNC.ID EQ '' THEN
        Y.SEL.CMD = 'SSELECT ':FN.EB.EXTERNAL.USER:' WITH CHANNEL EQ INTERNET AND PROD.USED LIKE CORP...'
        CALL EB.READLIST(Y.SEL.CMD,Y.SEL.CMD.LIST,'',Y.SEL.CMD.LIST.NO,SEL.CMD.ERR)
        Y.SEL.FINAL.LIST = Y.SEL.CMD.LIST
        Y.USER.TOT.CNT =DCOUNT(Y.SEL.FINAL.LIST,@FM)
        GOSUB GET.CORP.USR
    END
RETURN

************
GET.PROFILE:
************

    Y.PROFILE = ''
    Y.PROFILE = R.EB.EXTERNAL.USER<EB.XU.LOCAL.REF><1,PROD.USED.POS>

    IF Y.PROFILE EQ 'CORPINPUT' THEN
        IF LNGG EQ 1 THEN
            Y.PROFILE = 'INPUTTER'
        END ELSE
            Y.PROFILE = 'INGRESADOR'
        END
* Y.ID.CTTO = 'CANIBCIN'
    END

    IF Y.PROFILE EQ 'CORPAUTH' THEN
        IF LNGG EQ 1 THEN
            Y.PROFILE = 'AUTHORISER'
        END ELSE
            Y.PROFILE = 'AUTORIZADOR'
        END
* Y.ID.CTTO = 'CANIBCAU'
    END

    IF Y.PROFILE EQ 'CORPADMIN' THEN
        IF LNGG EQ 1 THEN
            Y.PROFILE = 'ADMINISTRATOR'
        END ELSE
            Y.PROFILE = 'ADMINISTRADOR'
        END
* Y.ID.CTTO = 'CANIBCAD'
    END

RETURN

*****************
GET.ID.FOR.RESET:
*****************

    CUR.TIME = TIME()
    Y.ID.F.RESET = TODAY:CUR.TIME

RETURN

*************
GET.CORP.USR:
*************

    Y.CNT = 1
    LOOP
    WHILE Y.CNT LE Y.USER.TOT.CNT
        Y.EXT.USER.ID = Y.SEL.FINAL.LIST<Y.CNT>
        IF Y.USER.ID THEN
            IF Y.EXT.USER.ID NE Y.USER.ID THEN
                Y.CNT += 1
                CONTINUE
            END
        END
        CALL CACHE.READ(FN.EB.EXTERNAL.USER, Y.EXT.USER.ID, R.EB.EXTERNAL.USER, EEU.ERR)      ;*R22 Auto Conversion  - F.READ to CACHE.READ
        GOSUB GET.PROFILE
        GOSUB GET.ID.FOR.RESET
        IF Y.PROFILE NE '' THEN
            Y.NAME = R.EB.EXTERNAL.USER<EB.XU.NAME>
            Y.CHANNEL = R.EB.EXTERNAL.USER<EB.XU.CHANNEL>
            Y.CUST = R.EB.EXTERNAL.USER<EB.XU.CUSTOMER>
            Y.STATUS = R.EB.EXTERNAL.USER<EB.XU.STATUS>
            Y.ARR = R.EB.EXTERNAL.USER<EB.XU.ARRANGEMENT>
* GOSUB GET.CHCTTO
* R.DATA<-1> := Y.EXT.USER.ID:'*':Y.NAME:'*':Y.CHANNEL:'*':Y.CUST:'*':Y.STATUS:'*':Y.ARR:'*':Y.PROFILE:'*':Y.CHCTTO.STATUS:'*':Y.ID.CTTO:'*':Y.ID.F.RESET
            R.DATA<-1> := Y.EXT.USER.ID:'*':Y.NAME:'*':Y.CHANNEL:'*':Y.CUST:'*':Y.STATUS:'*':Y.ARR:'*':Y.PROFILE:'*':Y.ID.F.RESET
        END
        Y.CNT += 1
    REPEAT

RETURN

***********
GET.CHCTTO:
***********

    Y.CHCTTO.STATUS.ID = ''
    Y.CHCTTO.STATUS = ''

* Y.CHCTTO.ID = Y.CUST:'*':Y.ID.CTTO
    Y.CHCTTO.ID = Y.CUST:'*CANIB'
    R.CUST.DOCUMENT = '' ; CD.ERR = ''
    CALL F.READ(FN.CUST.DOCUMENT,Y.CHCTTO.ID,R.CUST.DOCUMENT,F.CUST.DOCUMENT,CD.ERR)
    IF R.CUST.DOCUMENT THEN
        Y.CHCTTO.STATUS.ID = R.CUST.DOCUMENT<CUS.DOC.STATUS>
    END

    R.DOCUMENT.STATUS = ''; DS.ERR = ''
    CALL F.READ(FN.DOCUMENT.STATUS,Y.CHCTTO.STATUS.ID,R.DOCUMENT.STATUS,F.DOCUMENT.STATUS,DS.ERR)
    IF R.DOCUMENT.STATUS THEN
        Y.CHCTTO.STATUS = R.DOCUMENT.STATUS<DOC.STAT.DESCRIPTION>
        IF LNGG EQ 1 THEN
            Y.CHCTTO.STATUS = FIELD(Y.CHCTTO.STATUS,@VM,1)
        END ELSE
            Y.CHCTTO.STATUS = FIELD(Y.CHCTTO.STATUS,@VM,2)
        END
    END

RETURN

END
