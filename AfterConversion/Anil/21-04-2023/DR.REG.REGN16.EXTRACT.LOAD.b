$PACKAGE APAP.LAPAP
SUBROUTINE DR.REG.REGN16.EXTRACT.LOAD
*-----------------------------------------------------------------------------
* Company Name   : APAP
* Developed By   : gangadhar@temenos.com
* Program Name   : DR.REG.REGN16.EXTRACT
* Date           : 3-May-2013
*-----------------------------------------------------------------------------
* Description:
*------------
* This multi-thread job is meant for to extact the REDO.ISSUE.CLAIMS Details for each Customer
*-----------------------------------------------------------------------------
*
* Modification History :
* ----------------------
*   Date          Author              Modification Description
* 21-08-2014      Ashokkumar           PACS00366332- Added auto date to run on monthly.
* 28-11-2017      Ashokkumar           CN006499 - Changed the parameter file to add more data.
* Date                  who                   Reference              
* 21-04-2023         CONVERSTION TOOL      R22 AUTO CONVERSTION - SM TO @SM AND VM TO @VM AND $INSERT TAM.BP TO $INSERT AND $INSERT LAPAP.BP TO $INSERT 
* 21-04-2023          ANIL KUMAR B         R22 MANUAL CONVERSTION -NO CHANGES
*-----------------------------------------------------------------------------

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_BATCH.FILES
    $INSERT I_F.DATES
    $INSERT I_DR.REG.REGN16.EXTRACT.COMMON
    $INSERT I_F.REDO.H.REPORTS.PARAM

    GOSUB INIT.PROCESS
    GOSUB DAYS.INIT
RETURN

INIT.PROCESS:
*-----------*

    FN.CUSTOMER = 'F.CUSTOMER'
    F.CUSTOMER = ''
    CALL OPF(FN.CUSTOMER,F.CUSTOMER)

    FN.REDO.ISSUE.CLAIMS = 'F.REDO.ISSUE.CLAIMS'
    F.REDO.ISSUE.CLAIMS = ''
    CALL OPF(FN.REDO.ISSUE.CLAIMS,F.REDO.ISSUE.CLAIMS)

    FN.DR.REG.REGN16.WORKFILE = 'F.DR.REG.REGN16.WORKFILE'
    F.DR.REG.REGN16.WORKFILE = ''
    CALL OPF(FN.DR.REG.REGN16.WORKFILE,F.DR.REG.REGN16.WORKFILE)

    FN.RELATION = 'F.RELATION'
    F.RELATION = ''
    CALL OPF(FN.RELATION,F.RELATION)

    FN.ACCOUNT = 'F.ACCOUNT'
    F.ACCOUNT = ''
    CALL OPF(FN.ACCOUNT,F.ACCOUNT)

    FN.ACCOUNT.H = 'F.ACCOUNT$HIS'; F.ACCOUNT.H = ''
    CALL OPF(FN.ACCOUNT.H,F.ACCOUNT.H)

    FN.DR.REG.REGN16.EXT.PARAM = 'F.DR.REG.REGN16.EXT.PARAM'
    F.DR.REG.REGN16.EXT.PARAM = ''
    CALL OPF(FN.DR.REG.REGN16.EXT.PARAM,F.DR.REG.REGN16.EXT.PARAM)

    FN.REDO.H.REPORTS.PARAM = "F.REDO.H.REPORTS.PARAM"
    F.REDO.H.REPORTS.PARAM  = ""
    CALL OPF(FN.REDO.H.REPORTS.PARAM,F.REDO.H.REPORTS.PARAM)

    Y.REPORT.PARAM.ID = "PU01-REGN16"
    CALL CACHE.READ(FN.REDO.H.REPORTS.PARAM,Y.REPORT.PARAM.ID,R.REDO.H.REPORTS.PARAM,PARAM.ERR)
    IF R.REDO.H.REPORTS.PARAM THEN
        Y.OUT.FILE.NAME = R.REDO.H.REPORTS.PARAM<REDO.REP.PARAM.OUT.FILE.NAME>
        Y.FIELD.NME.ARR = R.REDO.H.REPORTS.PARAM<REDO.REP.PARAM.FIELD.NAME>
        Y.FIELD.VAL.ARR = R.REDO.H.REPORTS.PARAM<REDO.REP.PARAM.FIELD.VALUE>
        Y.DISP.TEXT.ARR = R.REDO.H.REPORTS.PARAM<REDO.REP.PARAM.DISPLAY.TEXT>
    END

    REP.START.DATE = ''; REP.END.DATE = ''; TYPE.VAL = ''
    LOCATE "START.DATE" IN Y.FIELD.NME.ARR<1,1> SETTING NMS.POS THEN
        REP.START.DATE = Y.FIELD.VAL.ARR<1,NMS.POS>
    END

    LOCATE "END.DATE" IN Y.FIELD.NME.ARR<1,1> SETTING NME.POS THEN
        REP.END.DATE = Y.FIELD.VAL.ARR<1,NME.POS>
    END

    LOCATE "CLAIM.TYPE" IN Y.FIELD.NME.ARR<1,1> SETTING NMCT.POS THEN
        TYPE.VAL = Y.FIELD.VAL.ARR<1,NMCT.POS>
        TYPE.VAL = CHANGE(TYPE.VAL,@SM,@VM)
    END

    LOCATE "AC.PROD.TYPE" IN Y.FIELD.NME.ARR<1,1> SETTING TXNAC.POS THEN
        Y.TXNCDE.VAL.ARR = Y.FIELD.VAL.ARR<1,TXNAC.POS>
        Y.TXNCDE.DIS.ARR = Y.DISP.TEXT.ARR<1,TXNAC.POS>
    END
    Y.TXNCDE.VAL.ARR = CHANGE(Y.TXNCDE.VAL.ARR,@SM,@VM)
    Y.TXNCDE.DIS.ARR = CHANGE(Y.TXNCDE.DIS.ARR,@SM,@VM)

    LOCATE "CC.PROD.TYPE" IN Y.FIELD.NME.ARR<1,1> SETTING TXNCC.POS THEN
        Y.TXNTYE.VAL.ARR = Y.FIELD.VAL.ARR<1,TXNCC.POS>
        Y.TXNTYE.DIS.ARR = Y.DISP.TEXT.ARR<1,TXNCC.POS>
    END
    Y.TXNTYE.VAL.ARR = CHANGE(Y.TXNTYE.VAL.ARR,@SM,@VM)
    Y.TXNTYE.DIS.ARR = CHANGE(Y.TXNTYE.DIS.ARR,@SM,@VM)
RETURN

DAYS.INIT:
**********
    YST.DAT = ''; YED.DAT = ''
    LAST.WRK.DATE = R.DATES(EB.DAT.LAST.WORKING.DAY)
    YLST.MTH = LAST.WRK.DATE
    BEGIN CASE
        CASE LAST.WRK.DATE[5,2] EQ '03'
            YST.DAT = YLST.MTH[1,4]:'0101'
            YED.DAT = YLST.MTH[1,4]:'0331'
        CASE LAST.WRK.DATE[5,2] EQ '06'
            YST.DAT = YLST.MTH[1,4]:'0401'
            YED.DAT = YLST.MTH[1,4]:'0630'
        CASE LAST.WRK.DATE[5,2] EQ '09'
            YST.DAT = YLST.MTH[1,4]:'0701'
            YED.DAT = YLST.MTH[1,4]:'0930'
        CASE LAST.WRK.DATE[5,2] EQ '12'
            YST.DAT = YLST.MTH[1,4]:'1001'
            YED.DAT = YLST.MTH[1,4]:'1231'
        CASE 1
            YST.DAT = LAST.WRK.DATE[1,6]:'01'
            YED.DAT = LAST.WRK.DATE
    END CASE

    IF REP.START.DATE EQ '' AND REP.END.DATE EQ '' THEN
        REP.START.DATE = YST.DAT
        REP.END.DATE = YED.DAT
    END
RETURN

END
