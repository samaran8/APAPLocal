$PACKAGE APAP.AA;*MANUAL R22 CODE CONVERSION
SUBROUTINE REDO.NOF.CLIENT.DETAILS (Y.CUST.ID,Y.PROCESSED.ACCOUNTS,Y.RESULT.ARRAY,MAIN.CARD.FLAG)
*******************************************************************************

*-----------------------------------------------------------------------------------
* Modification History:
*DATE              WHO                REFERENCE                        DESCRIPTION
*29-03-2023      CONVERSION TOOL         AUTO R22 CODE CONVERSION          FM TO @FM, VM TO @VM
*29-03-2023      MOHANRAJ R        MANUAL R22 CODE CONVERSION         Package name added APAP.AA
*-----------------------------------------------------------------------------------


*-------------------------------------------------------------------------------
* Company Name : ASOCIACION POPULAR DE AHORROS Y PRESTAMOS
* Developed By : DHAMU S & MOHAMMED ANIES.K
* Program Name : REDO.NOF.CLIENT.DETAILS
*--------------------------------------------------------------------------------
*Description :This subroutine is attached to the ENQUIRY REDO.PRD.CLIENT.ENQ
*             which displays the client details
*--------------------------------------------------------------------------------
* Linked With : ENQUIRY REDO.PRD.CLIENT.ENQ
* In Parameter : None
* Out Parameter : None
*---------------------------------------------------------------------------------
*Modification History:
*------------------------
*     Date            Who                  Reference               Description
*    ------          ------               -----------             --------------
*   19-10-2010       DHAMU S          ODR-2010-08-0182 113       Initial Creation
*--------------------------------------------------------------------------------------------------------
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.CUSTOMER
    $INSERT I_F.ACCOUNT
    $INSERT I_F.CUSTOMER.ACCOUNT
    $INSERT I_F.AA.ARRANGEMENT
    $INSERT I_F.AA.INTEREST.ACCRUALS
    $INSERT I_F.AZ.ACCOUNT
    $INSERT I_F.EB.CONTRACT.BALANCES
    $INSERT I_F.ACCT.ACTIVITY
    $INSERT I_F.LATAM.CARD.ORDER
    $INSERT I_F.LATAM.CARD.CUSTOMER
    $INSERT I_ENQUIRY.COMMON
    $INSERT I_F.RELATION
    $INSERT I_F.DEPT.ACCT.OFFICER
    $INSERT I_F.AA.ACCOUNT
    $INSERT I_AA.LOCAL.COMMON
*Tus Start
*   $INSERT I_F.EB.CONTRACT.BALANCES
*Tus End


    GOSUB INIT
    GOSUB PROCESS
    GOSUB RESULT.ARRAY

RETURN

*****
INIT:
*****
    APL.ARRAY = ''; APL.FIELD = '' ; LOC.L.CU.TIPO.CL.POS = '' ; LOC.L.CU.SEGMENTO.POS = '' ; LOC.L.AC.STATUS.POS = ''; LOC.L.AC.REINVESTED.POS = ''
    LOC.L.AC.STAT.INT.RATE.POS = '' ; LOC.L.AC.STATUS2.POS = ''; CLIENT.VAL = '' ;AGENCY.VAL = '' ; IDENTIFICATION.NUM.VAL = '';DATE.CLIENT.VAL  = ''
    RELATIONSHIP.VAL = '';ACCOUT.OFFICE.VAL = '';CATEGORY1.VAL = '';CATEGORY2.VAL = '';PERSON.VAL = ''; MOST.RECENT.DATE.VAL = ''
    SAVINGS.STATUS.VAL = '' ; INVEST.VAL = ''; INVESTMENT.STAT.VAL = '' ; ACCR.INT.INVEST.VAL = '' ;CATEGORY.VAL ='' ;ACCT.ID.VAL = '';ACCT.BALANCE.VAL = ''
    SAVINGS.INTEREST.VAL = '';SAVINGS.ACCOUNT.RATE.VAL = '';INVEST.NUM.VAL = '';TYPE.OF.INVS.VAL = '';INVESTMENT.INTEREST.RATE.VAL = '';PRINCIPAL.VAL = ''
    INT.LIQ.ACCT.VAL = ''; INT.LIQ.ACCT.VAL = '';INVESTMENT.BALANCE.VAL = '' ;LOAN.PORTFOLIO.VAL = '';LOAN.PRODUCT.VAL = '';LOAN.STATUS.VAL = '' ;LOAN.NUMBER.VAL = ''
    LOAN.INTEREST.RATE.VAL = '';ACCRUED.INTEREST.VAL = '';Y.OUTSTANDING.BAL1 = '';Y.OUTSTANDING.BAL = '';CARD.STAGE.FLAG  = ''; Y.CARD.LIST = '';Y.TOTAL.CARDS = ''
    Y.INT.CARD = '';DEBIT.NO.VAL = '';DEBIT.STAT.VAL = '';DEBIT.TYPE.VAL = '';ISSUE.STAGE.VAL = '';Y.ARR.ID ='' ; CARD.STAGE.VAL = ''
    Y.REL.CODE = '';Y.CUS.NAMES = '';Y.RELATION.COUNT = '';RELATION.ID = '';CUSTOMER.ID = '';Y.CUS.NAME = '';Y.CLIENT.CODE = '';Y.BDY.CUSTOMER.NAME = ''
    Y.NAME = '';Y.ACC.EXEC.DIS = '';Y.OTHERNAME = '';Y.AGENCY.DIS = '';SAVINGS.ACCOUNT.TYPE.VAL = ''
    PREVIOUS.ACCOUNT.NUMBER.VAL = ''; PREVIOUS.INVESTMENT.NUMBER.VAL = ''; PREVIOUS.LOAN.NUMBER.VAL = ''; Y.PREV.LOAN.NUMBER.VAL = ''



    FN.CUSTOMER = 'F.CUSTOMER'
    F.CUSTOMER  = ''
    CALL OPF(FN.CUSTOMER,F.CUSTOMER)

    FN.ACCOUNT = 'F.ACCOUNT'
    F.ACCOUNT  = ''
    CALL OPF(FN.ACCOUNT,F.ACCOUNT)

    FN.AA.ARRANGEMENT = 'F.AA.ARRANGEMENT'
    F.AA.ARRANGEMENT  = ''
    CALL OPF(FN.AA.ARRANGEMENT,F.AA.ARRANGEMENT)


    FN.EB.CONTRACT.BALANCES = 'F.EB.CONTRACT.BALANCES'
    F.EB.CONTRACT.BALANCES  = ''
    CALL OPF(FN.EB.CONTRACT.BALANCES,F.EB.CONTRACT.BALANCES)

    FN.AZ.ACCOUNT = 'F.AZ.ACCOUNT'
    F.AZ.ACCOUNT  = ''
    CALL OPF(FN.AZ.ACCOUNT,F.AZ.ACCOUNT)


    FN.CUSTOMER.ACCOUNT = 'F.CUSTOMER.ACCOUNT'
    F.CUSTOMER.ACCOUNT = ''
    CALL OPF(FN.CUSTOMER.ACCOUNT,F.CUSTOMER.ACCOUNT)

    FN.AA.INTEREST.ACCRUALS = 'F.AA.INTEREST.ACCRUALS'
    F.AA.INTEREST.ACCRUALS = ''
    CALL OPF(FN.AA.INTEREST.ACCRUALS,F.AA.INTEREST.ACCRUALS)


    FN.LATAM.CARD.ORDER = 'F.LATAM.CARD.ORDER'
    F.LATAM.CARD.ORDER = ''
    CALL OPF(FN.LATAM.CARD.ORDER,F.LATAM.CARD.ORDER)

    FN.LATAM.CARD.CUSTOMER = 'F.LATAM.CARD.CUSTOMER'
    F.LATAM.CARD.CUSTOMER = ''
    CALL OPF(FN.LATAM.CARD.CUSTOMER,F.LATAM.CARD.CUSTOMER)

    FN.DEPT.ACCT.OFFICER = 'F.DEPT.ACCT.OFFICER'
    F.DEPT.ACCT.OFFICER = ''
    CALL OPF(FN.DEPT.ACCT.OFFICER,F.DEPT.ACCT.OFFICER)

    FN.AA.ARR.ACCOUNT = 'F.AA.ARR.ACCOUNT'
    F.AA.ARR.ACCOUNT  = ''
    CALL OPF(FN.AA.ARR.ACCOUNT,F.AA.ARR.ACCOUNT)


    MAIN.CARD.FLAG=''

RETURN

************************
PROCESS:
*************************
    APL.ARRAY = 'CUSTOMER':@FM:'ACCOUNT' ;*AUTO R22 CODE CONVERSION
    APL.FIELD = 'L.CU.TIPO.CL':@VM:'L.CU.SEGMENTO':@VM:'L.CU.CIDENT':@VM:'L.CU.ACTANAC':@VM:'L.CU.RNC':@VM:'L.CU.NOUNICO':@FM:'L.AC.STATUS':@VM:'L.AC.REINVESTED':@VM:'L.STAT.INT.RATE':@VM:'L.AC.STATUS2' ;*AUTO R22 CODE CONVERSION
    FLD.POS = ''
    CALL MULTI.GET.LOC.REF(APL.ARRAY,APL.FIELD,FLD.POS)
    LOC.L.CU.TIPO.CL.POS = FLD.POS<1,1>
    LOC.L.CU.SEGMENTO.POS = FLD.POS<1,2>
    LOC.L.CU.CIDENT.POS = FLD.POS<1,3>
    LOC.L.CU.ACTANAC.POS = FLD.POS<1,4>
    LOC.L.CU.RNC.POS = FLD.POS<1,5>
    LOC.L.CU.NOUNICO.POS = FLD.POS<1,6>
    LOC.L.AC.STATUS.POS = FLD.POS<2,1>
    LOC.L.AC.REINVESTED.POS = FLD.POS<2,2>
    LOC.L.AC.STAT.INT.RATE.POS = FLD.POS<2,3>
    LOC.L.AC.STATUS2.POS = FLD.POS<2,4>


    GOSUB GET.CUST.HIS
    CALL F.READ(FN.CUSTOMER,Y.CUST.ID,R.CUSTOMER,F.CUSTOMER,CUSTOMER.ERR)
    CLIENT.VAL = Y.CUST.ID
    AGENCY.VAL = R.CUSTOMER<EB.CUS.OTHER.OFFICER>

    IF AGENCY.VAL THEN
    * CALL F.READ(FN.DEPT.ACCT.OFFICER,AGENCY.VAL,R.DEPT.ACCT.OFFICER,F.DEPT.ACCT.OFFICER,F.ERR)
        CALL CACHE.READ(FN.DEPT.ACCT.OFFICER, AGENCY.VAL, R.DEPT.ACCT.OFFICER, F.ERR) ;*AUTO R22 CODE CONVERSION
        Y.OTHERNAME = R.DEPT.ACCT.OFFICER<EB.DAO.NAME>
        Y.AGENCY.DIS = Y.OTHERNAME
    END
    IDENTIFICATION.NUM.VAL = ''
    IDENTIFICATION.NUM.VAL = R.CUSTOMER<EB.CUS.LEGAL.ID>

    IF IDENTIFICATION.NUM.VAL EQ '' THEN
        IDENTIFICATION.NUM.VAL = R.CUSTOMER<EB.CUS.LOCAL.REF,LOC.L.CU.CIDENT.POS>
    END
    IF IDENTIFICATION.NUM.VAL EQ '' THEN
        IDENTIFICATION.NUM.VAL = R.CUSTOMER<EB.CUS.LOCAL.REF,LOC.L.CU.ACTANAC.POS>
    END
    IF IDENTIFICATION.NUM.VAL EQ '' THEN
        IDENTIFICATION.NUM.VAL = R.CUSTOMER<EB.CUS.LOCAL.REF,LOC.L.CU.RNC.POS>
    END
    IF IDENTIFICATION.NUM.VAL EQ '' THEN
        IDENTIFICATION.NUM.VAL = R.CUSTOMER<EB.CUS.LOCAL.REF,LOC.L.CU.NOUNICO.POS>
    END

    RELATIONSHIP.VAL = R.CUSTOMER<EB.CUS.CUSTOMER.SINCE>
    ACCOUNT.OFFICE.VAL = R.CUSTOMER<EB.CUS.ACCOUNT.OFFICER>
    IF ACCOUNT.OFFICE.VAL THEN
    *CALL F.READ(FN.DEPT.ACCT.OFFICER,ACCOUNT.OFFICE.VAL,R.DEPT.ACCT.OFFICER,F.DEPT.ACCT.OFFICER,F.ERR)
        CALL CACHE.READ(FN.DEPT.ACCT.OFFICER, ACCOUNT.OFFICE.VAL, R.DEPT.ACCT.OFFICER, F.ERR) ;*AUTO R22 CODE CONVERSION
        Y.NAME = R.DEPT.ACCT.OFFICER<EB.DAO.NAME>
        Y.ACC.EXEC.DIS = Y.NAME
    END

    CATEGORY1.VAL = R.CUSTOMER<EB.CUS.CR.PROFILE.TYPE>
    CATEGORY2.VAL = R.CUSTOMER<EB.CUS.LOCAL.REF,LOC.L.CU.SEGMENTO.POS>
    PERSON.VAL = R.CUSTOMER<EB.CUS.LOCAL.REF,LOC.L.CU.TIPO.CL.POS>
    MOST.RECENT.DATE.VAL = R.CUSTOMER<EB.CUS.DATE.TIME>
    GOSUB CUSTOMER.ACCOUNT

RETURN

*****************
GET.CUST.HIS:
*****************

    FN.CUST$HIS = 'F.CUSTOMER$HIS'
    F.CUST$HIS = ''

    CALL OPF(FN.CUST$HIS,F.CUST$HIS)

    Y.CUST.ID.HIS = Y.CUST.ID : ';1'

    CALL F.READ(FN.CUST$HIS,Y.CUST.ID.HIS,R.CUST.HIS,F.CUST$HIS,Y.CUST.HIS.ERR)

    DATE.CLIENT.VAL.HIS = R.CUST.HIS<EB.CUS.DATE.TIME>

RETURN

************************
CUSTOMER.ACCOUNT:
*************************
    LOOP
        REMOVE Y.ACCT.ID FROM Y.PROCESSED.ACCOUNTS SETTING ACCT.POS
    WHILE Y.ACCT.ID:ACCT.POS

        CALL F.READ(FN.ACCOUNT,Y.ACCT.ID,R.ACCOUNT,F.ACCOUNT,ACCT.ERR)
        R.ECB='' ; ECB.ERR= '' ;*Tus Start
        CALL EB.READ.HVT("EB.CONTRACT.BALANCES",Y.ACCT.ID,R.ECB,ECB.ERR);*Tus End
        ACCOUNT.VAL = Y.ACCT.ID
        SAVINGS.STATUS.VAL<-1> = R.ACCOUNT<AC.LOCAL.REF,LOC.L.AC.STATUS.POS>
        Y.ARR.ID = ''
        Y.ARR.ID<-1> = R.ACCOUNT<AC.ARRANGEMENT.ID>
        INVEST.VAL<-1> = R.ACCOUNT<AC.LOCAL.REF,LOC.L.AC.REINVESTED.POS>
        IF R.ACCOUNT THEN
            PREVIOUS.ACCOUNT.NUMBER.VAL<-1> = R.ACCOUNT<AC.ALT.ACCT.ID>
        END ELSE
            PREVIOUS.ACCOUNT.NUMBER.VAL<-1> = ' '
        END

        INVESTMENT.STAT.VAL<-1> = R.ACCOUNT<AC.LOCAL.REF,LOC.L.AC.STATUS2.POS>
        ACCR.INT.INVEST.VAL<-1> = R.ACCOUNT<AC.ACCR.CR.AMOUNT>

        CATEGORY.VAL = R.ACCOUNT<AC.CATEGORY>

        IF CATEGORY.VAL GT "6000" AND CATEGORY.VAL LE "6499" THEN
            ACCT.ID.VAL<-1> = Y.ACCT.ID
        END ELSE
            ACCT.ID.VAL<-1> = ' '
        END
        SAVINGS.ACCOUNT.TYPE.VAL<-1> = CATEGORY.VAL
* ACCT.BALANCE.VAL<-1> = R.ACCOUNT<AC.ONLINE.ACTUAL.BAL>;*Tus Start
        ACCT.BALANCE.VAL<-1> = R.ECB<ECB.ONLINE.ACTUAL.BAL>;*Tus End
        SAVINGS.INTEREST.VAL<-1> = R.ACCOUNT<AC.ACCR.CR.AMOUNT>
        SAVINGS.ACCOUNT.RATE.VAL<-1> = R.ACCOUNT<AC.LOCAL.REF,LOC.L.AC.STAT.INT.RATE.POS>

        GOSUB AZ.ACCOUNT
        GOSUB CHECK.ARRANGEMENT.DETAILS
    REPEAT
    GOSUB LATAM.CARD.FIELDS
    GOSUB CHANGE.DELIMITER
RETURN

*************************
CHECK.ARRANGEMENT.DETAILS:
*************************
    IF Y.ARR.ID NE '' THEN
        CALL F.READ(FN.AA.ARRANGEMENT,Y.ARR.ID,R.AA.ARRANGEMENT,F.AA.ARRANGEMENT,AA.ERR)
        GOSUB CHECK.AA.ARR.ACCOUNT
        LOAN.PORTFOLIO.VAL<-1> = R.AA.ARRANGEMENT<AA.ARR.PRODUCT.GROUP>
        LOAN.PRODUCT.VAL<-1> = R.AA.ARRANGEMENT<AA.ARR.PRODUCT>
        LOAN.STATUS.VAL<-1> = R.AA.ARRANGEMENT<AA.ARR.ARR.STATUS>
        LOAN.NUMBER.VAL<-1> = Y.ARR.ID
        Y.ARR.ACCRUAL.ID = Y.ARR.ID:"-":"PRINCIPALINT"

        CALL F.READ(FN.AA.INTEREST.ACCRUALS,Y.ARR.ACCRUAL.ID,R.AA.INTEREST.ACCRUALS,F.AA.INTEREST.ACCRUALS,AA.INT.ACCRUAL.ERR)
        AA.INT.ACCRUAL.ERR = ''
        LOAN.INTEREST.RATE.VAL<-1> = R.AA.INTEREST.ACCRUALS<AA.INT.ACC.RATE,1,1>
        ACCRUED.INTEREST.VAL<-1> = R.AA.INTEREST.ACCRUALS<AA.INT.ACC.ACCRUAL.AMT,1,1>
        PREVIOUS.LOAN.NUMBER.VAL = Y.PREV.LOAN.NUMBER.VAL
        PREVIOUS.INVESTMENT.NUMBER.VAL<-1> = ' '
        GOSUB EB.CONTRACT.FIELDS
    END ELSE
        LOAN.PORTFOLIO.VAL<-1> = ' '
        LOAN.PRODUCT.VAL<-1> = ' '
        LOAN.STATUS.VAL<-1> = ' '
        LOAN.NUMBER.VAL<-1> = ' '
        LOAN.INTEREST.RATE.VAL<-1> = ' '
        ACCRUED.INTEREST.VAL<-1> = ' '
        Y.OUTSTANDING.BAL<-1> = ' '
        PREVIOUS.INVESTMENT.NUMBER.VAL<-1> = R.ACCOUNT<AC.ALT.ACCT.ID>
        PREVIOUS.LOAN.NUMBER.VAL<-1> = ' '
    END
RETURN

******************
AZ.ACCOUNT:
*******************
    CALL F.READ(FN.AZ.ACCOUNT,Y.ACCT.ID,R.AZ.ACCOUNT,F.AZ.ACCOUNT,AZ.ACCT.ERR)
    IF R.AZ.ACCOUNT THEN
        INVEST.NUM.VAL<-1> = Y.ACCT.ID
        TYPE.OF.INVS.VAL<-1> = R.AZ.ACCOUNT<AZ.CATEGORY>
        INVEST.VAL<-1>= R.ACCOUNT<AC.LOCAL.REF,LOC.L.AC.REINVESTED.POS>
        INVESTMENT.INTEREST.RATE.VAL<-1>  = R.AZ.ACCOUNT<AZ.INTEREST.RATE>
        PRINCIPAL.VAL = R.AZ.ACCOUNT<AZ.PRINCIPAL>

        IF INVEST.VAL EQ "Y" THEN

            INT.LIQ.ACCT.VAL = R.AZ.ACCOUNT<AZ.INTEREST.LIQU.ACCT>
            R.ACCOUNT = ''
            ACCT.ERR = ''
            CALL F.READ(FN.ACCOUNT,INT.LIQ.ACCT.VAL,R.ACCOUNT,F.ACCOUNT,ACCT.ERR)
            R.ECB1='' ; ECB.ERR1= '' ;*Tus Start
            CALL EB.READ.HVT("EB.CONTRACT.BALANCES",INT.LIQ.ACCT.VAL,R.ECB1,ECB.ERR1)
*  ONLINE.ACTUAL.BALANCE.VAL = R.ACCOUNT<AC.ONLINE.ACTUAL.BAL>
            ONLINE.ACTUAL.BALANCE.VAL = R.ECB1<ECB.ONLINE.ACTUAL.BAL>;*Tus End
            INVESTMENT.BALANCE.VAL<-1> = PRINCIPAL.VAL + ONLINE.ACTUAL.BALANCE.VAL
        END ELSE

            INVESTMENT.BALANCE.VAL<-1> = PRINCIPAL.VAL

        END
    END ELSE
        INVEST.NUM.VAL<-1> = ' '
        TYPE.OF.INVS.VAL<-1> = ' '
        INVEST.VAL<-1> = ' '
        INVESTMENT.INTEREST.RATE.VAL<-1> = ' '
        INVESTMENT.BALANCE.VAL<-1> = ' '
    END

RETURN
*****************
CHECK.AA.ARR.ACCOUNT:
*****************
    ArrangementID = Y.ARR.ID
    idPropertyClass = "ACCOUNT"
    idProperty = ''; effectiveDate=''; returnIds =''; returnConditions = ''; returnError = ''

    CALL AA.GET.ARRANGEMENT.CONDITIONS(ArrangementID,idPropertyClass,idProperty,effectiveDate,returnIds, returnConditions,returnError)

    R.CONDITION = RAISE(returnConditions)

    IF R.CONDITION THEN
        Y.PREV.LOAN.NUMBER.VAL<-1> = R.CONDITION<AA.AC.ALT.ID>
    END
RETURN

*********************************
EB.CONTRACT.FIELDS:
********************************

    Y.BALANCE.TO.CHECK.ARRAY = 'CURACCOUNT':@VM:'DUEACCOUNT':@VM:'GRCACCOUNT':@VM:'DELACCOUNT':@VM:'NABACCOUNT' ;*AUTO R22 CODE CONVERSION
    R.EB.CONTRACT.BALANCES = ''
    EB.CONTRACT.ERR = ''
    CALL F.READ(FN.EB.CONTRACT.BALANCES,Y.ACCT.ID,R.EB.CONTRACT.BALANCES,F.EB.CONTRACT.BALANCES,EB.CONTRACT.ERR)
    Y.CURR.ASSET.TYPE.LIST = R.EB.CONTRACT.BALANCES<ECB.CURR.ASSET.TYPE>
    Y.OUTSTANDING.BAL1 = 0
    LOOP
        REMOVE BALANCE.TO.CHECK FROM Y.BALANCE.TO.CHECK.ARRAY SETTING Y.BAL.CHECK.POS
    WHILE BALANCE.TO.CHECK:Y.BAL.CHECK.POS

        LOCATE BALANCE.TO.CHECK IN Y.CURR.ASSET.TYPE.LIST<1,1> SETTING Y.CURR.ASSET.TYPE.POS THEN
            Y.OUTSTANDING.BAL1 += R.EB.CONTRACT.BALANCES<ECB.OPEN.BALANCE,Y.CURR.ASSET.TYPE.POS,1>
        END
    REPEAT

    Y.OUTSTANDING.BAL<-1> = Y.OUTSTANDING.BAL1

RETURN
****************************************
LATAM.CARD.FIELDS:
****************************************
    CARD.STAGE.FLAG = 0
    LOCATE "CARD.STAGE" IN D.FIELDS<1> SETTING Y.CARD.POS THEN
        CARD.STAGE.VAL = ''
        CARD.STAGE.VAL = D.RANGE.AND.VALUE<Y.CARD.POS>
        CARD.STAGE.FLAG = 1
    END

    CALL F.READ(FN.LATAM.CARD.CUSTOMER,Y.CUST.ID,R.LATAM.CARD.CUSTOMER,F.LATAM.CARD.CUSTOMER,LATAM.CARD.CUSTOMER.ERR)
    Y.CARD.LIST = R.LATAM.CARD.CUSTOMER<APAP.DC.CARD.NO>
    Y.TOTAL.CARDS = DCOUNT(Y.CARD.LIST,@VM) ;*AUTO R22 CODE CONVERSION

    IF CARD.STAGE.VAL EQ '' AND R.LATAM.CARD.CUSTOMER EQ '' THEN
        MAIN.CARD.FLAG = 'Y'
    END
    Y.INT.CARD = 1
    LOOP

    WHILE Y.INT.CARD LE Y.TOTAL.CARDS
        Y.CARD.ID = Y.CARD.LIST<1,Y.INT.CARD>
        CALL F.READ(FN.LATAM.CARD.ORDER,Y.CARD.ID,R.LATAM.CARD.ORDER,F.LATAM.CARD.ORDER,CARD.ORDER.ERR)
        GOSUB CHECK.CARD.STAGE
        Y.INT.CARD +=1
    REPEAT

RETURN
******************************************
RESULT.ARRAY:
***************************************
    Y.RESULT.ARRAY = DATE.CLIENT.VAL.HIS:"*":RELATIONSHIP.VAL:"*":CLIENT.VAL:"*":ACCOUNT.VAL:"*":IDENTIFICATION.NUM.VAL:"*":Y.AGENCY.DIS:"*":CATEGORY1.VAL:"*":CATEGORY2.VAL:"*":Y.ACC.EXEC.DIS:"*":PERSON.VAL:"*":MOST.RECENT.DATE.VAL:"*":LOAN.NUMBER.VAL:"*":LOAN.PORTFOLIO.VAL:"*":LOAN.PRODUCT.VAL:"*":LOAN.STATUS.VAL:"*":Y.OUTSTANDING.BAL:"*":ACCRUED.INTEREST.VAL:"*":LOAN.INTEREST.RATE.VAL:"*":INVEST.NUM.VAL:"*":TYPE.OF.INVS.VAL:"*":INVESTMENT.STAT.VAL:"*":INVESTMENT.BALANCE.VAL:"*":SAVINGS.INTEREST.VAL:"*":INVESTMENT.INTEREST.RATE.VAL:"*":ACCT.ID.VAL:"*":SAVINGS.STATUS.VAL:"*":ACCT.BALANCE.VAL:"*":SAVINGS.INTEREST.VAL:"*":SAVINGS.ACCOUNT.RATE.VAL:"*":DEBIT.NO.VAL:"*":DEBIT.TYPE.VAL:"*":DEBIT.STAT.VAL:"*":SAVINGS.ACCOUNT.TYPE.VAL:"*":PREVIOUS.ACCOUNT.NUMBER.VAL:"*":PREVIOUS.INVESTMENT.NUMBER.VAL:"*":PREVIOUS.LOAN.NUMBER.VAL
*                        1                       2                  3                4                    5                    6                7                 8                    9               10                 11                    12                    13                     14                   15                  16                     17                         18                      19                   20                  21                      22                           23                        24                           25             26                     27                   28                          29                        30                31                 32                  33                             34                           35                                    36
RETURN
********************************************
CHECK.CARD.STAGE:
****************

    BEGIN CASE

        CASE CARD.STAGE.FLAG EQ 1
            ISSUE.STAGE.VAL = R.LATAM.CARD.ORDER<CARD.IS.ISSUE.STAGE>
            IF CARD.STAGE.VAL EQ ISSUE.STAGE.VAL THEN
                MAIN.CARD.FLAG='Y'
                DEBIT.NO.VAL<-1> =  R.LATAM.CARD.ORDER<CARD.IS.CARD.NUMBER,Y.INT.CARD>
                DEBIT.STAT.VAL<-1> = R.LATAM.CARD.ORDER<CARD.IS.ISSUE.STAGE,Y.INT.CARD>
                DEBIT.TYPE.VAL<-1> = R.LATAM.CARD.ORDER<CARD.IS.CARD.TYPE,Y.INT.CARD>
            END

        CASE CARD.STAGE.FLAG EQ 0
            MAIN.CARD.FLAG = 'Y'
            DEBIT.NO.VAL<-1> =  R.LATAM.CARD.ORDER<CARD.IS.CARD.NUMBER,Y.INT.CARD>
            DEBIT.STAT.VAL<-1> = R.LATAM.CARD.ORDER<CARD.IS.ISSUE.STAGE,Y.INT.CARD>
            DEBIT.TYPE.VAL<-1> = R.LATAM.CARD.ORDER<CARD.IS.CARD.TYPE,Y.INT.CARD>

    END CASE

RETURN
****************************************************************
CHANGE.DELIMITER:
*****************

    CHANGE @FM TO @VM IN SAVINGS.STATUS.VAL ;*AUTO R22 CODE CONVERSION
    CHANGE @FM TO @VM IN Y.ARR.ID
    CHANGE @FM TO @VM IN INVEST.VAL
    CHANGE @FM TO @VM IN INVESTMENT.STAT.VAL
    CHANGE @FM TO @VM IN ACCR.INT.INVEST.VAL
    CHANGE @FM TO @VM IN ACCT.ID.VAL
    CHANGE @FM TO @VM IN ACCT.BALANCE.VAL
    CHANGE @FM TO @VM IN SAVINGS.INTEREST.VAL
    CHANGE @FM TO @VM IN SAVINGS.ACCOUNT.RATE.VAL
    CHANGE @FM TO @VM IN INVEST.NUM.VAL
    CHANGE @FM TO @VM IN TYPE.OF.INVS.VAL
    CHANGE @FM TO @VM IN INVEST.VAL
    CHANGE @FM TO @VM IN INVESTMENT.INTEREST.RATE.VAL
    CHANGE @FM TO @VM IN INVESTMENT.BALANCE.VAL
    CHANGE @FM TO @VM IN LOAN.PORTFOLIO.VAL
    CHANGE @FM TO @VM IN LOAN.PRODUCT.VAL
    CHANGE @FM TO @VM IN LOAN.STATUS.VAL
    CHANGE @FM TO @VM IN LOAN.NUMBER.VAL
    CHANGE @FM TO @VM IN LOAN.INTEREST.RATE.VAL
    CHANGE @FM TO @VM IN ACCRUED.INTEREST.VAL
    CHANGE @FM TO @VM IN Y.OUTSTANDING.BAL
    CHANGE @FM TO @VM IN DEBIT.STAT.VAL
    CHANGE @FM TO @VM IN DEBIT.TYPE.VAL
    CHANGE @FM TO @VM IN DEBIT.NO.VAL
    CHANGE @FM TO @VM IN SAVINGS.ACCOUNT.TYPE.VAL
    CHANGE @FM TO @VM IN PREVIOUS.LOAN.NUMBER.VAL
    CHANGE @FM TO @VM IN PREVIOUS.ACCOUNT.NUMBER.VAL
    CHANGE @FM TO @VM IN PREVIOUS.INVESTMENT.NUMBER.VAL


RETURN
*-----------------------------------
END       ;*end of the routine
