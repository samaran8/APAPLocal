* @ValidationCode : MjotNzU0MzYyNTc6Q3AxMjUyOjE2ODU1MzU3NzI4NDE6c2FtYXI6LTE6LTE6MDowOmZhbHNlOk4vQTpERVZfMjAyMTA4LjA6LTE6LTE=
* @ValidationInfo : Timestamp         : 31 May 2023 17:52:52
* @ValidationInfo : Encoding          : Cp1252
* @ValidationInfo : User Name         : samar
* @ValidationInfo : Nb tests success  : N/A
* @ValidationInfo : Nb tests failure  : N/A
* @ValidationInfo : Rating            : N/A
* @ValidationInfo : Coverage          : N/A
* @ValidationInfo : Strict flag       : N/A
* @ValidationInfo : Bypass GateKeeper : false
* @ValidationInfo : Compiler Version  : DEV_202108.0
* @ValidationInfo : Copyright Temenos Headquarters SA 1993-2021. All rights reserved.
$PACKAGE APAP.REDOAPAP
*---------------------------------------------------------------------------------------
*Modification History:
*DATE                 WHO                    REFERENCE                         DESCRIPTION
*25/05/2023      CONVERSION TOOL         AUTO R22 CODE CONVERSION         FM TO @FM, VM TO @VM, SM TO @SM,++ TO +=
*25/05/2023      HARISH VIKRAM              MANUAL R22 CODE CONVERSION     CALL routine format modified
*----------------------------------------------------------------------------------------
SUBROUTINE REDO.B.ADDGEST.CORRECT(ARR.ID)
*-----------------------------------------------------------------------------
*
*-----------------------------------------------------------------------------
* Modification History :
*-----------------------------------------------------------------------------
***
*---------------------------------------- -------------------------------------

    $INSERT I_EQUATE
    $INSERT I_COMMON
    $INSERT I_F.AA.OVERDUE
    $INSERT I_F.AA.ARRANGEMENT
    $INSERT I_F.ACCOUNT
    $INSERT I_F.AA.ACCOUNT
    $INSERT I_F.AA.BILL.DETAILS
    $INSERT I_F.AA.ACCOUNT.DETAILS
    $INSERT I_F.AA.ARRANGEMENT.ACTIVITY
    $INSERT I_REDO.B.ADDGEST.CORRECT.COMMON
    
    $USING APAP.AA

****GETTING ARRANGEMENT ID*****
    FN.AA.ARR.ACCOUNT = 'F.AA.ARR.ACCOUNT'
    F.AA.ARR.ACCOUNT = ''
    CALL OPF(FN.AA.ARR.ACCOUNT,F.AA.ARR.ACCOUNT)
    MIG.DATE = '20220907'

    IF ARR.ID NE '' THEN

        GOSUB GET.OD.STATUS
        GOSUB GET.ACCT.FLD.DET
        IF ACC.LOAN.STATUS EQ 'NAB' THEN
            AGING.STATUS = ACC.LOAN.STATUS
            AGING.STATUS.2 = ACC.LOAN.STATUS.2
        END
        R.APP.RECORD<AA.ARR.ACT.ARRANGEMENT> = ARR.ID
        R.APP.RECORD<AA.ARR.ACT.ACTIVITY> = "LENDING-UPDATE-OD.STATUS"
        R.APP.RECORD<AA.ARR.ACT.EFFECTIVE.DATE> = ''
        R.APP.RECORD<AA.ARR.ACT.PROPERTY,1> = 'ACCOUNT'
        R.APP.RECORD<AA.ARR.ACT.FIELD.NAME,1,-1> = 'L.OD.STATUS:1:1'
        R.APP.RECORD<AA.ARR.ACT.FIELD.VALUE,1,-1> = AGING.STATUS
        R.APP.RECORD<AA.ARR.ACT.FIELD.NAME,1,-1> = 'L.OD.STATUS.2:1:1'
        R.APP.RECORD<AA.ARR.ACT.FIELD.VALUE,1,-1> = AGING.STATUS.2

        CHANGE @VM TO @FM IN Y.ALT.ID.LIST
        TOT.ALT = DCOUNT(Y.ALT.ID.LIST, @FM)
        ALT.CNT = 1

        LOOP
        WHILE ALT.CNT LE TOT.ALT
            Y.ALT.ID = ''
            Y.ALT.ID = Y.ALT.ID.LIST<ALT.CNT>
            IF Y.ALT.ID THEN
                R.APP.RECORD<AA.ARR.ACT.FIELD.NAME,1,-1> = 'ALT.ID:':ALT.CNT:':1'
                R.APP.RECORD<AA.ARR.ACT.FIELD.VALUE,1,-1> = Y.ALT.ID
            END
            ALT.CNT += 1 ;*AUTO R22 CODE CONVERSION
        REPEAT

        R.APP.RECORD<AA.ARR.ACT.FIELD.NAME,1,-1> = 'ANNIVERSARY:1:1'
        R.APP.RECORD<AA.ARR.ACT.FIELD.VALUE,1,-1> = Y.ANNIVERSARY

        IF Y.L.AA.AGENCY.CODE THEN
            R.APP.RECORD<AA.ARR.ACT.FIELD.NAME,1,-1> = 'L.AA.AGNCY.CODE:1:1'
            R.APP.RECORD<AA.ARR.ACT.FIELD.VALUE,1,-1> = Y.L.AA.AGENCY.CODE
        END
        IF Y.L.AA.LOAN.DSN THEN
            R.APP.RECORD<AA.ARR.ACT.FIELD.NAME,1,-1> = 'L.AA.LOAN.DSN:1:1'
            R.APP.RECORD<AA.ARR.ACT.FIELD.VALUE,1,-1> = Y.L.AA.LOAN.DSN
        END
        IF Y.L.CR.FACILITY THEN
            R.APP.RECORD<AA.ARR.ACT.FIELD.NAME,1,-1> = 'L.CR.FACILITY:1:1'
            R.APP.RECORD<AA.ARR.ACT.FIELD.VALUE,1,-1> = Y.L.CR.FACILITY
        END

        R.APP.RECORD<AA.ARR.ACT.FIELD.NAME,1,-1> = 'L.LOAN.STATUS:1:1'
        R.APP.RECORD<AA.ARR.ACT.FIELD.VALUE,1,-1> = Y.L.LOAN.STATUS

        IF Y.ORIGEN.RECURSOS THEN
            R.APP.RECORD<AA.ARR.ACT.FIELD.NAME,1,-1> = 'ORIGEN.RECURSOS:1:1'
            R.APP.RECORD<AA.ARR.ACT.FIELD.VALUE,1,-1> = Y.ORIGEN.RECURSOS
        END
        CHANGE @SM TO @FM IN Y.L.AA.LOAN.DOC
        CHANGE @SM TO @FM IN Y.L.AA.DOC.RECVD
        CHANGE @SM TO @FM IN Y.L.AA.REMARKS
        TOT.MV = DCOUNT(Y.L.AA.LOAN.DOC, @FM)
        MV = 1
        LOOP
        WHILE MV LE TOT.MV
            R.APP.RECORD<AA.ARR.ACT.FIELD.NAME,1,-1> = 'L.AA.LOAN.DOC:':MV:':1'
            R.APP.RECORD<AA.ARR.ACT.FIELD.VALUE,1,-1> = Y.L.AA.LOAN.DOC<MV>
            R.APP.RECORD<AA.ARR.ACT.FIELD.NAME,1,-1> = 'L.AA.DOC.RECVD:':MV:':1'
            R.APP.RECORD<AA.ARR.ACT.FIELD.VALUE,1,-1> = Y.L.AA.DOC.RECVD<MV>
            R.APP.RECORD<AA.ARR.ACT.FIELD.NAME,1,-1> = 'L.AA.REMARKS:':MV:':1'
            R.APP.RECORD<AA.ARR.ACT.FIELD.VALUE,1,-1> = Y.L.AA.REMARKS<MV>
            MV += 1
        REPEAT

        APP.NAME = 'AA.ARRANGEMENT.ACTIVITY'
        OFS.FUNCTION='I'
        PROCESS='PROCESS'
        OFSVERSION='AA.ARRANGEMENT.ACTIVITY,REDO.ADDGEST'
        GTSMODE=''
        NO.OF.AUTH='0'
        TRANSACTION.ID= ''

        OFS.STRING=''

        CALL OFS.BUILD.RECORD(APP.NAME,OFS.FUNCTION,PROCESS,OFSVERSION,GTS.MODE,NO.OF.AUTH,TRANSACTION.ID,R.APP.RECORD,OFS.MESSAGE)

        OFS.DATA = OFS.MESSAGE

        OFS.ID = 'AA.COB'
        CALL OFS.CALL.BULK.MANAGER(OFS.ID,OFS.DATA,RESP,COMM)
****SUCESS OR FAIL MSG EXTRACT****
        IF INDEX(RESP,'//-1/',1) THEN
            Y.RESP.ARRAY1 = FIELD(RESP,'//-1/',2)
            Y.OUT.MSG = FIELD(Y.RESP.ARRAY1,'</request><request>',1)
            Y.OUT.MSG = Y.OUT.MSG[1,60]
        END ELSE
            IF (NOT(Y.OUT.MSG) OR Y.OUT.MSG EQ 'SUCCESS') AND ARR.ID THEN
                Y.OUT.MSG = 'SUCCESS'
            END
        END
****WRITING***
        DELIM = '|'
        FINAL.ARRAY = ''
        FINAL.ARRAY<-1> = ARR.ID:DELIM:Y.OUT.MSG
        WRITE FINAL.ARRAY TO F.TEMP.FILE.PATH,ARR.ID

    END
RETURN

GET.OD.STATUS:
**************
    FN.ACCOUNT = 'F.ACCOUNT'
    F.ACCOUNT = ''
    CALL OPF(FN.ACCOUNT, F.ACCOUNT)

    FN.AA.BILL.DETAILS = 'F.AA.BILL.DETAILS'
    F.AA.BILL.DETAILS = ''
    CALL OPF(FN.AA.BILL.DETAILS,F.AA.BILL.DETAILS)

    FN.AA.ACCOUNT.DETAILS = 'F.AA.ACCOUNT.DETAILS'
    F.AA.ACCOUNT.DETAILS = ''
    CALL OPF(FN.AA.ACCOUNT.DETAILS,F.AA.ACCOUNT.DETAILS)

    FN.AA.ARRANGEMENT = 'F.AA.ARRANGEMENT'
    F.AA.ARRANGEMENT = ''
    CALL OPF(FN.AA.ARRANGEMENT,F.AA.ARRANGEMENT)

    FN.AA.PROPERTY = 'F.AA.PROPERTY'
    F.AA.PROPERTY = ''
    CALL OPF(FN.AA.PROPERTY,F.AA.PROPERTY)



    CALL F.READ(FN.AA.ACCOUNT.DETAILS,ARR.ID,R.ACCOUNT.DETAILS,F.AA.ACCOUNT.DETAILS,ACC.DET.ERR)

    CALL F.READ(FN.AA.ARRANGEMENT,ARR.ID,R.AA.ARRANGEMENT,F.AA.ARRANGEMENT,ARR.ERR)

    R.ACCOUNT.ID = R.AA.ARRANGEMENT<AA.ARR.LINKED.APPL.ID>


    AGING.STATUS = ''

    LOC.REF.FIELDS = 'L.OD.STATUS':@VM:'L.OD.STATUS.2':@FM:'L.OD.STATUS':@VM:'L.OD.STATUS.2':@VM:'L.AA.AGNCY.CODE':@VM:'L.CR.FACILITY':@VM:'L.AA.LOAN.DSN':@VM:'L.LOAN.STATUS':@VM:'ORIGEN.RECURSOS':@VM:'L.AA.LOAN.DOC':@VM:'L.AA.DOC.RECVD':@VM:'L.AA.REMARKS'

    LOC.REF.APP = 'ACCOUNT':@FM:'AA.PRD.DES.ACCOUNT'

    LOC.REF.POS = ''

    CALL MULTI.GET.LOC.REF(LOC.REF.APP, LOC.REF.FIELDS, LOC.REF.POS)

    L.OD.STATUS.POS    = LOC.REF.POS<1,1>
    L.OD.ST.2.POS      = LOC.REF.POS<1,2>
    POS.L.OD.STATUS    = LOC.REF.POS<2,1>
    POS.L.OD.STATUS.2  = LOC.REF.POS<2,2>
    POS.L.AA.AGENCY.CODE = LOC.REF.POS<2,3>
    POS.L.CR.FACILITY = LOC.REF.POS<2,4>
    POS.L.AA.LOAN.DSN = LOC.REF.POS<2,5>
    POS.L.LOAN.STATUS = LOC.REF.POS<2,6>
    POS.ORIGEN.RECURSOS = LOC.REF.POS<2,7>
    POS.L.AA.LOAN.DOC = LOC.REF.POS<2,8>
    POS.L.AA.DOC.RECVD =  LOC.REF.POS<2,9>
    POS.L.AA.REMARKS = LOC.REF.POS<2,10>


    EFF.DATE         = ''
    PROP.CLASS       = 'ACCOUNT'
    PROPERTY         = ''
    R.CONDITION.ACC  = ''
    ERR.MSG          = ''

    APAP.AA.redoCrrGetConditions(ARR.ID,EFF.DATE,PROP.CLASS,PROPERTY,R.CONDITION.ACC,ERR.MSG) ;*MANUAL R22 CODE CONVERSION

    ACC.LOAN.STATUS   = R.CONDITION.ACC<AA.AC.LOCAL.REF,POS.L.OD.STATUS>
    ACC.LOAN.STATUS.2 = R.CONDITION.ACC<AA.AC.LOCAL.REF,POS.L.OD.STATUS.2>

    IF (ACC.LOAN.STATUS NE 'NAB') THEN  ;* Dont check Bills and Trigger Activity if the Arrangement is already in NAB

        GOSUB GET.BILL.INFO

    END

RETURN

GET.BILL.INFO:

    Y.ALL.NTST = ''

    ARR.BILL.IDS = R.ACCOUNT.DETAILS<AA.AD.BILL.ID>         ;* Get the Arrangement BILL ids
    ARR.BILL.STATUS = R.ACCOUNT.DETAILS<AA.AD.BILL.STATUS>  ;* Get the Bill Status for the corresponding BILLS
    ARR.BILL.TYPE = R.ACCOUNT.DETAILS<AA.AD.BILL.TYPE>      ;* Get the Bill Type, Coz only the Scheduled BILLS are aged

    ACC.PROPERTY = 'ACCOUNT'

    CHANGE @VM TO @FM IN ARR.BILL.IDS
    CHANGE @SM TO @FM IN ARR.BILL.IDS

    CHANGE @VM TO @FM IN ARR.BILL.STATUS
    CHANGE @SM TO @FM IN ARR.BILL.STATUS

    CHANGE @VM TO @FM IN ARR.BILL.TYPE
    CHANGE @SM TO @FM IN ARR.BILL.TYPE


    BILL.COUNT = DCOUNT(ARR.BILL.IDS, @FM)

*FOR BILL = 1 TO BILL.COUNT          ;* Loop thru each BILL
    BILL = 1
    BREAK.UP = 0

    LOOP
    WHILE (BILL LE BILL.COUNT) AND (BREAK.UP NE 1) DO

        IF ARR.BILL.STATUS<BILL> NE 'SETTLED' AND ARR.BILL.TYPE<BILL> EQ 'PAYMENT' THEN   ;* Check whether the BILL is in UNPAID status. The first BILL would be

            AGING.STATUS = ''
            GOSUB GET.BILL.DETAILS

            IF (AGING.STATUS) THEN

                BREAK.UP = 1  ;* Break the FOR Loop. No need to loop other bills. We have already got the worst aging status
                Y.ALL.NTST = 'YES'

            END

        END

        BILL += 1 ;*AUTO R22 CODE CONVERSION

    REPEAT



    IF Y.ALL.NTST EQ '' THEN
        IF NOT(AGING.STATUS) OR ACC.LOAN.STATUS NE 'CUR' THEN         ;*If No ageing status is available for the arrangement, then default it to CUR
            AGING.STATUS = 'CUR'
            AGING.STATUS.2 = 'CUR'

        END
    END


    IF Y.ALL.NTST EQ 'YES' THEN
        IF AGING.STATUS EQ 'NAB' THEN
            AGING.STATUS.2 = 'NAB'
        END
        IF NOT(AGING.STATUS.2) THEN
            GOSUB GET.STATUS.2
        END
    END


RETURN

GET.STATUS.2:

    Y.LATEST.BILL = ARR.BILL.IDS<BILL.COUNT>

    BILL -= 1 ;*AUTO R22 CODE CONVERSION

    LOOP
    WHILE BILL LE BILL.COUNT DO
        BILL += 1 ;*AUTO R22 CODE CONVERSION
        IF ARR.BILL.STATUS<BILL> NE 'SETTLED' AND ARR.BILL.TYPE<BILL> EQ 'PAYMENT' THEN
            AGING.STATUS.2 = ''
            Y.BL.BILL = ARR.BILL.IDS<BILL>
            CALL F.READ(FN.AA.BILL.DETAILS,Y.BL.BILL,R.BL.DETAILS,F.AA.BILL.DETAILS,BILL.ERR)
            IF R.BL.DETAILS THEN        ;*PACS00687323
                GOSUB GET.PERF.ST
            END     ;*PACS00687323
        END
    REPEAT

    IF AGING.STATUS.2 EQ '' THEN
        AGING.STATUS.2 = AGING.STATUS
    END

RETURN

GET.PERF.ST:

    LOCATE ACC.PROPERTY IN R.BL.DETAILS<AA.BD.PROPERTY,1> SETTING  ACC.POS THEN


        Y.AGE.ST = R.BL.DETAILS<AA.BD.AGING.STATUS,1>


        IF NOT(Y.AGE.ST) THEN
            AGING.STATUS.2 = 'CUR'
        END ELSE
            IF Y.AGE.ST EQ 'SETTLED' THEN
                AGING.STATUS.2 = 'CUR'
            END ELSE
                AGING.STATUS.2 = Y.AGE.ST
            END
        END
        BILL = BILL.COUNT + 1
    END ELSE
        AGING.STATUS.2 = 'CUR'
    END

RETURN


GET.BILL.DETAILS:

    BILL.ID = ARR.BILL.IDS<BILL>


    CALL F.READ(FN.AA.BILL.DETAILS, BILL.ID, R.BILL.DETAILS, F.AA.BILL.DETAILS, RET.ERR)

    LOCATE ACC.PROPERTY IN R.BILL.DETAILS<AA.BD.PROPERTY,1> SETTING  ACC.POS THEN

        AGING.STATUS = R.BILL.DETAILS<AA.BD.AGING.STATUS,1> ;* This has the worst aging status

        IF AGING.STATUS EQ 'SETTLED' THEN
*            AGING.STATUS = 'CUR'                ;*PACS00687323
            AGING.STATUS = '' ;*PACS00687323
        END

    END

RETURN

GET.ACCT.FLD.DET:
*****************

    CALL F.READ(FN.AA.REF,ARR.ID,R.AA.REF,F.AA.REF,YERR2)
    PROP.LIST = R.AA.REF<1>
    DATE.LIST = R.AA.REF<2>
    CHANGE @VM TO @FM IN PROP.LIST
    CHANGE @VM TO @FM IN DATE.LIST

    LOCATE "ACCOUNT" IN PROP.LIST SETTING PROP.POS THEN
        ACC.DATE.LIST = ''
        ACC.DATE.LIST = DATE.LIST<PROP.POS>
        CHANGE @SM TO @FM IN ACC.DATE.LIST
        TOT.CNT = DCOUNT(ACC.DATE.LIST,@FM)
        CNT = 1
        LOOP
        WHILE CNT LE TOT.CNT
            TEMP.DATE = '' ; FINAL.DATE = ''
            TEMP.DATE = FIELD(ACC.DATE.LIST<CNT>, '.', 1)
            IF MIG.DATE GT TEMP.DATE THEN
                FINAL.DATE = ACC.DATE.LIST<CNT>
                CNT = TOT.CNT + 1
            END
            CNT += 1 ;*AUTO R22 CODE CONVERSION
        REPEAT
    END

    FILE.ID = ARR.ID:"-":"ACCOUNT":"-":FINAL.DATE
    CALL F.READ(FN.AA.ARR.ACCOUNT,FILE.ID,R.AA.ARR.ACCOUNT,F.AA.ARR.ACCOUNT,ACC.ERR)
    IF R.AA.ARR.ACCOUNT THEN
        Y.ORIGINATION.REF =  R.AA.ARR.ACCOUNT<AA.AC.ORIGINATION.REF>
        Y.ANNIVERSARY = R.AA.ARR.ACCOUNT<AA.AC.ANNIVERSARY>
        Y.ALT.ID.LIST = R.AA.ARR.ACCOUNT<AA.AC.ALT.ID>
        Y.L.AA.AGENCY.CODE = R.AA.ARR.ACCOUNT<AA.AC.LOCAL.REF,POS.L.AA.AGENCY.CODE>
        Y.L.CR.FACILITY= R.AA.ARR.ACCOUNT<AA.AC.LOCAL.REF,POS.L.CR.FACILITY>
        Y.L.AA.LOAN.DSN = R.AA.ARR.ACCOUNT<AA.AC.LOCAL.REF,POS.L.AA.LOAN.DSN>
        Y.L.LOAN.STATUS = R.AA.ARR.ACCOUNT<AA.AC.LOCAL.REF,POS.L.LOAN.STATUS>
        Y.ORIGEN.RECURSOS = R.AA.ARR.ACCOUNT<AA.AC.LOCAL.REF,POS.ORIGEN.RECURSOS>
        Y.L.AA.LOAN.DOC = R.AA.ARR.ACCOUNT<AA.AC.LOCAL.REF,POS.L.AA.LOAN.DOC>
        Y.L.AA.DOC.RECVD = R.AA.ARR.ACCOUNT<AA.AC.LOCAL.REF,POS.L.AA.DOC.RECVD>
        Y.L.AA.REMARKS = R.AA.ARR.ACCOUNT<AA.AC.LOCAL.REF,POS.L.AA.REMARKS>

    END
RETURN
END
