* @ValidationCode : MjotMTY3ODU1MzUyMzpDcDEyNTI6MTY4NDg1NDM4Mjk5NTpJVFNTOi0xOi0xOjQ0NjoxOmZhbHNlOk4vQTpSMjFfQU1SLjA6LTE6LTE=
* @ValidationInfo : Timestamp         : 23 May 2023 20:36:22
* @ValidationInfo : Encoding          : Cp1252
* @ValidationInfo : User Name         : ITSS
* @ValidationInfo : Nb tests success  : N/A
* @ValidationInfo : Nb tests failure  : N/A
* @ValidationInfo : Rating            : 446
* @ValidationInfo : Coverage          : N/A
* @ValidationInfo : Strict flag       : true
* @ValidationInfo : Bypass GateKeeper : false
* @ValidationInfo : Compiler Version  : R21_AMR.0
* @ValidationInfo : Copyright Temenos Headquarters SA 1993-2021. All rights reserved.
$PACKAGE APAP.REDOBATCH
SUBROUTINE REDO.B.COMMER.DEBTR.CL(Y.AA.CUS.ID)
*--------------------------------------------------------------------------------------------------
*
* Description           : This is the Batch Main Process Routine used to process the all AA Customer Id
*                         and get the Report Related details and Write the details in file.
*
* Development Reference : CL01
*
*--------------------------------------------------------------------------------------------------
*  M O D I F I C A T I O N S
* ***************************
*--------------------------------------------------------------------------------------------------
* Defect Reference       Modified By                    Date of Change        Change Details
* (RTC/TUT/PACS)
* PACS00466001           Ashokkumar.V.P                 29/06/2016            Initial Release
* Date                   who                   Reference              
* 10-04-2023         CONVERSTION TOOL     R22 AUTO CONVERSTION FM TO @FM AND REMOVED TAM.BP
* 10-04-2023          ANIL KUMAR B        R22 MANUAL CONVERSTION -NO CHANGES
*--------------------------------------------------------------------------------------------------
* Include files
*--------------------------------------------------------------------------------------------------

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.CUSTOMER
    $INSERT I_F.ACCOUNT
    $INSERT I_F.AA.ARRANGEMENT
    $INSERT I_F.DATES
    $INSERT I_TSA.COMMON
    $INSERT I_BATCH.FILES
    $INSERT I_F.REDO.H.REPORTS.PARAM  ;*R22 AUTO CONVERSTION REMOVED TAM.BP
    $INSERT I_F.REDO.CUSTOMER.ARRANGEMENT ;*R22 AUTO CONVERSTION REMOVED TAM.BP
    $INSERT I_REDO.GENERIC.FIELD.POS.COMMON ;*R22 AUTO CONVERSTION REMOVED TAM.BP
    $INSERT I_REDO.B.COMMER.DEBTR.CL.COMMON ;*R22 AUTO CONVERSTION REMOVED TAM.BP

    GOSUB PROCESS
RETURN

PROCESS:
*------
    C$SPARE(451) = ''; C$SPARE(452) = ''; C$SPARE(453) = ''; C$SPARE(454) = ''
    C$SPARE(455) = ''; C$SPARE(456) = ''; C$SPARE(457) = ''; C$SPARE(458) = ''
    C$SPARE(459) = ''; C$SPARE(460) = ''; C$SPARE(461) = ''; C$SPARE(462) = ''
    C$SPARE(463) = ''; C$SPARE(464) = ''; C$SPARE(465) = ''; C$SPARE(466) = ''
    C$SPARE(467) = '';C$SPARE(468) = ''; R.REDO.CUSTOMER.ARRANGEMENT = ''; CUS.ARR.ERR = ''
    CALL F.READ(FN.REDO.CUSTOMER.ARRANGEMENT,Y.AA.CUS.ID,R.REDO.CUSTOMER.ARRANGEMENT,F.REDO.CUSTOMER.ARRANGEMENT,CUS.ARR.ERR)
    IF R.REDO.CUSTOMER.ARRANGEMENT THEN
        Y.AA.OWNER = R.REDO.CUSTOMER.ARRANGEMENT<CUS.ARR.OWNER>
        IF Y.AA.OWNER THEN
            GOSUB GET.FLD.VALUES
        END
    END ELSE
        GOSUB RAISE.ERR.C.22
    END
RETURN

GET.FLD.VALUES:
*--------------
    Y.DCNT.OWNER = DCOUNT(Y.AA.OWNER,@VM)
    Y.STA.COUNT = '1'; Y.WRITE.FLG = 0
    Y.APPROVED.AMT = ''; Y.FIN.ECB.AMT.HIP = '0'; Y.FIN.ECB.AMT.COM = '0'; Y.FIN.ECB.AMT.CONS = '0'
    LOOP
    WHILE Y.STA.COUNT LE Y.DCNT.OWNER
        AA.ARR.ID = Y.AA.OWNER<1,Y.STA.COUNT>
        GOSUB GET.DTLS.AA.ID
        Y.STA.COUNT += 1
        IF Y.WRITE.FLG EQ 1 THEN
            Y.STA.COUNT = Y.DCNT.OWNER + 1
        END
    REPEAT
    IF Y.WRITE.FLG EQ '1' AND R.CUSTOMER THEN
        GOSUB CHK.CUS.DTLS
        GOSUB WRITE.REC.FLE
    END
RETURN

GET.DTLS.AA.ID:
*--------------
    C$SPARE(451) = ''; C$SPARE(452) = ''; C$SPARE(453) = ''; C$SPARE(454) = ''; C$SPARE(455) = ''
    C$SPARE(456) = ''; C$SPARE(457) = ''; R.ARR.APPL = ''; ARR.ERR = ''
    CALL AA.GET.ARRANGEMENT(AA.ARR.ID,R.ARR.APPL,ARR.ERR)
    IF NOT(R.ARR.APPL) THEN
        GOSUB RAISE.ERR.C.22
    END
    Y.MAIN.PRDT.LNE = R.ARR.APPL<AA.ARR.PRODUCT.LINE>
    Y.MAIN.PROD.GROUP = R.ARR.APPL<AA.ARR.PRODUCT.GROUP>
    Y.MAIN.ARR.STATUS = R.ARR.APPL<AA.ARR.ARR.STATUS>
    Y.MAIN.ARR.PRCT = R.ARR.APPL<AA.ARR.PRODUCT>
    Y.MAIN.STRT.DTE = R.ARR.APPL<AA.ARR.START.DATE>

    IF Y.MAIN.PRDT.LNE NE "LENDING" THEN
        RETURN
    END
    IF Y.MAIN.STRT.DTE GT YLST.TODAY THEN
        RETURN
    END
    IF Y.MAIN.ARR.STATUS EQ "CURRENT" OR Y.MAIN.ARR.STATUS EQ "EXPIRED" ELSE
        RETURN
    END
    IF Y.MAIN.PROD.GROUP EQ "COMERCIAL" AND Y.WRITE.FLG NE 1 THEN
        GOSUB CHK.LN.STATUS
        Y.WRITE.FLG = 1
    END
    IF Y.MAIN.PROD.GROUP EQ "LINEAS.DE.CREDITO" AND Y.WRITE.FLG NE 1 THEN
        FINDSTR "COM" IN Y.MAIN.ARR.PRCT SETTING Y.MAIN.PRCT.POS THEN
            GOSUB CHK.LN.STATUS
            Y.WRITE.FLG = 1
        END
    END
RETURN

CHK.LN.STATUS:
*------------
    ARRAY.VAL = ''; Y.LOAN.STATUS = ''; Y.CLOSE.LN.FLG = 0
    CALL REDO.RPT.CLSE.WRITE.LOANS(AA.ARR.ID,R.ARR.APPL,ARRAY.VAL)
    Y.LOAN.STATUS = ARRAY.VAL<1>
    Y.CLOSE.LN.FLG = ARRAY.VAL<2>
    IF Y.LOAN.STATUS EQ "Write-off" THEN
        RETURN
    END
    IF Y.CLOSE.LN.FLG EQ 1 THEN
        RETURN
    END
    Y.CUSTOMER.ID = R.ARR.APPL<AA.ARR.CUSTOMER>
    Y.CURRENCY    = R.ARR.APPL<AA.ARR.CURRENCY>
    Y.LINKED.APPL = R.ARR.APPL<AA.ARR.LINKED.APPL>
    Y.LINKED.APPL.ID = R.ARR.APPL<AA.ARR.LINKED.APPL.ID>
    LOCATE "ACCOUNT" IN Y.LINKED.APPL<1,1> SETTING Y.LINK.POS THEN
        Y.ACCOUNT =Y.LINKED.APPL.ID<Y.LINK.POS>
    END
    R.CUSTOMER = ''; CUS.ERR = ''
    CALL F.READ(FN.CUSTOMER,Y.AA.CUS.ID,R.CUSTOMER,F.CUSTOMER,CUS.ERR)
RETURN

CHK.CUS.DTLS:
*-----------
    OUT.ARR  = ""; Y.CUST.IDEN = ''; Y.CUST.TYPE = ''; Y.CUST.NAME = ''; Y.REL.CODE = ''
    Y.INDUS.CODE = ''; Y.CUST.GN.NAME = ''; Y.L.TIP.CLI = ''; Y.L.LOCALIDAD = ''
    YEXT.TCLI.VAL = ''; Y.L.CU.GRP.RIESGO = ''; YRISK.VAL = ''; NO.DEPDENT = ''
    YL.CU.TOT.ASSET = ''; YL.CU.DATE.INFO = ''; YL.CU.FIN.TYPE = ''; Y.SALARY = ''
    CALL REDO.S.REP.CUSTOMER.EXTRACT(Y.AA.CUS.ID,Y.MAIN.PROD.GROUP,Y.REL.CODE,OUT.ARR)
    Y.CUST.IDEN    = OUT.ARR<1>
    Y.CUST.TYPE    = OUT.ARR<2>
    Y.CUST.NAME    = OUT.ARR<3>
    Y.CUST.GN.NAME = OUT.ARR<4>
    Y.L.TIP.CLI = OUT.ARR<8>
    Y.L.LOCALIDAD = R.CUSTOMER<EB.CUS.LOCAL.REF,L.LOCALIDAD.POS>
    Y.INDUS.CODE = R.CUSTOMER<EB.CUS.LOCAL.REF,L.APAP.INDUSTRY.POS>

    LOCATE Y.L.TIP.CLI IN Y.TCLI.VAL.ARR<1,1> SETTING C.TCLI.POS THEN
        YEXT.TCLI.VAL = Y.TCLI.DIS.ARR<1,C.TCLI.POS>
    END ELSE
        YEXT.TCLI.VAL = "O"
    END
    IF NOT(Y.L.TIP.CLI) THEN
        YEXT.TCLI.VAL = ''
    END

    Y.L.CU.GRP.RIESGO = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.GRP.RIESGO.POS>
    IF Y.L.CU.GRP.RIESGO THEN
        YRISK.VAL = "S"
    END ELSE
        YRISK.VAL = "N"
    END
    NO.DEPDENT = R.CUSTOMER<EB.CUS.NO.OF.DEPENDENTS>
    YL.CU.TOT.ASSET = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.TOT.ASSET.POS>
    YL.CU.DATE.INFO = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.DATE.INFO.POS>
    YL.CU.FIN.TYPE = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.FIN.TYPE.POS>
    Y.SALARY = R.CUSTOMER<EB.CUS.SALARY>
    ERR.CUST.ACCT = ''; R.CUSTOMER.ACCOUNT = ''
    CALL F.READ(FN.CUSTOMER.ACCOUNT,Y.AA.CUS.ID,R.CUSTOMER.ACCOUNT,F.CUSTOMER.ACCOUNT,ERR.CUST.ACCT)
    GOSUB CUST.ACCT.DETAILS
    GOSUB ASSIGN.VALUES
RETURN

CUST.ACCT.DETAILS:
******************
    YSAV.GRP = 0; YTMDEPT.GRP = 0; YNEGVAL.GRP = 0; YCURR.GRP = 0
    LOOP
        REMOVE ACCT.ID FROM R.CUSTOMER.ACCOUNT SETTING CUST.POSN
    WHILE ACCT.ID:CUST.POSN

        ERR.ACCOUNT = ''; R.ACCOUNT = ''; YCATEG = ''; C.SCATG.POS = ''
        C.TMDEPT.POS = ''; C.NEGVAL.POS = ''; C.CURR.POS = ''
        CALL F.READ(FN.ACCOUNT,ACCT.ID,R.ACCOUNT,F.ACCOUNT,ERR.ACCOUNT)
        YCATEG = R.ACCOUNT<AC.CATEGORY>

        LOCATE YCATEG IN Y.SAVING.VAL.ARR<1,1> SETTING C.SCATG.POS THEN
            YSAV.GRP +=1
            CONTINUE
        END
        LOCATE YCATEG IN Y.TMDEPT.VAL.ARR<1,1> SETTING C.TMDEPT.POS THEN
            YTMDEPT.GRP +=1
            CONTINUE
        END
        LOCATE YCATEG IN Y.NEGVAL.VAL.ARR<1,1> SETTING C.NEGVAL.POS THEN
            YNEGVAL.GRP +=1
            CONTINUE
        END
        LOCATE YCATEG IN Y.CURR.VAL.ARR<1,1> SETTING C.CURR.POS THEN
            YCURR.GRP +=1
        END

    REPEAT
RETURN

ASSIGN.VALUES:
*------------
    C$SPARE(451) = Y.CUST.IDEN
    C$SPARE(452) = Y.CUST.TYPE
    C$SPARE(453) = Y.CUST.NAME
    C$SPARE(454) = Y.CUST.GN.NAME
    C$SPARE(455) = Y.L.TIP.CLI
    C$SPARE(456) = Y.L.LOCALIDAD
    C$SPARE(457) = YCURR.GRP
    C$SPARE(458) = YSAV.GRP
    C$SPARE(459) = YTMDEPT.GRP
    C$SPARE(460) = YNEGVAL.GRP
    C$SPARE(461) = Y.INDUS.CODE
    C$SPARE(462) = YEXT.TCLI.VAL
    C$SPARE(463) = Y.L.CU.GRP.RIESGO
    C$SPARE(464) = NO.DEPDENT
    C$SPARE(465) = YL.CU.TOT.ASSET
    C$SPARE(466) = Y.SALARY
    C$SPARE(467) = YL.CU.DATE.INFO
    C$SPARE(468) = YL.CU.FIN.TYPE
RETURN

WRITE.REC.FLE:
*------------
    MAP.FMT = "MAP"
    Y.MAP.ID = "REDO.RCL.CL01"
    Y.RCL.APPL = FN.CUSTOMER
    Y.RCL.AA.ID = Y.AA.CUS.ID
    CALL RAD.CONDUIT.LINEAR.TRANSLATION(MAP.FMT,Y.MAP.ID,Y.RCL.APPL,Y.RCL.AA.ID,R.CUSTOMER,R.RETURN.MSG,ERR.MSG)
    CALL F.WRITE(FN.DR.REG.CL01.WORKFILE,AA.ARR.ID,R.RETURN.MSG)
RETURN
RAISE.ERR.C.22:
*-------------
    MON.TP = "CL01"
    Y.ERR.MSG = "Record not found"
    REC.CON = "CL01-":Y.AA.CUS.ID:Y.ERR.MSG
    DESC = "CL01-":Y.AA.CUS.ID:Y.ERR.MSG
    INT.CODE = 'REP001'
    INT.TYPE = 'ONLINE'
    BAT.NO = ''
    BAT.TOT = ''
    INFO.OR = ''
    INFO.DE = ''
    ID.PROC = ''
    EX.USER = ''
    EX.PC = ''
    CALL REDO.INTERFACE.REC.ACT(INT.CODE,INT.TYPE,BAT.NO,BAT.TOT,INFO.OR,INFO.DE,ID.PROC,MON.TP,DESC,REC.CON,EX.USER,EX.PC)
RETURN
END
