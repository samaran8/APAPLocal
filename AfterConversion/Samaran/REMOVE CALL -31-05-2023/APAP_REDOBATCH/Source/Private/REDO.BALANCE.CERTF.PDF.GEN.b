* @ValidationCode : Mjo3MzEwMDU2MDpDcDEyNTI6MTY4NDg1NDQwMzYzMDpJVFNTOi0xOi0xOjEwNjE6MTpmYWxzZTpOL0E6UjIxX0FNUi4wOi0xOi0x
* @ValidationInfo : Timestamp         : 23 May 2023 20:36:43
* @ValidationInfo : Encoding          : Cp1252
* @ValidationInfo : User Name         : ITSS
* @ValidationInfo : Nb tests success  : N/A
* @ValidationInfo : Nb tests failure  : N/A
* @ValidationInfo : Rating            : 1061
* @ValidationInfo : Coverage          : N/A
* @ValidationInfo : Strict flag       : true
* @ValidationInfo : Bypass GateKeeper : false
* @ValidationInfo : Compiler Version  : R21_AMR.0
* @ValidationInfo : Copyright Temenos Headquarters SA 1993-2021. All rights reserved.
$PACKAGE APAP.REDOBATCH
SUBROUTINE REDO.BALANCE.CERTF.PDF.GEN(Y.ARRAY)
***********************************************************************
* COMPANY NAME: ASOCIACION POPULAR DE AHORROS Y PRESTAMOS
* DEVELOPED BY: H GANESH
* PROGRAM NAME: REDO.BALANCE.CERTF.PDF.GEN
* ODR NO      : ODR-2009-10-0838
*----------------------------------------------------------------------
*DESCRIPTION: This routine is attched in NOFILE ENQUIRY
* to get the details of the Product selected for LETTER

*IN PARAMETER:  NA
*OUT PARAMETER: NA
*LINKED WITH: REDO.LETTER.ISSUE
*----------------------------------------------------------------------
* Modification History :
*-----------------------
*DATE           WHO           REFERENCE         DESCRIPTION
*18.03.2010  H GANESH     ODR-2009-10-0838   INITIAL CREATION
*24.08.2011  Sudharsanan   PACS00120764   Get the id value of REDO.ISSUE.LETTER and get the customer and product values
*10.09.2011  riyas            PACS00121225   get only one documentation type either LEGAL.ID or CEDULA
*21.09.2011  SUDHARSANAN S   PACS00131241     Get the rnc /cedula/passport based on L.CU.TIPO.CL
* Date                   who                   Reference              
* 17-04-2023         CONVERSTION TOOL     R22 AUTO CONVERSTION - FM TO @FM AND VM TO @VM AND COMMENTED I_F.AA.TERM.AMOUNT AND ADDED IF E EQ "EB-UNKNOWN.VARIABLE" THEN VARIABLE NULL AND END 
* 17-04-2023          ANIL KUMAR B        R22 MANUAL CONVERSTION -NO CHANGES

*----------------------------------------------------------------------


    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.ACCOUNT
    $INSERT I_F.AA.TERM.AMOUNT
    $INSERT I_F.REDO.LETTER.ISSUE
    $INSERT I_F.REDO.OFFICERS.LIST
    $INSERT I_F.TELLER
    $INSERT I_F.CUSTOMER
    $INSERT I_System
    $INSERT I_F.AA.ARRANGEMENT
    $INSERT I_F.FUNDS.TRANSFER
    $INSERT I_F.AA.CHARGE.DETAILS
    $INSERT I_F.AA.ARRANGEMENT.ACTIVITY
    $INSERT I_F.REDO.H.AA.DIS.CHG
*   $INSERT I_F.AA.TERM.AMOUNT  ;*R22 AUTO CONVERSTION ADDED COMMENT I_F.AA.TERM.AMOUNT
    $INSERT I_F.AA.ACCOUNT.DETAILS
    $INSERT I_F.AA.BILL.DETAILS
    $INSERT I_F.REDO.OFS.PARAM
    $INSERT I_F.ACCT.ACTIVITY


    GOSUB INIT
    GOSUB OPENFILES
    GOSUB LOCAL.REF
    GOSUB PROCESS
RETURN

*----------------------------------------------------------------------
INIT:
*----------------------------------------------------------------------
    FN.ACCOUNT='F.ACCOUNT'
    F.ACCOUNT=''
    FN.ACCOUNT.HIS = 'F.ACCOUNT$HIS'
    F.ACCOUNT.HIS = ''
    FN.REDO.OFFICERS.LIST='F.REDO.OFFICERS.LIST'
    F.REDO.OFFICERS.LIST=''
    FN.REDO.LETTER.ISSUE='F.REDO.LETTER.ISSUE'
    F.REDO.LETTER.ISSUE=''
    FN.CUSTOMER='F.CUSTOMER'
    F.CUSTOMER=''
    FN.REDO.LOAN.PAYOFF.DATE = 'F.REDO.LOAN.PAYOFF.DATE'
    F.REDO.LOAN.PAYOFF.DATE = ''
    Y.RNC.NO = ''
RETURN

*----------------------------------------------------------------------
OPENFILES:
*----------------------------------------------------------------------
    CALL OPF(FN.ACCOUNT,F.ACCOUNT)
    CALL OPF(FN.ACCOUNT.HIS,F.ACCOUNT.HIS)
    CALL OPF(FN.CUSTOMER,F.CUSTOMER)
    CALL OPF(FN.REDO.OFFICERS.LIST,F.REDO.OFFICERS.LIST)
    CALL OPF(FN.REDO.LETTER.ISSUE,F.REDO.LETTER.ISSUE)
    CALL OPF(FN.REDO.LOAN.PAYOFF.DATE,F.REDO.LOAN.PAYOFF.DATE)
RETURN

*----------------------------------------------------------------------
LOCAL.REF:
*----------------------------------------------------------------------

    LOC.REF.APPLICATION="ACCOUNT":@FM:"TELLER":@FM:"CUSTOMER"
    LOC.REF.FIELDS='L.AC.STATUS1':@FM:'L.LETTER.ID':@FM:'L.CU.CIDENT':@VM:'L.CU.TIPO.CL':@VM:'L.CU.RNC'
    LOC.REF.POS=''
    CALL MULTI.GET.LOC.REF(LOC.REF.APPLICATION,LOC.REF.FIELDS,LOC.REF.POS)
    POS.L.AC.STATUS1=LOC.REF.POS<1,1>
    POS.L.LETTER.ID=LOC.REF.POS<2,1>
    POS.L.CU.CIDENT=LOC.REF.POS<3,1>
    POS.L.CU.TIPO.CL = LOC.REF.POS<3,2>
    POS.L.CU.RNC = LOC.REF.POS<3,3>
RETURN
*----------------------------------------------------------------------
PROCESS:
*----------------------------------------------------------------------
* Get the details of Customer and Product Details
*PACS00120764 - S

    IF APPLICATION EQ 'TELLER' THEN
        Y.LETTER.BAL.ISSUE.ID=R.NEW(TT.TE.LOCAL.REF)<1,POS.L.LETTER.ID>
        CALL F.READ(FN.REDO.LETTER.ISSUE,Y.LETTER.BAL.ISSUE.ID,R.LETTER.BAL.ISSUE,F.REDO.LETTER.ISSUE,LETTER.ERR)
    END ELSE
        Y.LETTER.BAL.ISSUE.ID = System.getVariable("CURRENT.BAL")
        IF E EQ "EB-UNKNOWN.VARIABLE" THEN  ;*R22 AUTO CONVERSTION ADDED IF E EQ "EB-UNKNOWN.VARIABLE" THEN VARIABLE NULL AND END
            Y.LETTER.BAL.ISSUE.ID = ""
        END
        CALL F.READ(FN.REDO.LETTER.ISSUE,Y.LETTER.BAL.ISSUE.ID,R.LETTER.BAL.ISSUE,F.REDO.LETTER.ISSUE,ERR.IS)
    END
    Y.RECIPIENT.NAME=R.LETTER.BAL.ISSUE<REDO.LET.ISS.RECIPIENT>
    Y.RECIPIENT.CITY=R.LETTER.BAL.ISSUE<REDO.LET.ISS.RECIPIENT.CITY>
    Y.CUSTOMER.ID=R.LETTER.BAL.ISSUE<REDO.LET.ISS.CUSTOMER.ID>
    CALL F.READ(FN.CUSTOMER,Y.CUSTOMER.ID,R.CUSTOMER,F.CUSTOMER,CUS.ERR)
    Y.TIPO.CL = R.CUSTOMER<EB.CUS.LOCAL.REF,POS.L.CU.TIPO.CL>
    Y.CU.NAME=R.LETTER.BAL.ISSUE<REDO.LET.ISS.CU.NAME>
    IF Y.TIPO.CL EQ 'PERSONA FISICA' THEN
        Y.CUDELA= R.CUSTOMER<EB.CUS.LOCAL.REF,POS.L.CU.CIDENT>
        IF NOT(Y.CUDELA) THEN
            Y.CUDELA = R.CUSTOMER<EB.CUS.LEGAL.ID,1>
        END
    END ELSE
        IF Y.TIPO.CL EQ 'PERSONA JURIDICA' THEN
            Y.CUDELA= R.CUSTOMER<EB.CUS.LOCAL.REF,POS.L.CU.RNC>
        END
    END

    Y.PRODUCT=R.LETTER.BAL.ISSUE<REDO.LET.ISS.PRODUCTS>
    Y.PRODUCT.CNT=DCOUNT(Y.PRODUCT,@VM)
    VAR1=1
    LOOP
    WHILE VAR1 LE Y.PRODUCT.CNT
        Y.AA.PRODUCT.ID=Y.PRODUCT<1,VAR1>
        IN.ACC.ID=Y.AA.PRODUCT.ID
*Y.ARR.ID=''
*CALL REDO.CONVERT.ACCOUNT(IN.ACC.ID,Y.ARR.ID,OUT.ID,ERR.TEXT)
*Y.PRODUCT.ID=OUT.ID
        CALL EB.READ.HISTORY.REC(F.ACCOUNT.HIS,IN.ACC.ID,R.ACCOUNT,ACC.ERR)
        Y.OPENING.DATE=R.ACCOUNT<AC.OPENING.DATE>
        IF Y.OPENING.DATE NE '' THEN
            Y.OPENING.DATE=Y.OPENING.DATE[7,2]:'/':Y.OPENING.DATE[5,2]:'/':Y.OPENING.DATE[1,4]
        END

        ACCOUNT.ID = FIELDS(IN.ACC.ID,';',1)
        BALANCE.TO.CHECK = 'TOTCOMMITMENT'
        DATE.OPTIONS = ''
        EFFECTIVE.DATE = TODAY
        DATE.OPTIONS<4>  = 'ECB'
        BALANCE.AMOUNT = ""
        CALL AA.GET.PERIOD.BALANCES(ACCOUNT.ID, BALANCE.TO.CHECK, DATE.OPTIONS, EFFECTIVE.DATE, "", "", BAL.DETAILS, "")
        Y.BALANCE = ABS(BAL.DETAILS<IC.ACT.BALANCE>)

        IF Y.BALANCE EQ '' THEN
            Y.BALANCE=0
        END
        Y.BALANCE=FMT(Y.BALANCE,"L2,RD$#15")
*****************************PACS00208729 - S
*Y.MATURITY.DATE = R.ACCOUNT<AC.CLOSURE.DATE>
        CALL F.READ(FN.REDO.LOAN.PAYOFF.DATE,ACCOUNT.ID,R.REDO.LOAN.PAYOFF.DATE,F.REDO.LOAN.PAYOFF.DATE,PAYOFF.ERR)
        Y.MATURITY.DATE = FIELD(R.REDO.LOAN.PAYOFF.DATE,'-',2)
*******************************PACS00208729-E
        IF Y.MATURITY.DATE NE '' THEN
            Y.MATURITY.DATE=Y.MATURITY.DATE[7,2]:'/':Y.MATURITY.DATE[5,2]:'/':Y.MATURITY.DATE[1,4]
        END
        Y.PROD.NO=FMT(Y.AA.PRODUCT.ID,"L#12")
        Y.ARRAY1:=Y.PROD.NO:":":Y.OPENING.DATE:":":Y.BALANCE:":":Y.MATURITY.DATE:"@":@VM
        VAR1 += 1
    REPEAT
    Y.OFFICER.CODE=R.LETTER.BAL.ISSUE<REDO.LET.ISS.ISS.OFFICER>
    CALL F.READ(FN.REDO.OFFICERS.LIST,Y.OFFICER.CODE,R.REDO.OFFICERS.LIST,F.REDO.OFFICERS.LIST,OFF.ERR)
    Y.OFFICER.NAME=R.REDO.OFFICERS.LIST<REDO.OFF.LIS.OFFICER.NAME>
    Y.OFFICER.POSITION=R.REDO.OFFICERS.LIST<REDO.OFF.LIS.DESIGNATION>
    Y.OFFICER.DEPT=R.REDO.OFFICERS.LIST<REDO.OFF.LIS.DEPT.BRANCH>
    Y.DEBIT.ACC=R.LETTER.BAL.ISSUE<REDO.LET.ISS.CHARGE.LIQ.ACT>
    Y.CHARGE.AMT=R.LETTER.BAL.ISSUE<REDO.LET.ISS.CHARGE.AMT>
    Y.CHARGE.AMT=FMT(Y.CHARGE.AMT,"L2,RD$#25")
    Y.CHARGE.CCY=R.LETTER.BAL.ISSUE<REDO.LET.ISS.CHARGE.CCY>
    Y.ARRAY=TODAY[7,2]:'/':TODAY[5,2]:'/':TODAY[1,4]:"#":Y.RECIPIENT.NAME:"#":Y.RECIPIENT.CITY:"#":Y.CU.NAME:"#":Y.CUDELA:"#":Y.RNC.NO:"#":Y.ARRAY1:"#":Y.OFFICER.NAME:"#":Y.OFFICER.POSITION:"#":Y.OFFICER.DEPT:"#":Y.DEBIT.ACC:"#":Y.CHARGE.AMT:"#":Y.CHARGE.CCY
*PACS00120764 - E
RETURN
*----------------------------------------------------------------------------------
END
