$PACKAGE APAP.REDOENQ
SUBROUTINE NOFILE.REDO.CR.LOG.INTERACT(Y.CLAIM.ARR)
*-----------------------------------------------------------------------------
*DESCRIPTION:
*------------
* This is Nofile routine to to show the log of interactions of REDO.APAP.CLAIMS,
* REDO.APAP.REQUESTS,REDO.APAP.COMPLAINTS
* This development is for ODR Reference ODR-2009-12-0283
* Input/Output:
*--------------
* IN  : Y.CLAIM.ARR
* OUT : Y.CLAIM.ARR
* Dependencies:
*---------------
* CALLS     : -NA-
* CALLED BY : -NA-
* Revision History:
*------------------------------------------------------------------------------------------
* Date              who              Reference            Description
* 27-JUL-2010       B Renugadevi     ODR-2009-12-0283    Initial Creation
*  DATE             WHO                   REFERENCE 
* 06-APRIL-2023      Conversion Tool       R22 Auto Conversion  - VM to @VM , FM to @FM 
* 06-APRIL-2023      Harsha                R22 Manual Conversion - No changes
*------------------------------------------------------------------------------------------

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_ENQUIRY.COMMON
    $INSERT I_F.CUSTOMER
    $INSERT I_F.CR.CONTACT.LOG
    $INSERT I_F.REDO.ISSUE.CLAIMS
    $INSERT I_F.REDO.ISSUE.REQUESTS
    $INSERT I_F.REDO.ISSUE.COMPLAINTS
    $INSERT I_F.REDO.SLA.PARAM

    GOSUB INIT
    GOSUB PROCESS
RETURN
*****
INIT:
*****

    FN.REDO.ISSUE.CLAIMS     = 'F.REDO.ISSUE.CLAIMS'
    F.REDO.ISSUE.CLAIMS      = ''
    CALL OPF(FN.REDO.ISSUE.CLAIMS,F.REDO.ISSUE.CLAIMS)

    FN.REDO.ISSUE.REQUESTS   = 'F.REDO.ISSUE.REQUESTS'
    F.REDO.ISSUE.REQUESTS    = ''
    CALL OPF(FN.REDO.ISSUE.REQUESTS,F.REDO.ISSUE.REQUESTS)

    FN.REDO.ISSUE.COMPLAINTS = 'F.REDO.ISSUE.COMPLAINTS'
    F.REDO.ISSUE.COMPLAINTS  = ''
    CALL OPF(FN.REDO.ISSUE.COMPLAINTS,F.REDO.ISSUE.COMPLAINTS)

    FN.CUSTOMER              = 'F.CUSTOMER'
    F.CUSTOMER               = ''
    CALL OPF(FN.CUSTOMER,F.CUSTOMER)

    FN.REDO.SLA.PARAM        = 'F.REDO.SLA.PARAM'
    F.REDO.SLA.PARAM         = ''
    CALL OPF(FN.REDO.SLA.PARAM,F.REDO.SLA.PARAM)

    FN.CR.CONTACT.LOG        = 'F.CR.CONTACT.LOG'
    F.CR.CONTACT.LOG         = ''
    CALL OPF(FN.CR.CONTACT.LOG,F.CR.CONTACT.LOG)

    LREF.APPL                = 'CUSTOMER':@FM:'CR.CONTACT.LOG'
    LREF.FIELDS              = 'L.CU.SEGMENTO':@FM:'L.CR.TYPE':@VM:'L.CR.COMMENT':@VM:'L.CR.CHANNEL':@VM:'L.CR.DATE':@VM:'L.CR.USER'
    LREF.POS                 = ''
    CALL MULTI.GET.LOC.REF(LREF.APPL,LREF.FIELDS,LREF.POS)
    L.CU.SEGMENTO.POS        = LREF.POS<1,1>
    L.CR.TYPE.POS            = LREF.POS<2,1>
    L.CR.COMMENT.POS         = LREF.POS<2,2>
    L.CR.CHANNEL.POS         = LREF.POS<2,3>
    L.CR.DATE.POS            = LREF.POS<2,4>
    L.CR.USER.POS            = LREF.POS<2,5>

RETURN

********
PROCESS:
********

    Y.SEL.CMD = "SELECT ":FN.CR.CONTACT.LOG
    LOCATE "SEGMENT" IN D.FIELDS<1> SETTING SEG.POS THEN
        Y.SEG   = D.RANGE.AND.VALUE<SEG.POS>
        IF Y.SEG NE '' THEN
            SEL.CMD = "SELECT ":FN.CUSTOMER:" WITH L.CU.SEGMENTO EQ ":Y.SEG
            CALL EB.READLIST(SEL.CMD,SEL.LIST,'',NOR,RET.CODE)
            CUSTOMER.LIST1 = SEL.LIST
            CHANGE @FM TO 'EQ' IN CUSTOMER.LIST1
            Y.SEL.CMD :=" AND WITH  CONTRACT.CLIENT EQ":" ":CUSTOMER.LIST1
        END
    END
*
    LOCATE "CUSTOMER" IN D.FIELDS<1> SETTING CUST.POS THEN
        Y.CUST = D.RANGE.AND.VALUE<CUST.POS>
        Y.SEL.CMD :=" AND WITH CONTACT.CLIENT EQ":" ":Y.CUST
    END
*
    LOCATE "BRANCH" IN D.FIELDS<1> SETTING BR.POS THEN
        Y.BRANCH = D.RANGE.AND.VALUE<BR.POS>
        IF Y.BRANCH NE '' THEN
            SEL.CMD1 = "SELECT ":FN.REDO.ISSUE.CLAIMS: " WITH BRANCH EQ ":Y.BRANCH
            CALL EB.READLIST(SEL.CMD1,SEL.LIST1,'',NOR1,RET.CODE)
            SEL.CMD2 = "SELECT ":FN.REDO.ISSUE.REQUESTS: " WITH BRANCH EQ ":Y.BRANCH
            CALL EB.READLIST(SEL.CMD2,SEL.LIST2,'',NOR2,RET.CODE)
            SEL.CMD3 = "SELECT ":FN.REDO.ISSUE.COMPLAINTS: " WITH BRANCH EQ ":Y.BRANCH
            CALL EB.READLIST(SEL.CMD3,SEL.LIST3,'',NOR3,RET.CODE)
            ID.LIST1 = SEL.LIST1:@FM:SEL.LIST2:@FM:SEL.LIST3
            CHANGE @FM TO 'EQ' IN ID.LIST1
            Y.SEL.CMD :=" AND WITH CONTRACT.ID EQ":" ":ID.LIST1
        END
    END
*
    LOCATE "ACCOUNT.OFFICER" IN D.FIELDS<1> SETTING OFF.POS THEN
        Y.OFF   = D.RANGE.AND.VALUE<OFF.POS>
        IF Y.OFF NE '' THEN

            SEL.CMD1 = "SELECT ":FN.CUSTOMER:" WITH ACCOUNT.OFFICER EQ ":Y.OFF
            CALL EB.READLIST(SEL.CMD1,SEL.LIST1,'',NOR1,RET.CODE)
            SEL.CMD2 = "SELECT ":FN.REDO.ISSUE.REQUESTS: " WITH ACCOUNT.OFFICER EQ ":Y.OFF
            CALL EB.READLIST(SEL.CMD2,SEL.LIST2,'',NOR2,RET.CODE)
            SEL.CMD3 = "SELECT ":FN.REDO.ISSUE.COMPLAINTS: " WITH ACCOUNT.OFFICER EQ ":Y.OFF
            CALL EB.READLIST(SEL.CMD3,SEL.LIST3,'',NOR3,RET.CODE)
            CUSTOMER.LIST1 = SEL.LIST1:@FM:SEL.LIST2:@FM:SEL.LIST3
            CHANGE @FM TO 'EQ' IN CUSTOMER.LIST1
            Y.SEL.CMD :=" AND WITH  CONTRACT.CLIENT EQ":" ":CUSTOMER.LIST1
        END
    END
*
    LOCATE "DATE" IN D.FIELDS<1> SETTING DT.POS THEN
        Y.DATE = D.RANGE.AND.VALUE<DT.POS>
        Y.SEL.CMD :=" AND WITH DATE EQ":" ":Y.DATE
    END
*
    LOCATE "TYPE" IN D.FIELDS<1> SETTING TY.POS THEN
        Y.TYPE = D.RANGE.AND.VALUE<TY.POS>
        Y.SEL.CMD :=" AND WITH L.CR.TYPE EQ":" ":Y.TYPE
    END
*
    CALL EB.READLIST(Y.SEL.CMD,SEL.LIST,"",NO.OF.REC,RET.CODE)
    LOOP
        REMOVE Y.CR.ID FROM SEL.LIST SETTING CR.POS
    WHILE Y.CR.ID:CR.POS

        CALL F.READ(FN.CR.CONTACT.LOG, Y.CR.ID, R.CR.CONTACT.LOG, F.CR.CONTACT.LOG, CR.ERR)
        IF R.CR.CONTACT.LOG THEN
            Y.CLIENT      = R.CR.CONTACT.LOG<CR.CONT.LOG.CONTACT.CLIENT>
            CALL F.READ(FN.CUSTOMER, Y.CLIENT, R.CUSTOMER, F.CUSTOMER, CUST.ERR)
            IF R.CUSTOMER THEN
                Y.SEGMENT = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.SEGMENTO.POS>
            END
            Y.TYPE        = R.CR.CONTACT.LOG<CR.CONT.LOG.LOCAL.REF,L.CR.TYPE.POS>
            Y.DATE        = R.CR.CONTACT.LOG<CR.CONT.LOG.CONTACT.DATE>
            Y.CONTACT.ID  = R.CR.CONTACT.LOG<CR.CONT.LOG.CONTRACT.ID>
            Y.CASE.ID     = Y.CONTACT.ID[1,2]

            GOSUB FETCH.ISSUE.DETAILS
        END
        Y.CLAIM.ARR<-1> = Y.SEGMENT:"*":Y.CLIENT:"*":Y.BRANCH:"*":Y.ACCT.OFFICER:"*":Y.DATE:"*":Y.TYPE
    REPEAT
RETURN

********************
FETCH.ISSUE.DETAILS:
********************

    IF Y.CASE.ID EQ "57" THEN
        Y.ID1            = 0:Y.CONTACT.ID[1,3]
        Y.ID2            = Y.CONTACT.ID[4,4]
        Y.ID3            = Y.CONTACT.ID[8,2]
        Y.ID4            = Y.CONTACT.ID[10,5]
        Y.ID             = Y.ID1:'-':Y.ID2:'-':Y.ID3:'-':Y.ID4

        CALL F.READ(FN.REDO.ISSUE.CLAIMS, Y.ID, R.REDO.ISSUE.CLAIMS, F.REDO.ISSUE.CLAIMS, CLAIM.ERR)
        Y.BRANCH       = R.REDO.ISSUE.CLAIMS<ISS.CL.BRANCH>
        Y.ACCT.OFFICER = R.REDO.ISSUE.CLAIMS<ISS.CL.ACCOUNT.OFFICER>
    END ELSE
        IF Y.CASE.ID EQ "99" THEN
            CALL F.READ(FN.REDO.ISSUE.REQUESTS, Y.CONTACT.ID, R.REDO.ISSUE.REQUESTS, F.REDO.ISSUE.REQUESTS, REQ.ERR)
            Y.BRANCH       = R.REDO.ISSUE.REQUESTS<ISS.REQ.BRANCH>
            Y.ACCT.OFFICER = R.REDO.ISSUE.REQUESTS<ISS.REQ.ACCOUNT.OFFICER>
        END ELSE
            IF Y.CASE.ID EQ "88" THEN
                CALL F.READ(FN.REDO.ISSUE.COMPLAINTS, Y.CONTACT.ID, R.REDO.ISSUE.COMPLAINTS, F.REDO.ISSUE.COMPLAINTS, COMP.ERR)
                Y.BRANCH       = R.REDO.ISSUE.COMPLAINTS<ISS.COMP.BRANCH>
                Y.ACCT.OFFICER = R.REDO.ISSUE.COMPLAINTS<ISS.COMP.ACCOUNT.OFFICER>
            END
        END
    END
RETURN
END
