$PACKAGE APAP.REDOENQ
SUBROUTINE REDO.NOF.PLASTIC.CARD.GNB130.BCK(Y.FINAL.ARRAY)
*-------------------------------------------------------------------------
*Company Name : APAP Bank
*Developed By : Temenos Application Management
*Program Name : REDO.NOF.PLASTIC.CARD.GNB130.BCK
*Date : 12.11.2010
*-------------------------------------------------------------------------
* Incoming/Outgoing Parameters
*-------------------------------
* In : --N/A--
* Out : Y.FINAL.ARRAY
*-----------------------------------------------------------------------------
* Revision History:
* -----------------
* Date Name Reference Version
* ------- ---- ---------- --------
* 1/11/2010 Riyas Ahamad Basha J ODR-2010-03-0108 Initial Version
*-------------------------------------------------------------------------
*------------------------------------------------------------------------
* Modification History :
*------------------------------------------------------------------------
*  DATE             WHO                   REFERENCE                  
* 13-APRIL-2023      Conversion Tool       R22 Auto Conversion - VM to @VM , ++ to += , -- to -= , ! to * , I to I.VAR and SM to @SM
* 13-APRIL-2023      Harsha                R22 Manual Conversion - No changes                             
*------------------------------------------------------------------------
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_ENQUIRY.COMMON
    $INSERT I_F.ACCOUNT
    $INSERT I_F.STOCK.ENTRY
    $INSERT I_F.REDO.CARD.REQUEST
    $INSERT I_F.REDO.CARD.GENERATION
    $INSERT I_F.REDO.CARD.PRINT.LOST
    $INSERT I_F.REDO.CARD.DAMAGE
    $INSERT I_F.LATAM.CARD.ORDER
    $INSERT I_F.REDO.CARD.REORDER.DEST
    $INSERT I_F.REDO.CARD.DES.HIS


    GOSUB INIT
    GOSUB OPENFILES
    GOSUB PROCESS
RETURN
*------------------------------------------------------------------------------------
INIT:
*------------------------------------------------------
    GOSUB FILE.INIT
    GOSUB VAR.INIT
RETURN
*---------------------------------------------------------------------------------------
FILE.INIT:
*---------------
* initialise the files

    FN.ACCOUNT='F.ACCOUNT'
    F.ACCOUNT=''

    FN.REDO.CARD.REQUEST='F.REDO.CARD.REQUEST'
    F.REDO.CARD.REQUEST=''

    FN.REDO.CARD.GENERATION='F.REDO.CARD.GENERATION'
    F.REDO.CARD.GENERATION=''

    FN.STOCK.ENTRY='F.STOCK.ENTRY'
    F.STOCK.ENTRY=''

    FN.REDO.CARD.DAMAGE='F.REDO.CARD.DAMAGE'
    F.REDO.CARD.DAMAGE=''

    FN.REDO.CARD.DAMAGE.HIS='F.REDO.CARD.DAMAGE$HIS'
    F.REDO.CARD.DAMAGE.HIS =''

    FN.REDO.CARD.REQUEST.HIS='F.REDO.CARD.REQUEST$HIS'
    F.REDO.CARD.REQUEST.HIS=''
    CALL OPF(FN.REDO.CARD.REQUEST.HIS,F.REDO.CARD.REQUEST.HIS)

    FN.LATAM.CARD.ORDER='F.LATAM.CARD.ORDER'
    F.LATAM.CARD.ORDER=''

    FN.REDO.CARD.REORDER.DEST='F.REDO.CARD.REORDER.DEST'
    F.REDO.CARD.REORDER.DEST=''

    FN.CARD.ISSUE='F.CARD.ISSUE'
    F.CARD.ISSUE=''

    FN.LATAM.CARD.ORDER.HIS='F.LATAM.CARD.ORDER$HIS'
    F.LATAM.CARD.ORDER.HIS=''

* FN.REDO.CARD.REORDER.DEST.HIS='F.REDO.CARD.REORDER.DEST$HIS'
* F.REDO.CARD.REORDER.DEST.HIS=''


    FN.REDO.CARD.DES.HIS='F.REDO.CARD.DES.HIS'
    F.REDO.CARD.DES.HIS =''

    FN.REDO.CARD.PRINT.LOST='F.REDO.CARD.PRINT.LOST'
    F.REDO.CARD.PRINT.LOST =''
    CALL OPF(FN.REDO.CARD.PRINT.LOST,F.REDO.CARD.PRINT.LOST)

    FN.REDO.CARD.PRINT.LOST.HIS='F.REDO.CARD.PRINT.LOST$HIS'
    F.REDO.CARD.PRINT.LOST.HIS =''
    CALL OPF(FN.REDO.CARD.PRINT.LOST,F.REDO.CARD.PRINT.LOST)

RETURN
*--------------------------------------------------------------------------------------------
VAR.INIT:
*------------

* intialise the variables

    Y.CARD.TYPE='' ; Y.CARD.NUMBER='' ; Y.REQ.NUMBER='' ; Y.REQ.STATUS='' ; Y.DUE.DATE='' ; Y.BRANCH='' ; Y.REQ.DATE=''
    Y.REQ.USER='' ; Y.CREATE.DATE='' ; Y.CREATE.USER='' ; Y.DATE.OF.LOST='' ; Y.USER.LOST='' ; Y.BR.REL.DATE='' ; Y.BR.REL.USER=''
    Y.BR.RECV.DATE='' ; Y.BR.RECV.USER='' ; Y.BR.COMMENT='' ; Y.DATE.NOT.RECV='' ; Y.DATE.REL.ACCT='' ; Y.USER.TR.LOST=''
    Y.REL.USER='' ; Y.REL.ACCT='' ; Y.HOLDER.NAME='' ; Y.REL.ACCT.STATUS='' ; Y.ACT.DATE='' ; Y.ACT.USER='' ; Y.DEST.DATE=''
    Y.DATE.TR.LOST='' ; Y.CAN.DATE='' ; Y.CAN.USER='' ; Y.CAN.REASON='' ; Y.CURR.STATUS='' ; Y.TIME.IN.BR='' ; Y.FIN.USER.STATUS='' ; Y.TIME.IN.BR.CR=''



    Y.CUSTOMER.POS = ''
    GOSUB LOCAL.REFERENCE.FIELDS
RETURN

*-------------------------------------------------
LOCAL.REFERENCE.FIELDS:
*---------------------------------------------------
    LOC.REF.APPL="ACCOUNT"
    LOC.REF.FIELDS="L.AC.STATUS1"
    LOC.REF.POS=" "
    CALL GET.LOC.REF(LOC.REF.APPL,LOC.REF.FIELDS,LOC.REF.POS)

    Y.PL.RT.POS=LOC.REF.POS

RETURN
*------------------------------------------------------------------------------------------------
OPENFILES:
*---------------------------------------------------------------

* open all the files

    CALL OPF(FN.ACCOUNT,F.ACCOUNT)
    CALL OPF(FN.REDO.CARD.REQUEST,F.REDO.CARD.REQUEST)
    CALL OPF(FN.REDO.CARD.GENERATION,F.REDO.CARD.GENERATION)
    CALL OPF(FN.STOCK.ENTRY,F.STOCK.ENTRY)
    CALL OPF(FN.REDO.CARD.DAMAGE,F.REDO.CARD.DAMAGE)
    CALL OPF(FN.REDO.CARD.DAMAGE.HIS,F.REDO.CARD.DAMAGE.HIS)
    CALL OPF(FN.LATAM.CARD.ORDER,F.LATAM.CARD.ORDER)
    CALL OPF(FN.REDO.CARD.REORDER.DEST,F.REDO.CARD.REORDER.DEST)
    CALL OPF(FN.CARD.ISSUE,F.CARD.ISSUE)
    CALL OPF(FN.LATAM.CARD.ORDER.HIS,F.LATAM.CARD.ORDER.HIS)
    CALL OPF(FN.REDO.CARD.DES.HIS,F.REDO.CARD.DES.HIS)
* CALL OPF(FN.REDO.CARD.REORDER.DEST.HIS,F.REDO.CARD.REORDER.DEST.HIS)

RETURN
*-----------------------------------------------------------------
PROCESS:
*-----------------------------------------------------------------
    Y.CARD.NO=''

    Y.SEL.CR=''
    LOCATE 'PLASTIC.REQ.NO' IN D.FIELDS<1> SETTING Y.PL.REQ.POS THEN
        SEL.CMD:="SELECT ":FN.REDO.CARD.REQUEST: " WITH @ID EQ ":D.RANGE.AND.VALUE<Y.PL.REQ.POS>:" AND WITH STATUS GE 4 "
        Y.SEL.CR=1
    END
    ELSE
        SEL.CMD="SELECT ":FN.REDO.CARD.REQUEST: " WITH STATUS GE 4"
    END

    LOCATE "PLASTIC.CARD.NUM" IN D.FIELDS<1> SETTING Y.CUSTOMER.POS THEN
        Y.CARD.NO = D.RANGE.AND.VALUE<Y.CUSTOMER.POS>
        Y.SEL.CR=1
    END
    LOCATE 'PLASTIC.CARD.TYPE' IN D.FIELDS<1> SETTING Y.CARD.TYPE.POS THEN
        SEL.CMD:=" AND CARD.TYPE EQ ":D.RANGE.AND.VALUE<Y.CARD.TYPE.POS>
        Y.SEL.CR=1
        Y.CARD.TYPE.SEL=D.RANGE.AND.VALUE<Y.CARD.TYPE.POS>
    END
    LOCATE 'PLASTIC.STATUS' IN D.FIELDS<1> SETTING Y.PL.STATUS THEN
        Y.CARD.STATUS=D.RANGE.AND.VALUE<Y.PL.STATUS>
        Y.SEL.CR=1
    END

    LOCATE 'PLASTIC.REQ.BRANCH' IN D.FIELDS<1> SETTING Y.PL.REQ.BRANCH THEN
        SEL.CMD:=" AND AGENCY EQ ":D.RANGE.AND.VALUE<Y.PL.REQ.BRANCH>
        Y.SEL.CR=1
    END
    LOCATE 'PLASTIC.DATE' IN D.FIELDS<1> SETTING Y.PL.DATE THEN
        SEL.CMD:=" AND BR.RECEIVE.DATE EQ ":D.RANGE.AND.VALUE<Y.PL.DATE>
        Y.SEL.CR=1
    END
*
    IF NOT(Y.SEL.CR) THEN
        RETURN
    END

* select all the id's if staus equal to delivered to branch
    Y.CARD.NUMBER=''
    CALL EB.READLIST(SEL.CMD,SEL.LIST,'',NO.OF.REC,RET.CODE)
    LOOP
        REMOVE Y.REQ.ID FROM SEL.LIST SETTING POS
    WHILE Y.REQ.ID:POS
* Read card request
        R.REDO.CARD.REQUEST=''
        Y.REQ.ERR=''
        R.LATAM.CARD.ORDER=''
        Y.LTM.ERR=''
        R.REDO.CARD.GENERATION=''
        Y.GEN.ERR=''
        R.STOCK.ENTRY=''
        Y.STK.ERR=''
        Y.RE.LST=''
        Y.USER.TR.LOST=''
        Y.DATE.TR.LOST=''

        CALL F.READ(FN.REDO.CARD.REQUEST,Y.REQ.ID,R.REDO.CARD.REQUEST,F.REDO.CARD.REQUEST,Y.REQ.ERR)
        Y.CARD.TYPE=R.REDO.CARD.REQUEST<REDO.CARD.REQ.CARD.TYPE>
        Y.REQ.NUMBER=Y.REQ.ID
        Y.REQ.STATUS=R.REDO.CARD.REQUEST<REDO.CARD.REQ.STATUS>
        Y.BRANCH =R.REDO.CARD.REQUEST<REDO.CARD.REQ.AGENCY>
        Y.REQ.DATE =R.REDO.CARD.REQUEST<REDO.CARD.REQ.DATE.TIME>
        Y.REQ.DATE =TODAY[1,2]:Y.REQ.DATE[1,6]
        Y.REDO.REQ =R.REDO.CARD.REQUEST<REDO.CARD.REQ.AUTO.REQUEST.FLAG>
        R.REDO.CARD.REQUEST.HIS=R.REDO.CARD.REQUEST
        Y.BR.REL.DATE=''
        Y.BR.REL.USER=''
        Y.BR.RECV.DATE=''
        Y.BR.RECV.USER=''
        Y.DATE.NOT.RECV=''
        Y.BR.COMMENT=''
        Y.USER.NOT.RECV=''
        Y.TIME.IN.BR=''
        Y.REQ.USER =''
        GOSUB GET.BR.REC.DET
        Y.HIST.CUR.NO=R.REDO.CARD.REQUEST<REDO.CARD.REQ.CURR.NO>
        LOOP
        WHILE Y.HIST.CUR.NO GE 1
            Y.REQ.HIS.ID=Y.REQ.ID:';':Y.HIST.CUR.NO
            CALL F.READ(FN.REDO.CARD.REQUEST.HIS,Y.REQ.HIS.ID,R.REDO.CARD.REQUEST.HIS,F.REDO.CARD.REQUEST.HIS,Y.REQ.ERR)
            GOSUB GET.BR.REC.DET
            Y.HIST.CUR.NO -= 1
        REPEAT

        Y.REQ.CODE=R.REDO.CARD.REQUEST<REDO.CARD.REQ.CO.CODE>
        GOSUB READ.CARD.GENERATION
    REPEAT
RETURN

*---------------------
GET.BR.REC.DET:
*---------------------

    IF R.REDO.CARD.REQUEST.HIS<REDO.CARD.REQ.STATUS> EQ '5' THEN
        Y.BR.REL.DATE=R.REDO.CARD.REQUEST.HIS<REDO.CARD.REQ.DATE.TIME>
        Y.BR.REL.DATE=TODAY[1,2]:Y.BR.REL.DATE[1,6]
        Y.BR.REL.USER=R.REDO.CARD.REQUEST.HIS<REDO.CARD.REQ.INPUTTER>
    END
    IF R.REDO.CARD.REQUEST.HIS<REDO.CARD.REQ.STATUS> EQ '6' THEN
        Y.BR.RECV.DATE=R.REDO.CARD.REQUEST.HIS<REDO.CARD.REQ.BR.RECEIVE.DATE>
        Y.BR.RECV.USER=FIELD(R.REDO.CARD.REQUEST.HIS<REDO.CARD.REQ.INPUTTER>,'_',2)
        Y.TIME.IN.BR.1 =R.REDO.CARD.REQUEST.HIS<REDO.CARD.REQ.BR.RECEIVE.DATE>
        IF Y.TIME.IN.BR.1 THEN
            Y.REGION = ''
            CALL CDD(Y.REGION,Y.TIME.IN.BR.1,TODAY,Y.DIFF)
            Y.TIME.IN.BR = Y.DIFF
        END
    END
    IF R.REDO.CARD.REQUEST.HIS<REDO.CARD.REQ.STATUS> EQ '7' THEN
        Y.DATE.TR.LOST=R.REDO.CARD.REQUEST.HIS<REDO.CARD.REQ.DATE.TIME>
        Y.DATE.TR.LOST=TODAY[1,2]:Y.DATE.TR.LOST[1,6]
        Y.USER.TR.LOST=R.REDO.CARD.REQUEST.HIS<REDO.CARD.REQ.INPUTTER>
        Y.RE.LST='1'
    END
    IF R.REDO.CARD.REQUEST.HIS<REDO.CARD.REQ.STATUS> EQ '1' THEN
        IF Y.REDO.REQ EQ 'YES' THEN
            Y.REQ.USER = '_AUTOM'
        END ELSE
            Y.REQ.USER=R.REDO.CARD.REQUEST.HIS<REDO.CARD.REQ.AUTHORISER>
        END
    END
RETURN

*--------------------------------------------------
READ.CARD.GENERATION:
*--------------------------------------------------------
    Y.DUE.DATE=''
    CALL F.READ(FN.REDO.CARD.GENERATION,Y.REQ.ID,R.REDO.CARD.GENERATION,F.REDO.CARD.GENERATION,Y.GEN.ERR)

    Y.CARD.NUMBER=R.REDO.CARD.GENERATION<REDO.CARD.GEN.CARD.NUMBERS>

    Y.NO.CARD1 = DCOUNT(Y.CARD.TYPE,@VM)
    FOR I.VAR = 1 TO Y.NO.CARD1
        Y.CARD1 = FIELD(Y.CARD.TYPE,@VM,I.VAR,1)
        Y.DUE.DATE.GEN=R.REDO.CARD.GENERATION<REDO.CARD.GEN.EXPIRY,I.VAR>
        Y.CARD.NUMBER=R.REDO.CARD.GENERATION<REDO.CARD.GEN.CARD.NUMBERS,I.VAR>
        GOSUB CARD.NO.LOOP.PROCESS
    NEXT I.VAR
RETURN
*--------------------
CARD.NO.LOOP.PROCESS:
*--------------------
    LOOP
        REMOVE Y.CURR.CARD.NO FROM Y.CARD.NUMBER SETTING Y.CARD.POS
    WHILE Y.CURR.CARD.NO:Y.CARD.POS
        IF Y.CARD.NO THEN
            IF Y.CARD.NO EQ Y.CURR.CARD.NO THEN
*
                Y.CREATE.USER=R.REDO.CARD.GENERATION<REDO.CARD.GEN.AUTHORISER>
                GOSUB NEXTLATAMPROCESS
                GOSUB READ.CARD.DAMAGE
                GOSUB READ.CARD.PRINT.LOST
                GOSUB SELECT.CARD.ISSUE
* GOSUB READ.CARD.REORDER
                GOSUB READ.CARD.DEST
                GOSUB FINAL.ARRAY
                GOSUB PGM.END
            END ELSE
                CONTINUE
            END
        END
        Y.CREATE.USER=R.REDO.CARD.GENERATION<REDO.CARD.GEN.AUTHORISER>
        GOSUB NEXTLATAMPROCESS
        GOSUB READ.CARD.DAMAGE
        GOSUB READ.CARD.PRINT.LOST
        GOSUB SELECT.CARD.ISSUE
* GOSUB READ.CARD.REORDER
        GOSUB READ.CARD.DEST
        GOSUB FINAL.ARRAY
    REPEAT
RETURN
*-------------------
READ.CARD.PRINT.LOST:
*-------------------

    Y.DATE.OF.LOST=''
    Y.USER.LOST =''
    CALL F.READ(FN.REDO.CARD.PRINT.LOST,Y.REQ.ID,R.REDO.CARD.PRINT.LOST,F.REDO.CARD.PRINT.LOST,Y.DMG.ERR)
    Y.PR.LOST.CARD.LIST=R.REDO.CARD.PRINT.LOST<REDO.PRN.LST.OLD.CRD.LOST>
    Y.CARD.TYPE.LIST =R.REDO.CARD.PRINT.LOST<REDO.PRN.LST.OLD.CRD.TYPE>

    Y.PR.LOST.CARD.CNT=1
    Y.PR.LOST.CARD.TOT=DCOUNT(Y.CARD.TYPE.LIST,@VM)
    Y.PL.CON.FL=''
    LOOP
    WHILE Y.PR.LOST.CARD.CNT LE Y.PR.LOST.CARD.TOT
        Y.IN.LOOP.PLC=1
        Y.IN.LOOP.PLC.TOT=DCOUNT(Y.PR.LOST.CARD.LIST<1,Y.PR.LOST.CARD.CNT>,@SM)
        LOOP WHILE Y.IN.LOOP.PLC LE Y.IN.LOOP.PLC.TOT
            IF Y.CURR.CARD.NO EQ Y.PR.LOST.CARD.LIST<1,Y.PR.LOST.CARD.CNT,Y.IN.LOOP.PLC> AND Y.CARD1 EQ Y.CARD.TYPE.LIST<1,Y.PR.LOST.CARD.CNT> THEN
                Y.DATE.OF.LOST =R.REDO.CARD.PRINT.LOST<REDO.PRN.LST.DATE.TIME>
                Y.USER.LOST =R.REDO.CARD.PRINT.LOST<REDO.PRN.LST.AUTHORISER>
                Y.TIME.IN.BR.CR=''
                Y.PL.CON.FL='1'
            END
            Y.IN.LOOP.PLC += 1
        REPEAT
        Y.PR.LOST.CARD.CNT += 1
    REPEAT

    IF NOT(Y.PL.CON.FL) THEN
        RETURN
    END

    Y.PR.LOST.ID =Y.REQ.ID
    Y.PR.CARD.LOST.CUR.NO=R.REDO.CARD.PRINT.LOST<REDO.PRN.LST.CURR.NO>-1
    LOOP
    WHILE Y.PR.CARD.LOST.CUR.NO GE 1
        Y.PL.CON.FL=''
        Y.PR.LOST.ID=Y.REQ.ID:';':Y.PR.CARD.LOST.CUR.NO

        CALL F.READ(FN.REDO.CARD.PRINT.LOST.HIS,Y.PR.LOST.ID,R.REDO.CARD.PRINT.LOST.HIS,F.REDO.CARD.PRINT.LOST.HIS,ERR)

        Y.PR.LOST.CARD.LIST.HIS =R.REDO.CARD.PRINT.LOST.HIS<REDO.PRN.LST.OLD.CRD.LOST>
        Y.CARD.TYPE.LIST.HIS =R.REDO.CARD.PRINT.LOST.HIS<REDO.PRN.LST.OLD.CRD.TYPE>

        Y.PR.LOST.CARD.HIS.CNT=1
        Y.PR.LOST.CARD.HIS.TOT=DCOUNT(Y.CARD.TYPE.LIST.HIS,@VM)

        LOOP
        WHILE Y.PR.LOST.CARD.HIS.CNT LE Y.PR.LOST.CARD.HIS.TOT
            Y.PRLC.HIS.CNT=1
            Y.PRLC.HIS.TOT=DCOUNT(Y.PR.LOST.CARD.LIST.HIS<1,Y.PR.LOST.CARD.HIS.CNT>,@SM)
            LOOP
            WHILE Y.PRLC.HIS.CNT LE Y.PRLC.HIS.TOT
                IF Y.CURR.CARD.NO EQ Y.PR.LOST.CARD.LIST.HIS<1,Y.PR.LOST.CARD.HIS.CNT,Y.PRLC.HIS.CNT> AND Y.CARD1 EQ Y.CARD.TYPE.LIST.HIS<1,Y.PR.LOST.CARD.HIS.CNT> THEN
                    Y.DATE.OF.LOST=R.REDO.CARD.PRINT.LOST.HIS<REDO.PRN.LST.DATE.TIME>
                    Y.USER.LOST =R.REDO.CARD.PRINT.LOST.HIS<REDO.PRN.LST.AUTHORISER>
                    Y.TIME.IN.BR.CR=''
                    Y.PL.CON.FL='1'
                END
                Y.PRLC.HIS.CNT += 1
            REPEAT
            Y.PR.LOST.CARD.HIS.CNT += 1
        REPEAT
        IF NOT(Y.PL.CON.FL) THEN
            RETURN
        END
        Y.PR.CARD.LOST.CUR.NO -= 1
    REPEAT
RETURN

*-------------------------------------------------------------------------------------
READ.CARD.DAMAGE:
*-----------------------------------------------------------------------------------
    IF Y.RE.LST THEN
        RETURN
    END

    CALL F.READ(FN.REDO.CARD.DAMAGE,Y.REQ.ID,R.REDO.CARD.DAMAGE,F.REDO.CARD.DAMAGE,Y.DMG.ERR)

    Y.DATE.NOT.RECV=''
    Y.BR.COMMENT=''
    Y.USER.NOT.RECV=''
    Y.DAM.CARD.TYPE =R.REDO.CARD.DAMAGE<REDO.CARD.DAM.CARD.TYPE>
    Y.DAM.CARD.LIST =R.REDO.CARD.DAMAGE<REDO.CARD.DAM.CARD.NUMBER.OLD>
    GOSUB CHECK.DAMAGE.LOST
    Y.CURR.NO=R.REDO.CARD.DAMAGE<REDO.CARD.DAM.CURR.NO>
    Y.CURR.NO-=1
    LOOP
    WHILE Y.CURR.NO GE 1
        Y.RCD.HIS.ID=Y.REQ.ID:";":Y.CURR.NO
        CALL F.READ(FN.REDO.CARD.DAMAGE.HIS,Y.RCD.HIS.ID,R.REDO.CARD.DAMAGE,F.REDO.CARD.DAMAGE.HIS,ERR)
        Y.DAM.CARD.TYPE =R.REDO.CARD.DAMAGE<REDO.CARD.DAM.CARD.TYPE>
        Y.DAM.CARD.LIST =R.REDO.CARD.DAMAGE<REDO.CARD.DAM.CARD.NUMBER.OLD>
        GOSUB CHECK.DAMAGE.LOST
        Y.CURR.NO -= 1
    REPEAT

RETURN
*----------------
CHECK.DAMAGE.LOST:
*----------------
    LOCATE Y.CARD1 IN Y.DAM.CARD.TYPE<1,1> SETTING Y.DAM.CARD.TY.POS THEN
        LOCATE Y.CURR.CARD.NO IN Y.DAM.CARD.LIST<1,Y.DAM.CARD.TY.POS,1> SETTING Y.DAM.CARD.TY.POS1 THEN
            GOSUB CHECK.DL.ASSIGN
        END
    END

RETURN
*---------------
CHECK.DL.ASSIGN:
*---------------
    Y.DATE.NOT.RECV=R.REDO.CARD.DAMAGE<REDO.CARD.DAM.DATE.TIME>
    Y.DATE.NOT.RECV=TODAY[1,2]:Y.DATE.NOT.RECV[1,6]
    Y.BR.COMMENT =R.REDO.CARD.DAMAGE<REDO.CARD.DAM.REMARKS.OLD><1,Y.DAM.CARD.TY.POS,Y.DAM.CARD.TY.POS1>
    Y.USER.NOT.RECV=R.REDO.CARD.DAMAGE<REDO.CARD.DAM.AUTHORISER>
    Y.TIME.IN.BR.CR=''
RETURN

NEXTLATAMPROCESS:
    R.ACCOUNT=''
    Y.ACC.ERR=''

    Y.ACT.USER=''
    Y.DATE.REL.ACCT=''
    Y.REL.ACCT=''
    Y.HOLDER.NAME=''
    Y.ACT.DATE=''
    Y.CAN.DATE=''
    Y.CAN.USER=''
    Y.CAN.REASON=''
    Y.CURR.STATUS=''
    ISS.STAGE =''
    Y.REL.ACCT.STATUS=''
    Y.TIME.IN.BR.CR=''
    Y.REL.USER=''
    Y.FIN.USER.STATUS=''
    Y.LTM.ID = Y.CARD1:".":Y.CURR.CARD.NO
    CALL F.READ(FN.LATAM.CARD.ORDER,Y.LTM.ID,R.LATAM.CARD.ORDER,F.LATAM.CARD.ORDER,Y.LTM.ERR)

    IF NOT(R.LATAM.CARD.ORDER) THEN
        Y.TIME.IN.BR.CR=Y.TIME.IN.BR
    END

    Y.DUE.DATE =R.LATAM.CARD.ORDER<CARD.IS.EXPIRY.DATE>

    IF Y.DUE.DATE LT Y.DUE.DATE.GEN THEN
        Y.DUE.DATE=''
        Y.TIME.IN.BR.CR=Y.TIME.IN.BR
        RETURN
    END

    Y.DATE.REL.ACCT=R.LATAM.CARD.ORDER<CARD.IS.ISSUE.DATE>

    Y.REL.ACCT=R.LATAM.CARD.ORDER<CARD.IS.ACCOUNT>

    Y.HOLDER.NAME=R.LATAM.CARD.ORDER<CARD.IS.NAME.ON.PLASTIC>
    Y.ACT.DATE=R.LATAM.CARD.ORDER<CARD.IS.ACTIVE.DATE>

    Y.FIN.USER.DET=R.LATAM.CARD.ORDER<CARD.IS.INPUTTER>
    Y.FIN.USER.STATUS=FIELD(Y.FIN.USER.DET,'_',2)

    Y.CURR.STATUS=R.LATAM.CARD.ORDER<CARD.IS.CARD.STATUS>

    CALL F.READ(FN.ACCOUNT,Y.REL.ACCT,R.ACCOUNT,F.ACCOUNT,Y.ACC.ERR)
    Y.REL.ACCT.STATUS=R.ACCOUNT<AC.LOCAL.REF,Y.PL.RT.POS>
    ISS.STAGE=R.LATAM.CARD.ORDER<CARD.IS.ISSUE.STAGE>
    R.LATAM.CARD.ORDER.HIS=R.LATAM.CARD.ORDER
    GOSUB READ.ASS.LCO.DATA
    GOSUB SEL.LATAM.HIS.FILE
RETURN
*------------------------------------------------------------
SELECT.CARD.ISSUE:
*------------------------------------------------------------
    Y.CREATE.DATE=20:R.REDO.CARD.GENERATION<REDO.CARD.GEN.DATE.TIME>[1,6]
RETURN
*-------------
READ.CARD.DEST:
*-------------
    Y.DEST.DATE=''
    Y.RCDH.ID =Y.REQ.ID:"-":Y.CARD1
    CALL F.READ(FN.REDO.CARD.DES.HIS,Y.RCDH.ID,R.REDO.CARD.DES.HIS,F.REDO.CARD.DES.HIS,ERR)
    Y.CARD.DES.NO=R.REDO.CARD.DES.HIS<REDO.DES.HIS.CARD.NUMBER>
    LOCATE Y.CURR.CARD.NO IN Y.CARD.DES.NO<1,1> SETTING Y.RCDH.POS THEN
        Y.DEST.DATE=R.REDO.CARD.DES.HIS<REDO.DES.HIS.DEST.DATE>
        Y.TIME.IN.BR.CR=''
    END
RETURN
*-----------------------------------------------------------
SEL.LATAM.HIS.FILE:
*-----------------------------------------------------------
*select all the card order history files
    Y.LAT.CURR.NO=R.LATAM.CARD.ORDER<CARD.IS.CURR.NO>
    Y.LAT.CARD.STATUS=R.LATAM.CARD.ORDER<CARD.IS.CARD.STATUS>
    R.LATAM.CARD.ORDER.HIS.LAT=R.LATAM.CARD.ORDER
    Y.LAT.CURR.NO -= 1
    GOSUB READ.LATAM.HIS
RETURN
*-----------------------------------------------------------------
READ.LATAM.HIS:
*-----------------------------------------------------------------
    Y.SET.USER=''
    IF Y.LAT.CURR.NO GE 1 THEN
        Y.CTR.CURR=Y.LAT.CURR.NO
        LOOP
        WHILE Y.CTR.CURR GE 1
            Y.LTMHIS.ID=Y.LTM.ID:';':Y.CTR.CURR
            CALL F.READ(FN.LATAM.CARD.ORDER.HIS,Y.LTMHIS.ID,R.LATAM.CARD.ORDER.HIS,F.LATAM.CARD.ORDER.HIS,Y.LTM.HIS.ERR)
            Y.OLD.CARD.STATUS=R.LATAM.CARD.ORDER.HIS<CARD.IS.CARD.STATUS>
            IF Y.OLD.CARD.STATUS NE Y.LAT.CARD.STATUS AND NOT(Y.SET.USER) THEN
                Y.FIN.USER.STATUS=FIELD(R.LATAM.CARD.ORDER.HIS.LAT<CARD.IS.INPUTTER>,'_',2)
                Y.SET.USER='1'
            END
            GOSUB READ.ASS.LCO.DATA
            R.LATAM.CARD.ORDER.HIS.LAT=R.LATAM.CARD.ORDER.HIS
            Y.CTR.CURR -= 1
        REPEAT
    END
RETURN
*------------------
READ.ASS.LCO.DATA:
*------------------

    Y.OLD.CARD.STATUS=R.LATAM.CARD.ORDER.HIS<CARD.IS.CARD.STATUS>

    IF Y.OLD.CARD.STATUS EQ '94' THEN
        Y.ACT.USER=FIELD(R.LATAM.CARD.ORDER.HIS<CARD.IS.INPUTTER>,'_',2)
    END
    IF Y.OLD.CARD.STATUS EQ '90' THEN
        Y.REL.USER=R.LATAM.CARD.ORDER.HIS<CARD.IS.INPUTTER>
    END
    IF Y.OLD.CARD.STATUS EQ '93' THEN
        Y.CAN.DATE=R.LATAM.CARD.ORDER.HIS<CARD.IS.CANCELLATION.DATE>
        Y.CAN.USER=FIELD(R.LATAM.CARD.ORDER.HIS<CARD.IS.INPUTTER>,'_',2)
        Y.CAN.REASON=R.LATAM.CARD.ORDER.HIS<CARD.IS.CAN.REASON>
    END
RETURN
*--------------------------------------------------------------
FINAL.ARRAY:
*--------------------------------------------------------------

    IF Y.DUE.DATE THEN
        IF Y.DUE.DATE.GEN NE Y.DUE.DATE THEN
            RETURN
        END
    END

    IF Y.CARD.STATUS AND Y.CURR.STATUS NE Y.CARD.STATUS THEN
        RETURN
    END
    IF Y.CARD.TYPE.SEL AND Y.CARD.TYPE.SEL NE Y.CARD1 THEN
        RETURN
    END
    IF NOT(Y.USER.LOST) THEN
        Y.FINAL.ARRAY<-1> = Y.CARD1:"*":Y.CURR.CARD.NO:"*":Y.REQ.NUMBER:"*":Y.REQ.STATUS:"*":Y.DUE.DATE:"*":Y.BRANCH:"*":Y.REQ.DATE:"*":Y.REQ.USER:"*":Y.CREATE.DATE:"*":Y.CREATE.USER:"*":Y.DATE.OF.LOST:"*":Y.USER.LOST:"*":Y.BR.REL.DATE:"*":Y.BR.REL.USER:"*":Y.BR.RECV.DATE:"*":Y.BR.RECV.USER:"*":Y.DATE.NOT.RECV:"*":Y.USER.NOT.RECV:"*":Y.BR.COMMENT:"*":Y.DATE.REL.ACCT:"*":Y.REL.USER:"*":Y.REL.ACCT:"*":Y.HOLDER.NAME:"*":Y.REL.ACCT.STATUS:"*":Y.ACT.DATE:"*":Y.ACT.USER:"*":Y.DEST.DATE:"*":Y.USER.TR.LOST:"*":Y.DATE.TR.LOST:"*":Y.CAN.DATE:"*":Y.CAN.USER:"*":Y.CAN.REASON:"*":Y.CURR.STATUS:"*":Y.TIME.IN.BR.CR:"*":Y.FIN.USER.STATUS
    END
    ELSE
        Y.FINAL.ARRAY<-1> = Y.CARD1:"*":Y.CURR.CARD.NO:"*":Y.REQ.NUMBER:"*":Y.REQ.STATUS:"*":Y.DUE.DATE:"*":Y.BRANCH:"*":Y.REQ.DATE:"*":Y.REQ.USER:"*":Y.CREATE.DATE:"*":Y.CREATE.USER:"*":Y.DATE.OF.LOST:"*":Y.USER.LOST:"*":"*":"*":"*":"*":"*":"*":"*":Y.DATE.REL.ACCT:"*":Y.REL.USER:"*":Y.REL.ACCT:"*":Y.HOLDER.NAME:"*":Y.REL.ACCT.STATUS:"*":Y.ACT.DATE:"*":Y.ACT.USER:"*":"*":"*":"*":Y.CAN.DATE:"*":Y.CAN.USER:"*":Y.CAN.REASON:"*":Y.CURR.STATUS:"*":Y.TIME.IN.BR.CR:"*":Y.FIN.USER.STATUS
    END
RETURN
*---------------------------------------------------------------
PGM.END:
*--------
END
